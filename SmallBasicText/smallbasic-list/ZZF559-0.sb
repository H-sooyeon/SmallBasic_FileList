'   Gauss-Jordan Method
'==================

'  ZZF559-0
'http://social.msdn.microsoft.com/Forums/en-US/smallbasic/thread/
'4ded71e7-97dc-455f-97b7-c9b9246ff74d

'  Section 0 - Initialization Procedures:
TAB = Text.GetCharacter(9)
LF  = Text.GetCharacter(10)
ESC = Text.GetCharacter(27)

Start:

TextWindow.Clear()
TextWindow.ForegroundColor = "Blue"
TextWindow.WriteLine ("Gauss-Jordan Matrix Calculation")
TextWindow.WriteLine ("===============================" + LF)

'  Section 1 - Request User's Input for the 3 Linear Equations:
For i = 1 To 3
  TextWindow.ForegroundColor = "Magenta"
  TextWindow.Write     (LF + "Enter the following 4 values for -> x")
  TextWindow.WriteLine (i + " + y" + i + " + z" + i + " = t" + i)
  TextWindow.ForegroundColor = "Cyan"
  
  x[i] = TextWindow.ReadNumber() * 1
  y[i] = TextWindow.ReadNumber() * 1
  z[i] = TextWindow.ReadNumber() * 1
  t[i] = TextWindow.ReadNumber() * 1
EndFor

'  Section 2 - Display Linear Equations:
TextWindow.Clear()
TextWindow.ForegroundColor = "Green"

For i = 1 To 3
  TextWindow.Write     ("| " + TAB + x[i] + "x" + TAB + y[i] + "y" + TAB)
  TextWindow.WriteLine (z[i] + "z" + TAB  + "=" + TAB + t[i] + TAB + "|")
EndFor

'  Section 3 - Calculate by Gauss-Jordan Method:

y[1] =  y[1] / x[1]
z[1] =  z[1] / x[1]
t[1] =  t[1] / x[1]
x[1] =  x[1] / x[1]

For i = 2 To 3
  y[i] = -x[i] * y[1] + y[i]
  z[i] = -x[i] * z[1] + z[i]
  t[i] = -x[i] * t[1] + t[i]
  x[i] = -x[i] * x[1] + x[i]
EndFor

z[2] =  z[2] / y[2]
t[2] =  t[2] / y[2]
y[2] =  y[2] / y[2]

For i = 1 To 3 Step 2
  z[i] = -y[i] * z[2] + z[i]
  t[i] = -y[i] * t[2] + t[i]
  y[i] = -y[i] * y[2] + y[i]
EndFor

t[3] =  t[3] / z[3]
z[3] =  z[3] / z[3]

For i = 1 To 2
  t[i] = -z[i] * t[3] + t[i]
  z[i] = -z[i] * z[3] + z[i]
EndFor

'  Section 4 - Display the 3 #s Which Satisfy the Linear System:

TextWindow.ForegroundColor = "Yellow"
TextWindow.WriteLine(LF)
var = Text.GetCharacterCode("x")

For i = var To var+2
  TextWindow.WriteLine (Text.GetCharacter(i) + " = " + t[i - var + 1])
EndFor

'  Section 5 - Decide Whether to End or Restart Depending on the Key Pressed:

TextWindow.ForegroundColor= "Magenta
TextWindow.Write     (LF + LF + "Press 'Q' or 'Esc' to quit ")
TextWindow.WriteLine ("or any other to restart..." + LF)

var = Text.ConvertToUpperCase ( TextWindow.ReadKey() )

If var = "Q" Or var = ESC Then
  Program.End()
EndIf

Goto Start
