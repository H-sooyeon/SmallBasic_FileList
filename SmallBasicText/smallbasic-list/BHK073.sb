'Simon by Yoc. Published with the id : MFF438

'Canvas
GraphicsWindow.Title = "Simon"
GraphicsWindow.CanResize = "no"
GraphicsWindow.BackgroundColor = "white"
GraphicsWindow.Width = "400"
GraphicsWindow.Height = "430"

'red square
GraphicsWindow.BrushColor = "red"
red_square=Shapes.AddRectangle(200,200)

'blue_square
GraphicsWindow.BrushColor = "blue"
blue_square = Shapes.AddRectangle(200,200)
Shapes.Move(blue_square,200,0)

'green_square
GraphicsWindow.BrushColor = "green"
green_square = Shapes.AddRectangle(200,200)
Shapes.Move(green_square,0,200)

'yellow_square
GraphicsWindow.BrushColor = "yellow"
yellow_square = Shapes.AddRectangle(200,200)
Shapes.Move(yellow_square,200,200)

'Initialization
count = 1
how_many_clicks = 0
lost = "no"
difficulty = 1000

'Events detection
GraphicsWindow.MouseDown = OnMouseClick
GraphicsWindow.MouseUp = OnMouseRelease


'Main loop
While lost="no"
  DisplayScore()
  Program.Delay(1000)
  
  'We create and show the color sequence
  OnCreateSequence()
  
  
  
  how_many_clicks = 0
  While how_many_clicks < count
    'We listen to mouse clicks with OnMouseClick and we check for mistakes
  endwhile
  
  
  count = count + 1 
  If difficulty > 200 Then
    difficulty = difficulty - 50
  EndIf
EndWhile

Sub OnCreateSequence    
  'We create the color sequence and highlight it
  
  'We add a new random  color to the array
  sequence[count]=Math.GetRandomNumber(4)
  
  'For each array element, we highlight the corresponding color
  For i = 1 To count  
    selected_color = sequence[i]
    OnHighLightColor()
    Program.Delay(difficulty)
    OnMouseRelease()
    Program.Delay(difficulty/5)
  EndFor
  
endsub

Sub OnMouseClick
  
  how_many_clicks = how_many_clicks + 1
  'We get mouse coordonates
  x=GraphicsWindow.MouseX
  y=GraphicsWindow.MouseY
  
  'We detect clicked color
  If x < 200 And y < 200 Then
    cliked_color = 1 'red
  EndIf
  If x > 200 And y < 200 Then
    cliked_color = 2 'blue
  EndIf
  If x < 200 And y > 200 Then
    cliked_color = 3 'green
  EndIf
  If x > 200 And y > 200 Then
    cliked_color = 4 'yellow
  EndIf
  
  
  'We highlight the corresponing color
  selected_color = cliked_color
  OnHighLightColor()
  
  If cliked_color <> sequence[how_many_clicks] Then
    lost="yes"
    WeLost()
  endif
  
EndSub

Sub OnHighLightColor
  
  'Which color do we have to take care of  ?
  If selected_color = 1 then 'red
    selected_shape = red_square
  EndIf
  If selected_color = 2 then 'blue
    selected_shape = blue_square
  EndIf
  If selected_color = 3 then 'green
    selected_shape = green_square
  EndIf
  If selected_color = 4 then 'yellow
    selected_shape = yellow_square
  EndIf
  
  'We highlight the corresponding color
  
  Shapes.SetOpacity(selected_shape,30)
EndSub

Sub OnMouseRelease
  Shapes.SetOpacity(selected_shape,100)
endsub 

Sub DisplayScore
  GraphicsWindow.BrushColor = "white"
  GraphicsWindow.FillRectangle(175, 405, 70, 20)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(180, 405, "Score: " + (count-1))
EndSub

Sub WeLost 
  GraphicsWindow.ShowMessage("Lost ! Your score : " + (count-1),"Simon")
  Program.End()
EndSub
