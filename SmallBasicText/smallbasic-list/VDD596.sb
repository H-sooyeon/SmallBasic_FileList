' Zigsaw

' Copyright Â© 2015 Nonki Takahashi.  The MIT License.

' Last Update 2015-05-05

' Version 0.2b

'

GraphicsWindow.Title = "Zigsaw 0.2b"

gw = 598

gh = 428

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

folder = "http://www.nonkit.com/smallbasic.files"

nPiece = 4

For piece = 1 To nPiece

  If Math.Remainder(piece, 2) = 1 Then

    img[piece] = ImageList.LoadImage(folder + "/hpiece.png")

  Else

    img[piece] = ImageList.LoadImage(folder + "/vpiece.png")

  EndIf

  p[piece] = Shapes.AddImage(img[piece])

  pw[piece] = ImageList.GetWidthOfImage(img[piece])

  ph[piece] = ImageList.GetHeightOfImage(img[piece])

  px[piece] = Math.GetRandomNumber(gw - pw[piece]) - 1

  py[piece] = Math.GetRandomNumber(gh - ph[piece]) - 1

  Shapes.Move(p[piece], px[piece], py[piece])

  Shapes.SetOpacity(p[piece], 50) ' for debug

EndFor

mouseDown = "False"

mouseMove = "False"

GraphicsWindow.MouseDown = OnMouseDown

GraphicsWindow.MouseUp = OnMouseUp

GraphicsWindow.MouseMove = OnMouseMove

While "True"

  If mouseDown Then

    mx = GraphicsWindow.MouseX

    my = GraphicsWindow.MouseY

    GetPiece()

    If piece <> "" Then

      MovePiece()

    EndIf

  EndIf

EndWhile

Sub GetPiece

  ' param mx, my - mouse position

  ' return piece - piece index

  ' return ox, oy - offset of mouse position in the piece

  piece = ""

  For i = 4 To 1 Step -1

    If px[i] <= mx And mx <= px[i] + pw[i] And py[i] <= my And my <= py[i] + ph[i] Then

      GraphicsWindow.DrawImage(img[i], px[i], py[i])

      If GraphicsWindow.GetPixel(mx, my) = "#000000" Then

        ox = mx - px[i]

        oy = my - py[i]

        piece = i

      EndIf

      GraphicsWindow.BrushColor = "#FFFFFF"

      GraphicsWindow.FillRectangle(px[i], py[i], pw[i], ph[i])

      If piece <> "" Then

        i = 1 ' break

      EndIf

    EndIf

  EndFor

EndSub

Sub MovePiece

  ' param piece

  While mouseDown

    If mouseMove Then

      mx = GraphicsWindow.MouseX

      my = GraphicsWindow.MouseY

      px[piece] = mx - ox

      py[piece] = my - oy

      Shapes.Move(p[piece], px[piece], py[piece])

      mouseMove = "False"

    EndIf

  EndWhile

EndSub

Sub OnMouseDown

  mouseDown = "True"

EndSub

Sub OnMouseMove

  mouseMove = "True"

EndSub

Sub OnMouseUp

  mouseDown = "False"

EndSub


