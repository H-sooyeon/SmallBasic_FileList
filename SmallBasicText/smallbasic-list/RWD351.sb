' SmallBasic Version 1.2
' Program: Move ComboShape
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/b9dd2148-0663-4459-b072-700880d387f1/mahreen-miangul-9th-september-2016
' ImportURL: http://smallbasic.com/program/?
' Extension: 
' Comment: 
'  
' Variables: 
' ToDo: 
' ================================================================================
cols = "1=Red;2=Lime;3=Blue;4=Yellow;"
nShps = Array.GetItemCount(cols)    '' let's use same number of single shapes as colors (4)
w = 50                              '' w = h (4 squares)

gw = GraphicsWindow.Width
gh = GraphicsWindow.Height
offY = gh/2 - w                       '' offset down in y-direction, vert. center in GW.Height

'' a.)  CREATE ComboShape 'shp[]' and   b.) place in initial position
GraphicsWindow.Title = "Moving ComboShape"
GraphicsWindow.PenWidth = 0

For n = 1 To nShps
  '' a.)
  GraphicsWindow.BrushColor = cols[n]
  shp[n] = Shapes.AddRectangle(w,w)
  
  '' b.)
  x = Math.Remainder(n-1, 2) * w    '' or keep x in array eg. x[n] for later moving
  y = Math.Floor((n-1) / 2) * w
  Shapes.Move(shp[n], x, y + offY)
EndFor

Program.Delay(2000)

'' MOVE ComboShape 'shp[]' continuously,  part by part
dx = 1                              '' step width 1 (move right) or -1 (move left)
While "True"
  For n = 1 To nShps
    x = Shapes.GetLeft(shp[n]) + dx '' or   x[n] = x[n] + dx   , if kept in array
    y = Shapes.GetTop(shp[n])
    Shapes.Move(shp[n], x,y)
  EndFor
  
  If (x >= gw-w) Or (x <= w) Then   '' If shp[2|4] IsRightOut  OR  shp[1|3] IsLeftOut  ....
    dx = -dx                        '' ... reverse (-)  direction
  EndIf
  
  Program.Delay(10)
EndWhile
  
