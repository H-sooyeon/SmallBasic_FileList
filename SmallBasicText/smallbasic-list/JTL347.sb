gw = 500

GraphicsWindow.Width = gw

GraphicsWindow.BackgroundColor = "Yellow"

GraphicsWindow.PenWidth = 0

GraphicsWindow.BrushColor = "Blue"

nPoint = 50

damping = 0.005

For i = 1 To nPoint

  xPos[i] = gw/(nPoint-1)*(i-1)

  yPos[i] = 200

  yVel[i] = 0

  rod[i] = Shapes.AddRectangle(gw/(nPoint-1)+1,1000)

  Shapes.Move(rod[i],xPos[i],yPos[i])

EndFor



down = 0

up = 0

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp



While ("True")

  If (down = 1) Then

    yPos[1] = yPos[1]+5

  EndIf

  If (up = 1) Then

    yPos[1] = yPos[1]-5

  EndIf

  update()

  Program.Delay(10)

EndWhile



Sub update

  For i = 2 To nPoint-1

    yVel[i] = (1-damping)*yVel[i]+(yPos[i-1]+yPos[i+1]-2*yPos[i])/2

  EndFor

  For i = 1 To nPoint

    yPos[i] = yPos[i]+yVel[i]

  EndFor

  For i = 1 To nPoint

    Shapes.Move(rod[i],xPos[i],yPos[i])

  EndFor

EndSub



Sub OnKeyDown

  key = GraphicsWindow.LastKey

  If (key = "Down") Then

    down = 1

  ElseIf (key = "Up") Then

    up = 1

  EndIf

EndSub



Sub OnKeyUp

  key = GraphicsWindow.LastKey

  If (key = "Down") Then

    down = 0

  ElseIf (key = "Up") Then

    up = 0

  EndIf

EndSub


