Controls.ButtonClicked = OnBtClk 

Init()

Controls.showControl(B1)

While T = T

  ShowClock()

  WUTime = Controls.GetTextBoxText(TB)

  If BtClk = T Then

    BtClk = F 

    Sound.PlayClickAndWait()

    ReactionBtClk()

  EndIf 

  If Alarm = T Then 

    CheckAlarm()

  EndIf 

EndWhile 



Sub CheckAlarm

  A = Text.GetSubText(ATime,1,2) 

  B = Text.GetSubText(ATime,4,2)

  C = Text.GetSubText(ATime,7,2)

  AA = Text.Append(A, B)

  AA = Text.Append(AA, C)

  A = Text.GetSubText(WUTime,1,2) 

  B = Text.GetSubText(WUTime,4,2)

  C = Text.GetSubText(WUTime,7,2)

  BB = Text.Append(A, B)

  BB = Text.Append(BB, C)

  If AA > BB Then

    GraphicsWindow.DrawText(10,60,"A L A R M")

    Sound.PlayClickAndWait()

    Sound.PlayBellRingAndWait()

    Sound.PlayClickAndWait()

    Sound.PlayChimeAndWait()

    Sound.PlayClickAndWait()

  EndIf 

EndSub 'CheckAlarm



Sub ReactionBtClk

  If Controls.LastClickedButton = B1 Then 'Set Wake Up Time

    Controls.HideControl(B1)

    Controls.ShowControl(TB)

    Controls.ShowControl(B3) 

  EndIf 

  If Controls.LastClickedButton = B2 Then 'Stop Alarm

    Controls.HideControl(B2)

    Controls.ShowControl(B1)

    Alarm = F 

  EndIf 

  If Controls.LastClickedButton = B3 Then 'Activate Alarm

    Controls.HideControl(B3)

    Controls.HideControl(TB)

    Controls.ShowControl(B2)

    Alarm = T 

  EndIf 

EndSub 'ReactionBtClk



Sub ShowClock

  ATime = Clock.Time 

  GraphicsWindow.BrushColor = "Yellow"

  GraphicsWindow.FillRectangle(0,0,ww, 90)

  GraphicsWindow.BrushColor = "Black" 

  GraphicsWindow.DrawText(10,10,"Actual Time")

  GraphicsWindow.DrawText(100,10,"Alarm Time")

  GraphicsWindow.DrawText(10,30,ATime)

  GraphicsWindow.DrawText(100,30,WUTime)

  If Alarm = T Then

    GraphicsWindow.DrawText(100,60,"Alarm is on")

  Else 

    GraphicsWindow.DrawText(100,60,"Alarm is off")

  EndIf 

  GraphicsWindow.BrushColor = br 

  Program.Delay(PD)

EndSub 'ShowClock



Sub Init

  ww = 200

  wh = 200

  wt = "Alarm Clock"

  br = "SteelBlue"

  ba = "White"

  GraphicsWindow.BrushColor = br

  GraphicsWindow.BackgroundColor = ba 

  GraphicsWindow.Width = ww

  GraphicsWindow.Height = wh 

  GraphicsWindow.Title = wt

  T = "True"

  F = "False" 

  PD = 100

  Alarm = F 

  B1 = Controls.AddButton("Set the wake up time", 10,170)

  Controls.HideControl(B1)

  B2 = Controls.AddButton("Stop Alarm", 10, 140)

  Controls.HideControl(B2)

  B3 = Controls.AddButton("Activate Alarm", 10, 140)

  Controls.HideControl(B3)

  TB = Controls.AddTextBox(10, 110)

  Controls.HideControl(TB)

  ATime = Clock.Time 

  WUTime = ATime

  Controls.SetTextBoxText(TB, WUTime) 

EndSub 'Init



Sub OnBtClk 

  BtClk = t 

EndSub 


