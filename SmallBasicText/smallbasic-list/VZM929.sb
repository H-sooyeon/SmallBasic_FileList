'  reverse  All to white   by NaochanON 



init()

GraphicsWindow.Mousemove=onmove

GraphicsWindow.MouseDown=ondown



Sub ondown

  Shapes.SetText(sXY," X= "+NX+ " ; Y=  "+NY+Text.GetCharacter(10)+"clicked")

  For i=1 To row

    For j=1 To clm

      If i=NY And j=NX Then 

        OnOffcheck()

      EndIf

      If j=NX and math.Abs(i-ny)=1 Then 

        OnOffcheck()

      EndIf

      If i=NY and math.Abs(j-nx)=1 Then 

        OnOffcheck()

      EndIf

    EndFor

  EndFor

  wincheck()

EndSub



Sub OnOffcheck

  If BOnOFF[i][j]=1 Then 

    BOnOFF[i][j]=0 

    Shapes.SetOpacity(B[i][j],0)

  Elseif  BOnOFF[i][j]=0 then 

    BOnOFF[i][j]=1 

    Shapes.SetOpacity(B[i][j],100)

  EndIf

EndSub



Sub onmove

  MX= GraphicsWindow.MouseX

  MY= GraphicsWindow.MouseY

  If x0<MX And MX<X0+clm*DR8 And y0<MY And MY<y0+row*DR8 Then 

    NX= Math.Floor((MX-X0)/DR8)+1

    NY= Math.Floor((MY-Y0)/DR8)+1

    Shapes.SetText(sXY," X= "+NX+ " ; Y=  "+NY)

  EndIf

EndSub



Sub wincheck

  s=0

  For I=1 To row

    For j=1 To clm

      s=s+BOnOFF[i][j]

    EndFor

  EndFor

  If s=0 Then 

    Shapes.Animate(swin,1000,800,3000)

    Sound.PlayBellRing()

  EndIf

EndSub



Sub init

  clm=2+math.GetRandomNumber(4)           '  column

  row=2+math.GetRandomNumber(3)           '  row 

  X0=50                                   '  start point

  Y0=100

  DR=100                                  '  circle  diamitar

  DR8=DR+8                                '  shift

  GraphicsWindow.BackgroundColor="Green"

  GraphicsWindow.Width=(clm+1)*DR8+X0

  GraphicsWindow.Height=(row+1)*DR8+Y0

  '-----------------  Message  ------------------

  GraphicsWindow.BrushColor="White"

  GraphicsWindow.PenColor="white"

  GraphicsWindow.FontSize=20

  smsg= Shapes.AddText("Click and change them all white!")

  Shapes.Move(smsg,50,10)

  sXY= Shapes.AddText(" X=   ; Y=   ")

  Shapes.Move(sXY,50,50)

  swin = Shapes.AddText(" You Win! ")

  Shapes.Move(swin,0,-100)

  '-----------------  white  ------------------

  For i=1 To row

    For J=1 To clm

      W[i][j]= Shapes.AddEllipse(DR,DR)

      X=X0+(j-1)*DR8

      Y=Y0+(i-1)*DR8

      Shapes.Move(w[i][j],X+4,Y+4)

    EndFor

  endfor

  '-----------------   black  ------------------

  GraphicsWindow.BrushColor="black"

  GraphicsWindow.PenColor="Black"

  For i=1 To row

    For J=1 To clm

      B[i][j]= Shapes.AddEllipse(DR,DR)

      X=X0+(j-1)*DR8

      Y=Y0+(i-1)*DR8

      Shapes.Move(B[i][j],X+4,Y+4)

      BOnOFF[i][j]=1 

    EndFor

  EndFor 

EndSub






