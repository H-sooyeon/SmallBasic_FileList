'The position where the arrow will be on the screen

GraphicsWindow.BackgroundColor = "Teal"

arrowX = GraphicsWindow.Width/2

arrowY = GraphicsWindow.Height/2



'Create arrow

'For a triangle or line that is moved and rotated best to create with top left at the origin (0,0), then center of rotation and movement is easier



'Arrow head is initially in box (0,0) to (20,20) - this is the region the rotation is based on so the center is at (10,10)

GraphicsWindow.BrushColor = "Red"

GraphicsWindow.PenWidth = 0

arrowHead = Shapes.AddTriangle(0,0,0,20,20,10)



'Arrow line is initially in box (0,0) to (50,8) - this is the region the rotation is based on so the center is at (25,4)

GraphicsWindow.PenColor = "Red"

GraphicsWindow.PenWidth = 8

arrowLine = Shapes.AddLine(0,4,50,4)



'A pin at the center of the arrow (center of rotation)

GraphicsWindow.PenWidth = 1

GraphicsWindow.PenColor = "Black"

Shapes.Move(Shapes.AddEllipse(6,6),arrowX-3,arrowY-3)



While ("True")

  moveArrow()

  Program.Delay(20)

EndWhile



Sub moveArrow

  'get the angle in radians between mouse and position of arrow, we have to account for which quadrant we are in

  dx = GraphicsWindow.MouseX-arrowX

  dy = GraphicsWindow.MouseY-arrowY

  If (dx = 0) Then

    angle = Math.Pi/2

    If (dy < 0) Then

      angle = angle+Math.Pi

    EndIf

  Else

    angle = Math.ArcTan(dy/dx)

    If (dx < 0) Then

      angle = angle+Math.Pi

    EndIf

  EndIf

  

  'Move arrow parts the required position, subract half width and heights to center on position

  'Also move arrow head to end of arrow line

  Shapes.Move(arrowHead,arrowX-10+25*Math.Cos(angle),arrowY-10+25*Math.Sin(angle))

  Shapes.Move(arrowLine,arrowX-25,arrowY-4)

  

  'Rotate each shape about their centers (rotation angle is in degrees)

  angle = angle*180/Math.Pi

  'GraphicsWindow.Title = angle

  Shapes.Rotate(arrowHead,angle)

  Shapes.Rotate(arrowLine,angle)

EndSub


