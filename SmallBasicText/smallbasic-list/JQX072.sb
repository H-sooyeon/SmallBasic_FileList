'  Cat's EYE  clock  ver2.0  by    NaochanON   

GW=500     '   Graphic window  size    400   -  1200   ã€€  you can   resize  cat's face 
INIT()
catface()
Program.Delay(2000)
cateye()

Timer.Tick=Cateye
Timer.Interval=5000
Timer.Resume()

Sub cateye
  SX= 15*(3-2*Math.GetRandomNumber(2))
  SY= 8*(3-2*Math.GetRandomNumber(2))
  X0=100+SX    '  SX...  shift  X
  Y0=50+SY     '  SY...  shift  Y
  ' ---------------------------------   cat's nose  ----------------------------------------------------
  Shapes.Animate(sNd,X0+(Ex[1]+11)*FS,y0+(Ey[1]+7)*FS,500)
  '  ---------------------------------   cat's EYE Pupil  ----------------------------------------------------
  PL= math.Abs(Clock.hour+Clock.Minute/60-12)/6+0.1    ' Pupil  ratio
  If PL>1.0 Then 
    PL=1.0
  EndIf
  For L=1 To 2
    Shapes.Animate(sEd[L],X0+Ex[L]*FS,y0+Ey[L]*FS,500)
    Shapes.Animate(sCd[L],X0+(Ex[L]+5-2.5)*FS,y0+Ey[L]*FS,500) 
    Shapes.Zoom(sCd[L],ZM*PL,ZM)
  EndFor  
  Program.Delay(500)
  ' ---------------------------------  --------------------------------- 
  flicker()
  '   ---------------------------------   Date &  Hour  ----------------------------------------------------
  Shapes.SetText(smsg,"           Cat's EYE Clock......       "+text.GetCharacter(10)+"Date & Hour=   "+Clock.Date+"   at " +Clock.Hour)
  Shapes.Animate(smsg,X0-50,Y0-20,500)
EndSub

Sub flicker
  For j=1 To 0.05 Step -0.05  
    For L=1 To 2
      Shapes.Zoom(sEd[L],1,j)
      Shapes.Zoom(sCd[L],ZM*PL,ZM*j)
      Program.Delay(10)
    EndFor
  EndFor
   Program.Delay(200)
  For j=0.05 To 1 Step 0.05  
    For L=1 To 2
      Shapes.Zoom(sEd[L],1,j)
      Shapes.Zoom(sCd[L],ZM*PL,ZM*j)
      Program.Delay(10)
    EndFor
  EndFor
EndSub  

Sub catface
  ' -------------------  cat's face  data  --------------------------------------------------------
  X="1=10;2=11;3=11;4=12;5=12;6=13;7=14;8=15;9=16;10=17;11=18;12=19;13=20;14=21;15=22;16=23;17=24;18=24;19=25;20=25;"
  X=X+"21=26;22=27;23=28;24=29;25=30;26=30;27=31;28=31;29=31;30=32;31=32;32=33;33=33;34=33;35=33;36=34;37=34;38=34;39=34;40=35;"
  X=X+"41=35;42=35;43=35;44=34;45=33;46=32;47=31;48=30;49=29;50=28;51=27;52=26;53=26;54=25;55=24;56=23;57=22;58=21;59=20;60=19;"
  X=X+"61=18;62=17;63=16;64=15;65=14;66=13;67=12;68=11;69=11;70=10;71=9;72=8;73=7;74=6;75=5;76=4;77=3;78=2;79=1;80=1;"
  X=X+"81=1;82=1;83=1;84=1;85=1;86=2;87=2;88=2;89=2;90=3;91=3;92=4;93=4;94=4;95=5;96=6;97=7;98=8;99=9;100=10"
  '
  Y="1=1;2=2;3=3;4=4;5=5;6=6;7=7;8=7;9=7;10=7;11=7;12=7;13=7;14=7;15=7;16=7;17=6;18=5;19=4;20=3;"
  Y=Y+"21=2;22=1;23=2;24=3;25=4;26=5;27=6;28=7;29=8;30=9;31=10;32=11;33=12;34=13;35=14;36=15;37=16;38=17;39=18;40=19;"
  Y=Y+"41=20;42=21;43=22;44=23;45=24;46=25;47=26;48=27;49=27;50=28;51=28;52=28;53=29;54=29;55=29;56=29;57=29;58=29;59=29;60=29;"
  Y=Y+"61=29;62=29;63=29;64=29;65=29;66=29;67=29;68=29;69=28;70=28;71=28;72=27;73=27;74=26;75=26;76=25;77=24;78=23;79=22;80=21;"
  Y=Y+"81=20;82=19;83=18;84=17;85=16;86=15;87=14;88=13;89=12;90=11;91=10;92=9;93=8;94=7;95=6;96=5;97=4;98=3;99=2;100=1"
  ' ---------------------------------   cat's face  ----------------------------------------------------
  GraphicsWindow.BrushColor="Darkgreen"
  GraphicsWindow.PenColor="Darkgreen"
  X0=100+SX    '  SX...  shift  X
  Y0=50+SY     '  SY...  shift  Y
  For i=1 to 100
    sd[i]= Shapes.AddRectangle(FS,FS)
    Shapes.move(sd[i],500+1000*math.Cos(math.GetRadians(Math.GetRandomNumber(360))),500+1000*math.sin(math.GetRadians(Math.GetRandomNumber(360))))
    Shapes.animate(sd[i],X0+x[i]*FS,y0+y[i]*FS,2000)        '   cat's  face
  endfor
  ' ---------------------------------   cat's whisker   ----------------------------------------------------
  GraphicsWindow.BrushColor="Darkgray"
  GraphicsWindow.PenColor="Darkgray"
  GraphicsWindow.PenWidth=5
  HX="1=4;2=32;3=5;4=32"
  HY="1=17;2=17;3=21;4=22"
  angl="1=185;2=-10;3=170;4=10"
  For j=1 To 4
    SHD[j]= Shapes.Addline(0,0,15*FS,0)
    dx= 15*FS*Math.Remainder(j,2)
    Shapes.animate(sHd[j],X0+Hx[j]*FS-dx,y0+Hy[j]*FS,2000)          '      cat's whisker
    Shapes.rotate(sHd[j],angl[j])
  EndFor
  ' ---------------------------------   cat's EYE  ----------------------------------------------------
  GraphicsWindow.BrushColor="khaki"
  GraphicsWindow.PenColor="khaki"
  EX="1=6;2=21"
  EY="1=14;2=14"
  For K=1 To 2
    SED[K]= Shapes.AddEllipse(10*FS,5*FS)
    Shapes.Animate(sEd[K],X0+Ex[K]*FS,y0+Ey[K]*FS,2000)             '  cat's EYE
  EndFor  
  ' ---------------------------------   cat's  nose  ----------------------------------------------------
  SND= Shapes.AddEllipse(2*FS,1*FS)
  Shapes.Animate(sNd,X0+(Ex[1]+11)*FS,y0+(Ey[1]+7)*FS,2000)
  '  ---------------------------------   cat's  EYE Pupil  ----------------------------------------------------
  GraphicsWindow.BrushColor="Orange"
  GraphicsWindow.PenColor="Orange" 
  For L=1 To 2
    SCD[L]= Shapes.AddEllipse(5*FS*WT,5*FS)
    Shapes.Animate(sCd[L],X0+(Ex[L]+5-2.5)*FS,y0+Ey[L]*FS,2000)       '  cat's  pupil
  EndFor  
  ' ---------------------------------  Date & Hour  ----------------------------------------------------
  GraphicsWindow.FontSize=16
  smsg= Shapes.AddText("           Cat's EYE Clock......       "+text.GetCharacter(10)+"Date & Hour=   "+Clock.Date+"   at " +Clock.Hour)
  Shapes.Move(smsg,X0,Y0-50)
endsub


Sub INIT
  GraphicsWindow.Title="Cat's EYE Clock...... "
  GraphicsWindow.Width=GW
  GraphicsWindow.Height=GW*3/4
  GraphicsWindow.Left=50
  GraphicsWindow.Top=20
  GraphicsWindow.BackgroundColor="Black"
  FS=15*GW/850    '  enlarge full scale 
  SX=0     '  initial  shift  X
  SY=0     '  initial   shift  Y
  WT=1     '  initial  full  width of EYE
  ZM=1     '  initial  zoom  ratio
  PL= 1    ' initial Pupil  ratio
EndSub




