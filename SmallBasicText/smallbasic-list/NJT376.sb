sum = 0
primes = ""
numPrimes = 0
count = 1000

start = Clock.ElapsedMilliseconds
For value = 2 To count '1 has no prime factors
  NextValue()
EndFor
end = Clock.ElapsedMilliseconds

Report()

Sub NextValue
  'TextWindow.WriteLine("value "+value)
  remain = value 'Working remainder as we divide by prime factors
  For i = 1 To numPrimes 'Search existing primes
    try = remain/primes[i]
    While (try = Math.Round(try)) 'This is a prime factor
      'TextWindow.WriteLine("factor "+primes[i])
      sum = sum+primes[i]
      remain = try
      try = remain/primes[i] 'We may have more than 1 factor so test again
    EndWhile
    If (try <= 1) Then 'Don't keep searching if we have found all factors - if the current prime test <= 1 then a larger prime cannot be a factor)
      i = numPrimes 'End loop
    EndIf
  EndFor
  If (remain > 1) Then 'We have a new prime (couldn't factor with existing primes)
    'TextWindow.WriteLine("factor "+remain)
    numPrimes = numPrimes+1
    primes[numPrimes] = remain
    sum = sum+remain
  EndIf
  'TextWindow.WriteLine("")
  'Program.Delay(1000)
EndSub

Sub Report
  TextWindow.Title = "Sum of prime factors"
  TextWindow.BackgroundColor = "White"
  TextWindow.Clear()
  TextWindow.ForegroundColor = "Black"
  TextWindow.Write("The sum of the first ")
  TextWindow.ForegroundColor = "Blue"
  TextWindow.Write(count)
  TextWindow.ForegroundColor = "Black"
  TextWindow.Write(" prime factors is ")
  TextWindow.ForegroundColor = "Red"
  TextWindow.Write(sum)
  TextWindow.ForegroundColor = "Black"
  TextWindow.Write(" found in ")
  TextWindow.ForegroundColor = "Red"
  TextWindow.Write(end-start)
  TextWindow.ForegroundColor = "Black"
  TextWindow.WriteLine(" ms")
  TextWindow.WriteLine("")
  TextWindow.WriteLine("The following prime numbers were found:")
  TextWindow.ForegroundColor = "DarkYellow"
  For i = 1 To numPrimes
    x = Math.Remainder(i-1,10)
    y = Math.Floor((i-1)/10)
    TextWindow.CursorLeft = (x+3)*5
    TextWindow.CursorTop = 4+y
    TextWindow.Write(primes[i])
  EndFor
  TextWindow.ForegroundColor = "Black"
  TextWindow.WriteLine("")
  TextWindow.WriteLine("")
EndSub
