' SmallBasic Version 1.0
' Program: Shapes ZIndex
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/bf34e37e-9e45-44c5-8ebf-e82ebc580c1b/ms-small-basic-images-layer-priority-oder-command-help-plz
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev (LDShapes)
' Comment: 
'  
' Variables: 
' ToDo: 
' ================================================================================
'zIdx = 0    '' all new created Shapes/Controls will have a default ZIndex of 0 at the beginning

' Create 5 Circles and 5 Squares, largest at the bottom, smallest on top of the Z layers
For n = 5 To 1 Step -1
  GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
  Shapes.AddEllipse(20*n, 20*n)
  GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
  Shapes.AddRectangle(20*n, 20*n)
EndFor


GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.MouseDown = OnMouseDown
GraphicsWindow.MouseUp = OnMouseUp


' //////////  SUBs  \\\\\\\\\\
Sub OnMouseMove         ' move selected Shape with Mouse, based on its anchor point
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
  If dragOn Then
    Shapes.Move(currShp, mx-xOffset, my-yOffset)
  EndIf
EndSub

Sub OnMouseDown
  allUnderMouse = LDShapes.GetAllShapesAt(mx, my)     ' which Shapes are under the mouse ?
  If allUnderMouse <> "False" Then                    ' is there any Shape selected at all ?
    currShp = allUnderMouse[1]                        ' yes, then take the upper one
    xOffset = mx - Shapes.GetLeft(currShp)            ' get the Shape's X anchor point from MouseX
    yOffset = my - Shapes.GetTop(currShp)             ' ... same for Y
    zIdx = zIdx+1                                     ' increase the ZIndex counter ...
    LDShapes.ZIndex(currShp, zIdx)                    ' and set it for this Shape (to be topmost)
    dragOn = "True"                                   ' then switch dragging On
  EndIf
EndSub

Sub OnMouseUp
  dragOn = ""                                         ' switch dragging Off
EndSub
  
