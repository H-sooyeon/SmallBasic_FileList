' Draws the Ms Pacman maze
' By Anthony Yarrell, February 2012

GraphicsWindow.Width  = 360
GraphicsWindow.Height = 400
'
  'Maze data structure. "O" are borders, "." are pellets, and "+" is walkable areas without pellets.
  M[1] =  "oooooooooooooooooooooooooooo"
  M[2] =  "o......oo..........oo......o"
  M[3] =  "o.oooo.oo.oooooooo.oo.oooo.o"
  M[4] =  "o.oooo.oo.oooooooo.oo.oooo.o"
  M[5] =  "o..........................o"
  M[6] =  "ooo.oo.ooooo.oo.ooooo.oo.ooo"
  M[7] =  "ooo.oo.ooooo.oo.ooooo.oo.ooo"
  M[8] =  "ooo.oo.ooooo.oo.ooooo.oo.ooo"
  M[9] =  "+++.oo.......oo.......oo.+++"
  M[10] = "ooo.ooooo+oooooooo+ooooo.ooo"
  M[11] = "++o.ooooo+oooooooo+ooooo.o++"
  M[12] = "++o.++++++++++++++++++++.o++"
  M[13] = "++o.ooooo+oooooooo+ooooo.o++"
  M[14] = "++o.ooooo+o++++++o+ooooo.o++"
  M[15] = "++o.oo++++o++++++o++++oo.o++"
  M[16] = "++o.oo+oo+o++++++o+oo+oo.o++"
  M[17] = "ooo.oo+oo+oooooooo+oo+oo.ooo"
  M[18] = "+++.+++oo++++++++++oo+++.+++"
  M[19] = "ooo.oooooooo+oo+oooooooo.ooo"
  M[20] = "++o.oooooooo+oo+oooooooo.o++"
  M[21] = "++o.......+++oo+++.......o++"
  M[22] = "++o.ooooo.oooooooo.ooooo.o++"
  M[23] = "ooo.ooooo.oooooooo.ooooo.ooo"
  M[24] = "o..........................o"
  M[25] = "o.oooo.ooooo.oo.ooooo.oooo.o"
  M[26] = "o.oooo.ooooo.oo.ooooo.oooo.o"
  M[27] = "o.oooo.oo....oo....oo.oooo.o"
  M[28] = "o.oooo.oo.oooooooo.oo.oooo.o"
  M[29] = "o.oooo.oo.oooooooo.oo.oooo.o"
  M[30] = "o..........................o"
  M[31] = "oooooooooooooooooooooooooooo"

  mazeRows = Array.GetItemCount(M)  'Get total rows in maze
  mazeCols = Text.GetLength(M[1])   'Get total columns in maze
  mazeScreenX = 0
  mazeScreenY = 0
  scale = 10                        'Size of the maze on screen.
  borderSize = 1.9                  'Sets thickness of borders (best at 1.9)
  pelletSize = 2                    'Size of pellets in pixels
  mazeColor = "pink"                'Color of maze borders
  offset = scale - pelletSize       'Offset to center pellets in maze
  
  'Sets background color out of which the maze pattern will be carved.
  GraphicsWindow.BackgroundColor  = "black"
  GraphicsWindow.Brushcolor       = mazeColor
  GraphicsWindow.FillRectangle(mazeScreenX+scale+7, mazeScreenY+scale+7, scale*mazeCols, scale*mazeRows)  
  
  For rows = 1 To mazeRows
    For cols = 1 To mazeCols
      mazeArray[rows][cols] = Text.GetSubText(M[rows], cols, 1) ''Load the Maze_Array [] with the data above.
      mazeData = mazeArray[rows][cols]
      
      If (mazeData <> "o") Then       'Carve the border patten out of the background color
        GraphicsWindow.BrushColor="black"
        GraphicsWindow.FillRectangle(mazeScreenX+(scale*cols), mazeScreenY+(scale*rows), (scale*borderSize), (scale*borderSize))
      EndIf
      
      If (mazeData = ".") Then       'add the pellets
        GraphicsWindow.BrushColor="white"
        GraphicsWindow.FillRectangle(mazeScreenX+(scale*cols) + offset,mazeScreenY+(scale*rows) + offset, pelletSize, pelletSize)
      EndIf
      
    EndFor
  EndFor
