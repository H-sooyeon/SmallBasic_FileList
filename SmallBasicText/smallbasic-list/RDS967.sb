'======= make random file ===========

'  WhTurner 141209

'Challenge october 2014;  community suggestion 1 by Pappa Lapub

'===============================

FO=Program.Directory+"\Ranfile2.txt"

''FO=""     '   uncomment for no file-output

TextWindow.WriteLine("")

TextWindow.WriteLine("GENERATION OF A FILE WITH RANDOM CONTENTS")

TextWindow.WriteLine("")

TextWindow.WriteLine("Choise of format")

TextWindow.WriteLine("1 - Continuous stream of characters")

TextWindow.WriteLine("2 - Lines with continouous stream of characters")

TextWindow.WriteLine("3 - Lines with random 'words' separated by space")

TextWindow.Write("       Give number of choice ")

C1=TextWindow.ReadNumber()

TextWindow.WriteLine("")

TextWindow.WriteLine("Choice of characters")

TextWindow.WriteLine("1   - lower case alphabetical")

TextWindow.WriteLine("10  - UPPER CASE alphabetical")

TextWindow.WriteLine("100 - numerical")

TextWindow.Write("    Give number, or sum for combinations e.g. 101 : ")

C2=TextWindow.ReadNumber()



cho=""

If Math.Remainder(C2,10)=1 Then

  cho=cho+"abcdefghijklmnopqrstuvwxyz"

EndIf

If Math.Remainder(C2,100)=10 Or Math.Remainder(C2,100)=11 Then

  cho=cho+"ABCDEFGHIJKLMNOPQRSTUVWXYZ"

EndIf

If C2>99 Then

  cho=cho+"1234567890"

EndIf

choL=Text.GetLength(cho)



If C1=1 Then

  TextWindow.Write("Give number of characters ")

  N1=TextWindow.ReadNumber()

ElseIf C1=2 then

  TextWindow.Write("Give number of lines ")

  N2= TextWindow.ReadNumber()

  TextWindow.Write("Give length of line, range for random e.g. 40-60 : ")

  N3= TextWindow.Read()

elseif C1=3 then

  TextWindow.Write("Give number of lines ")

  N2= TextWindow.ReadNumber()

  TextWindow.Write("Give number of words, range for random e.g. 6-9 : ")

  N4= TextWindow.Read()

  TextWindow.Write("Give length of words, range for random e.g. 5-12 : ")

  N5= TextWindow.Read()

Else

EndIf



out=""

If C1=1 Then

  For i=1 To N1

    out=Text.Append(out,Text.GetSubText(cho,Math.GetRandomNumber(choL),1))

  EndFor

  TextWindow.WriteLine(out)

  Output()

ElseIf C1=2 then

  If Text.IsSubText(n3,"-") then

    posmin=Text.GetIndexOf(N3,"-")

    n31=0+Text.GetSubText(N3,1,posmin-1)

    n32=Text.GetSubTextToEnd(N3,posmin+1)

  Else

    n31=n3

    n32=n3

  EndIf

  For i=1 to N2

    ''length

    N3=N31+Math.GetRandomNumber(1+n32-n31)-1

    For j=1 to N3

      out=Text.Append(out,Text.GetSubText(cho,Math.GetRandomNumber(choL),1))

    endfor

    TextWindow.WriteLine(out)

    Output()

    out=""

  endfor

ElseIf C1=3 then

  If Text.IsSubText(n4,"-") then

    posmin=Text.GetIndexOf(N4,"-")

    n41=0+Text.GetSubText(N4,1,posmin-1)

    n42=Text.GetSubTextToEnd(N4,posmin+1)

  Else

    n41=n4

    n42=n4

  EndIf

  If Text.IsSubText(n5,"-") then

    posmin=Text.GetIndexOf(N5,"-")

    n51=0+Text.GetSubText(N5,1,posmin-1)

    n52=Text.GetSubTextToEnd(N5,posmin+1)

  Else

    n51=n5

    n52=n5

  EndIf

  For i=1 to N2  'lines

    N4=N41+Math.GetRandomNumber(1+n42-n41)-1

    For j=1 to N4  'words

      N5=N51+Math.GetRandomNumber(1+n52-n51)-1

      For k=1 to N5  'chars

        out=Text.Append(out,Text.GetSubText(cho,Math.GetRandomNumber(choL),1))

      endfor

      If j<N4 then

        out=out+" "

      endif      

    endfor

    TextWindow.WriteLine(out)

    Output()

    out=""

  endfor

EndIf



Sub Output

  If FO<>"" then

    If C1=1 then

' The following line could be harmful and has been automatically commented.

'       File.WriteContents(FO,out)  

    Else

' The following line could be harmful and has been automatically commented.

'       File.AppendContents(FO,out)

    EndIf

endif

EndSub


