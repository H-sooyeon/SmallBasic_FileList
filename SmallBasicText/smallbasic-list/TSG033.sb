'FullScreen
gw = Desktop.Width-16
gh = Desktop.Height-100
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.Top = 0
GraphicsWindow.Left = 0
GraphicsWindow.CanResize = "False"
Mouse.HideCursor()

'Download and store song
GraphicsWindow.FontSize = 30
GraphicsWindow.DrawText(gw/4,gh/2,"Loading...")
song = Network.DownloadFile("http://public.bay.livefilestore.com/y1p5GYYDcXDW4uapszSHoVFEV-YWGyyS96QfB6pvm4ocEfF7okBFSggIlA2Uo8CFmY29qbolxizxFWBVvcFTaLNrw/02%20Denis.wma?download&psid=1")
' The following line could be harmful and has been automatically commented.
' File.CopyFile(song,Program.Directory+"\Denis.wma")
song = Program.Directory+"\Denis.wma"
GraphicsWindow.Clear()

'Parameters for visuals
GraphicsWindow.PenWidth = 4
rad = 80
diam = 2*rad
speed = 1.125

'To get the play time of a file use the following extension
'http://cid-92f7df0e57232aba.office.live.com/self.aspx/.Public/SmallBasic/C%5EN%20Extensions/Music%5E_v0.9.zip
' The following line could be harmful and has been automatically commented.
' 'playTime = MusicFile.PlayTime(song)*1000
playTime = 135*1000
reStartSong()
GraphicsWindow.MouseDown = OnMouseDown 'Pause and restart with mouse click

While "True"
  currentTime = Clock.ElapsedMilliseconds 'This is used several times so get it only once
  intervalTime = currentTime - startTime
  If (clicked = 1) Then
    clicked = 0
    If (isPlaying = 1) Then
      pauseSong()
    Else
      startSong()
    EndIf
  EndIf
  If (isPlaying = 1) Then
    playingTime = playingTotalTime + intervalTime
    If (playingTime > playTime) Then
      reStartSong()
    EndIf
    updateDot()
    GraphicsWindow.Title = 0.1*Math.Floor(playingTime/100)+" / "+playTime/1000
  EndIf
  delay = 10-(Clock.ElapsedMilliseconds-currentTime)
  If (delay > 0) Then
    Program.Delay(delay)
  EndIf
EndWhile

Sub startSong
  Sound.Play(song)
  isPlaying = 1
  startTime = clock.ElapsedMilliseconds
EndSub

Sub reStartSong
  Program.Delay(100)
  Sound.Stop(song)
  Program.Delay(100)
  Sound.Play(song)
  isPlaying = 1
  startTime = clock.ElapsedMilliseconds
  playingTime = 0
  playingTotalTime = 0
  createDot()
EndSub

Sub pauseSong
  Sound.Pause(song)
  isPlaying = 0
  playingTotalTime = playingTime
EndSub

Sub OnMouseDown
  clicked = 1
EndSub

Sub updateDot
  size = size*speed
  Shapes.Zoom(dot,size,size)
  Shapes.SetOpacity(dot,100*(Math.Log(size)+1)/2)
  'Shapes.SetOpacity(dot,size*10)
  If (size > 10) Then
    createDot()
  EndIf
EndSub

Sub createDot
  GraphicsWindow.BackgroundColor = GraphicsWindow.BrushColor
  GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
  Shapes.Remove(dot)
  size = 0.1
  x = gw/8+Math.GetRandomNumber(gw*3/4)-rad
  y = gh/8+Math.GetRandomNumber(gh*3/4)-rad
  'x = GraphicsWindow.MouseX-rad
  'y = GraphicsWindow.MouseY-rad
  dot = Shapes.AddEllipse(diam,diam)
  Shapes.Move(dot,x,y)
  Shapes.Zoom(dot,size,size)
  Shapes.SetOpacity(dot,0)
EndSub
