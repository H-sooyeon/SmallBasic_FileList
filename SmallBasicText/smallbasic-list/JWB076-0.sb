' screensize as you like



gwWIDTH = 640

gwHEIGHT = 480



GraphicsWindow.Width = gwWIDTH

GraphicsWindow.Height = gwHEIGHT

GraphicsWindow.BackgroundColor ="black"

GraphicsWindow.Show()



'---------------------------------------

' leave that alone

If gwHEIGHT < gwWIDTH Then

  fsizeY = gwHEIGHT * 0.25

  fsizeX = fsizeY

Else

  fsizeY = gwWIDTH * 0.25

  fsizeX = fsizeY

EndIf 

zerofX =gwWIDTH * 0.5 

zerofY = 0

'---------------------------------------



DEPTHMAX = 255  ''low number=fast calculate, high number= needs longer but more precisely 

X_MIN = -0.79      ' free to try

X_MAX = 2.01      ' change if you want

Y_MIN = -1.42     ' play with it

Y_MAX = Y_MIN + (X_MAX - X_MIN) * fsizeY/fsizeX





' colour front and side

useColF  = GraphicsWindow.GetColorFromRGB(160, 160, 120 )

useColS  = GraphicsWindow.GetColorFromRGB(120, 120 , 160 )



' you might setup your own colour-table instead of random colours

myCol[0] = "black"

For i1 = 1 To 31

  myCol[i1] = GraphicsWindow.GetRandomColor()  

EndFor



'start drawing

deltaX = (X_MAX - X_MIN)/ fsizeX

deltaY = (Y_MAX - Y_MIN)/fsizeY

realVal = X_MIN

imagineVal = Y_MIN



' activate time counter

Timer.Tick = Counter

Timer.Interval = 1000



For iY = 0 to fsizeY  

  For iX = 0 To fsizeX   

    iterate = 0 

    xVal = 0    

    yVal = 0

    Xlen = 0

    Ylen = 0

    While (iterate < DEPTHMAX) And (Xlen + Ylen < 8 )

      yVal = 2 * xVal * yVal - imagineVal

      xVal = Xlen - Ylen - realVal      

      Xlen =  math.Power(xVal,2)

      Ylen = Math.Power(Yval,2)

      iterate = iterate + 1

    EndWhile

    If iterate >= DEPTHMAX Then

      fHeight = 0

    Else      ' since there's no MOD...

      fHeight = iterate -  (31 * Math.Floor( iterate/31))

    EndIf

    realVal = realVal + deltaX 

    

    

    If fHeight > 0 Then

      For iHeight = -32  to -fHeight   

        

        dispX = zerofX + 2 * iX  - 2 *  iY 

        dispY =  zerofY + 2 * iY + 2 * iX - iHeight 

        

        GraphicsWindow.SetPixel(dispX, dispY+4, useColF)

        GraphicsWindow.SetPixel(dispX+1, dispY+4, useColS)

        

        GraphicsWindow.SetPixel(dispX-1, dispY+3, useColF)

        GraphicsWindow.SetPixel(dispX, dispY+3, useColF)

        GraphicsWindow.SetPixel(dispX+ 1, dispY+3, useColS)

        GraphicsWindow.SetPixel(dispX +2, dispY+3, useColS)

        

        GraphicsWindow.SetPixel(dispX-1, dispY+2, useColF)

        GraphicsWindow.SetPixel(dispX, dispY+2, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX+ 1, dispY+2, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX +2, dispY+2, useColS)

        

        GraphicsWindow.SetPixel(dispX-1, dispY+1, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX, dispY+1, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX+ 1, dispY+1, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX +2, dispY+1, myCol[fHeight])

        

        GraphicsWindow.SetPixel(dispX, dispY, myCol[fHeight])

        GraphicsWindow.SetPixel(dispX+1, dispY, myCol[fHeight])



      EndFor

    EndIf

    

    

  EndFor

  realVal = X_MIN

  imagineVal = imagineVal + deltaY 

EndFor



' stop timer

Timer.Pause()



While GraphicsWindow.LastKey <>"Escape"

endwhile

Program.End()



Sub Counter

  

  elapsed = elapsed + 1

  GraphicsWindow.Title = "Mandelbrot 3D    ?вт?вт??    Elapsed: " + elapsed + " s"

  

EndSub


