'Rotating 3D cube
'I owe the maths to this page: http://codentronix.com/2011/05/24/rotating-wireframe-cube-using-vb-net-and-gdi/

'show window'
size=0.5
center=250
GraphicsWindow.Show()
GraphicsWindow.Height=500
GraphicsWindow.Width=500

'define cube corners, based on distance from centre point
point3d[1]["x"]=-100
point3d[1]["y"]=-100
point3d[1]["z"]=100
point3d[2]["x"]=100
point3d[2]["y"]=-100
point3d[2]["z"]=100
point3d[3]["x"]=-100
point3d[3]["y"]=100
point3d[3]["z"]=100
point3d[4]["x"]=100
point3d[4]["y"]=100
point3d[4]["z"]=100
point3d[5]["x"]=-100
point3d[5]["y"]=-100
point3d[5]["z"]=-100
point3d[6]["x"]=100
point3d[6]["y"]=-100
point3d[6]["z"]=-100
point3d[7]["x"]=-100
point3d[7]["y"]=100
point3d[7]["z"]=-100
point3d[8]["x"]=100
point3d[8]["y"]=100
point3d[8]["z"]=-100
'define edges between end points
edge[1][1]=1
edge[1][2]=2
edge[2][1]=2
edge[2][2]=4
edge[3][1]=1
edge[3][2]=3
edge[4][1]=3
edge[4][2]=4
edge[5][1]=1
edge[5][2]=5
edge[6][1]=2
edge[6][2]=6
edge[7][1]=4
edge[7][2]=8
edge[8][1]=3
edge[8][2]=7
edge[9][1]=5
edge[9][2]=6
edge[10][1]=6
edge[10][2]=8
edge[11][1]=7
edge[11][2]=8
edge[12][1]=7
edge[12][2]=5

factor=600

For i = 1 to 5000
  Program.Delay(10)
  For pointno = 1 to 8
    rotatez()
    rotatex()
  endfor  
  convert3dto2d()
  draw2d()
EndFor  

Sub convert3dto2d
  For points = 1 To 8
    point2d[points]["x"] = point3d[points]["x"] + point3d[points]["x"] *(point3d[points]["z"]/factor)
    point2d[points]["y"] = point3d[points]["y"]  + point3d[points]["y"] *(point3d[points]["z"]/factor)
  EndFor
EndSub

Sub rotatez
  rad =  1 * Math.Pi/180
  cosa = Math.Cos(rad)
  sina = Math.Sin(rad)
  newx= point3d[pointno]["x"] * cosa - point3d[pointno]["y"] * sina
  newy = point3d[pointno]["x"] * sina + point3d[pointno]["y"] * cosa
  point3d[pointno]["x"] = (newx)
  point3d[pointno]["y"] = (newy)
EndSub

Sub rotatex
  rad =  1 * Math.Pi/180
  cosa = Math.Cos(rad)
  sina = Math.Sin(rad)
  newy = point3d[pointno]["y"] * cosa - point3d[pointno]["z"] * sina
  newz = point3d[pointno]["y"] * sina + point3d[pointno]["z"] * cosa
  point3d[pointno]["y"] = newy
  point3d[pointno]["z"] = newz
EndSub

Sub draw2d
  GraphicsWindow.Clear()
  For line = 1 To 12
    GraphicsWindow.DrawLine(center+point2d[edge[line][1]]["x"]*size,center+point2d[edge[line][1]]["y"]*size,center+point2d[edge[line][2]]["x"]*size,center+point2d[edge[line][2]]["y"]*size)
  endfor
EndSub
