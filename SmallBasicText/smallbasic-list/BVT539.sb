'    Painting Program    By   NaochanON      2012/04/12  

'   this program requires      DATA  EXTENSION    

'  If   "Save" ,  cropped screen shot is saved in your program.directory  as named automatically      



  Init_View()

  Pen3()               '  gets pen Icon

  Eraser3()            '  gets  eraser Icon 

  color_Palette()      '  make palette 

  OtherButton()        '  add     Flickr   Album  Save  Clear   buttons  

    

  GraphicsWindow.MouseDown=onmousedown

  GraphicsWindow.MouseMove=onmousemove

  Controls.ButtonClicked=Onbutton  

   

Sub onmousemove

  MX2= GraphicsWindow.MouseX

  MY2= GraphicsWindow.MouseY

  If MX2<75 Then 

   Shapes.hideShape(seraser0)            '  hide   pen and eraser

   Shapes.HideShape(spen0)

  endif

  If (110<MY2 and 75<MX2 ) Then 

    If ER=1 Then 

      GraphicsWindow.PenColor="White"

      GraphicsWindow.BrushColor="white"

      PESIZE=ersize

      Shapes.ShowShape(seraser0)        '   show   Eraser

      Shapes.HideShape(spen0)

    Else   

      GraphicsWindow.PenColor=get_color

      GraphicsWindow.BrushColor=get_color

      PESIZE=pensize

      Shapes.ShowShape(spen0)           ' show   Pen 

      Shapes.HideShape(seraser0)

    endif

      GraphicsWindow.PenWidth =2

      Shapes.Move(spen0,MX2-5,MY2-50)

      Shapes.Move(seraser0,MX2-40,MY2-55)

      If Mouse.IsLeftButtonDown Then 

        GraphicsWindow.FillEllipse(MX2,MY2,PESIZE*2,PESIZE*2)  '  draw  or  erase 

        MX=MX2

        MY=MY2

      endif  

  EndIf

EndSub



Sub onmousedown

  MX= GraphicsWindow.MouseX

  MY= GraphicsWindow.MouseY

   If (MY<70 And 450<MX And MX<560) Then      '  Pen Size  Down  Up 

     Pen_Up_Down()

   elseIf (MY<70 And 600<MX And MX<750) Then  ' Eraser Size Down  Up

     Eraser_Up_Down()

   EndIf

EndSub

   

Sub Onbutton        

  nm=controls.GetButtonCaption(Controls.LastClickedButton) 

  If text.IsSubText(nm,"?–²") or text.IsSubText(nm,"?–¼") Then  '  Color  Number  Up  or Down 

    change_Color()

    color_Name()

  elseif nm="Flickr" then     '   gets  a Photo  from  Flickr

    getFLKR()

  elseif nm="Album" then      '   gets  a Photo  from  your  Album File 

    getAlbum()

  elseif nm="Save" then       '  save  cropped screen at  Program.directory 

    PhotoSave()

  elseif nm="Clear" then      '  clear  view 

    AreaClear()

  EndIf

Endsub

   

Sub getFLKR   

  FN=""

  FLKN=Flickr.GetRandomPicture("Flower")     '   gets  a Photo  from  Flickr

  GraphicsWindow.Title=FLKN

  PIC= imagelist.LoadImage(FLKN)

  GraphicsWindow.DrawResizedImage(PIC,76,106,900-75,600-105)

EndSub



Sub getAlbum

  FLKN=""

  FN=Dialogs.AskForFile("*.jpg *.PNG")  '   gets  a Photo  from  your  Album File  '  <---     Data Extension required 

  GraphicsWindow.Title=FN

  PHOT=  imagelist.LoadImage(FN)  

  GraphicsWindow.DrawResizedImage(PHOT,76,106,900-75,600-105)

EndSub



Sub AreaClear

    GraphicsWindow.BrushColor="White"

    GraphicsWindow.FillRectangle(76,106,900-75,600-105)

EndSub



Sub PhotoSave

    Crop=PictureTools.TakeScreenShot(GL+MW+3,Desktop.Width-GL-GW,GT+MH+30,Desktop.Height-GH-GT-30) '  <---     Data Extension

    NN=NN+1

    If FN="" and FLKN<>"" Then 

      FN2= Program.Directory+"\"+FilePlus.GetFileName(FLKN)+"_"+NN+fileplus.GetFileExtension(FLKN) '  <---     Data Extension

    ElseIf FN<>"" and FLKN="" then  

      FN2= Program.Directory+"\"+FilePlus.GetFileName(FN)+"_"+NN+fileplus.GetFileExtension(FN) '  <---     Data Extension

    ElseIf FN="" and FLKN="" then  

      FN2= Program.Directory+"\Paint_"+NN+".jpg"

    endif

    PictureTools.SaveImage(Crop,FN2)                          '  <---     Data Extension

EndSub



Sub color_Palette

    GraphicsWindow.FontSize=13

    mark="1=R?–².;2=R.?–²;3=G?–².;4=G.?–²;5=B?–².;6=B.?–²;7=R?–¼.;8=R.?–¼;9=G?–¼.;10=G.?–¼;11=B?–¼.;12=B.?–¼"  '  button name

    base="0000FF0000"

    For i= 1 To 3    '  color number  up      mark=1  to 6 

      GraphicsWindow.BrushColor="#"+Text.GetSubText(base,5-2*(i-1),6)    ' Red;#FF0000,  Green;#00FF00,  Blue;#0000FF

      btnNum=2*(i-1)+1

      btn[btnNum]=Controls.AddButton(mark[btnNum],10+50*(btnnum-1),30)

      btn[btnNum+1]=Controls.AddButton(mark[btnNum],10+50*(btnnum),30)

    EndFor

    For i= 1 To 3    '  color number down      mark=7  to 12 

      GraphicsWindow.BrushColor="#"+Text.GetSubText(base,5-2*(i-1),6)    ' Red;#FF0000,  Green;#00FF00,  Blue;#0000FF

      btnNum=2*(i-1)+1+6

      btn[btnNum]=Controls.AddButton(mark[btnNum],10+50*(btnnum-1-6),60)

      btn[btnNum+1]=Controls.AddButton(mark[btnNum],10+50*(btnnum-6),60)

    EndFor

    GraphicsWindow.BrushColor=get_color

    color_Name()

EndSub



Sub change_Color

   If text.IsSubText(nm,"?–²") Then 

      cnum=text.GetSubTextToEnd(controls.LastClickedButton,7)  '   Button6   --> cnum= 6

      r[cnum]=r[cnum]+1

      If r[cnum]>16 Then 

         r[cnum]=16

      EndIf

   ElseIf text.IsSubText(nm,"?–¼") then 

      cnum=text.GetSubTextToEnd(controls.LastClickedButton,7)-6

      r[cnum]=r[cnum]-1

      If r[cnum]<1 Then 

         r[cnum]=1

      EndIf

   EndIf

      get_color=Text.GetSubText(get_color,1,cnum)+Text.GetSubText(HexName,r[cnum],1)+Text.GetSubTextToEnd(get_color,cnum+2)'  color code

EndSub   



Sub color_name

    GraphicsWindow.BrushColor="Red"

    GraphicsWindow.FontSize=15

    Shapes.Remove(sgc)

    sgc=Shapes.AddText(get_color)               '   color name Hexa

    shapes.Move(sgc,305,5)

    GraphicsWindow.BrushColor= get_color

    GraphicsWindow.FillRectangle(310,30,60,60)  '  color Box

EndSub



Sub Pen_Up_Down

  If MX>515 Then 

    pensize=pensize+1

    If pensize>15 then

      pensize=15

    EndIf

  ElseIf MX<515 then 

    Pensize=Pensize-1

    If pensize<1 then

      pensize=1

    EndIf

  EndIf

  Shapes.setText(spsize,pensize)

  ER=0

EndSub



Sub Eraser_Up_Down

  If MX>675 Then 

    ERsize=ERsize+1

    If ERsize>15 then

      ERsize=15

    EndIf

  ElseIf MX<675 then 

    ERsize=ERsize-1

    If ERsize<1 then

      ERsize=1

    EndIf

  EndIf

  Shapes.setText(sERAsize,ERsize)

  ER=1

EndSub



Sub Init_View

     GL=150

     GT=50

     GW=900

     GH=600

     MH=105

     MW=75

     GraphicsWindow.Left=GL

     GraphicsWindow.Top=GT

     GraphicsWindow.Width=GW

     GraphicsWindow.Height=GH

     GraphicsWindow.BackgroundColor="White"

     GraphicsWindow.Show() 

     GraphicsWindow.BrushColor="LightCyan"

     GraphicsWindow.FillRectangle(0,0,900,MH)   '   Pen Eraser Color select area

     GraphicsWindow.FillRectangle(0,0,MW,600)

     GraphicsWindow.BrushColor="Red"

     GraphicsWindow.FontSize=15

     GraphicsWindow.DrawText(30,5,"Push and change color") 

     HexName="0123456789ABCDEF"   '  Hex number

     get_color="#000FFF"          '  initial color

     For i=1 To 3

       r[i]=1                     '  set   initial color number

       r[i+3]=16

     EndFor

     Pensize=4    '  initial pensize  

     ERsize=6     ' initial erasersize   

     spsize=Shapes.AddText(pensize)

     Shapes.Move(spsize,470,75)

     sERAsize=Shapes.AddText(ERsize)

     Shapes.Move(sERAsize,660,75)

     GraphicsWindow.BrushColor="Navy"

     FN=""                          '  Initial FileName for save   

     FLKN=""                        '  Initial Flickr Name for save    

EndSub

  

Sub Pen3  

    url2="http://www.nesoup.com/free_1/material_73_1_s.png"  '  Pen   Free material

    pen = ImageList.LoadImage(url2)

    spen0=Shapes.AddImage(pen)

    Shapes.Move(spen0,450,10)              ' for  pen writing  

    Shapes.Zoom(spen0,0.5,0.5)            

    spen1=Shapes.AddImage(pen)

    Shapes.Move(spen1,450,10)

    Shapes.Zoom(spen1,0.5,0.5)             '   let  small

    spen2=Shapes.AddImage(pen)

    Shapes.Move(spen2,520,10)              '  let large

    GraphicsWindow.DrawRectangle(460,70,40,30)   ' size area

Endsub  

     

Sub Eraser3 

    url1="http://chiru.eco.to/images/symbol/082_044_blue.gif"  'Eraser   Free material

    eraser= ImageList.LoadImage(url1)

    seraser0=Shapes.AddImage(eraser)      '  for   erase  

    Shapes.Zoom(seraser0,0.25,0.25)       

    Shapes.Move(seraser0,600,0)

    seraser1=Shapes.AddImage(eraser)

    Shapes.Zoom(seraser1,0.25,0.25)       '  let small

    Shapes.Move(seraser1,600,0)

    seraser2=Shapes.AddImage(eraser)

    Shapes.Zoom(seraser2,0.8,0.8)         '  let larg

    Shapes.Move(seraser2,680,0)

    GraphicsWindow.DrawRectangle(650,70,40,30)   '  size area

EndSub



Sub otherbutton

     FLKR= Controls.AddButton("Flickr",5,120)

     Controls.SetSize(FLKR,60,40)

     ALBM= Controls.AddButton("Album",5,200)

     Controls.SetSize(ALBM,60,40)

     CLR=Controls.AddButton("Clear",5,280)

     Controls.SetSize(CLR,60,40)

     SAV=Controls.AddButton("Save",5,360)

     Controls.SetSize(SAV,60,40)

EndSub




