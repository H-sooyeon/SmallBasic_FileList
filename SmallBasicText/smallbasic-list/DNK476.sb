'Initial Prototype 1 -- by Matthew L. Parets aka codingCat
'High score initial entery system. This version
'is an in place loop, essentially halting all
'other operations

GraphicsWindow.Show()
GraphicsWindow.BackgroundColor = "black"
exiting = "False"
GraphicsWindow.KeyDown = OnKeyDown

InitialEntryBuild()
InitialEntryShow()
EnterInitials()
InitialEnteryHide()


Sub EnterInitials
  'Prompt for initials when a high score has been earned
  If exiting <> "True" then
    initials = ""
    
    GraphicsWindow.FontBold = "True"
    GraphicsWindow.FontSize = 70
    
    cur = 1
    pressed = "False"
    pos = 0
    InitEntry = "true"
    While InitEntry = "True"
      Shapes.HideShape(cursor[cur])
      cur = cur + 1
      If cur > Array.GetItemCount(cursor) Then
        cur = 1
      EndIf
      
      If pressed = "True" Then 
        key = GraphicsWindow.LastKey
        If key = "Return" then
          InitEntry = "False"
        EndIf
        If key = "Space" then
          key = " "
        endif
        If Text.GetLength(key) = 2 and Text.GetSubText(key,1,1) = "D" then
          key = Text.GetSubTextToEnd(key,2)
        endif
        If (key = "Back" Or key = "Left") and pos > 0 then
          Shapes.HideShape(initialsShapes[pos])
          Shapes.HideShape(initialsShadows[pos])
          initials = Text.GetSubText(initials,1,Text.GetLength(initials)-1)
          pos = pos - 1
        ElseIf pos < 3 then
          If Text.GetLength(key) = 1 then
            keycode = Text.GetCharacterCode(key)
            If keycode >= 97 And keycode <= 122 then
              keycode = keycode - 32
            EndIf
            If keycode = 32 or (keycode >= 65 and keycode <= 90) or (keycode >= 48 and keycode < 57) then
              key = Text.GetCharacter(keycode)
              initials = initials + key
              GraphicsWindow.BrushColor = "darkgray"
              initialsShadows[pos+1] = Shapes.AddText(key)
              GraphicsWindow.BrushColor = "white"
              initialsShapes[pos+1] = Shapes.AddText(key)
              Shapes.Move(initialsShapes[pos+1],promptx + (((curWid + 5) * pos+1)) + 2,prompty)
              Shapes.Move(initialsShadows[pos+1],promptx + (((curWid + 5) * pos+1)) + 2 + 2,prompty + 2)
              pos = pos + 1
            endif
          EndIf
        endif
        pressed = "False"
      EndIf
      
      Shapes.Move(cursor[cur],promptx + ((curWid + 5) * pos),prompty+5)
      Shapes.ShowShape(cursor[cur])
      
      Program.Delay(50)
    EndWhile
    For ii = 1 To Text.GetLength(initials)
      Shapes.HideShape(initialsShapes[ii])
      Shapes.HideShape(initialsShadows[ii])
    EndFor
    For ii = 1 To Array.GetItemCount(cursor)
      Shapes.HideShape(cursor[ii])
    EndFor    
  EndIf
endsub

Sub InitialEntryBuild
  curWid = 50
  curHei = 65
  cur = 1 
  pos = 1
  
  promptx = (GraphicsWindow.Width / 2) - (((curWid + 5) * 3) / 2)
  prompty = (GraphicsWindow.Height / 2) - 0
  GraphicsWindow.FontName = "Tahoma"
  GraphicsWindow.BrushColor = "white"
  GraphicsWindow.FontSize = 20
  initialsPrompt1 = Shapes.AddText("Enter initials for the")
  Shapes.HideShape(initialsPrompt1)
  GraphicsWindow.FontSize = 30 
  initialsPrompt2 = Shapes.AddText("Leader Board")
  Shapes.HideShape(initialsPrompt2)
  Shapes.Move(initialsPrompt1, 25, prompty-25)
  Shapes.Move(initialsPrompt2, 25, prompty+0)
  
  cclrnam[1] = "cyan"
  cclrnam[2] = "magenta"
  cclrnam[3] = "yellow"
  
  For cci = 1 To Array.GetItemCount(cclrnam)
    GraphicsWindow.PenColor = cclrnam[cci]
    GraphicsWindow.BrushColor = cclrnam[cci]
    cursor[cci] = Shapes.AddRectangle(curWid,curHei)
    Shapes.HideShape(cursor[cci])  
    Shapes.Move(cursor[cur],promptx + ((curWid + 5) * pos),prompty+5)  
  EndFor
EndSub

sub InitialEntryShow
  Shapes.ShowShape(initialsPrompt1)
  Shapes.ShowShape(initialsPrompt2)
  cur = 1 
  pos = 1
endsub

Sub InitialEnteryHide
  Shapes.HideShape(initialsPrompt1)
  Shapes.HideShape(initialsPrompt2)
EndSub

Sub OnKeyDown
  'Take note of the fact that a key has been pressed
  pressed = "True"
EndSub
