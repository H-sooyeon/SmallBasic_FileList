'   Challenge of the Month - May 2015      Math Challenge   by NaochanON   PBH516-0

MaxRound=10

Score=0

Controls.ButtonClicked=onbutton

initLevel()



Sub onbutton

  nm= Controls.GetButtonCaption(Controls.LastClickedButton)

  If Text.IsSubText(nm,"Question") Then 

    Controls.SetButtonCaption(btn," [ Answer ] ")

    Controls.SetTextBoxText(box," ")

    Round=Round+1

    GraphicsWindow.Title="Math test   |  Round= "+Round

    CalcExample()

  elseIf Text.IsSubText(nm,"Answer") Then 

    CheckAnswer()   

  EndIf

EndSub



Sub CheckAnswer

  thisanswer= Controls.GetTextBoxText(box)

  If thisanswer=Answer Then 

    Sound.Stop(PGD+"\Correct.mp3")

    Sound.Play(PGD+"\Correct.mp3")

    score=score+1 

    Shapes.SetText(msg2,"Your answer is correct !!"+CRLF+CRLF+" Push [ Next Question ] ")

  Else 

    NG=NG+1

    Shapes.SetText(msg2,"Your answer is wrong !!"+CRLF+CRLF+" Push [ Next Question ] ")

    Sound.Stop(PGD+"\Wrong.mp3")

    Sound.Play(PGD+"\Wrong.mp3")

  EndIf

  Shapes.SetText(msg3,"Your score = "+Score+" / "+(NG+Score))

  Controls.SetButtonCaption(btn,"[ Next Question ]") 

  If Round=MaxRound then

    Controls.SetButtonCaption(btn,"[ Finished  ]") 

    Shapes.SetText(msg2,"Question is finished !!")

  EndIf

EndSub



Sub CalcExample

  ITM= Math.GetRandomNumber(Array.GetItemCount(Item))

  Type= Math.GetRandomNumber(3)

  NMB1= 3+Math.GetRandomNumber(7)

  NMB2= Math.GetRandomNumber(10) 

  ShowImage()

  ' Shapes.SetText(msg1,"What is  "+NMB1+"  "+Calc[type]+"  "+NMB2 +"  ?")

  Shapes.SetText(msg2,"Enter Answer and"+CRLF+"Push [ Answer ] ")

  If type=1 Then 

    Answer=NMB1 + NMB2 

  ElseIf type=2 then 

    Answer=NMB1 - NMB2 

  ElseIf type=3 then 

    Answer=NMB1 * NMB2 

  EndIf

EndSub



Sub ShowImage

  Shapes.SetText(msg0,Calc[type])

  For i=1 To 10

    Shapes.Remove(img1[i])

    Shapes.Remove(img2[i])

  EndFor

  For i=1 To NMB1

    img1[i]= Shapes.AddImage(PGD+"\"+Item[ITM]+".gif")

    Shapes.Move(img1[i],450+i*5,-100)

    Shapes.Animate(img1[i],480+math.Remainder(i-1,2)*100,50+35*i,2000)

  EndFor

  For j=1 To NMB2

    img2[j]= Shapes.AddImage(PGD+"\"+Item[ITM]+".gif")

    Shapes.Move(img2[j],800+i*5,-100)

    Shapes.Animate(img2[j],820+math.Remainder(j-1,2)*100,50+35*j,2000)

  EndFor

EndSub



Sub initLevel

  GraphicsWindow.BackgroundColor="Lightcyan" 

  GraphicsWindow.Top=20

  GraphicsWindow.Width=1150

  GraphicsWindow.Height=500

  GraphicsWindow.FontSize=30

  box= Controls.AddTextBox(60,300)

  GraphicsWindow.BrushColor="Red"

  btn= Controls.AddButton(" Question Start ! ",60,400)

  msg0= Shapes.AddText(" ? ")

  Shapes.Move(msg0,760,300)

  msg1= Shapes.AddText(" ")

  Shapes.Move(msg1,60,100)

  GraphicsWindow.BrushColor="Darkgreen"

  msg2= Shapes.AddText("Push Question Start ")

  Shapes.Move(msg2,60,150)

  GraphicsWindow.BrushColor="Orange"

  msg3= Shapes.AddText("Your score = ")

  Shapes.Move(msg3,60,50)

  GraphicsWindow.BrushColor="Lightgray"

  GraphicsWindow.FillRectangle(450,50,300,440)

  GraphicsWindow.FillRectangle(800,50,300,440)

  Calc="1=+;2=-;3=*"    

  Item="1=Apple;2=Cat;3=Crab;4=Flower;5=Bird;6=Fish"

  CRLF= Text.GetCharacter(13)+Text.GetCharacter(10)

  PGD= Program.Directory

EndSub 

  


