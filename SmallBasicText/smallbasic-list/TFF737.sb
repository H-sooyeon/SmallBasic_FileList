TRUE = 1

FALSE = 0



GraphicsWindow.BackgroundColor = "LightYellow"



' add the multiline text box and set its size

mlTxt = Controls.AddMultiLineTextBox(10,10)

Controls.SetSize(mlTxt,GraphicsWindow.Width - 20, GraphicsWindow.Height - 50)

Controls.SetTextBoxText(mlTxt,"Output...")

' draw a shape over the multiline text block and set it to invisible. This stops someone clicking and changing

' the text on the multiline textbox - although they can tab to it and change so we are going to get around that in a sec :/

blocker = Shapes.AddRectangle(GraphicsWindow.Width - 20 - 20, GraphicsWindow.Height - 50 - 20)

Shapes.Move(blocker,10,10)

Shapes.SetOpacity(blocker,0)



'draw the input box

txtbox = Controls.AddTextBox(10, GraphicsWindow.Height - 30)

Controls.SetSize(txtbox,GraphicsWindow.Width - 20, 25)

Controls.SetTextBoxText(txtbox,"input...")

' set listener for text type events

Controls.TextTyped = txtType

GraphicsWindow.KeyDown = keyPress



'boolean to record if theres starter text in the input.

typed = FALSE



'store the current output - change this when we add to it. we can use this temp value

' to go back to if they change the text.

mlTxtLast = Controls.GetTextBoxText(mlTxt)



Sub txtType

  last = Controls.LastTypedTextBox

  ' GraphicsWindow.Title = GraphicsWindow.LastKey

  

  If last = mlTxt Then

    ' they are in our output - set it to the stored output.

    Controls.SetTextBoxText(mlTxt, mlTxtLast)

  ElseIf last = txtbox then

    if typed = FALSE then

      'if its first time then clear the silly starter text

      Controls.SetTextBoxText(txtbox, "")

      typed = TRUE      

    EndIf   

  EndIf

  

EndSub





Sub keyPress

  

  ' handle return key presses

  If text.GetLength(Controls.GetTextBoxText(txtbox)) > 0 then

    ' there is  text in the box

    

    

    If GraphicsWindow.LastKey = "Return" Or GraphicsWindow.LastKey = "Enter" Then

      ' add to our text array and then add to the output.

      ' adds a new line ascii character then the text see http://www.asciitable.com/

      mlTxtLast = Text.Append(mlTxtLast, Text.GetCharacter(13))

      mlTxtLast = Text.Append(mlTxtLast, Controls.GetTextBoxText(txtbox))

      

      ' sets the text

      Controls.SetTextBoxText(mlTxt,mlTxtLast)

      Controls.SetTextBoxText(txtbox, "")

      

    EndIf

    

  EndIf

  

endsub








