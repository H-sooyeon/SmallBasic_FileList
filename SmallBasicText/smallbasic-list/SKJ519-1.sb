' Missile Class (v1.01)
' by GoToLoop (2014-Nov-08)
' SKJ519-1
'
' https://social.msdn.microsoft.com/Forums/en-US/
' 3113bf7b-d5b3-42f3-af4f-acb576a7a04f/missile

FPS = 60
DELAY = 1000/FPS

GW = 1000
GH = 300

GraphicsWindow.CanResize = "False"
GraphicsWindow.Width = GW
GraphicsWindow.Height = GH
GraphicsWindow.BackgroundColor = "LightGray"
GraphicsWindow.PenColor = "Black"
GraphicsWindow.PenWidth = 2

CreateMissileClass()
GraphicsWindow.KeyDown = KeyPressed
pTime = Clock.ElapsedMilliseconds

Loop:

CalculateDeltaTime()
movement = dTime/1000 * Missile_V

If isRequestingForMissile Then
  For i = 1 To Missile_N
    If Missile_isVisible[i] <> "True" Then
      Shapes.ShowShape(Missile_shape[i])
      Missile_isVisible[i] = "True"
      nTime = cTime+Missile_D
      i = Missile_N
      isRequestingForMissile = "False"
    EndIf
  EndFor
EndIf

For i = 1 To Missile_N
  If Missile_isVisible[i] Then
    Missile_Move()
  EndIf
EndFor

wait = DELAY - dTime
If wait > 0 Then
  Program.Delay(wait)
EndIf

Goto Loop

Sub KeyPressed
  k = GraphicsWindow.LastKey
  
  If k = "Space" And cTime >= nTime Then
    isRequestingForMissile = "True"
  ElseIf k = "Escape" Then
    Program.End()
  EndIf
EndSub

Sub CalculateDeltaTime
  cTime = Clock.ElapsedMilliseconds  ' current time
  dTime = cTime - pTime                    ' delta time
  pTime = cTime                                  ' previous time
EndSub

Sub CreateMissileClass
  ' Constants:
  Missile_N = 5         ' quantity
  Missile_W = 30      ' width
  Missile_H = 10       ' height
  Missile_V = 30       ' velocity
  Missile_D = 5000   ' delay
  Missile_C = "Green"
  
  GraphicsWindow.BrushColor = Missile_C
  
  ' Properties:
  For i = 1 To Missile_N
    Missile_x[i] = 0
    Missile_y[i] = GH/2
    
    Missile_shape[i] = Shapes.AddRectangle(Missile_W, Missile_H)
    Missile_isVisible[i] = "False"
    Shapes.HideShape(Missile_shape[i])
    Shapes.Move(Missile_shape[i], Missile_x[i], Missile_y[i])
    
  EndFor
EndSub

Sub Missile_Move
  Missile_x[i] = Missile_x[i]+movement
  Shapes.Move(Missile_shape[i], Missile_x[i], Missile_y[i])

  If Missile_x[i] > GW Then
    Missile_x[i] = 0
    Missile_isVisible[i] = "False"
    Shapes.HideShape(Missile_shape[i])
  EndIf
EndSub
