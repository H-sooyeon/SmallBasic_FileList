' SmallBasic Version 1.2

' Program: Windmill

' Changelog: 

' Author: Pappa Lapub

' Website: https://social.msdn.microsoft.com/Forums/en-US/ba0a4827-f072-4afd-8fc4-f2e93a088663/how-can-a-3d-model-be-displayed

' ImportURL: http://smallbasic.com/program/?

' Extension: LitDev

' Comment: http://animium.com/2008/06/windmill

'  http://animium.com/wp-content/uploads/downloads/2015/01/windmill.rar

'  http://tf3dm.com/3d-model/windmill-56151.html

'  http://www.mediafire.com/file/yyqew7y0uzjwf4m/Windmill.7z

' Variables: 

' ToDo: 

' ================================================================================

modPath = Program.Directory +"\windmill.3DS"

dist = 2200 '2000 ' 1000

'durat = 5

lf = Text.GetCharacter(10)



GraphicsWindow.Top = 0

gw = GraphicsWindow.Width

gh = GraphicsWindow.Width

GraphicsWindow.Title = "Windmill  [R Reset | N aNimate | -/+ Speed | M camMode | Esc Exit]"

GraphicsWindow.BackgroundColor = "SkyBlue"

view = LD3DView.AddView(gw,gh, "")  ' "False" for load by Drag&Drop on Geometry via DControls.FileDropped



lAmb = LD3DView.AddAmbientLight(view, "Gray")

lDir = LD3DView.AddDirectionalLight(view, "LightGray", -1,1,-1)

LD3DView.AutoControl("True","True", dist, 100)



mod = LD3DView.LoadModel(view, modPath)

LD3DView.ResetMaterial(view, mod[5], "Maroon", "D")     ' door

LD3DView.ResetMaterial(view, mod[6], "Brown", "E")      ' window

LD3DView.ResetMaterial(view, mod[7], "Green", "D")      ' ground

LD3DView.TranslateGeometry(view, mod[4], -95,-610,400)  ' wheel



sun = LD3DView.AddSphere(view, 2000, 20, "OrangeRed","E")

LD3DView.TranslateGeometry(view, sun, -25000,2*dist,dist/2)



ResetView()

Animation()



GraphicsWindow.KeyDown = OnKeyDown





' //////////  EVENTs  \\\\\\\\\\

Sub OnKeyDown ' not X,Y,Z, A.D,W,S, Up,Down, Left,Right   (W,S, Up,Down  NOT working)

  lastKey = GraphicsWindow.LastKey

  If lastKey = "Escape" Then

    Program.End()

  ElseIf lastKey = "R" Then

    ResetView()

  ElseIf lastKey = "N" Then

    Animation()

  ElseIf lastKey = "M" Then

    CamMod()

    

  ElseIf lastKey = "OemPlus" Then

    durat = Math.Max(1, durat - 1)

    LD3DView.AnimateRotation(view, mod[4], -1,0.184528724970932,0, 0,360, durat,-1) 

  ElseIf lastKey = "OemMinus" Then

    durat = Math.Min(10, durat + 1)

    LD3DView.AnimateRotation(view, mod[4], -1,0.184528724970932,0, 0,360, durat,-1)

    

  ElseIf lastKey = "V" Then

    GetView()

  ElseIf lastKey = "B" Then

    GetBoxPos()

  EndIf

EndSub





' //////////  SUBs  \\\\\\\\\\

Sub ResetView

  'Blick entlang x abw√§rts, +z oben, +y rechts/-y links

  LD3DView.ResetCamera(view, dist,0,450, -1,0,0, 0,0,1)

  durat = 5

  bAnim = LDLogic.Not(bAnim)

  Animation()

EndSub



Sub Animation

  If bAnim Then

    LD3DView.AnimateRotation(view, mod[4], -1,0.184528724970932,0, 0,360, durat,0)  ' Stop Anim.

  Else

    LD3DView.AnimateRotation(view, mod[4], -1,0.184528724970932,0, 0,360, durat,-1) ' Start Anim.

  EndIf

  bAnim = LDLogic.Not(bAnim)

EndSub



Sub CamMod

  If bCamMod Then

    LD3DView.CameraProperties(view, 0.125,"", 60)  'default

  Else

    LD3DView.CameraProperties(view, 0.125,"", -dist)  ' parallel

  EndIf

  bCamMod = LDLogic.Not(bCamMod)

EndSub





Sub GetBoxPos

  TextWindow.WriteLine("1=base;2=house;3=shaft;4=sails;5=door;6=window;7=ground;")

  TextWindow.WriteLine(mod +lf)

  nMod = Array.GetItemCount(mod)  ' 7

  For n = 1 To nMod

    box[n] = LD3DView.BoundingBox(view, mod[n])

    pos[n] = LD3DView.GetPosition(view, mod[n])

    TextWindow.WriteLine(mod[n] +lf+ "BoundingBox: (1=dimX,2=dimY;3=dimZ;4=minX;5=minY;6=minZ)")

    TextWindow.WriteLine(box[n])

    TextWindow.WriteLine("Position: (1=centX;2=centY;3=centZ)")

    TextWindow.WriteLine(pos[n] +lf)

  EndFor

EndSub



Sub GetView

  pos = LD3DView.GetCameraPosition(view)

  dir = LD3DView.GetCameraDirection(view)

  up = LD3DView.GetCameraUpDirection(view)

  TextWindow.WriteLine("camPos (1=xPos;2=yPos;3=zPos)" +lf+ pos)

  TextWindow.WriteLine("camDir (1=xDir;2=yDir;3=zDir)" +lf+ dir)

  TextWindow.WriteLine("cUpDir (1=xUp;2=yUp;3=zUp)" +lf+ up +lf)

EndSub

  


