''  Factorial      2012-04-20  WhTurner
'''==========================
''    Adjustable parameters:
First=52       '' first factorial in output  (make equal to Last for only one factorial)
Last=52      '' last factorial in output  
Sdigit=6     '' number of significant digits in scientific notation (1 .. 12)
                          ''  use  0(zero) for string output
Ts=""        ''Thousends separator ( " " space  ; "," komma  or "" nothing )
Ds=","        ''Decimal separator
'' ==========================
m[1]=1
Mmax=1
Empty="         "
For i=1 to Last
  mult=i
  multiply()
  If i>=First Then   ''output
    Tout=m[Mmax]
    For j=Mmax-1 To  1 Step -1
      out="x00000000"+m[j]
      Lout= Text.GetLength(out)
      out=Text.GetSubTextToEnd(out,Lout-5)
      Tout= Text.Append(Tout,out)
    endfor  
    Lout= Text.GetLength(Tout)
    L= Text.GetLength(i)
    TextWindow.Write(Text.GetSubText(Empty,1,4-L)+i+"! =  ")
    If Sdigit=0 then   '''print string
      L= Math.Ceiling(Lout/3)*3-Lout
      Tout= Text.Append(Text.GetSubText(Empty,1,L),Tout)
      Lout=Lout+L
      For j=1 To Lout-3 Step 3
        TextWindow.Write(Text.GetSubText(Tout,j,3)+Ts)
      endfor
      TextWindow.WriteLine(Text.GetSubTextToEnd(Tout,Lout-2))
    Else     ''scientific notation with Sdigit significant digits
      If Lout<Sdigit+1 then
        TextWindow.WriteLine(Tout)
      else
        Temp=Text.GetSubText(Tout,1,1+Sdigit)
        Temp= Math.Round(Temp/10)
        TextWindow.Write(Text.GetSubText(Tout,1,1)+Ds+Text.GetSubTextToEnd(Temp,2))
        TextWindow.WriteLine(" * 10^"+(Lout-1))
      endif
    EndIf
  EndIf   ''output
EndFor
''=============================================
Sub multiply
  ''in/output vars: m[1..Mmax] array for 6-digit parts
  ''  Mmax: maximum index of array;  mult: multiplier
  ''local vars:   M_i  M_carry
  For M_i=1 To Mmax
    m[M_i]=m[M_i]*mult
  EndFor
  For M_i=1 To  Mmax
    M_carry= Math.Floor(m[M_i]/1000000)
    m[M_i]= Math.Remainder(m[M_i],1000000)
    m[1+M_i]=M_carry+m[1+M_i]
  EndFor
  If m[Mmax+1]>0 Then
    Mmax=Mmax+1  
  EndIf
EndSub
