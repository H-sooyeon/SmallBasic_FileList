'Eventhandler

Controls.ButtonClicked = OnButtonClicked



'Initialisation

Init()

FillTextBoxes()



'Mainloop

While ContinuFlag1 = "True"

  If (ContinuFlag2 = "False") Then 'Show new index if it has changed

    FillTextBoxes()

    ContinuFlag2 = "True"

  EndIf

EndWhile



'Save all data before program end with button "end"

'No save if you breack the program, because it is not possible

Save()

Program.End()



'*******************

' * SUBROUTINES *

'*******************



Sub Init

  'Initialisation Path and Filename

  p = "C:\"                  'Paht from directory database

  Path = "C:\SB-Database"    'Fullpath database

  FN = "\Adresses.SBD"           'Filename database

  

  ' If database directory not exist, create it

' The following line could be harmful and has been automatically commented.

'   Dir = File.GetDirectories(p)

  If (Array.ContainsValue(DIR, Path) = "False") Then

' The following line could be harmful and has been automatically commented.

'     File.CreateDirectory(Path) 

  EndIf

  

  'Initialisation window

  GraphicsWindow.Top = 10

  GraphicsWindow.Left = 10

  GraphicsWindow.Height = 480

  GraphicsWindow.Width = 620

  GraphicsWindow.Title = " AddressManager "

  GraphicsWindow.CanResize = "False"

  GraphicsWindow.FontName = "Arrial"

  GraphicsWindow.FontSize = 14

  FontHight = 24

  ContinuFlag1 = "True" 'Flags controles the main loop

  ContinuFlag2 = "True"

  PB = 40 'Position text vor button

  PTB = 170 'Postion button

  GraphicsWindow.Clear()

  GraphicsWindow.BackgroundColor = "LightBlue"

  GraphicsWindow.BrushColor = "PowderBlue"

  GraphicsWindow.PenColor = "LightGray"

  GraphicsWindow.PenWidth = 1

  GraphicsWindow.FillRectangle(PB-5,45,560,FontHight*11+20)

  For i = 1 to 10

    GraphicsWindow.DrawLine(PB-1,i*25+50,552+PB,i*25+48)

  EndFor 

  GraphicsWindow.BrushColor = "SteelBlue"

  GraphicsWindow.FillRectangle(PB-7,363,574,84)

  GraphicsWindow.BrushColor = "LightGray"

  GraphicsWindow.FillRectangle(PB-5,365,570,80)

  

  'Initialisation Textboxes 

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.PenColor = "Gray"

  GraphicsWindow.PenWidth = 2

  

  Address = "Address:"

  GraphicsWindow.DrawText(PB, 50, Address)

  TBAddress = Controls.AddTextBox(PTB, 50)

  Controls.SetSize(TBAddress,420,FontHight)

  

  AcademicTitle = "Academic Title:"

  GraphicsWindow.DrawText(PB, 75, AcademicTitle)

  TBAcademicTitle = Controls.AddTextBox(PTB, 75)

  Controls.SetSize(TBAcademicTitle,420,FontHight)

  

  FirstName = "First Name:"

  GraphicsWindow.DrawText(PB, 100, FirstName)

  TBFirstName = Controls.AddTextBox(PTB, 100)

  Controls.SetSize(TBFirstName,420,FontHight)

  

  LastName = "Last Name:"

  GraphicsWindow.DrawText(PB, 125, LastName)

  TBLastName = Controls.AddTextBox(PTB, 125)

  Controls.SetSize(TBLastName,420,FontHight)

  

  StreetAndHouseNumber = "Street & number:"

  GraphicsWindow.DrawText(PB, 150, StreetAndHouseNumber)

  TBStreetAndHouseNumber = Controls.AddTextBox(PTB, 150)

  Controls.SetSize(TBStreetAndHouseNumber,420,FontHight)

  

  PostCode = "Post Code:"

  GraphicsWindow.DrawText(PB, 175, PostCode)

  TBPostCode = Controls.AddTextBox(PTB, 175)

  Controls.SetSize(TBPostCode,140,FontHight)

  

  Place = "City:"

  GraphicsWindow.DrawText(PB, 200, Place)

  TBPlace = Controls.AddTextBox(PTB, 200)

  Controls.SetSize(TBPlace,280,FontHight)

  

  TelephoneNumber = "Telephone:"

  GraphicsWindow.DrawText(PB, 225, TelephoneNumber)

  TBTelephoneNumber = Controls.AddTextBox(PTB, 225)

  Controls.SetSize(TBTelephoneNumber,280,FontHight)

  

  MobileNumber = "Mobile phone:"

  GraphicsWindow.DrawText(PB, 250, MobileNumber)

  TBMobileNumber = Controls.AddTextBox(PTB, 250)

  Controls.SetSize(TBMobileNumber,280,FontHight)

  

  FaxNumber = "Fax:"

  GraphicsWindow.DrawText(PB, 275, FaxNumber)

  TBFaxNumber = Controls.AddTextBox(PTB, 275)

  Controls.SetSize(TBFaxNumber,280,FontHight)

  

  EMailAddress = "E-Mail:"

  GraphicsWindow.DrawText(PB, 300, EMailAddress) 

  TBEMailAddress = Controls.AddTextBox(PTB, 300)

  Controls.SetSize(TBEMailAddress,420,FontHight)



  'Initialisation controle button

  GraphicsWindow.BrushColor = "SteelBlue"

  GraphicsWindow.FontBold = "True"

  GraphicsWindow.FontSize = 16

  B1 = Controls.AddButton(" << ", 47, 370)

  BB = Controls.AddButton(" < ", 107, 370)

  BC = Controls.AddButton(" Save ", 175,370)

  BH = Controls.AddButton(" Help ", 300,370)

  BE = Controls.AddButton(" Close ", 390,370)

  BF = Controls.AddButton(" > ", 500, 370)

  BL = Controls.AddButton(" >> ", 550, 370)

  BD = Controls.AddButton(" Delete ", 300,410)

  Controls.ShowControl(B1)

  Controls.ShowControl(BB)

  Controls.ShowControl(BC)

  Controls.ShowControl(BH)

  Controls.ShowControl(BE)

  Controls.ShowControl(BF)

  Controls.ShowControl(Bl)

  Controls.ShowControl(BD)

  

  GraphicsWindow.FontBold = "False"

  GraphicsWindow.FontSize = 14

  GraphicsWindow.BrushColor = "Black"

  Load()

  If (Index = 0) Then 'Nothing present, then start at the beginning

    Index = 1

  EndIf

EndSub 'Init



Sub FillArray 

  Anschr[Index] = Index

  Anschr[Index][Address] = Controls.GetTextBoxText(TBAddress)

  Anschr[Index][AcademicTitle] = Controls.GetTextBoxText(TBAcademicTitle)

  Anschr[Index][FirstName] = Controls.GetTextBoxText(TBFirstName)

  Anschr[Index][LastName] = Controls.GetTextBoxText(TBLastName)

  Anschr[Index][StreetAndHouseNumber] = Controls.GetTextBoxText(TBStreetAndHouseNumber)

  Anschr[Index][PostCode] = Controls.GetTextBoxText(TBPostCode)

  Anschr[Index][Place] = Controls.GetTextBoxText(TBPlace)

  Anschr[Index][TelephoneNumber] = Controls.GetTextBoxText(TBTelephoneNumber)

  Anschr[Index][MobileNumber] = Controls.GetTextBoxText(TBMobileNumber)

  Anschr[Index][FaxNumber] = Controls.GetTextBoxText(TBFaxNumber)

  Anschr[Index][EMailAddress] = Controls.GetTextBoxText(TBEMailAddress)

EndSub 'FillArray



Sub FillTextBoxes 'Output current index to textboxes

  Controls.SetTextBoxText(TBAddress, Anschr[Index][Address])

  Controls.SetTextBoxText(TBAcademicTitle, Anschr[Index][AcademicTitle])

  Controls.SetTextBoxText(TBFirstName, Anschr[Index][FirstName])

  Controls.SetTextBoxText(TBLastName, Anschr[Index][LastName])

  Controls.SetTextBoxText(TBStreetAndHouseNumber, Anschr[Index][StreetAndHouseNumber])

  Controls.SetTextBoxText(TBPostCode, Anschr[Index][PostCode])

  Controls.SetTextBoxText(TBPlace, Anschr[Index][Place])

  Controls.SetTextBoxText(TBTelephoneNumber, Anschr[Index][TelephoneNumber])

  Controls.SetTextBoxText(TBMobileNumber, Anschr[Index][MobileNumber])

  Controls.SetTextBoxText(TBFaxNumber, Anschr[Index][FaxNumber])

  Controls.SetTextBoxText(TBEMailAddress, Anschr[Index][EMailAddress])

EndSub 'FillTextBoxes



Sub Load



' The following line could be harmful and has been automatically commented.

'   Anschr = File.ReadContents(Path+FN)

  Index = Array.GetItemCount(Anschr)+1

EndSub 'Load



Sub Save

  FillArray()

' The following line could be harmful and has been automatically commented.

'   File.WriteContents(path+FN, Anschr)

EndSub 'Save



Sub Delete

  NIndex = Array.GetItemCount(Anschr)

  For i = Index + 1 to NIndex + 1

    Index = i - 1

    Anschr[Index] = Index 

    Anschr[Index][Address] = Anschr[i][Address]

    Anschr[Index][AcademicTitle] = Anschr[i][AcademicTitle]

    Anschr[Index][FirstName] = Anschr[i][FirstName]

    Anschr[Index][LastName] = Anschr[i][LastName]

    Anschr[Index][StreetAndHouseNumber] = Anschr[i][StreetAndHouseNumber]

    Anschr[Index][PostCode] = Anschr[i][PostCode]

    Anschr[Index][Place] = Anschr[i][Place]

    Anschr[Index][TelephoneNumber] = Anschr[i][TelephoneNumber]

    Anschr[Index][MobileNumber] = Anschr[i][MobileNumber]

    Anschr[Index][FaxNumber] = Anschr[i][FaxNumber]

    Anschr[Index][EMailAddress] = Anschr[i][EMailAddress]

  EndFor

EndSub 'Delete



Sub OnButtonClicked

  MaxIDNr = Array.GetItemCount(Anschr) + 1

  CButton = Controls.LastClickedButton

  If CButton = B1 Then '  <<

    FillArray() 

    Index = 1

    ContinuFlag2 = "False"

  EndIf  

  If CButton = BB Then '  <

    FillArray() 

    Index = Index - 1

    If (Index < 1) Then

      Index = 1

    EndIf 

    ContinuFlag2 = "False"

  EndIf 

  If CButton = BC Then 'Save

    Save()

  EndIf 

  If CButton = BH Then 'Help

    notice = "The Button:"+Text.GetCharacter(13)

    notice = notice + "- Click << to go to the first address."+Text.GetCharacter(13)

    notice = notice + "- Click >> to go to the last address. It is empty."+Text.GetCharacter(13)

    notice = notice + "- Click < for the backwards entry."+Text.GetCharacter(13)

    notice = notice + "- Click > for the next entry."+Text.GetCharacter(13)+Text.GetCharacter(13)

    notice = notice + "- Click Save to save it in a database."+Text.GetCharacter(13)

    notice = notice + "- Click Help for this Notice"+Text.GetCharacter(13)

    notice = notice + "- Click Close to save the data and close the Program"+Text.GetCharacter(13)

    notice = notice + "- Click Delete to delete the actual entry and show a empty entry at the end."+Text.GetCharacter(13)+Text.GetCharacter(13)

    notice = notice + "!!! WARNING !!! : If you click cancel (the X in the red filled rectangel on right top of the window) the data do not saved."+Text.GetCharacter(13)+Text.GetCharacter(13)

    notice = notice + "Good luck"+Text.GetCharacter(13)

    GraphicsWindow.ShowMessage(notice, "Help")

  EndIf 

  If CButton = BE Then  'End

    ContinuFlag1 = "False"

    ContinuFlag2 = "False"

  EndIf

  If CButton = BF Then  '  >

    FillArray() 

    Index = Index + 1

    If (Index > MaxIDNr) Then

      Index = MaxIDNr

    EndIf 

    ContinuFlag2 = "False"

  EndIf 

  If CButton = BL Then  '  >>

    FillArray() 

    Index = Array.GetItemCount(Anschr) + 1 

    ContinuFlag2 = "False"

  EndIf 

  If CButton = BD Then 'Delete

    Delete()

    ContinuFlag2 = "False"

  EndIf 

EndSub 'OnButtonClicked




