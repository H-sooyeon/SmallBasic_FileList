'Just trying Screen Updates outside the UI thread, since SB has no async methods or forms.

'NOT SURE HERE, but
'Seems Not too tricky for SB since there's no forms with properties that have to be set in the UI thread.
'ref: http://msdn.microsoft.com/en-us/magazine/cc188732.aspx

'PURPOSE: is to build an Applet that can include a game mode without using an infinite Main loop 
'   so the whole program waits for an event to happen then Resumes, processes Event Flags then 
'   pauses and waits for next event. Not a resource greedy structure when nothing is happening.
'   WIP will put future programs into an UpdateProgram()  UpdateScreen() structure WITHOUT  an infinite loop. 
'Hopefully.
'******************************************************************************************************

Initialise()
Timer.Tick = UpdateProgram

'=====================================================
'Update Program Only when an Event Happens (simple example) w/o game mode
'=====================================================
Sub UpdateProgram
  Timer.Pause()  
  
  If keyUp Then
    name = Controls.GetTextBoxText(nameBox)
    
    If name <> nameBoxMessage And GraphicsWindow.LastKey = "Return" Then
      AnimateName()
    EndIf
  EndIf    
  
  updated = "True"
EndSub

'======================================================
'Subroutines
'======================================================
Sub AnimateName  
  'Index Letters
  For i = Text.GetLength(name) To 1 Step -1
    letter[i] = Shapes.AddText(Text.GetSubText(name, i, 1))  
    Shapes.Zoom(letter[i], nameScale, nameScale)
    Shapes.Move(letter[i], gw - (letterWidth_approx/2)-15 , 15)
    Shapes.SetOpacity(letter[i], letterOpacity)
  EndFor 
  
  'Animate Letters
  For i = 1 To  Array.GetItemCount(letter)
    letterOpacity = 0
    X = gw - (letterWidth_approx/2)-5  
    
    If Text.GetSubText(name, i, 1) <> " " Then    
      While X > nameLeftOffset + letterWidth_approx * (i-1)
        While letterOpacity < 100
          letterOpacity = letterOpacity + 2
          Shapes.SetOpacity(letter[i], letterOpacity)
          Program.Delay(5)
        EndWhile
        X = X - 4
        Shapes.Move(letter[i], X, nameTopOffset)
        Program.Delay(5)
      EndWhile
    EndIf
    
    appendName = Text.Append(appendName, Text.GetSubText(name, i, 1))
    Shapes.HideShape(letter[i])
    Shapes.SetText(displayName, appendName)
  EndFor
EndSub

'======================================================
'Initialise
'======================================================
Sub Initialise
  
  'Initialise Variables
  gw = GraphicsWindow.Width
  nameBoxMessage = "Type your name here. Then press ENTER."
  nameScale = 4
  letterWidth_approx = 35 'Used for Zooming about the centre (will VARY)
  nameLeftOffset = 20
  nameTopOffset = 200
  letterOpacity = 0
  appendName = ""
  
  'Initialise Flags
  updated = "True"
  keyUp = "False"
  
  'Add Controls & Shapes
  nameBox = Controls.AddTextBox(10, 10)
  Controls.SetSize(nameBox, 260, 25)
  Controls.SetTextBoxText(nameBox, nameBoxMessage)
  
  displayName = Shapes.AddText("")
  Shapes.Zoom(displayName, nameScale, nameScale)
  Shapes.Move(displayName, nameLeftOffset, nameTopOffset)
  
  'Register Events
  GraphicsWindow.KeyUp = OnKeyUp
  
  'Initialise Timer
  Timer.Interval = 20
  Timer.Pause()  
EndSub

'==================================================
'Event Handlers
'==================================================
Sub OnKeyUp  
  If updated Then
    keyUp = "True"
    updated = "False"    
    Timer.Resume()    
  EndIf
EndSub
