'  suudoku  solver    by NaochanON     2012/09/21                 
'  graphic version 

Controls.ButtonClicked=onclicked


initData()
Sub onclicked
  While iniNMB+NN<81
    RCdata()            '  calculate   Row  Column  Block  data 
    For j=1 to 9
      For i=1 to 9
        KK= 3*Math.Floor((j-1)/3)+Math.Floor((i-1)/3)+1  '  block number 
        If Controls.GetTextBoxText(cell[J][i])=" " Then 
          check()
        endif
      endfor
    endfor
  endwhile
  Controls.SetButtonCaption(btn,"End")
endsub

Sub check
  NMB="123456789"
  word=text.Append(text.Append(R[j],C[i]),B[KK])
  For L=1 To Text.GetLength(word)
    MidP=text.GetSubText(word,L,1)
    For M=1 To 9
      If text.GetSubText(NMB,M,1)=MidP Then 
        NMB= text.Append(Text.GetSubText(NMB,1,M-1),Text.GetSubTextToEnd(NMB,M+1))
      EndIf
    EndFor
  EndFor
  If Text.GetLength(NMB)=1 Then  
    D[J][i]=NMB                           ' <-------------  decided number 
    Controls.SetTextBoxText(cell[j][i],NMB)
    NN=NN+1
  endif  
EndSub

Sub RCdata
  '----------  Row data  -------------
  For J=1 To 9
    R[J]=""
    For i=1 To 9
      If Controls.GetTextBoxText(cell[J][i])<>" " Then 
        R[J]=text.Append(R[J],Controls.GetTextBoxText(cell[J][i]))
      EndIf
    EndFor
  EndFor
  '----------  Columndata  -------------
  For i=1 To 9
    C[i]=""
    For J=1 To 9
      If Controls.GetTextBoxText(cell[J][i])<>" " Then 
        C[i]=text.Append(C[i],Controls.GetTextBoxText(cell[J][i]))
      EndIf
    EndFor
  EndFor
  '---------- Block data  -------------
  for K=1 To 9
    B[K]=""
    For j2=1 To 3
      j=j2+3*Math.Floor((k-1)/3)
      For i2=1 To 3
        i=i2+3*((k-1)-3*Math.Floor((k-1)/3))
        If Controls.GetTextBoxText(cell[J][i])<>" " Then 
          B[K]=text.Append(B[K],Controls.GetTextBoxText(cell[J][i]))
        EndIf
      EndFor  
    EndFor 
  EndFor
EndSub

Sub initData
  '----------  data  -------------------------------------------
  D[1]="1=7;2=8;3= ;4=9;5= ;6= ;7= ;8=5;9= "
  D[2]="1= ;2= ;3= ;4=4;5=8;6= ;7=1;8= ;9= "
  D[3]="1= ;2= ;3= ;4= ;5= ;6=7;7=8;8= ;9= "
  D[4]="1=4;2= ;3= ;4= ;5=9;6=2;7= ;8=8;9= "
  D[5]="1=1;2= ;3=5;4= ;5= ;6= ;7=2;8= ;9=7"
  D[6]="1= ;2=7;3= ;4=1;5=3;6= ;7= ;8= ;9=4"
  D[7]="1= ;2= ;3=7;4=3;5= ;6= ;7= ;8= ;9= "
  D[8]="1= ;2= ;3=6;4= ;5=5;6=4;7= ;8= ;9= "
  D[9]="1= ;2=4;3= ;4= ;5= ;6=6;7= ;8=2;9=9"
  '------------  count initial data number  ---------------- 
  GraphicsWindow.BackgroundColor="darkgreen"
  GraphicsWindow.FontSize=25
  For j=1 To 9
    For i=1 To 9
      If D[J][i]<>" " Then 
        GraphicsWindow.BrushColor="Red"
        cell[j][i]= Controls.AddTextBox(40*i,40*j)
        Controls.SetSize(cell[j][i],35,35) 
        Controls.SetTextBoxText(cell[j][i],D[J][i])
        iniNMB=iniNMB+1 
      elseif D[J][i]=" " Then 
        GraphicsWindow.BrushColor="Navy"
        cell[j][i]= Controls.AddTextBox(40*i,40*j)
        Controls.SetSize(cell[j][i],35,35) 
        Controls.SetTextBoxText(cell[j][i]," ")
      EndIf
    EndFor
  EndFor
  
  btn= Controls.AddButton("Calc",450,50)
  Controls.SetSize(btn,80,40)
EndSub
  
