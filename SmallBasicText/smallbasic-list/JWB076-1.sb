'  Mandelbrot 3D
'  Rene_Miner

'  JWB076-1

'  http://social.msdn.microsoft.com/Forums/en-US/smallbasic
'/thread/f9a8cf1f-2a4c-4019-bea9-ebdcb7b86c15

gwWIDTH  = 640
gwHEIGHT = 480

GraphicsWindow.Width  = gwWIDTH
GraphicsWindow.Height = gwHEIGHT
GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.Title = "Mandelbrot 3D"

'---------------------------------------
' leave that alone
If gwHEIGHT < gwWIDTH Then
  fsizeX = Math.Round( gwHEIGHT / 4 )
  fsizeY = fsizeX
Else
  fsizeX = Math.Round( gwWIDTH  / 4 )
  fsizeY = fsizeX
EndIf

zerofX = Math.Round( gwWIDTH / 2 )
zerofY = 0
'---------------------------------------

DEPTHMAX = 255    'low number=fast calculate, high number= needs longer but more precise
X_MIN = -.79      ' free to try
X_MAX = 2.01      ' change if you want
Y_MIN = -1.42     ' play with it
Y_MAX = Y_MIN + (X_MAX - X_MIN) * fsizeY/fsizeX

' colour front and side
useColF  = GraphicsWindow.GetColorFromRGB(160, 160, 120)
useColS  = GraphicsWindow.GetColorFromRGB(120, 120, 160)

' you might setup your own colour-table instead of random colours
myCol[0] = GraphicsWindow.BackgroundColor
For i = 1 To 31
  myCol[i] = GraphicsWindow.GetRandomColor()  
EndFor

' start drawing
deltaX  = (X_MAX - X_MIN) / fsizeX
deltaY  = (Y_MAX - Y_MIN) / fsizeY
realVal = X_MIN
imagineVal = Y_MIN

' activate time counter
Timer.Tick = Counter
Timer.Interval = 1000

' main loop
For iY = 0 To fsizeY
  For iX = 0 To fsizeX
    
    xVal = 0    
    yVal = 0
    Xlen = 0
    Ylen = 0
    iter = 0
    
    While Xlen+Ylen < 8 And iter < DEPTHMAX
      yVal = 2*xVal*yVal - imagineVal
      xVal = Xlen - Ylen - realVal
      
      Xlen = xVal*xVal
      Ylen = Yval*yVal
      
      iter = iter + 1
    EndWhile
    
    If iter >= DEPTHMAX Then
      fHeight = 0
    Else
      fHeight = Math.Remainder(iter,31)
    EndIf
    
    realVal = realVal + deltaX
    
    If fHeight > 0 Then
      For iHeight = -32 To -fHeight
        
        dispX = zerofX + 2*iX - 2*iY
        dispY = zerofY + 2*iY + 2*iX - iHeight
        color = myCol[fHeight]
        
        GraphicsWindow.SetPixel(dispX    dispY,   color)
        GraphicsWindow.SetPixel(dispX+1  dispY,   color)
        
        dispY = dispY + 1
        For x = -1 To 2
          GraphicsWindow.SetPixel(dispX+x  dispY, color)
        EndFor
        
        dispY = dispY + 1
        GraphicsWindow.SetPixel(dispX-1  dispY, useColF)
        GraphicsWindow.SetPixel(dispX    dispY, color)
        GraphicsWindow.SetPixel(dispX+1  dispY, color)
        GraphicsWindow.SetPixel(dispX+2  dispY, useColS)
        
        dispY = dispY + 1
        GraphicsWindow.SetPixel(dispX-1  dispY, useColF)
        GraphicsWindow.SetPixel(dispX    dispY, useColF)
        GraphicsWindow.SetPixel(dispX+1  dispY, useColS)
        GraphicsWindow.SetPixel(dispX+2  dispY, useColS)
        
        dispY = dispY + 1
        GraphicsWindow.SetPixel(dispX    dispY, useColF)
        GraphicsWindow.SetPixel(dispX+1  dispY, useColS)
        
      EndFor
    EndIf
    
  EndFor
  realVal = X_MIN
  imagineVal = imagineVal + deltaY
EndFor

' stop timer
Timer.Pause()

Sub Counter
  
  elapsed = elapsed + 1
  GraphicsWindow.Title = "Mandelbrot 3D    •••    Elapsed: " + elapsed + " s"
  
EndSub
