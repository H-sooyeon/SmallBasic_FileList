'================================================

'INITIALISATION

'================================================



'Create GraphicsWindow and draw the ground level (equivalent to 1 pixel = 1m)

gw = 800

gh = 600

ground = 500 ' Y coord of the ground

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.DrawLine(0,ground,gw,ground)

radius = 10 'Radius of rock

rock = Shapes.AddEllipse(2*radius,2*radius) 'Create the rock shape

'Rock position - start point

rockPosX = radius

rockPosY = ground-radius

Shapes.Move(rock,rockPosX-radius,rockPosY-radius) 'Centre at (rockPosX,rockPosY)

'Rock velocity - 45 degrees up and right - note that up is negative since the Y direction increases down on the screen

rockVelX = 60

rockVelY = -60

'Rock acceleration - just gravity

rockAccX = 0

rockAccY = 10

'Update timestep

dt = 0.01



'================================================

'MAIN PROGRAM

'================================================



While (rockPosY <= ground-radius)

  start = Clock.ElapsedMilliseconds

  'Update velocity using acceleration

  rockVelX = rockVelX + dt*rockAccX

  rockVelY = rockVelY + dt*rockAccY

  'Update position

  rockPosX = rockPosX + dt*rockVelX

  rockPosY = rockPosY + dt*rockVelY

  'Move the rock

  Shapes.Move(rock,rockPosX-radius,rockPosY-radius)

  'Delay in ms

  delay = 1000*dt - (Clock.ElapsedMilliseconds-start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile


