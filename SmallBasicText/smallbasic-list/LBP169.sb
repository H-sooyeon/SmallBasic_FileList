' Peach, Plum, Cherry
' Copyright Â© 2016 Nonki Takahashi.  The MIT License.
'
GraphicsWindow.Title = "Peach, Plum, Cherry"
GraphicsWindow.BackgroundColor = "SkyBlue"
GraphicsWindow.PenWidth = 0
Turtle.PenUp()
Turtle.Speed = 8
edge = 50
size = 50
half = size / 2
x = 140
y = 220
type = "peach"
Flower()
x = 340
y = 220
type = "plum"
Flower()
x = 540
y = 220
type = "cherry"
Flower()
Sub Flower
  Turtle.MoveTo(x, y)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(x - 45, y + 50, type)
  Turtle.Angle = 270
  For i = 0 To 4
    x = Turtle.X
    y = Turtle.Y
    GraphicsWindow.BrushColor = "Pink"
    ell = Shapes.AddEllipse(size, size)
    Shapes.Move(ell, x - half, y - half)
    If type <> "plum" Then
      Turtle.Turn(-126)
      If type = "peach" Then
        Turtle.Move(edge / 1.414)
        x = Turtle.X
        y = Turtle.Y
        a = Turtle.Angle
        GraphicsWindow.BrushColor = "Pink"
        tri = Shapes.AddTriangle(0, 0, -half, 0, 0, -half)
        Shapes.Move(tri, x, y)
        Shapes.Rotate(tri, a - 135)
        Turtle.Turn(180)
        Turtle.Move(edge / 1.414)
      Else
        Turtle.Move(edge)
        x = Turtle.X
        y = Turtle.Y
        a = Turtle.Angle
        GraphicsWindow.BrushColor = "SkyBlue"
        tri = Shapes.AddTriangle(-half, -half, -half, 0, 0, -half)
        Shapes.Move(tri, x, y)
        Shapes.Rotate(tri, a - 135)
        Turtle.Turn(180)
        Turtle.Move(edge)
      EndIf
      Turtle.Turn(-54)
    EndIf
    Turtle.Move(edge)
    Turtle.Turn(72)
  EndFor
EndSub
Turtle.Hide()
