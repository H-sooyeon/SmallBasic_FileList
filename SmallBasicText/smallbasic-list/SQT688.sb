'Create a set of shapes to group
'Note the indices are rectangle then wheels, but create the wheels first to appear below car body
GraphicsWindow.PenWidth = 0
GraphicsWindow.BrushColor = "Black"
carGroup[2] = Shapes.AddEllipse(20,20)
carGroup[3] = Shapes.AddEllipse(20,20)
GraphicsWindow.BrushColor = "Red"
carGroup[1] = Shapes.AddRectangle(100,30)
carGroup[4] = Shapes.AddTriangle(0,0,45,15,0,30) 'Triangle is moved w.r.t origin
carGroup[5] = Shapes.AddTriangle(0,0,-45,15,0,30)
'Set their relative offest positions and store - subract half width/heigth of rectange to center on car body
carOffsetX[1] = 0-50
carOffsetY[1] = 0-15
carOffsetX[2] = 10-50
carOffsetY[2] = 20-15
carOffsetX[3] = 70-50
carOffsetY[3] = 20-15
carOffsetX[4] = 100-50
carOffsetY[4] = -15
carOffsetX[5] = 0-50
carOffsetY[5] = -15
'The position - screen center to start
carX = GraphicsWindow.Width/2
carY = GraphicsWindow.Height/2
moveCar()

'Move with mouse
While ("True")
  carX = GraphicsWindow.MouseX
  carY = GraphicsWindow.MouseY
  moveCar()
  Program.Delay(20)
EndWhile

'Sub to move all shapes in the car group
Sub moveCar
  For i = 1 To Array.GetItemCount(carGroup)
    Shapes.Move(carGroup[i],carX+carOffsetX[i],carY+carOffsetY[i])
  EndFor
EndSub
