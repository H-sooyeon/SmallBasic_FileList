
'program:  PSN522
' prgram: 

gw = 600
gh = 600
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.Title = "3D Rubic Cube"
GraphicsWindow.BackgroundColor = "Black"
view = LD3DView.AddView(gw,gh,"True")
LD3DView.ResetCamera(view,-5,-5,-5,1,1,1,0,1,0)
LD3DView.AutoControl("true","true",10,1)
LD3DView.AddAmbientLight(view,"White")
image = ImageList.LoadImage("http://litdev.co.uk/game_images/rubic.png")
'image = ImageList.LoadImage(Program.Directory+"/rubic.png")
'LD3DView.SpecularExponent = 5

col[1] = "#CC0000" ' red  
col[2] = "#FFFFFF"  '  white
col[3] = "#ffd700" ' yellow  
col[4] = "#fF6000" 'orange  
col[5] = "#00BB00" 'green  
col[6] = "#0000EE" ' blue  
 


numBit = 0
For iFace = 1 To 6
  For i = 1 To 3
    For j = 1 To 3
      indices = "0:2:1 1:2:3"
      i1 = i-2.5
      i2 = i-1.5
      j1 = j-2.5
      j2 = j-1.5
      k1 = -1.5
      k2 = 1.5
      If (iFace = 1) Then
        p0 = k1+":"+i1+":"+j1
        p1 = k1+":"+i2+":"+j1
        p2 = k1+":"+i1+":"+j2
        p3 = k1+":"+i2+":"+j2
        x = 1
        y = i
        z = j
      ElseIf (iFace = 2) Then
        p0 = k2+":"+i1+":"+j2
        p1 = k2+":"+i2+":"+j2
        p2 = k2+":"+i1+":"+j1
        p3 = k2+":"+i2+":"+j1
        x = 3
        y = i
        z = j
      ElseIf (iFace = 3) Then
        p0 = j1+":"+k1+":"+i1
        p1 = j1+":"+k1+":"+i2
        p2 = j2+":"+k1+":"+i1
        p3 = j2+":"+k1+":"+i2
        x = j
        y = 1
        z = i
      ElseIf (iFace = 4) Then
        p0 = j2+":"+k2+":"+i1
        p1 = j2+":"+k2+":"+i2
        p2 = j1+":"+k2+":"+i1
        p3 = j1+":"+k2+":"+i2
        x = j
        y = 3
        z = i
      ElseIf (iFace = 5) Then
        p0 = i1+":"+j1+":"+k1
        p1 = i2+":"+j1+":"+k1
        p2 = i1+":"+j2+":"+k1
        p3 = i2+":"+j2+":"+k1
        x = i
        y = j
        z = 1
      ElseIf (iFace = 6) Then
        p0 = i1+":"+j2+":"+k2
        p1 = i2+":"+j2+":"+k2
        p2 = i1+":"+j1+":"+k2
        p3 = i2+":"+j1+":"+k2
        x = i
        y = j
        z = 3
      EndIf
      points = p0+" "+p1+" "+p2+" "+p3
      
      numBit = numBit+1
      'bitFace[numBit] = iFace
      bitX[numBit] = x
      bitY[numBit] = y
      bitZ[numBit] = z
      'bitCube[numBit] = (z-1)*9+(y-1)*3+x
      bitCol[numBit] = col[iFace]
      bit[numBit] = LD3DView.AddGeometry(view,points,indices,"",bitCol[numBit],"D")
      LD3DView.AddImage(view,bit[numBit],"0:0 1:0 0:1 1:1",image,"D")
      bitPos[numBit] = LD3DView.GetPosition(view,bit[numBit])
      LD3DView.SetCentre(view,bit[numBit],0,0,0,"R2")
    EndFor
  EndFor
EndFor

time = 1
repeat = 1

While ("True")
  xVal = 2
  yVal = 0
  zVal = 2
  angle = -90
  Rotate()
  
  xVal = 0
  yVal = 1
  zVal = 1
  angle = 180
  Rotate()
  
  xVal = 0
  yVal = 1
  zVal = 2
  angle = 90
  Rotate()
  
  xVal = 1
  yVal = 2
  zVal = 3
  angle = -270
  Rotate()
  
  xVal = 1
  yVal = 0
  zVal = 2
  angle = -90
  Rotate()
  
  xVal = 0
  yVal = 1
  zVal = 4
  angle = 180
  Rotate()
  
  xVal = 1
  yVal = 1
  zVal = 0
  angle = 90
  Rotate()
  
   xVal = 1
  yVal = 0
  zVal = 2
  angle = -270
  Rotate()
  
  xVal = 4
  yVal = 1
  zVal = 2
  angle = 360
  Rotate()
  
  xVal = 1
  yVal = 3
  zVal = 2
  angle = -180  
  Rotate()
  
EndWhile

Sub Rotate
  For i = 1 To numBit
    If (bitX[i] = xVal) Then
      LD3DView.AnimateRotation(view,bit[i],1,0,0,0,angle,time,repeat)
    ElseIf (bitY[i] = yVal) Then
      LD3DView.AnimateRotation(view,bit[i],0,1,0,0,angle,time,repeat)
    ElseIf (bitZ[i] = zVal) Then
      LD3DView.AnimateRotation(view,bit[i],0,0,1,0,angle,time,repeat)
    EndIf
  EndFor
  
  Program.Delay(1000*time)
  
  LDGraphicsWindow.PauseUpdates()
  lastCol = bitCol
  For i = 1 To numBit
    If (bitX[i] = xVal Or bitY[i] = yVal Or bitZ[i] = zVal) Then
      pos1 = bitPos[i]
      For j = 1 To numBit
        If (bitX[j] = xVal Or bitY[j] = yVal Or bitZ[j] = zVal) Then
          pos2 = LD3DView.GetPosition(view,bit[j])
          distSquared = (pos1[1]-pos2[1])*(pos1[1]-pos2[1]) + (pos1[2]-pos2[2])*(pos1[2]-pos2[2]) + (pos1[3]-pos2[3])*(pos1[3]-pos2[3])
          If (distSquared < 0.001) Then 'bit j is now where i started
            bitCol[i] = lastCol[j]
            LD3DView.ResetMateral(view,bit[i],bitCol[i],"D")
            LD3DView.AddImage(view,bit[i],"0:0 1:0 0:1 1:1",image,"D")
          EndIf
        EndIf
      EndFor
    EndIf
  EndFor
  For i = 1 To numBit
    LD3DView.RotateGeometry2(view,bit[i],1,0,0,0)
  EndFor
  Program.Delay(10)
  LDGraphicsWindow.ResumeUpdates()
EndSub
