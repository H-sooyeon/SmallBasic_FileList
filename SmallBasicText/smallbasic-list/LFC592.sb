'-------------------------------------------step 1-----------------------------------------------------------------------
GraphicsWindow.Hide() 'hide the window till the shapes are hidden otherwise they flash
gw = 910
gh = 610
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.Top = 10
GraphicsWindow.Left = (Desktop.Width - gw) / 2 'centres window in the middle of the screen
GraphicsWindow.Title = "Show What You Know - Curriculum 3.4: Events & Interactivity"
GraphicsWindow.CanResize = "false"

GraphicsWindow.PenWidth = 3
GraphicsWindow.DrawRectangle(5, 5, gw-10, gh - 150)
GraphicsWindow.DrawRectangle(5, gh-137, 225, 133)
GraphicsWindow.DrawRectangle(5+8+225, gh-137, 225, 133)
GraphicsWindow.DrawRectangle(5+(8*2)+(225*2), gh-137, 225, 133)
GraphicsWindow.DrawRectangle(5+(8*3)+(225*3), gh-137, 200, 133)
'-------------------------------------------step 2------------------------------------------------------------------------
'caption is an Array
caption = "1=Triangle;2=Rectangle;3=Circle;4=Rotate;5=Zoom;6=Draw;7=Clear;8=Exit"

For i = 1 To 3
  button[i] = Controls.AddButton(caption[i], (225-180)/2, (gh-133)+((i-1)*43))
  Controls.SetSize(button[i], 180, 38)
EndFor

posX = "1=253;2=363;3=488;4=598;5=763" 'I just set these using trial & error

For i = 4 To 8
  button[i] = Controls.AddButton(caption[i], posX[i-3], gh-105)
  Controls.SetSize(button[i], 80, 80)
EndFor
'----------------------------------------------step 3 & 8---------------------------------------------------------------------
Controls.ButtonClicked = ButtonClicked
GraphicsWindow.MouseMove = MouseDrag
'-------------------------------------------------step 4------------------------------------------------------------------
shp[1] = Shapes.AddTriangle(0,0, 50,0, 25,40)
shp[2] = Shapes.AddRectangle(50, 60)
shp[3] = Shapes.AddEllipse(50, 50)
HideShapes()

GraphicsWindow.Show() 'show the window after the shapes are hidden otherwise they flash
'-------------------------------------------------step 5, 6 & 7------------------------------------------------------------------
Sub ButtonClicked
  If Controls.GetButtonCaption(Controls.LastClickedButton) = "Triangle" Then
    Shapes.ShowShape(shp[1])
    j = 1
    InitAngleAndZoom()
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Rectangle" Then
    Shapes.ShowShape(shp[2])
    j = 2
    InitAngleAndZoom()
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Circle" Then
    Shapes.ShowShape(shp[3])
    j = 3
    InitAngleAndZoom()
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Rotate" Then
    angle = angle + 25
    Shapes.Rotate(shp[j], angle)
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Zoom" Then
    zoom = zoom + 0.1
    Shapes.Zoom(shp[j], zoom, zoom)
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Draw" Then 'step 8
    startDrag = "yes"
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Clear" Then
    HideShapes()
  ElseIf Controls.GetButtonCaption(Controls.LastClickedButton) = "Exit" Then
    Program.End()
  EndIf
EndSub
'-------------------------------Subroutines----------------------------------------------------------------------
Sub MouseDrag  
  If startDrag = "yes" Then    
    x = GraphicsWindow.MouseX
    y = GraphicsWindow.MouseY
    If y < gh-150+3 And y > 5 And x > 5 And x < gw-10+3 Then
      If Mouse.IsLeftButtonDown = "true" Then    
        GraphicsWindow.DrawLine(OrgX, OrgY, x, y)  
      Endif
      OrgX = x
      OrgY = y
    EndIf
  EndIf
EndSub

Sub HideShapes
  posX = "1=150;2=350;3=600"  
  For i = 1 To Array.GetItemCount(shp)
    Shapes.Move(shp[i], posX[i], 200)
    Shapes.HideShape(shp[i])
  EndFor
EndSub

Sub InitAngleAndZoom
  angle = 0
  zoom = 1
EndSub
