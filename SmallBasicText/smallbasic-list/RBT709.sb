
'Alien chase game for SmallBasic by oldgrandadjoe Jan 2010

'Use the direction keys to move the target site over the alien then shoot using the spacebar.
'x key will pause the site movement. The lazer takes a second or two to recharge between shots.
'40 aliens will appear with increasing difficulty, shooting them quickly scores high points.
'Failing to shoot an alien after about 30 secs allows it to escape and you don't score.
'Have fun !


GraphicsWindow.Width = 1000
GraphicsWindow.Height = 600
GraphicsWindow.BackgroundColor = "black"
' Center it on desktop
GraphicsWindow.Left = Desktop.Width / 2 - GraphicsWindow.Width / 2
GraphicsWindow.Top = Desktop.Height / 2 - GraphicsWindow.Height / 2
score = 0
lazer = 100
alienhits = 0
alienesc = 0
rhstext ()
newscreen ()
'load  images and sounds
alien = ImageList.LoadImage("http://www.iconarchive.com/icons/etherbrian/robot-menace/32/Electroid-icon.png")
al = Shapes.AddImage (alien)
site = ImageList.LoadImage("http://www.iconarchive.com/icons/calle/smith-and-wesson/32/Target-icon.png")
site = Shapes.AddImage (site)
Sound.Play ("http://www.a1freesoundeffects.com/popular12008/explosion.mp3")
Sound.Stop ("http://www.a1freesoundeffects.com/popular12008/explosion.mp3")
Sound.Play ("http://www.a1freesoundeffects.com/popular12008/luger.mp3")
Sound.Stop ("http://www.a1freesoundeffects.com/popular12008/luger.mp3")
Sound.Play ("http://www.a1freesoundeffects.com/popular12008/fart4.mp3")
Sound.Stop ("http://www.a1freesoundeffects.com/popular12008/fart4.mp3")

'Start new alien ..............................................................
For runs = 1 To 40
  ra = Math.GetRandomNumber(360) 'alien start position
  x = 500 + (Math.cos (ra) * 280)
  y = 300 + (Math.Sin (ra) * 280)  
  Shapes.SetOpacity (al, 100)
  Shapes.SetOpacity (site, 75)
  xsite = 500 ' site x and y and drawsite
  ysite = 300
  Shapes.Move(site, xsite, ysite)  
  targetw = 32 ' set screen and target
  targeth = 32 'target width and height  
  Shapes.Move(al, x, y)
  deltaX = 1   'randomise target direction 
  c = Math.GetRandomNumber(100)  
  If ( c > 50) Then
    deltaX = - deltaX
  EndIf
  deltaY = 1
  c = Math.GetRandomNumber(100)
  If ( c > 50) Then
    deltaY = - deltaY 
  EndIf  
  maxw = 800 ' max screen for target
  maxh = 600  
  For i = 1 To 1000 'move target...................................................main loop.
    If lazer<100 and num <> 83 then 'charge lazer
      lazer = lazer + 1
      If lazer=100 then
        GraphicsWindow.BrushColor = "palegreen"
        GraphicsWindow.FillRectangle(850,340,100,10)
      endif      
    endif
    rand = Math.GetRandomNumber(100) ' for random target change direction     
    x = x  + deltaX
    y = y + deltaY    
    If (x + targetw > maxw Or rand = 1) Then ' keep target in bounds or random change
      deltaX = -1 
    EndIf
    If ( x <= 0 Or rand =2) Then
      deltaX = 1  
    EndIf
    If (y + targeth > maxh Or rand =3) Then
      deltaY = -1
    EndIf
    If ( y <= 0 Or rand = 4) Then
      deltaY = 1  
    EndIf    
    targeth = 32+(20-runs) 'set new target size and draw target
    targetw = 32+(20-runs)    
    Program.Delay(20)    
    Shapes.animate(al, x + (targetw - 32)/2, y + (targeth - 32)/2,1)
    Shapes.Zoom(al,((32/(12+runs))),((32/(12+runs))))    
    dxsite = 0    'move site
    dysite = 0
    char = GraphicsWindow.LastKey
    num = text.GetCharacterCode(char)
    if num = 82 and xsite < 800 Then  
      dxsite = 2
      dysite = 0  
    EndIf
    if num = 76 Then  
      dxsite = -2
      dysite = 0
    EndIf
    if num = 85 Then  
      dxsite = 0
      dysite = -2  
    EndIf
    if num = 68 Then  
      dxsite = 0
      dysite = 2  
    EndIf
    
    Shapes.animate(site,xsite-12, ysite-12,1)
    xsite = xsite + dxsite
    ysite = ysite + dysite    
    
    'check for hit and spacebar fire .........................................
    
    char = GraphicsWindow.LastKey      'fire!!!!
    num = text.GetCharacterCode(char)
    If num = 83 and lazer = 100 then  'char spacebar = 83
      Sound.stop ("http://www.a1freesoundeffects.com/popular12008/luger.mp3")
      Sound.play ("http://www.a1freesoundeffects.com/popular12008/luger.mp3")
      Program.Delay (20)
      lazer=80
      GraphicsWindow.BrushColor = "red"
      GraphicsWindow.FillRectangle(850,340,100,10)
      if num = 83 Then    'check for hit 
        If xsite + 5 > x And xsite +5 < x + targetw Then
          If ysite + 5 > y And ysite +5 < y + targeth Then            
            Sound.Stop ("http://www.a1freesoundeffects.com/popular12008/explosion.mp3")
            Sound.Play ("http://www.a1freesoundeffects.com/popular12008/explosion.mp3")
            Shapes.SetOpacity (al, 50)
            Shapes.SetOpacity (site, 25)
            hitalien = hitalien +1
            rhstext ()        
            'circular explosion
            rad = targeth/80
            drad = targeth/2
            For exi = 1 To 25              
              For j = 1 To 100
                ra = Math.GetRandomNumber(360) 'angle
                rd = Math.GetRandomNumber(rad) 'radius
                ry = (Math.cos (ra) * rd)
                rx = (Math.Sin (ra) * rd)
                GraphicsWindow.SetPixel( rx + x+drad, ry + y+drad, "red")
                GraphicsWindow.SetPixel( rx/2 +x+drad,ry/2 + y+drad, "yellow")
              endfor              
              rad = rad + drad/4 'sets growth of explosion              
            endfor            
            dscore  = 1000 - i
            score = score + dscore
            rhstext ()
            
            Goto bot
          endif
        endif
      EndIf
    EndIf
    Program.Delay(5)
    If i = 1000 then
      Sound.stop ("http://www.a1freesoundeffects.com/popular12008/fart4.mp3")
      Sound.play ("http://www.a1freesoundeffects.com/popular12008/fart4.mp3") 
      Program.Delay (100)
    endif
  endfor
  bot:
  rhstext ()
EndFor
Shapes.SetOpacity (al,0)
Shapes.SetOpacity (site,0)

Sub rhstext
  GraphicsWindow.BrushColor = "darkslategray"
  GraphicsWindow.FillRectangle(800,0,1000,600)
  GraphicsWindow.BrushColor = "yellow"
  GraphicsWindow.FontSize = 24
  GraphicsWindow.DrawText(860,100,"score")
  GraphicsWindow.DrawText(855,130,score)
  GraphicsWindow.DrawText(840,200,"alien hits")
  GraphicsWindow.DrawText(885,230,hitalien)
  alienesc = runs - hitalien
  GraphicsWindow.DrawText(820,400,"alien escapes")
  GraphicsWindow.DrawText(885,430,alienesc)
  GraphicsWindow.DrawText(870,300,"lazer")
  if lazer =100 then
    GraphicsWindow.BrushColor = "palegreen"
  else
    GraphicsWindow.BrushColor = "red"
  endif
  GraphicsWindow.FillRectangle(850,340,100,10)  
EndSub
Sub newscreen
  GraphicsWindow.BrushColor = "white"
  For i = 1 To 100
    xstar = Math.GetRandomNumber(800)  
    ystar = Math.GetRandomNumber(600)
    GraphicsWindow.FillEllipse(xstar,ystar,2,2)    
  EndFor  
  dbig = Math.GetRandomNumber(100)+100
  xbig = Math.GetRandomNumber(200)+100
  ybig = Math.GetRandomNumber(200)
  
  GraphicsWindow.BrushColor = "gray"
  GraphicsWindow.penColor = "gray"
  GraphicsWindow.FillEllipse(xbig,ybig,dbig,dbig)
  GraphicsWindow.drawEllipse(xbig-dbig/2,ybig+dbig/2.4,dbig*2,dbig*.2)
EndSub
GraphicsWindow.DrawText(830,500,"GAME OVER")
