'Simon Letters -- Project idea for the topic of Files and Arrays

'Matthew L. Parets



TextWindow.ForegroundColor = "black"

TextWindow.BackgroundColor = "white"

TextWindow.Clear()

TextWindow.ForegroundColor = "red"

TextWindow.WriteLine("Simon Letters")

TextWindow.ForegroundColor = "black"



TextWindow.WriteLine("During its turn the computer will give you a list of letters. During your turn,")

TextWindow.WriteLine("you have to enter the letters in the same order that the computer displayed")

TextWindow.WriteLine("them. If you make a mistake the game is over. Good Luck!")

TextWindow.WriteLine("")



letterList[1] = "A"

letterList[2] = "B"

letterList[3] = "C"

letterList[4] = "D"



roundNumber = 0

match = "true"



While match = "true"

  roundNumber = roundNumber + 1

  

  newChoice = Math.GetRandomNumber(4)

  computerList[roundNumber] = letterList[newChoice]

  TextWindow.Write("Round ")

  TextWindow.WriteLine(roundNumber)

  

  TextWindow.WriteLine("Computers Turn:")

  TextWindow.WriteLine("")

  For i = 1 To roundNumber

    TextWindow.WriteLine(computerList[i])

  EndFor

  TextWindow.WriteLine("")

  TextWindow.WriteLine("Press ENTER to take your turn")

  enter = TextWindow.Read() 'This is just to pause the program, value is not needed

  

  TextWindow.Clear()

  

  TextWindow.WriteLine("Players Turn -- Repeat the numbers that the computer displayed")

  TextWindow.WriteLine("")

  For j = 1 To roundNumber

    TextWindow.Write("Letter ")

    TextWindow.Write(j)

    TextWindow.Write(": ")

    letter = TextWindow.Read()

    letter = Text.ConvertToUpperCase(letter)

    playerList[j] = letter

  EndFor

  

  TextWindow.WriteLine("")

  

  match = "true"

  For k = 1 To roundNumber

    TextWindow.Write(computerList[k])

    TextWindow.Write(" --> ")

    TextWindow.Write(playerList[k])

    If computerList[k] <> playerList[k] then

      match = "false"

      TextWindow.WriteLine("    Buzzz!")

    Else 

      TextWindow.Writeline("    :-)")

    EndIf

  EndFor

  

  TextWindow.WriteLine("")

  

  If match = "true" Then

    TextWindow.WriteLine("Sucess!!")

  Else

    TextWindow.WriteLine("Buzzzz --- Sorry, you missed")

    TextWindow.Write("Good game, you reached round ")

    TextWindow.WriteLine(roundNumber)

  EndIf

  

  TextWindow.WriteLine("")

  TextWindow.WriteLine("Press ENTER to continue")  

  enter = TextWindow.Read() 'This is just to pause the program, value is not needed

  TextWindow.Clear()  

EndWhile



TextWindow.WriteLine("Thank you, Come again!")


