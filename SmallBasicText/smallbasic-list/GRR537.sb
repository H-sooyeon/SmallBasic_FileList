array[1] = "YouTube"

array[2] = "Programming"

array[3] = "You"

array[4] = "Professional"

array[5] = "Life"



len = Array.GetItemCount(array)

'bubble sort

For i = 1 To len

  For j = i+1 To len

    str1 = array[i]

    str2 = array[j]

    Compare()

    If (result = 2) Then 'swap

      array[i] = str2

      array[j] = str1

    EndIf

  EndFor

  TextWindow.WriteLine(array[i])

EndFor



'Subroutine to compare 2 strings lexically

'Input: 2 strings are held in variables called str1 and str2

'Output: result = 1 or 2 indicating the first lexical string, 0 if they are identical

Sub Compare

  'Assume shortest string comes first by default, in case one is an identical substring

  If (Text.GetLength(str1) < Text.GetLength(str2)) Then

    result = 1

  ElseIf (Text.GetLength(str2) < Text.GetLength(str1)) Then

    result = 2

  Else

    'If all checks fail to find a difference they are identical

    result = 0

  EndIf

  

  'Check characters looking for a difference

  For k = 1 To Math.Min(Text.GetLength(str1),Text.GetLength(str2))

    char1 = Text.GetSubText(str1,k,1)

    char2 = Text.GetSubText(str2,k,1)

    'Lower case comparison

    code1 = Text.GetCharacterCode(Text.ConvertToLowerCase(char1))

    code2 = Text.GetCharacterCode(Text.ConvertToLowerCase(char2))

    If (code1 < code2) Then

      result = 1

      Goto finished

    ElseIf (code2 < code1) Then

      result = 2

      Goto finished

    Else

      'Check case (capitals first) if case invariant check finds identical values

      code1 = Text.GetCharacterCode(char1)

      code2 = Text.GetCharacterCode(char2)

      If (code1 < code2) Then

        result = 1

        Goto finished

      ElseIf (code2 < code1) Then

        result = 2

        Goto finished

      EndIf

    EndIf

  EndFor

  

  finished:

EndSub


