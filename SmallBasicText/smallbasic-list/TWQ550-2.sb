M = 1
R = Math.GetRadians(10)
S = 1
CubeX = 400
CubeY = 300
CubeSize = 50
Info = "True"
'Event handling
GraphicsWindow.KeyDown=OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp
CreateProgram()
While Run = "True"
  If MovingDown = "True" Then
    CubeY = CubeY + M
    NeedToDraw = "True"
  EndIf
  If MovingUp = "True" Then
    CubeY = CubeY - M
    NeedToDraw = "True"
  EndIf
  If MovingRight = "True" Then
    CubeX = CubeX + M
    NeedToDraw = "True"
  EndIf
  If MovingLeft = "True" Then
    CubeX = CubeX - M
    NeedToDraw = "True"
  EndIf
  If RotatingDown = "True" Then
    angleV=angleV-R
    NeedToDraw = "True"
  EndIf
  If RotatingUp = "True" Then
    angleV=angleV+R
    NeedToDraw = "True"
  EndIf
  If RotatingRight = "True" Then
    angleH=angleH-R
    NeedToDraw = "True"
  EndIf
  If RotatingLeft = "True" Then
    angleH=angleH+R
    NeedToDraw = "True"
  EndIf
  If ZoomingPlus = "True" Then
    CubeSize=CubeSize+S
    NeedToDraw = "True"
  EndIf
  If ZoomingMinus = "True" Then
    CubeSize=CubeSize-S
    NeedToDraw = "True"
    If CubeSize < 1 Then
      CubeSize = 1
    EndIf
  EndIf  
  If NeedToDraw = "True" Then
    Draw()
  EndIf
EndWhile

Sub CreateProgram
  point[1]["x"]=1
  point[1]["y"]=1
  point[1]["z"]=1
  
  point[2]["x"]=-1
  point[2]["y"]=1
  point[2]["z"]=1
  
  point[3]["x"]=1
  point[3]["y"]=-1
  point[3]["z"]=1
  
  point[4]["x"]=1
  point[4]["y"]=1
  point[4]["z"]=-1
  
  point[5]["x"]=-1
  point[5]["y"]=-1
  point[5]["z"]=1
  
  point[6]["x"]=-1
  point[6]["y"]=1
  point[6]["z"]=-1
  
  point[7]["x"]=1
  point[7]["y"]=-1
  point[7]["z"]=-1
  
  point[8]["x"]=-1
  point[8]["y"]=-1
  point[8]["z"]=-1
  
  angleH=30*math.Pi/180
  angleV=30*math.Pi/180
  NeedToDraw = "False"
  Run = "True"
  GraphicsWindow.Show()
  GraphicsWindow.Title = "3D Cube"
  GraphicsWindow.Width = 800
  GraphicsWindow.Height = 600
  GraphicsWindow.Left = Desktop.Width/2-GraphicsWindow.Width/2
  GraphicsWindow.Top = Desktop.Height/2-GraphicsWindow.Height/2
  If Info = "True" Then
    TextWindow.Show()
    TextWindow.Title = "3D info"
    TextWindow.Left = Desktop.Width/2-330
    TextWindow.Top = Desktop.Height/2+330
  EndIf 
  RotatingLeft = "False"
  RotatingRight = "False"
  RotatingUp = "False"
  RotatingDown = "False"
  MovingLeft = "False"
  MovingRight = "False"
  MovingUp = "False"
  MovingDown = "False"
  ZoomingPlus = "False"
  ZoomingMinus = "False"
  Draw()
EndSub

Sub OnKeyDown
  K = GraphicsWindow.LastKey
  If K = "A" Then 
    RotatingLeft = "True"
  EndIF
  If K = "S" Then 
    RotatingRight = "True"
  EndIF
  If K = "W" Then
    RotatingUp = "True"
  EndIf
  If K = "Z" Then 
    RotatingDown = "True"
  EndIf
  If K = "Up" Then
    MovingUp = "True"
  EndIf
  If K = "Down" Then
    MovingDown = "True"
  EndIf
  If K = "Right" Then
    MovingRight = "True"
  EndIf
  If K = "Left" Then
    MovingLeft = "True"
  EndIf
  If K = "X" Then
    ZoomingPlus = "True"
  EndIf
  If K = "C" Then
    ZoomingMinus = "True"
  EndIf
EndSub

Sub OnKeyUp
  K = GraphicsWindow.LastKey
  If K = "A" Then 
    RotatingLeft = "False"
  EndIF
  If K = "S" Then 
    RotatingRight = "False"
  EndIF
  If K = "W" Then
    RotatingUp = "False"
  EndIf
  If K = "Z" Then 
    RotatingDown = "False"
  EndIf
  If K = "Up" Then
    MovingUp = "False"
  EndIf
  If K = "Down" Then
    MovingDown = "False"
  EndIf
  If K = "Right" Then
    MovingRight = "False"
  EndIf
  If K = "Left" Then
    MovingLeft = "False"
  EndIf
  If K = "X" Then
    ZoomingPlus = "False"
  EndIf
  If K = "C" Then
    ZoomingMinus = "False"
  EndIf
EndSub

Sub calcPoints
  For teller=1 To 8
    ' draai in horizontale richting  
    'x' = x*cos q - y*sin q
    point[teller]["x1"]=point[teller]["x"]*math.cos(angleH)-point[teller]["y"]*math.sin(angleH)
    'y' = x*sin q + y*cos q
    point[teller]["y1"]=point[teller]["x"]*math.sin(angleH)+point[teller]["y"]*math.cos(angleH)
    'z' = z 
    point[teller]["z1"]=point[teller]["z"]
    ' draai in verticale richting
    'y' = x*cos q - y*sin q
    point[teller]["y2"]=point[teller]["y1"]*math.cos(angleV)-point[teller]["z1"]*math.sin(angleV)
    'z' = x*sin q + y*cos q
    point[teller]["z2"]=point[teller]["y1"]*math.sin(angleV)+point[teller]["z1"]*math.cos(angleV)
    'x' = x
    point[teller]["x2"]=point[teller]["x1"]
    'Calculate X And Y
    point[teller]["x2"] = point[teller]["x2"]*CubeSize
    point[teller]["y2"] = point[teller]["y2"]*CubeSize
    point[teller]["x2"] = point[teller]["x2"]+CubeX
    point[teller]["y2"] = point[teller]["y2"]+CubeY
  endfor
endsub

Sub Draw
  'Draw Info
  If Info = "True" Then
    ShowInfo()
  EndIf
  'Calculate Points
  calcPoints()
  'Delete Old Cube
  GraphicsWindow.Clear()
  ' Draw The Cube
  GraphicsWindow.DrawLine(point[1]["x2"],point[1]["y2"],point[2]["x2"],point[2]["y2"])
  GraphicsWindow.DrawLine(point[1]["x2"],point[1]["y2"],point[3]["x2"],point[3]["y2"])
  GraphicsWindow.DrawLine(point[1]["x2"],point[1]["y2"],point[4]["x2"],point[4]["y2"])
  GraphicsWindow.DrawLine(point[2]["x2"],point[2]["y2"],point[5]["x2"],point[5]["y2"])
  GraphicsWindow.DrawLine(point[2]["x2"],point[2]["y2"],point[6]["x2"],point[6]["y2"])
  GraphicsWindow.DrawLine(point[3]["x2"],point[3]["y2"],point[5]["x2"],point[5]["y2"])
  GraphicsWindow.DrawLine(point[3]["x2"],point[3]["y2"],point[7]["x2"],point[7]["y2"])
  GraphicsWindow.DrawLine(point[4]["x2"],point[4]["y2"],point[6]["x2"],point[6]["y2"])
  GraphicsWindow.DrawLine(point[4]["x2"],point[4]["y2"],point[7]["x2"],point[7]["y2"])
  GraphicsWindow.DrawLine(point[5]["x2"],point[5]["y2"],point[8]["x2"],point[8]["y2"])
  GraphicsWindow.DrawLine(point[6]["x2"],point[6]["y2"],point[8]["x2"],point[8]["y2"])
  GraphicsWindow.DrawLine(point[7]["x2"],point[7]["y2"],point[8]["x2"],point[8]["y2"])
  'Set NeedToDrawFalse
  NeedToDraw = "False"
EndSub

Sub ShowInfo
  TextWindow.Clear()
  TextWindow.WriteLine("3D info")
  TextWindow.WriteLine("CubeX = "+CubeX)
  TextWindow.WriteLine("CubeY = "+CubeY)
  TextWindow.WriteLine("Cube Size = "+CubeSize)
  TextWindow.WriteLine("Cube Horizontal Angle = "+angleH)
  TextWindow.WriteLine("Cube Vertical Angle = "+angleV)
EndSub
