' Fireworks

' Version 0.1

' Copyright Â© 2017 Nonki Takahashi.  The MIT License.



GraphicsWindow.Title = "Fireworks"

GraphicsWindow.BackgroundColor = "Black"

Init()

DrawStars()

While "True"

  fire = ""

  x = Math.GetRandomNumber(gw)

  y = Math.GetRandomNumber(gh / 2)

  Shapes.Move(mask, x - mr, y - mr)

  Shapes.ShowShape(mask)

  r = Math.GetRandomNumber(3) * 10 + 50

  n = 40

  c1 = Math.GetRandomNumber(256) - 1

  c2 = Math.GetRandomNumber(256) - 1

  i = Math.GetRandomNumber(3)

  If i = 1 Then

    fc = GraphicsWindow.GetColorFromRGB(255, c1, c2)

  ElseIf i = 2 Then

    fc = GraphicsWindow.GetColorFromRGB(c1, 255, c2)

  Else

    fc = GraphicsWindow.GetColorFromRGB(c1, c2, 255)

  EndIf

  GraphicsWindow.PenColor = fc

  GraphicsWindow.BrushColor = fc

  a = 0

  GraphicsWindow.PenWidth = 1

  For i = 1 To n

    _a = Math.GetRadians(a)

    a = a + 9

    _x = x + r * Math.Cos(_a)

    _y = y + r * Math.Sin(_a)

    GraphicsWindow.DrawLine(x, y, _x, _y)

  EndFor

  Shapes.HideShape(mask)

  a = 0

  GraphicsWindow.PenWidth = 0

  For k = 1 To 250

    a = Math.GetRandomNumber(n) * 9

    _a = Math.GetRadians(a)

    _r = Math.GetRandomNumber(r)

    fire[k] = Shapes.AddRectangle(1, 1)

    _x = x + _r * Math.Cos(_a)

    _y = y + _r * Math.Sin(_a)

    Shapes.Move(fire[k], _x, _y)

  EndFor

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FillEllipse(x - mr, y - mr, 2 * mr, 2 * mr)

  For j = 1 To 20

    n = Array.GetItemCount(fire)

    id = Array.GetAllIndices(fire)

    For i = 1 To n

      If fire[id[i]] <> "" Then

        rnd = Math.GetRandomNumber(4)

        If rnd = 1 Then

          Shapes.Remove(fire[id[i]])

          fire[id[i]] = ""

        Else

          x = Shapes.GetLeft(fire[id[i]])

          rnd = Math.GetRandomNumber(2)

          If rnd = 1 Then

            x = x - 1

          Else

            x = x + 1

          EndIf 

          y = Shapes.GetTop(fire[id[i]])

          Shapes.Move(fire[id[i]], x, y + 4)

        EndIf

      EndIF

    EndFor

    Program.Delay(50)

  EndFor

  n = Array.GetItemCount(fire)

  id = Array.GetAllIndices(fire)

  For i = 1 To n

    Shapes.Remove(fire[id[i]])

  EndFor

EndWhile



Sub DrawStars

  GraphicsWindow.PenWidth = 0

  GraphicsWindow.BrushColor = "#CCFFFFFF"

  For i = 1 To 500

    x = Math.GetRandomNumber(gw)

    y = Math.GetRandomNumber(gh)

    star = Shapes.AddRectangle(1, 1)

    Shapes.Move(star, x, y)

  EndFor

  GraphicsWindow.BrushColor = "Black"

  mr = 90

  mask = Shapes.AddEllipse(2 * mr, 2 * mr)

  Shapes.HideShape(mask)

EndSub



Sub Init

  gw = 598

  gh = 428

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

EndSub


