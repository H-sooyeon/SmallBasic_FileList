' SmallBasic Version 1.2
' Program: SieveOfEratosthenes 
' Changelog: 
' Author: Pappa Lapub
' Website: 
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev
' Comment: 
'  
' Variables: 
' ToDo: 
' ================================================================================
max = 100 '150
bLPC = "" ' LowestPrimeColoring,  eg. 30 (2*3*5) is colored like 2 and not like 3 or 5; all even numbers have same color
'                                   "False": eg. 30 is colored like 2, then like 3 and finally like 5.

w = Math.Floor(Math.SquareRoot(max))     '' squared OR number of columns
s = 30        ' ShapeSize for numbers s=width=height
fs = 12       ' FontSize in right column of primes

h = Math.Ceiling(max / w)
gw = (w+2)*s
gh = h*s

GetPrimes()

'dy = fs*nPrim - gh
gh = Math.Max(h*s, fs*nPrim)

GraphicsWindow.Top = 0
GraphicsWindow.CanResize = ""
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.Title = "Sieve Of Eratosthenes ("+ nPrim +" Primes 1-"+ max +")"
LDGraphicsWindow.Icon = "C:\Program Files (x86)\Microsoft\Small Basic\Projects\Icons\Sieve.ico"

GraphicsWindow.BrushColor = "DimGray"
GraphicsWindow.FillRectangle(w*s,0, 2*s,gh)             ' right side, 2 vert. columns
dx = Math.Remainder(max, w)
If dx > 0 Then
  GraphicsWindow.FillRectangle(dx*s,(h-1)*s, gw-dx*s,s) ' Fill up last horz. row (if dx > 0)
EndIf
GraphicsWindow.FillRectangle(0,h*s, (w+2)*s,gh-h*s)     ' horz. bottom row, if nPrime*fs > h*s

'' number fields
GraphicsWindow.FontSize = 13
GraphicsWindow.FontName = "Courier New"  ' FixedWidth
GraphicsWindow.BrushColor = "Black"

For y = 1 To h  '10
  For x = 1 To w  '10
    If n < max Then
      n = n + 1
      num = Text.GetCharacter(10) + Text.GetSubTextToEnd(Text.Append("  ",n), Text.GetLength(n)) 'numbers right aligned, prefix with 1 or 2 spc
      shp[n] = Shapes.AddText(num)
      Controls.SetSize(shp[n], s,s)
      Shapes.Move(shp[n], (x-1)*s,(y-1)*s)
    EndIf
  EndFor
EndFor

'' color multiples and draw right primes column
GraphicsWindow.FontSize = fs
For p = 1 To nPrim
  hue = 360 * p / nPrim - 1
  col = LDColours.HSLtoRGB(hue, 0.8,0.5)
  
  'c = 255 * (1 - p / nPrim)
  'col = GraphicsWindow.GetColorFromRGB(c,c,c)    'Shades of Gray (bright -> darker)
  
  GraphicsWindow.BrushColor = col   ' GraphicsWindow.GetRandomColor() ' cols[p]
  GraphicsWindow.DrawText((w+1)*s,(p-1)*fs, prim[p])
  
  For n = prim[p] To max Step prim[p]
    '' If Math.Remainder(n, prim[p]) = 0 Then         'Alt. store in done[] after being processed/colored
    If done[n] <> "True" Or bLPC <> "True" Then     ' 
      LDShapes.BrushColour(shp[n], col)
    EndIf
    done[n] = "True"
    Program.Delay(100)
  EndFor
EndFor
done = ""


GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.MouseDown = OnMouseDown


' //////////  SUBs  \\\\\\\\\\
Sub GetPrimes
  maxRt = Math.Floor(Math.SquareRoot(max))
  For i = 2 To max
    If elim[i] <> "True" Then
      nPrim = nPrim + 1
      prim[nPrim] = i
      If i <= maxRt Then
        For j = i*i To max Step i
          elim[j] = "True"
        EndFor
      EndIf
    EndIf
  EndFor
  elim = ""
EndSub

Sub FlashMultiples
  For i = n To max Step n
    LDShapes.AnimateOpacity(shp[i], 300,3)
  EndFor
EndSub


' //////////  EVENTs \\\\\\\\\\
Sub OnKeyDown
  If GraphicsWindow.LastKey = "Escape" Then
    Program.End()
  EndIf
EndSub

Sub OnMouseDown
  If GraphicsWindow.MouseX > w*s And done = "" Then
    n = prim[Math.Ceiling(GraphicsWindow.MouseY / fs)]
    FlashMultiples()
  EndIf
EndSub

