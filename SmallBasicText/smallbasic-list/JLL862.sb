gw = GraphicsWindow.Width
gh = GraphicsWindow.Height
GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.PenWidth = 0

GraphicsWindow.BrushColor = "Yellow"
sun = Shapes.AddEllipse(10,10)
sunX = gw/2
sunY = gh/2
Shapes.Move(sun,sunX-5,sunY-5)

nCommet = 25
For i = 1 To nCommet
  GraphicsWindow.BrushColor = "White"
  orbit[i] = Shapes.AddEllipse(4,4)
  orbitA[i] = 100+Math.GetRandomNumber(40)
  orbitB[i] = 40+Math.GetRandomNumber(20)
  orbitF[i] = Math.SquareRoot(orbitA[i]*orbitA[i]-orbitB[i]*orbitB[i])
  theta[i] = Math.GetRandomNumber(1000)/100
  Shapes.Zoom(orbit[i],0.4,0.4)
EndFor

nPlanet = 9
For i = nCommet+1 To nCommet+nPlanet
  GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
  orbit[i] = Shapes.AddEllipse(4,4)
  orbitA[i] = 20+Math.GetRandomNumber(100)
  orbitB[i] = orbitA[i]
  orbitF[i] = Math.SquareRoot(orbitA[i]*orbitA[i]-orbitB[i]*orbitB[i])
  theta[i] = Math.GetRandomNumber(1000)/100
  zoom = orbitA[i]/50
  Shapes.Zoom(orbit[i],zoom,zoom)
  
  If (Math.GetRandomNumber(2) = 1) Then
    GraphicsWindow.BrushColor = "LightGray"
    moon[i] = Shapes.AddEllipse(4,4)
    moonR[i] = 5+Math.GetRandomNumber(3)
    orbitMass[i] = 0.1*(2*Math.GetRandomNumber(2)-3) 'Mass of planet relative to sun,negative sign reverses moon orbit
    zoom = 0.5
    Shapes.Zoom(moon[i],zoom,zoom)
  EndIf
EndFor

nOrbit = nCommet+nPlanet
sunMass = 10 'Nominal sun mass, G = 1

While ("True")
  
  For i = 1 To nOrbit
    dx = orbitF[i]+orbitA[i]*Math.Cos(theta[i])
    dy = orbitB[i]*Math.Sin(theta[i])
    dist = Math.SquareRoot(dx*dx+dy*dy)
    
    orbitX = sunX+dx
    orbitY = sunY+dy
    Shapes.Move(orbit[i],orbitX-2,orbitY-2)
    
    dist = Math.Max(dist,2*sunMass) 'prevent excessively large speeds on closest approach
    theta[i] = theta[i] + sunMass/dist/Math.SquareRoot(dist)
    
    If (moon[i] <> "") Then
      moonTheta[i] = moonTheta[i]+sunMass*orbitMass[i]/moonR[i]/Math.SquareRoot(moonR[i])
      Shapes.Move(moon[i],orbitX-2+moonR[i]*Math.Cos(moonTheta[i]),orbitY-2+moonR[i]*Math.Sin(moonTheta[i]))
    EndIf
  EndFor
  
  Program.Delay(10)
EndWhile

