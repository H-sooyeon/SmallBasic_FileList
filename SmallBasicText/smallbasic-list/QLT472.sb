'Window

GraphicsWindow.Hide()

GraphicsWindow.Width = 1000

GraphicsWindow.Height = 600

GraphicsWindow.Left = (Desktop.Width - 1000) / 2

GraphicsWindow.Top = (Desktop.Height - 674) / 2

GraphicsWindow.Title = "Graphics Challenges"

GraphicsWindow.CanResize = "False"

GraphicsWindow.FontBold = "False"

GraphicsWindow.FontName = "Microsoft Sans Serif"

GraphicsWindow.PenWidth = 1



'Variables

CurrentMenu = ""

MaximumResources = 15

ValueLsBar = 0

DirectionMario = "None"

LeftMario = 391

TopMario = 89

RobotAngle = 0

RobotZoomLevel = .7

RobotOpacityLevel = 40

ShiftPressed = "False"

ControlPressed = "False"

RotateRobot = "False"

HwLeft = 0

HwTop = 0

DragHelpWindow = "False"

MaximumBubbles = 1000

Distance = 1200

Speed = 2200

ParticleSize = 200

CanAddEffect = "True"

DropDownCbAddVe = "False"

FireTop = 350

FireWidth = 1000

Flames = 250



'Colors

ClrGrayBasic = GraphicsWindow.GetColorFromRGB(50, 50, 50)

ClrGrayBack = GraphicsWindow.GetColorFromRGB(100, 100, 100)

ClrGrayBorder = GraphicsWindow.GetColorFromRGB(150, 150, 150)

ClrGrayText = GraphicsWindow.GetColorFromRGB(200, 200, 200)



'Main

AddLoadingScreen()

GraphicsWindow.Show()

LoadImages()



'CompleteLoading

Controls.SetSize(RecLsBar, 500, 10)

Program.Delay(500)

GraphicsWindow.Clear()

AddMainMenu()



'Subroutines - Custom

'********************

'Load Images

Sub LoadImages

  Load_ImgResources = "http://www.design.ddarsow.com/images/recover.png"

  GraphicsWindow.DrawResizedImage(Load_ImgResources, 400, 65, 200, 200)

  Load_ImgMario = ImageList.LoadImage("http://vignette4.wikia.nocookie.net/annoyingorange/images/b/b3/Super_mario.png/revision/latest?cb=20120728235111")

  AddProgressLsBar()

  Load_ImgMmBack = ImageList.LoadImage("http://orig10.deviantart.net/ca27/f/2006/321/4/e/motion_graphics_by_dulchis.jpg")

  AddProgressLsBar()

  Load_ImgBtnBack = ImageList.LoadImage("http://findicons.com/files/icons/694/longhorn_r2/256/back_button.png")

  AddProgressLsBar()

  Load_ImgRobot = ImageList.LoadImage("https://mir-s3-cdn-cf.behance.net/project_modules/disp/47e1c322329083.56310816f1004.png")

  AddProgressLsBar()

  Load_ImgMove = ImageList.LoadImage("http://icons.iconarchive.com/icons/shlyapnikova/toolbar-2/32/move-icon.png")

  AddProgressLsBar()

  Load_ImgRotate = ImageList.LoadImage("http://findicons.com/files/icons/1036/function/48/refresh.png")

  AddProgressLsBar()

  Load_ImgZoom = ImageList.LoadImage("http://findicons.com/files/icons/2321/plastic_xp_general/128/zoom.png")

  AddProgressLsBar()

  Load_ImgEye = ImageList.LoadImage("https://upload.wikimedia.org/wikipedia/commons/b/b1/Farm-Fresh_eye.png")

  AddProgressLsBar()

  Load_ImgBtnHelp = ImageList.LoadImage("https://en.opensuse.org/images/thumb/5/57/Icon-question.png/40px-Icon-question.png")

  AddProgressLsBar()

  Load_ImgBtnExit = ImageList.LoadImage("https://upload.wikimedia.org/wikipedia/commons/thumb/0/0c/Crystal_Clear_action_exit.png/40px-Crystal_Clear_action_exit.png")

  AddProgressLsBar()

  Load_ImgClose = ImageList.LoadImage("https://cdn4.iconfinder.com/data/icons/32x32-free-design-icons/32/Close.png")

  AddProgressLsBar()

  Load_ImgBulletPoint = ImageList.LoadImage("http://www.iconki.com/icons/Software-Applications/Good-24x24/bullet_ball_glass_blue.png")

  AddProgressLsBar()

  Load_ImgBtnOk = ImageList.LoadImage("http://wiki.thedarkmod.com/images/archive/7/75/20071111182411!Button_ok.png")

  AddProgressLsBar()

  Load_ImgGrass = ImageList.LoadImage("http://1.bp.blogspot.com/-YzHacJervsA/TxhuiS8HugI/AAAAAAAAA6Q/EKG9OoknNuo/s200/felt+grass+tile.jpg")

  AddProgressLsBar()

  Load_ImgSoil = ImageList.LoadImage("http://static.wixstatic.com/media/e967e4_e3d6344e5077452f90a9dcc6fdc2d7ee.jpg_256")

  AddProgressLsBar()

EndSub



'AddLoadingScreen

Sub AddLoadingScreen

  CurrentMenu = "LoadingScreen"

  GraphicsWindow.PenColor = ClrGrayBorder

  GraphicsWindow.BrushColor = ClrGrayBack

  RecLsTitleBack = Shapes.AddRectangle(350, 35)

  Shapes.Move(RecLsTitleBack, 325, 350)

  GraphicsWindow.BrushColor = ClrGrayText

  LblLsTitle = Shapes.AddText("Loading resources")

  Shapes.Move(LblLsTitle, 450, 360)

  GraphicsWindow.BackgroundColor = ClrGrayBasic

  GraphicsWindow.BrushColor = "Black"

  RecLsBarBack = Shapes.AddRectangle(500, 10)

  Shapes.Move(RecLsBarBack, 250, 450)

  GraphicsWindow.BrushColor = "LawnGreen"

  RecLsBar = Shapes.AddRectangle(0, 10)

  Shapes.Move(RecLsBar, 250, 450)

EndSub



'AddProgressLsBar

Sub AddProgressLsBar

  ValueLsBar = ValueLsBar + (500 / MaximumResources)

  Controls.SetSize(RecLsBar, ValueLsBar, 10)

EndSub



'AddMainMenu

Sub AddMainMenu

  CurrentMenu = "MainMenu"

  GraphicsWindow.BackgroundColor = ClrGrayBasic

  GraphicsWindow.DrawResizedImage(Load_ImgMmBack, 0, 0, 1000, 600)

  GraphicsWindow.PenColor = ClrGrayBorder

  GraphicsWindow.BrushColor = ClrGrayBack

  RecMmTitleBack = Shapes.AddRectangle(350, 35)

  Shapes.Move(RecMmTitleBack, 325, 150)

  RecMmBtnBack = Shapes.AddRectangle(300, 270)

  Shapes.Move(RecMmBtnBack, 350, 184)

  Shapes.SetOpacity(RecMmBtnBack, 50)

  GraphicsWindow.PenColor = ClrGrayBorder

  GraphicsWindow.BrushColor = ClrGrayText

  LblMmTitle = Shapes.AddText("Select a program")

  Shapes.Move(LblMmTitle, 450, 160)

  GraphicsWindow.BrushColor = "Black"

  CaptionsBtnMm = "1=Move image with keys;2=Move image with mouse;3=Moving river;4=Visual effects;"

  For Times = 1 To 4

    BtnMainMenu[Times] = Controls.AddButton("", 400, 190 + Times * 45)

    Controls.SetSize(BtnMainMenu[Times], 200, 30)

    Controls.SetButtonCaption(BtnMainMenu[Times], CaptionsBtnMm[Times])

  EndFor

  EffectFadeMm = Shapes.AddRectangle(1000, 600)

  Shapes.SetOpacity(EffectFadeMm, 0)

  Shapes.HideShape(EffectFadeMm)

  ImgExit = Shapes.AddImage(Load_ImgBtnExit)

  Shapes.Move(ImgExit, 480, 500)

  BtnExit = Controls.AddButton("", 480, 500)

  Controls.SetSize(BtnExit, 40, 40)

  Shapes.SetOpacity(BtnExit, 0)

EndSub



'LoadMoveImgWithKeysSoft

Sub LoadMoveImgWithKeysSoft

  CurrentMenu = "ImgKeys"

  ImportBackBtn()

  LeftMario = 391

  TopMario = 89

  ImgMario = Shapes.AddImage(Load_ImgMario)

  Shapes.Move(ImgMario, LeftMario, TopMario)

  GraphicsWindow.BrushColor = ClrGrayText

  GraphicsWindow.DrawText(10, 580, "Press the Spacebar to reset Mario's position")

EndSub



'LoadMoveImgWithMouseSoft

Sub LoadMoveImgWithMouseSoft

  CurrentMenu = "ImgMouse"

  ResetRobotSettings()

  ImportBackBtn()

  ImgRobot = Shapes.AddImage(Load_ImgRobot)

  Shapes.Move(ImgRobot, 200, 80)

  Shapes.SetOpacity(ImgRobot, RobotOpacityLevel)

  Shapes.Zoom(ImgRobot, .7, .7)

  GraphicsWindow.BrushColor = ClrGrayText

  LblRobotCoo = Shapes.AddText("")

  Shapes.Move(LblRobotCoo, 10, 560)

  LblInfoImgMouse = Shapes.AddText("Press the Spacebar to reset the image settings")

  Shapes.Move(LblInfoImgMouse, 10, 580)

  UpdateLblRobotCoo()

  GraphicsWindow.BrushColor = "Black"

  BtnHelp = Controls.AddButton("", 70, 10)

  Controls.SetSize(BtnHelp, 40, 40)

  Shapes.SetOpacity(BtnHelp, 0)

  GraphicsWindow.BrushColor = ClrGrayBorder

  GraphicsWindow.FillRectangle(60, 10, 1, 45)

  GraphicsWindow.DrawImage(Load_ImgBtnHelp, 70, 12)

  ImportCursors()

EndSub



'UpdateLblRobotCoo

Sub UpdateLblRobotCoo

  Shapes.SetText(LblRobotCoo, "Left: " + Shapes.GetLeft(ImgRobot) + " Right: " + Shapes.GetTop(ImgRobot))

EndSub



'ImportCursors

Sub ImportCursors

  ImgMove = Shapes.AddImage(Load_ImgMove)

  ImgRotate = Shapes.AddImage(Load_ImgRotate)

  ImgZoom = Shapes.AddImage(Load_ImgZoom)

  ImgEye = Shapes.AddImage(Load_ImgEye)

  Shapes.HideShape(ImgMove)

  Shapes.HideShape(ImgRotate)

  Shapes.HideShape(ImgZoom)

  Shapes.HideShape(ImgEye)

  Controls.SetSize(ImgZoom, 48, 48)

EndSub



'ResetRobotSettings

Sub ResetRobotSettings

  RobotAngle = 0

  RobotZoomLevel = .7

  RobotOpacityLevel = 40

  Shapes.Move(ImgRobot, 200, 80)

  Shapes.Rotate(ImgRobot, RobotAngle)

  Shapes.SetOpacity(ImgRobot, RobotOpacityLevel)

  Shapes.Zoom(ImgRobot, RobotZoomLevel, RobotZoomLevel)

  UpdateLblRobotCoo()

EndSub



'LoadHelpWindow

Sub LoadHelpWindow

  CurrentMenu = "HelpWindow"

  GraphicsWindow.BrushColor = "DodgerBlue"

  GraphicsWindow.PenColor = "DarkGray"

  RecHwOutter = Shapes.AddRectangle(600, 400)

  GraphicsWindow.BrushColor = ClrGrayBack

  GraphicsWindow.PenColor = "Gray"

  RecHwInner = Shapes.AddRectangle(584, 362)

  IconHelp = Shapes.AddImage(Load_ImgBtnHelp)

  Controls.SetSize(IconHelp, 20, 20)

  IconClose = Shapes.AddImage(Load_ImgClose)

  Controls.SetSize(IconClose, 25, 25)

  BtnClose = Controls.AddButton("", 567, 3)

  Controls.SetSize(BtnClose, 24, 24)

  Shapes.SetOpacity(BtnClose, 0)

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 14

  LblHwTitle = Shapes.AddText("Instructions")

  GraphicsWindow.FontSize = 12

  GraphicsWindow.BrushColor = ClrGrayText

  CaptionsLblHl = "1=Click the image with the left mouse button and then drag it.;2=Click the image with the right mouse button and hold it to perform a rotation.;3=While hodling down the left Shift key, move the mouse up or down to zoom the image.;4=Hold down the left Control key and move the mouse up or down to change the opacity of the image.;"

  For Times = 1 To 4

    BulletPoint[Times] = Shapes.AddImage(Load_ImgBulletPoint)

    LblHelpLine[Times] = Shapes.AddText(CaptionsLblHl[Times])

  EndFor

  ImgBtnOk = Shapes.AddImage(Load_ImgBtnOk)

  BtnOk = Controls.AddButton("", 465, 285)

  Controls.SetSize(BtnOk, 71, 30)

  Shapes.SetOpacity(BtnOk, 0)

EndSub



'UpdateHelpWindowPos

Sub UpdateHelpWindowPos

  Shapes.Move(RecHwOutter, HwLeft, HwTop)

  Shapes.Move(RecHwInner, HwLeft + 8, HwTop + 30)

  Shapes.Move(IconHelp, HwLeft + 7, HwTop + 5)

  Shapes.Move(IconClose, HwLeft + 567, HwTop + 3)

  Shapes.Move(LblHwTitle, HwLeft + 270, HwTop + 8)

  Shapes.Move(BtnClose, HwLeft + 567, HwTop + 3)

  For Times = 1 To 4

    Shapes.Move(BulletPoint[Times], HwLeft + 30, HwTop + Times * 50)

    Shapes.Move(LblHelpLine[Times], HwLeft + 55, 5 + HwTop + Times * 50)

  EndFor

  Shapes.Move(ImgBtnOk, HwLeft + 260, HwTop + 320)

  Controls.Move(BtnOk, HwLeft + 260, HwTop + 320)

EndSub



'CloseHelpWindow

Sub CloseHelpWindow

  Sound.PlayClick()

  Shapes.HideShape(RecHwOutter)

  Shapes.HideShape(RecHwInner)

  Shapes.HideShape(LblHwTitle)

  Shapes.HideShape(IconClose)

  Shapes.HideShape(IconHelp)

  Controls.HideControl(BtnClose)

  For Times = 1 To 4

    Shapes.HideShape(BulletPoint[Times])

    Shapes.HideShape(LblHelpLine[Times])

  EndFor

  Controls.HideControl(BtnClose)

  Shapes.HideShape(ImgBtnOk)

  CurrentMenu = "ImgMouse"

EndSub



'LoadRiverSoft

Sub LoadRiverSoft

  CurrentMenu = "River"

  GraphicsWindow.BrushColor = "Black"

  BtnAddRiver = Controls.AddButton("Add river", 65, 18)

  Controls.SetSize(BtnAddRiver, 100, 25)

  ImportBackBtn()

EndSub



'AddRiver

Sub AddRiver

  CurrentMenu = "AddRiver"

  For Times = 1 To 7

    For Loop = 1 To 4

      GraphicsWindow.DrawImage(Load_ImgGrass, Times * 157 - 157, (Loop * 157) - 157)

    EndFor

  EndFor

  For Times = 1 To 5

    GraphicsWindow.DrawImage(Load_ImgSoil, Times * 256 - 256, 150)

  EndFor

  ImportBackBtn()

  For Times = 1 To MaximumBubbles

    If CurrentMenu = "AddRiver" Then

      GraphicsWindow.PenColor = "DodgerBlue"

      GraphicsWindow.BrushColor = "DodgerBlue"

      Bubble[Times] = Shapes.AddEllipse(ParticleSize, 25)

      GraphicsWindow.PenColor = "PowderBlue"

      GraphicsWindow.BrushColor = "PowderBlue"

      Bubble2[Times] = Shapes.AddEllipse(ParticleSize, 25)

      GraphicsWindow.PenColor = GraphicsWindow.GetColorFromRGB(230, 230, 230)

      GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(230, 230, 230)

      Water[Times] = Shapes.AddEllipse(ParticleSize, 25)

      Shapes.HideShape(Bubble[Times])

      Shapes.HideShape(Bubble2[Times])

      Shapes.HideShape(Water[Times])

    EndIf

  EndFor

  For Times = 1 To MaximumBubbles

    If CurrentMenu = "AddRiver" Then

      RandomYPos0 = Math.GetRandomNumber(185)

      RandomYPos1 = Math.GetRandomNumber(185)

      RandomYPos2 = Math.GetRandomNumber(185)

      RandomYPos3 = Math.GetRandomNumber(185)

      Shapes.ShowShape(Bubble[Times])

      Shapes.ShowShape(Bubble2[Times])

      Shapes.ShowShape(Water[Times])

      Shapes.Move(Bubble[Times], -250, RandomYPos0 + 150)

      Shapes.Move(Bubble2[Times], -250, RandomYPos0 + 150)

      Shapes.Move(Water[Times], -250, RandomYPos0 + 150)

      Shapes.Animate(Bubble[Times], Distance, RandomYPos1 + 150, Speed)

      Shapes.Animate(Bubble2[Times], Distance, RandomYPos2 + 150, Speed)

      Shapes.Animate(Water[Times], Distance, RandomYPos3 + 150, Speed)

    EndIf

  EndFor

  Controls.SetButtonCaption(BtnAddRiver, "Add river")

EndSub



'LoadVisualEffectsSoft

Sub LoadVisualEffectsSoft

  CurrentMenu = "VisualEffects"

  ImportBackBtn()

  GraphicsWindow.BrushColor = "Black"

  CbAddVisualEffect = Controls.AddTextBox(65, 23)

  CbBtnAddVe = Controls.AddButton("?–¼", 225, 22)

  Controls.SetSize(CbBtnAddVe, 25, 22)

  BtnAddSmoke = Controls.AddButton("Smoke", 65, 45)

  Controls.SetSize(BtnAddSmoke, 185, 25)

  Controls.HideControl(BtnAddSmoke)

  BtnAddFire = Controls.AddButton("Fire", 65, 70)

  Controls.SetSize(BtnAddFire, 185, 25)

  Controls.HideControl(BtnAddFire)

  BtnCancelVe = Controls.AddButton("Cancel", 65, 18)

  Controls.SetSize(BtnCancelVe, 100, 25)

  Controls.HideControl(BtnCancelVe)

EndSub



'AddVisualEffect

Sub AddVisualEffect

  Controls.ShowControl(BtnCancelVe)

  CurrentEffect = Controls.GetTextBoxText(CbAddVisualEffect)

  If CurrentEffect = "Smoke" Then

    GraphicsWindow.BackgroundColor = "DodgerBlue"

    GraphicsWindow.PenColor = "Gray"

    GraphicsWindow.BrushColor = "Gray"

    For Loop = 1 To 20

      For Times = 1 To 400

        If CanAddEffect = "True" Then

          RandomPosX = Math.GetRandomNumber(50)

          RandomPosY = Math.GetRandomNumber(50)

          RandomPosX2 = Math.GetRandomNumber(150)

          RandomOpacity = Math.GetRandomNumber(100)

          RandomZoom = Math.GetRandomNumber(90)

          RandomColor = Math.GetRandomNumber(30)

          GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(RandomColor + 160, RandomColor + 160 , RandomColor + 160)

          GraphicsWindow.PenColor = GraphicsWindow.BrushColor

          ParticleSmoke[Times] = Shapes.AddEllipse(50, 50)

          Shapes.SetOpacity(ParticleSmoke[Times], RandomOpacity + 10)

          Shapes.Zoom(ParticleSmoke[Times], RandomZoom / 100, RandomZoom / 100)

          Shapes.Move(ParticleSmoke[Times], RandomPosX + 350, RandomPosY + 550)

          Shapes.Animate(ParticleSmoke[Times], RandomPosX2 + 400, -50, 20000)

        EndIf

      EndFor

    EndFor

  ElseIf CurrentEffect = "Fire" Then

    GraphicsWindow.BackgroundColor = ClrGrayBasic

    For Times = 1 To Flames

      GraphicsWindow.PenColor = "Red"

      GraphicsWindow.BrushColor = "Red"

      RandomPosX = Math.GetRandomNumber(FireWidth)

      RandomPosY = Math.GetRandomNumber(50)

      RandomWidth = Math.GetRandomNumber(20)

      FireRed[Times] = Shapes.AddEllipse(RandomWidth, 150)

      Shapes.Move(FireRed[Times], RandomPosX, FireTop + RandomPosY)

    EndFor

    For Times = 1 To Flames

      GraphicsWindow.PenColor = "Orange"

      GraphicsWindow.BrushColor = "Orange"

      RandomPosX = Math.GetRandomNumber(FireWidth)

      RandomPosY = Math.GetRandomNumber(50)

      RandomWidth = Math.GetRandomNumber(15)

      FireOrange[Times] = Shapes.AddEllipse(RandomWidth, 150)

      Shapes.Move(FireOrange[Times], RandomPosX, FireTop + RandomPosY + 40)

    EndFor

    For Times = 1 To Flames

      GraphicsWindow.PenColor = "Yellow"

      GraphicsWindow.BrushColor = "Yellow"

      RandomPosX = Math.GetRandomNumber(FireWidth)

      RandomPosY = Math.GetRandomNumber(50)

      RandomWidth = Math.GetRandomNumber(15)

      FireYellow[Times] = Shapes.AddEllipse(RandomWidth, 150)

      Shapes.Move(FireYellow[Times], RandomPosX, FireTop + RandomPosY + 80)

    EndFor

    For Times = 1 To Flames

      GraphicsWindow.PenColor = "White"

      GraphicsWindow.BrushColor = "White"

      RandomPosX = Math.GetRandomNumber(FireWidth)

      RandomPosY = Math.GetRandomNumber(50)

      RandomWidth = Math.GetRandomNumber(15)

      FireWhite[Times] = Shapes.AddEllipse(RandomWidth, 150)

      Shapes.Move(FireWhite[Times], RandomPosX, FireTop + RandomPosY + 130)

    EndFor

    For Loop = 1 To 3000

      If CanAddEffect = "True" Then

        Program.Delay(75)

        For Times = 1 To Flames

          RandomPosX = Math.GetRandomNumber(FireWidth)

          RandomPosY = Math.GetRandomNumber(80)

          Shapes.Move(FireRed[Times], RandomPosX, FireTop + RandomPosY)

        EndFor

        For Times = 1 To Flames

          RandomPosX = Math.GetRandomNumber(FireWidth)

          RandomPosY = Math.GetRandomNumber(80)

          Shapes.Move(FireOrange[Times], RandomPosX, FireTop + RandomPosY + 40)

        EndFor

        For Times = 1 To Flames

          RandomPosX = Math.GetRandomNumber(FireWidth)

          RandomPosY = Math.GetRandomNumber(80)

          Shapes.Move(FireYellow[Times], RandomPosX, FireTop + RandomPosY + 80)

        EndFor

        For Times = 1 To Flames

          RandomPosX = Math.GetRandomNumber(FireWidth)

          RandomPosY = Math.GetRandomNumber(80)

          Shapes.Move(FireWhite[Times], RandomPosX, FireTop + RandomPosY + 130)

        EndFor

      EndIf

    EndFor

  EndIf

EndSub



'PrepareForVisualEffect

Sub PrepareForVisualEffect

  Controls.HideControl(BtnAddSmoke)

  Controls.HideControl(BtnAddFire)

  Controls.HideControl(CbAddVisualEffect)

  Controls.HideControl(CbBtnAddVe)

  CanAddEffect = "True"

EndSub



'ImportBackBtn

Sub ImportBackBtn

  GraphicsWindow.DrawResizedImage(Load_ImgBtnBack, 10, 10, 40, 40)

  BtnBack = Controls.AddButton("", 10, 10)

  Controls.SetSize(BtnBack, 40, 40)

  Shapes.SetOpacity(BtnBack, 0)

EndSub



'Subroutines - System

'********************

'ButtonClicked

Controls.ButtonClicked = ButtonClicked

Sub ButtonClicked

  LastBtn = Controls.LastClickedButton

  If LastBtn = BtnMainMenu[1] Then

    GraphicsWindow.Clear()

    LoadMoveImgWithKeysSoft()

  ElseIf LastBtn = BtnMainMenu[2] Then

    GraphicsWindow.Clear()

    LoadMoveImgWithMouseSoft()

  ElseIf LastBtn = BtnMainMenu[3] Then

    GraphicsWindow.Clear()

    LoadRiverSoft()

  ElseIf LastBtn = BtnMainMenu[4] Then

    GraphicsWindow.Clear()

    LoadVisualEffectsSoft()

  ElseIf LastBtn = BtnBack Then

    If CurrentMenu = "HelpWindow" Then

    ElseIf CurrentMenu = "VisualEffects" Then

      CanAddEffect = "False"

      CurrentEffect = "None"

      GraphicsWindow.Clear()

      AddMainMenu()

    Else

      GraphicsWindow.Clear()

      AddMainMenu()      

    EndIf

  ElseIf LastBtn = BtnExit Then

    Program.End()

  ElseIf LastBtn = BtnHelp Then

    If CurrentMenu = "HelpWindow" Then

    Else

      LoadHelpWindow()

      HwLeft = 200

      HwTop = 100

      UpdateHelpWindowPos()

    EndIf

  ElseIf LastBtn = BtnClose Or LastBtn = BtnOk Then

    CloseHelpWindow()

  ElseIf LastBtn = BtnAddRiver Then

    If Controls.GetButtonCaption(BtnAddRiver) = "Add river" Then

      Controls.SetButtonCaption(BtnAddRiver, "Cancel")

      AddRiver()

    Else

      CurrentMenu = "River"

      Controls.SetButtonCaption(BtnAddRiver, "Add river")

    EndIf

  ElseIf LastBtn = CbBtnAddVe Then

    If DropDownCbAddVe = "False" Then

      Controls.ShowControl(BtnAddSmoke)

      Controls.ShowControl(BtnAddFire)

      DropDownCbAddVe = "True"

      Controls.SetButtonCaption(CbBtnAddVe, "?–²")

    Else

      Controls.HideControl(BtnAddSmoke)

      Controls.HideControl(BtnAddFire)

      DropDownCbAddVe = "False"

      Controls.SetButtonCaption(CbBtnAddVe, "?–¼")

    EndIf

  ElseIf LastBtn = BtnAddSmoke Then

    PrepareForVisualEffect()

    Controls.SetTextBoxText(CbAddVisualEffect, "Smoke")

    AddVisualEffect()

  ElseIf LastBtn = BtnAddFire Then

    PrepareForVisualEffect()

    Controls.SetTextBoxText(CbAddVisualEffect, "Fire")

    AddVisualEffect()

  ElseIf LastBtn = BtnCancelVe Then

    CanAddEffect = "False"

    CurrentEffect = "None"

    GraphicsWindow.Clear()

    LoadVisualEffectsSoft()

  EndIf

EndSub



'MouseDown

GraphicsWindow.MouseDown = MouseDown

Sub MouseDown

  Mx = GraphicsWindow.MouseX

  My = GraphicsWindow.MouseY

  If CurrentMenu = "ImgMouse" Then

    LeftPosRobot = Shapes.GetLeft(ImgRobot)

    TopPosRobot = Shapes.GetTop(ImgRobot)

    WidthRobot = ImageList.GetWidthOfImage(Load_ImgRobot)

    HeightRobot = ImageList.GetHeightOfImage(Load_ImgRobot)

    If Mx > LeftPosRobot And Mx < LeftPosRobot + WidthRobot And My > TopPosRobot And My < TopPosRobot + HeightRobot Then

      If ShiftPressed = "False" And ControlPressed = "False" And RotateRobot = "False" Then

        If Mouse.IsLeftButtonDown Then

          Shapes.SetOpacity(ImgRobot, 100)

          Shapes.Zoom(ImgRobot, RobotZoomLevel + 0.3, RobotZoomLevel + 0.3)

          Shapes.Move(ImgRobot, Mx - (WidthRobot / 2) - 60, My - (HeightRobot / 2) + 25)

          Mouse.HideCursor()

          Shapes.ShowShape(ImgMove)

          DragRobot = "True"

        EndIf

      ElseIf ShiftPressed = "True" And Mouse.IsLeftButtonDown Then

        ZoomRobot = "True"

        Mouse.HideCursor()

        Shapes.ShowShape(ImgZoom)

      ElseIf ControlPressed = "True" And Mouse.IsLeftButtonDown Then

        FadeRobot = "True"

        Mouse.HideCursor()

        Shapes.ShowShape(ImgMove)

        Shapes.ShowShape(ImgEye)

      EndIf

      If Mouse.IsRightButtonDown And ShiftPressed = "False" And ControlPressed = "False" And DragRobot = "False" Then

        RotateRobot = "True"

        Shapes.Zoom(ImgRobot, RobotZoomLevel + 0.3, RobotZoomLevel + 0.3)

        Shapes.SetOpacity(ImgRobot, 100)

        Mouse.HideCursor()

        Shapes.ShowShape(ImgRotate)

        While RotateRobot = "True"

          Shapes.Rotate(ImgRobot, RobotAngle)

          RobotAngle = RobotAngle + 0.003

        EndWhile

      EndIf

    EndIf

  ElseIf CurrentMenu = "HelpWindow" Then

    HwLeftPos = Shapes.GetLeft(RecHwOutter)

    HwTopPos = Shapes.GetTop(RecHwOutter)

    HwWidth = 600

    HwHeight = 400

    If Mx > HwLeftPos And Mx < HwLeftPos + HwWidth And My > HwTopPos And My < HwTopPos + 30 Then

      DragHelpWindow = "True"

    EndIf

  EndIf

EndSub



'MouseMove

GraphicsWindow.MouseMove = MouseMove

Sub MouseMove

  Mx = GraphicsWindow.MouseX

  My = GraphicsWindow.MouseY

  If CurrentMenu = "ImgMouse" Then

    Shapes.Move(ImgMove, Mx - 16, My - 16)

    Shapes.Move(ImgRotate, Mx - 24, My - 24)

    Shapes.Move(ImgZoom, Mx - 24, My - 24)

    Shapes.Move(ImgEye, Mx - 16, My - 16)

    If DragRobot = "True" Then

      Shapes.Move(ImgRobot, Mx - (WidthRobot / 2) - 60, My - (HeightRobot / 2) + 25)

      UpdateLblRobotCoo()

    ElseIf ZoomRobot = "True" Then

      PrevMy = GraphicsWindow.MouseY

      If PrevMy > NewMy Then

        RobotZoomLevel = RobotZoomLevel + 0.01

      ElseIf PrevMy < NewMy Then

        RobotZoomLevel = RobotZoomLevel - 0.01

      EndIf

      Shapes.Zoom(ImgRobot,  RobotZoomLevel, RobotZoomLevel)

      NewMy = GraphicsWindow.MouseY

    ElseIf FadeRobot = "True" Then

      RobotOpacityLevel = My / 6

      Shapes.SetOpacity(ImgRobot, RobotOpacityLevel)

    EndIf

  ElseIf CurrentMenu = "MainMenu" Then

    If Mx > 480 And Mx < 520 And My > 500 And My < 540 Then

      If Shapes.GetOpacity(EffectFadeMm) = 0 Then

        Shapes.ShowShape(EffectFadeMm)

        For Times = 0 To 65 Step 0.01

          Shapes.SetOpacity(EffectFadeMm, Times)

        EndFor

      EndIf

    Else

      If Shapes.GetOpacity(EffectFadeMm) = 65 Then

        For Times = 65 To 0 Step -0.01

          Shapes.SetOpacity(EffectFadeMm, Times)

        EndFor

        Shapes.HideShape(EffectFadeMm)

      EndIf

    EndIf

  EndIf

  If CurrentMenu = "HelpWindow" Then

    If DragHelpWindow = "True" Then

      HwLeft = Mx - 300

      HwTop = My - 15

      UpdateHelpWindowPos()

    EndIf

  EndIf

EndSub



'MouseUp

GraphicsWindow.MouseUp = MouseUp

Sub MouseUp

  If CurrentMenu = "ImgMouse" Then

    Shapes.SetOpacity(ImgRobot, RobotOpacityLevel)

    Shapes.Zoom(ImgRobot, RobotZoomLevel, RobotZoomLevel)

    Shapes.HideShape(ImgMove)

    Shapes.HideShape(ImgRotate)

    Shapes.HideShape(ImgZoom)

    Shapes.HideShape(ImgEye)

    Mouse.ShowCursor()

    DragRobot = "False"

    RotateRobot = "False"

    ZoomRobot = "False"

    ShiftPressed = "False"

    FadeRobot = "False"

    ControlPressed = "False"

  ElseIf CurrentMenu = "HelpWindow" Then

    DragHelpWindow = "False"

  EndIf

EndSub



'KeyDown

GraphicsWindow.KeyDown = KeyDown

Sub KeyDown

  LastKey = GraphicsWindow.LastKey

  If CurrentMenu = "ImgKeys" Then

    If LastKey = "Right" Then

      DirectionMario = "Right"

    ElseIf LastKey = "Left" Then

      DirectionMario = "Left"

    ElseIf LastKey = "Up" Then

      DirectionMario = "Up"

    ElseIf LastKey = "Down" Then

      DirectionMario = "Down"

    EndIf

    If LastKey = "Space" Then

      Shapes.Move(ImgMario, 391, 89)

      LeftMario = 391

      TopMario = 89

    EndIf

    Timer.Resume()

  ElseIf CurrentMenu = "ImgMouse" Then

    If LastKey = "LeftShift" Then

      ShiftPressed = "True"

    ElseIf LastKey = "LeftCtrl" Then

      ControlPressed = "True"

    ElseIf LastKey = "Space" Then

      ResetRobotSettings()

    EndIf

  EndIf

EndSub



'KeyUp

GraphicsWindow.KeyUp = KeyUp

Sub KeyUp

  Timer.Pause()

  DirectionMario = "None"

  ShiftPressed = "False"

  ControlPressed = "False"

EndSub



'TextTyped

Controls.TextTyped = TextTyped

Sub TextTyped

  If CurrentMenu = "VisualEffects" Then

    TxtVeInput = Controls.GetTextBoxText(CbAddVisualEffect)

    Controls.HideControl(BtnAddSmoke)

    Controls.HideControl(BtnAddFire)

    DropDownCbAddVe = "False"

    Controls.SetButtonCaption(CbBtnAddVe, "?–¼")

    If TxtVeInput = "Smoke" Or TxtVeInput = "Fire" Then

      PrepareForVisualEffect()

      CanAddEffect = "True"

      AddVisualEffect()

    EndIf

  EndIf

EndSub



'Timer

Timer.Interval = 0.1

Timer.Tick = MoveMario

Sub MoveMario

  If DirectionMario = "Right" Then

    LeftMario = LeftMario + 8

  ElseIf DirectionMario = "Left" Then

    LeftMario = LeftMario - 8

  ElseIf DirectionMario = "Up" Then

    TopMario = TopMario - 8

  ElseIf DirectionMario = "Down" Then

    TopMario = TopMario + 8

  EndIf

  Shapes.Move(ImgMario, LeftMario, TopMario)

EndSub


