' Challenge of the Month - May 2012     Cannon ball  Distance and Height   by NaochanON 
'

init()

V=100   '  m/s 
g= 10   '  m/s/s.

For deg = 0 To 90 
  
  ball[deg]= Shapes.AddEllipse(10,10)
  Shapes.Move(ball[deg],0,-100)
  Shapes.Rotate(cannon,-deg)
  Shapes.move(cannon,100-25,600-2.5)
  rad= Math.GetRadians(deg)
  t=0
  dt = 0.01
  resistance = 0.05
  headwind = 5
  vx = V*Math.Cos(rad)
  vy = V*Math.Sin(rad)
  x = 0
  y = 0
  While  y >= 0
    t=t+dt
    vx = (1-resistance*dt)*(vx+headwind) - headwind
    vy = (1-resistance*dt)*(vy - G*dt)
    xLast = x
    yLast = y
    x = x + vx*dt        
    y = y + vy*dt
    Shapes.Move(ball[deg],100+x-5,600-y-5)
    GraphicsWindow.DrawLine(100+xLast,600-yLast,100+x,600-y)
    If MaxHeight<=y Then 
      MaxHeight=y
      MaxHeightDeg=deg
    EndIf
    If MaxDistance<=X Then 
      MaxDistance=x
      MaxDistancedeg=deg
    EndIf
    Shapes.SetText(msg1,"degrees= "+deg+"   time= "+t+" sec    " +" Distance= "+x+" m   Height= "+y+" m" )
    Shapes.SetText(msg2,"MaxDistanceDeg= "+MaxDistanceDeg+"   MaxDistance= "+MaxDistance+" m" )
    Shapes.SetText(msg3," MaxHeightDeg = "+MaxHeightDeg+  "    MaxHeight = "+MaxHeight+" m" )
  endwhile
EndFor


Sub init
  GraphicsWindow.Width=1200
  GraphicsWindow.Height=700
  GraphicsWindow.Top=20
  GraphicsWindow.Left=20
  GraphicsWindow.Show()
  GraphicsWindow.BackgroundColor="Lightcyan"
  GraphicsWindow.PenColor="Navy"
  GraphicsWindow.PenWidth=3
  GraphicsWindow.BrushColor="brown"
  GraphicsWindow.FillRectangle(0,600,1200,300)
  GraphicsWindow.BrushColor="Navy"
  cannon=Shapes.AddRectangle(50,5)
  Shapes.Move(cannon,0,600)
  GraphicsWindow.FontSize=16
  msg1= Shapes.AddText("  ")
  Shapes.Move(msg1,100,50)
  msg2= Shapes.AddText("  ")
  Shapes.Move(msg2,100,100)
  msg3= Shapes.AddText("  ")
  Shapes.Move(msg3,100,150)
  GraphicsWindow.PenColor = "330000FF"
  GraphicsWindow.PenWidth = 0.1
EndSub

