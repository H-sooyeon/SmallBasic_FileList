Initialise()

While 0=0  
  UpdateGame() 
  
  'Update Frame 
  Shapes.Move(_turtle, X, Y)  
  
  'Frame Rate
  Program.Delay(Math.Min(7, Math.Max(0, 7 - (Clock.ElapsedMilliseconds - start))))
  start = Clock.ElapsedMilliseconds
  
EndWhile

Sub Initialise
  wall = "#FF00DC"  
  speed = "1=2;2=2"
  startX = "1=40;2=296"
  startY = "1=0;2=292" 
  
  GraphicsWindow.Top = 20
  GraphicsWindow.Left = (Desktop.Width - GraphicsWindow.Width) / 2  
  
  LoadLevel()
  
  GraphicsWindow.KeyDown = OnKeyDown
  GraphicsWindow.KeyUp = OnKeyUp
EndSub

Sub OnKeyDown
  keyDown = "True"
EndSub

Sub OnKeyUp
  keyDown = "False"
EndSub

Sub LoadLevel
  GraphicsWindow.Clear()
  GraphicsWindow.DrawText(100, 80, "Wait...")
  
  i = i + 1
  url = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Maze/assets/images/maze[" + i + "].png"
  'url = Program.Directory + "\maze[" + i + "].png"
  maze = ImageList.LoadImage(url)
  
  url = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Maze/assets/images/turtle[1].png"
  'url = Program.Directory + "\turtle[1].png"
  _turtle = ImageList.LoadImage(url)
  
  size[i] = ImageList.GetHeightOfImage(_turtle)  
  
  GraphicsWindow.Width = ImageList.GetWidthOfImage(maze)
  GraphicsWindow.Height = ImageList.GetHeightOfImage(maze)
  GraphicsWindow.Left = (Desktop.Width - GraphicsWindow.Width) / 2  
  
  GraphicsWindow.Clear()
  GraphicsWindow.DrawImage(maze, 0, 0)
  _turtle = Shapes.AddImage(_turtle)  
  
  X = startX[i]
  Y = startY[i]
  startClock = Clock.ElapsedMilliseconds
EndSub

Sub UpdateGame
  'Update Turtle
  If keyDown And GraphicsWindow.LastKey = "Down" Then    
    If GraphicsWindow.GetPixel(X, Y + size[i] + speed[i]) <> wall And GraphicsWindow.GetPixel(X + size[i], Y + size[i] + speed[i]) <> wall Then
      Y = Shapes.GetTop(_turtle) + speed[i]
      Shapes.Rotate(_turtle, 180)
    EndIf
  ElseIf keyDown And GraphicsWindow.LastKey = "Up" And Y > speed[i] Then
    If GraphicsWindow.GetPixel(X, Y - speed[i]) <> wall And GraphicsWindow.GetPixel(X + size[i], Y - speed[i]) <> wall Then
      Y = Shapes.GetTop(_turtle) - speed[i]
      Shapes.Rotate(_turtle, 0)
    EndIf
  ElseIf keyDown And GraphicsWindow.LastKey = "Right" Then
    If GraphicsWindow.GetPixel(X + size[i] + speed[i], Y) <> wall And GraphicsWindow.GetPixel(X + size[i] + speed[i], Y + size[i]) <> wall Then
      X = Shapes.GetLeft(_turtle) + speed[i]
      Shapes.Rotate(_turtle, 90)    
    EndIf
  ElseIf keyDown And GraphicsWindow.LastKey = "Left" Then
    If GraphicsWindow.GetPixel(X - speed[i], Y) <> wall And GraphicsWindow.GetPixel(X - speed[i], Y + size[i]) <> wall Then
      X = Shapes.GetLeft(_turtle) - speed[i]
      Shapes.Rotate(_turtle, 270)
    EndIf
  EndIf
  
  'Update Progress  
  If X > GraphicsWindow.Width - size[i] / 2 Or Y > GraphicsWindow.Height - size[i] / 2 Then    
    GraphicsWindow.ShowMessage("Time taken: " + Math.Round((Clock.ElapsedMilliseconds - startClock) / 1000) + " s.", "Congratulations")
    If i < 2 Then
      LoadLevel()
      keyDown = "False"
    Else
      Program.End()
    EndIf
  EndIf
EndSub
