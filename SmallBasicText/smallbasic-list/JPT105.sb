Initialise()

While 0=0
  If buttonClicked <> "" Then
    If buttonClicked = "Pull" Then
      Controls.HideControl(Controls.LastClickedButton)
      Sound.Stop(host + "assets/handleSFX.au")
      Sound.Play(host + "assets/handleSFX.au")  
      AnimateHandle()    
    ElseIf buttonClicked = "Add $20" Then
      credit = credit + 20      
      Controls.HideControl(Controls.LastClickedButton)
      Sound.PlayAndWait(host + "assets/drop22.au")
      Shapes.SetText(bal, "Credit: $" + credit)
      Controls.ShowControl(pullBtn)     
    EndIf
    buttonClicked = ""  
  EndIf 
  
  If payoutDelay <> "" And credit > 0 Then    
    Sound.Stop(host + "assets/PayoutSFX.mp3")
    Sound.Play(host + "assets/PayoutSFX.mp3")  
    Program.Delay(payoutDelay)
    Sound.Stop(host + "assets/PayoutSFX.mp3")
    Controls.ShowControl(pullBtn)
    payoutDelay = ""
  EndIf 
  
  Program.Delay(93) '93.6
EndWhile

Sub AnimateHandle
  c = 1
  f = 1
  While f + c > 0  'Animate Handle
    Shapes.HideShape(handle[f])
    f = f + c
    Shapes.ShowShape(handle[f])  
    If f = 4 Then
      c = -c
      Program.Delay(500)
      Sound.Stop(host + "assets/SpinSFX.mp3")
      Sound.Play(host + "assets/SpinSFX.mp3")
      Timer.Resume()
      credit = credit - 5
      Shapes.SetText(bal, "Credit: $" + credit)
      i = Math.GetRandomNumber(3) - 1
      GraphicsWindow.BrushColor = "Black"
      GraphicsWindow.FillRectangle(268, 136, 130, 25)
    EndIf 
    Program.Delay(100)
  EndWhile
EndSub

Sub Initialise
  host = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/SlotMachine/"
  color = "1=#78DBFF;2=#66E957;3=#FF973D"
  credit = 0
  
  GraphicsWindow.DrawText(100, 150, "D/loading....Wait a minute")
  
  Sound.Play(host + "assets/handleSFX.au")
  Sound.Stop(host + "assets/handleSFX.au")
  Sound.Play(host + "assets/drop22.au")
  Sound.Stop(host + "assets/drop22.au")
  Sound.Play(host + "assets/PayoutSFX.mp3") 
  Sound.Stop(host + "assets/PayoutSFX.mp3") 
  Sound.Play(host + "assets/SpinSFX.mp3")
  Sound.Stop(host + "assets/SpinSFX.mp3")
  
  For i = 1 To 4
    img[i] = ImageList.LoadImage(host + "assets/handle-" + i + ".png")
    imgWidth[i] = ImageList.GetWidthOfImage(img[i])
    handle[i] = Shapes.AddImage(img[i])
    Shapes.HideShape(handle[i])  
  EndFor
  machine = ImageList.LoadImage(host + "assets/background-5.png")
  
  If Array.ContainsValue(imgWidth, 0) Or ImageList.GetWidthOfImage(machine) = 0 Then
    GraphicsWindow.ShowMessage("A network error has occured!" + Text.GetCharacter(10) + "Close and try again.", "NETWORK ERROR!")
    Program.End()
  EndIf
  
  GraphicsWindow.Width = ImageList.GetWidthOfImage(machine)
  GraphicsWindow.Height = ImageList.GetHeightOfImage(machine)
  GraphicsWindow.Top = 20
  GraphicsWindow.Left = (Desktop.Width - GraphicsWindow.Width) / 2
  GraphicsWindow.BackgroundColor = "Black"
  
  GraphicsWindow.DrawImage(machine, 0, 0)
  Shapes.ShowShape(handle[1])
  pullBtn = Controls.AddButton("Pull", 458, 130)
  Controls.HideControl(pullBtn)
  addBtn = Controls.AddButton("Add $20", 458, 20)
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.FontSize = 18
  bal = Shapes.AddText("Credit: $" + credit)
  Shapes.Move(bal, 300, 382)
  
  GraphicsWindow.FontSize = 20
  
  Controls.ButtonClicked = OnButtonClicked
  Timer.Tick = Spin
  Timer.Interval = 50
  Timer.Pause()
EndSub

Sub OnButtonClicked
  buttonClicked = Controls.GetButtonCaption(Controls.LastClickedButton)
EndSub

Sub Spin  
  Timer.Interval = Timer.Interval + 10 
  j = (Math.GetRandomNumber(3) - 1) * 88  '0 88  176  '55
  GraphicsWindow.BrushColor = "Black" 
  GraphicsWindow.FillRectangle(132 + j, 265, 50, 50) '40 + j, 150, 50, 50
  k = color[Math.GetRandomNumber(3)]
  GraphicsWindow.BrushColor = k
  i = i + 1
  col[j] = i
  kol[j] = k
  If i = 1 Then
    GraphicsWindow.FillEllipse(132 + j, 265, 50, 50) '40 + j, 150, 50, 50
  ElseIf i = 2 Then
    GraphicsWindow.FillRectangle(132 + j, 265, 50, 50)  '40 + j, 150, 50, 50
  ElseIf i = 3 Then
    GraphicsWindow.FillTriangle(132 + j, 315, 157 + j, 265, 182 + j, 315)  '40 + j, 200, 65 + j, 150, 90 + j, 200    
    i = 0
  EndIf  
  If Timer.Interval > 300 Then
    GraphicsWindow.BrushColor = "Yellow"    
    If col[0] = 2 And col[88] = 2 And col[176] = 2 And Array.ContainsValue(kol, color[1]) And Array.ContainsValue(kol, color[2]) And Array.ContainsValue(kol, color[3]) Then
      credit = credit + 500
      payoutDelay = 9000      
      GraphicsWindow.DrawBoundText(268, 136, 130, "Small Basic!")   '120
    ElseIf kol[0] = kol[88] And kol[0] = kol[176] And col[0] = col[88] And col[0] = col[176] Then
      credit = credit + 100
      payoutDelay = 8000      
      GraphicsWindow.DrawBoundText(268, 136, 130, "Triple Flush!")  
    ElseIf col[0] = col[88] And col[0] = col[176] Then
      credit = credit + 50
      payoutDelay = 6000      
      GraphicsWindow.DrawBoundText(268, 136, 130, "3 of a kind!")      
    ElseIf col[0] = col[88] And kol[0] = kol[88] Or col[0] = col[176] And kol[0] = kol[176] Or col[88] = col[176] And kol[88] = kol[176] Then
      credit = credit + 10
      payoutDelay = 4000      
      GraphicsWindow.DrawBoundText(268, 136, 130, "Flush Pair!")
    ElseIf col[0] = col[88] Or col[0] = col[176] Or col[88] = col[176] Then
      credit = credit + 5
      payoutDelay = 3000      
      GraphicsWindow.DrawBoundText(268, 136, 130, "Pair!")
    Else
      payoutDelay = 1      
      GraphicsWindow.DrawBoundText(268, 136, 130, "You Lose.")      
    EndIf
    Shapes.SetText(bal, "Credit: $" + credit)    
    If credit = 0 Then
      Controls.HideControl(pullBtn)
      Controls.ShowControl(addBtn)
    EndIf    
    Timer.Interval = 50
    Timer.Pause()
  EndIf 
EndSub
