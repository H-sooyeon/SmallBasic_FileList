' TicTacToe    //  original  BJX729  made by  gucerni   //   shortened version  by NaochanON
menu()

Sub menu
  GraphicsWindow.CanResize = "no"
  GraphicsWindow.Width = 370
  GraphicsWindow.Height = 300
  GraphicsWindow.PenWidth = 5
  GraphicsWindow.PenColor = "gainsboro"
  GraphicsWindow.DrawEllipse(180,0,100,100)'circulo superior
  GraphicsWindow.DrawLine(0,80,100,50)'X izquierda
  GraphicsWindow.DrawLine(10,0,10,170)
  GraphicsWindow.DrawLine(260,150,260,300)'X derecha
  GraphicsWindow.DrawLine(180,280,370,220)
  dat="1=SMALL,00033,00130,00050;2=TIC  ,00032,00160,00090;3=TAC  ,00024,00165,00130;4=TOE  ,00018,00170,00160"
  GraphicsWindow.BrushColor = "black"
  For i=1 To  Array.GetItemCount(dat)
    GraphicsWindow.FontSize =text.GetSubText(dat[i],10,2)
    GraphicsWindow.DrawText(text.GetSubText(dat[i],13,5)*1,text.GetSubText(dat[i],19,5)*1,text.GetSubText(dat[i],1,5))
  EndFor
  GraphicsWindow.PenWidth = 2
  GraphicsWindow.PenColor = "black"
  lns="1=1\=0\;2\=190\;3\=150\;4\=0\;;2=1\=130\;2\=300\;3\=350\;4\=0\;;3=1\=30\;2\=0\;3\=370\;4\=190\;;4=1\=0\;2\=190\;3\=170\;4\=300\;;"
  For i=1 To Array.GetItemCount(lns)
    v=Shapes.AddLine(lns[i][1],lns[i][2],lns[i][3],lns[i][4])
    Shapes.SetOpacity(v,10)
  EndFor
  btnPlay1 = Controls.AddButton("1 Jugador",70,210)
  btnPlay2 = Controls.AddButton("2 Jugadores",200,210)
  Controls.ButtonClicked = Botones
EndSub  

Sub Botones
  ultimoBtn = Controls.LastClickedButton
  If ultimoBtn = btnPlay1 Then
    GraphicsWindow.MouseDown = clicX2  '  single player 
    inter()
    inicio()
    jugadas()
  EndIf
  If ultimoBtn = btnPlay2 Then
    GraphicsWindow.MouseDown = clicX  '  double player 
    inicio()
  EndIf
EndSub

Sub inicio
  GraphicsWindow.Clear()
  interruptor = 1
  GraphicsWindow.Width = 370
  GraphicsWindow.Height = 300
  GraphicsWindow.PenWidth = 2
  GraphicsWindow.PenColor = "Green"
  GraphicsWindow.CanResize = "no"
  GraphicsWindow.DrawLine(80,110,290,110)
  GraphicsWindow.DrawLine(80,170,290,170)
  GraphicsWindow.DrawLine(150,60,150,220)
  GraphicsWindow.DrawLine(220,60,220,220)
  matriz="1=1\=0\;2\=0\;3\=0\;;2=1\=0\;2\=0\;3\=0\;;3=1\=0\;2\=0\;3\=0\;;"
EndSub

Sub clicX    '  double player 
  If Math.Remainder(interruptor,2) = 0 Then
    figuraO()
  EndIf  
  If Math.Remainder(interruptor,2) <> 0  Then
    figuraX()
  EndIf
EndSub

Sub clicX2   '  single player 
  If Math.Remainder(interruptor,2) = 0  Then
    figuraX()
  EndIf
  If Math.Remainder(interruptor,2) <> 0 Then
    jugadas()
  EndIf  
EndSub

Sub inter
  interruptor = interruptor + 1
  If interruptor = 10 Then
    Reinicio()
  EndIf
EndSub

Sub FiguraX
  GraphicsWindow.PenColor = "Blue"
  gmX = GraphicsWindow.MouseX
  gmY = GraphicsWindow.MouseY
  dx=70
  dy=55
  For j=1 To 3
    For i=1 To 3
      If gmX >= 85+dx*(i-1) And gmX <= 145+dx*(i-1) And gmY >= 65+dy*(j-1) And gmY <= 105+dy*(j-1) Then
        If matriz[j][i] = 0 Then
          GraphicsWindow.DrawLine(100+dx*(i-1),70+dy*(j-1),130+dx*(i-1),100+dy*(j-1))
          GraphicsWindow.DrawLine(100+dx*(i-1),100+dy*(j-1),130+dx*(i-1),70+dy*(j-1))
          matriz[j][i] = 1
          inter()
          SumaMatriz()
          i=3
          j=3
        EndIf
      EndIf
    EndFor
  EndFor
EndSub

Sub FiguraO
  GraphicsWindow.PenColor  = "Red"
  gwX = GraphicsWindow.MouseX
  gwY = GraphicsWindow.MouseY
  For j=1 To 3
    For i=1 To 3
      If gwX >= 85+dx*(i-1) And gwX <= 145+dx*(i-1) And gwY >= 65+dy*(j-1) And gwY <= 105+dy*(j-1) Then
        If matriz[j][i] = 0 Then
          GraphicsWindow.DrawEllipse(100+dx*(i-1),70+dy*(j-1),30,30)
          matriz[j][i] = 9
          inter()
          SumaMatriz()
          i=3
          j=3
        EndIf
      EndIf
    EndFor
  EndFor
EndSub

Sub SumaMatriz
  GraphicsWindow.PenWidth = 5
  SLine=""
  If matriz[1][1] + matriz[1][2] + matriz[1][3] = 3 Or matriz[1][1] + matriz[1][2] + matriz[1][3] = 27 Then
    SLine="1=50;2=85;3=320;4=85"
  elseIf matriz[2][1] + matriz[2][2] + matriz[2][3] = 3 Or matriz[2][1] + matriz[2][2] + matriz[2][3] = 27 Then
    SLine="1=50;2=140;3=320;4=140"
  elseIf matriz[3][1] + matriz[3][2] + matriz[3][3] = 3 Or matriz[3][1] + matriz[3][2] + matriz[3][3] = 27 Then
    SLine="1=50;2=195;3=320;4=195"
  elseIf matriz[1][1] + matriz[2][1] + matriz[3][1] = 3 Or matriz[1][1] + matriz[2][1] + matriz[3][1] = 27 Then
    SLine="1=115;2=30;3=115;4=250"
  elseIf matriz[1][2] + matriz[2][2] + matriz[3][2] = 3 Or matriz[1][2] + matriz[2][2] + matriz[3][2] = 27 Then
    SLine="1=185;2=30;3=185;4=250"
  elseIf matriz[1][3] + matriz[2][3] + matriz[3][3] = 3 Or matriz[1][3] + matriz[2][3] + matriz[3][3] = 27 Then
    SLine="1=255;2=30;3=255;4=250"
  elseIf matriz[1][1] + matriz[2][2] + matriz[3][3] = 3 Or matriz[1][1] + matriz[2][2] + matriz[3][3] = 27 Then
    SLine="1=90;2=60;3=280;4=220"
  elseIf matriz[3][1] + matriz[2][2] + matriz[1][3] = 3 Or matriz[3][1] + matriz[2][2] + matriz[1][3] = 27 Then
    SLine="1=90;2=220;3=280;4=60"
  EndIf 
  If Sline<>"" Then 
    GraphicsWindow.DrawLine(SLine[1],SLine[2],SLine[3],SLine[4])
    interruptor = 0
    matriz="1=1\=0\;2\=0\;3\=0\;;2=1\=0\;2\=0\;3\=0\;;3=1\=0\;2\=0\;3\=0\;;"
    Reinicio()
    Sound.PlayBellRing()
  EndIf
  GraphicsWindow.PenWidth = 2
EndSub

Sub Reinicio
  GraphicsWindow.FontSize = 12
  reiniciar = Controls.AddButton("Reiniciar Partida",70,260)
  menuPrincipal = Controls.AddButton("Menu Principal",200,260)
  Controls.ButtonClicked = BtnReinicio
EndSub

Sub BtnReinicio
  ultimoBtnReinicio = Controls.LastClickedButton
  If ultimoBtnReinicio = reiniciar Then
    GraphicsWindow.Clear()
    inter()
    inicio()
    jugadas()
  EndIf
  If ultimoBtnReinicio = menuPrincipal Then
    GraphicsWindow.Clear()
    menu()
  EndIf
EndSub

Sub jugadas
  If math.Remainder(interruptor,2) = 1  Then
    comprobar = 0
    While comprobar = 0
      casilla = Math.GetRandomNumber(9)
      X=math.Remainder(casilla-1,3)+1
      Y=Math.Floor(casilla/3)+1 
      If matriz[Y][X] = 0 Then
        GraphicsWindow.PenColor  = "Red"   
        GraphicsWindow.DrawEllipse(100+dx*(X-1),70+dy*(Y-1),30,30)'ubicacion 1,1
        matriz[Y][X] = 9
        inter()
        SumaMatriz()
        comprobar = 1
      EndIf
    EndWhile
  EndIf
EndSub
