'Window

GraphicsWindow.Hide()

GraphicsWindow.Width = 640

GraphicsWindow.Height = 480

GraphicsWindow.Left = (Desktop.Width - 640) / 2

GraphicsWindow.Top = (Desktop.Height - 570) / 2

GraphicsWindow.CanResize = "False"

GraphicsWindow.Title = "Graphical Challenge"

GraphicsWindow.Show()



'Variables

CanMoveShape = "No"

ClickedOnce = "No"



'LeftMenu & Background & Slots

GraphicsWindow.PenWidth = 6

For TimesY = 0 To 480 Step 5

  Number = Number + 1

  Color = GraphicsWindow.GetColorFromRGB(Number + 100, Number + 100, Number + 100)

  GraphicsWindow.PenColor = Color

  Shapes.AddLine(150, 0 + TimesY, 640, TimesY)

EndFor

GraphicsWindow.PenColor = "Black"

GraphicsWindow.PenWidth = 1

GraphicsWindow.BrushColor = "Gray"

LMBackReck = Shapes.AddRectangle(150, 480)

GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(100, 100, 100)

LMBackReck2 = Shapes.AddRectangle(150, 33)

GraphicsWindow.BrushColor = "White"

GraphicsWindow.FontBold = "False"

GraphicsWindow.FontName = "Microsoft Sans Serif"

LMLabel = Shapes.AddText("Click and drag a shape")

Shapes.Move(LMLabel, 10, 10)

GraphicsWindow.BrushColor = "Black"

GraphicsWindow.FontSize = 15

BtnShowMore = Controls.AddButton("?†’", 150, 0)

Controls.SetSize(BtnShowMore, 33, 33)

GraphicsWindow.BrushColor = "Crimson"

Slot1 = Shapes.AddRectangle(100, 100)

Shapes.SetOpacity(Slot1, 50)

Shapes.Move(Slot1, 280, 270)

GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(23, 167, 117)

Slot2 = Shapes.AddEllipse(100, 100)

Shapes.SetOpacity(Slot2, 50)

Shapes.Move(Slot2, 230, 70)

GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(58, 68, 145)

Slot3 = Shapes.AddTriangle(50, 0, 0, 100, 100, 100)

Shapes.SetOpacity(Slot3, 50)

Shapes.Move(Slot3, 450, 100)



GraphicsWindow.BrushColor = "Crimson"

LMRectangle = Shapes.AddRectangle(100, 100)

Shapes.Move(LMRectangle, 25, 75)

GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(23, 167, 117)

LMEllipse = Shapes.AddEllipse(100, 100)

Shapes.Move(LMEllipse, 25, 200)

GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(58, 68, 145)

LMTriangle = Shapes.AddTriangle(50, 0, 0, 100, 100, 100)

Shapes.Move(LMTriangle, 25, 325)





'SUBROUTINES

'***************

'MouseMove

GraphicsWindow.MouseMove = MouseMove

Sub MouseMove

  x = GraphicsWindow.MouseX

  y = GraphicsWindow.MouseY

  GetShapePositions()

  

  If x > LMRecX And x < LMRecX + 100 And y > LMRecY And y < LMRecY + 100 Then

    Shapes.SetOpacity(LMRectangle, 50)

    Shapes.Zoom(LMRectangle, 0.9, 0.9)

  ElseIf x > LMEllX And x < LMEllX + 100 And y > LMEllY And y < LMEllY + 100 Then

    Shapes.SetOpacity(LMEllipse, 50)

    Shapes.Zoom(LMEllipse, 0.9, 0.9)

  ElseIf x > LMTriX And x < LMTriX + 100 And y > LMTriY And y < LMTriY + 100 Then

    Shapes.SetOpacity(LMTriangle, 50)

    Shapes.Zoom(LMTriangle, 0.9, 0.9)

  Else

    Shapes.SetOpacity(LMRectangle, 100)

    Shapes.Zoom(LMRectangle, 1, 1)

    Shapes.SetOpacity(LMEllipse, 100)

    Shapes.Zoom(LMEllipse, 1, 1)

    Shapes.SetOpacity(LMTriangle, 100)

    Shapes.Zoom(LMTriangle, 1, 1)

  EndIf

  

  If CanMoveShape = "Yes1" Then

    Shapes.Move(LMRectangle, x - 50, y - 50)

  ElseIf CanMoveShape = "Yes2" Then

    Shapes.Move(LMEllipse, x - 50, y - 50)

  ElseIf CanMoveShape = "Yes3" Then

    Shapes.Move(LMTriangle, x - 50, y - 50)

  EndIf

EndSub



'MouseDown

GraphicsWindow.MouseDown = MouseDown

Sub MouseDown

  x = GraphicsWindow.MouseX

  y = GraphicsWindow.MouseY

  

  If x > LMRecX And x < LMRecX + 100 And y > LMRecY And y < LMRecY + 100 Then

    Sound.PlayClick()

    CanMoveShape = "Yes1"

  ElseIf x > LMEllX And x < LMEllX + 100 And y > LMEllY And y < LMEllY + 100 Then

    Sound.PlayClick()

    CanMoveShape = "Yes2"

  ElseIf x > LMTriX And x < LMTriX + 100 And y > LMTriY And y < LMTriY + 100 Then

    Sound.PlayClick()

    CanMoveShape = "Yes3"

  EndIf

EndSub



'MouseUp

GraphicsWindow.MouseUp = MouseUp

Sub MouseUp

  CanMoveShape = "No"

  Shapes.Animate(LMTriangle, 25, 325, 1000)

  

  GetSlotPositions()

  

  If LMRecX > Slot1X - 80 And LMRecX < Slot1X + 100 And LMRecY > Slot1Y - 80 And LMRecY < Slot1Y + 100 Then

    Sound.PlayClick()

    CanMoveShape = "No"

    Shapes.Animate(LMRectangle, Slot1X, Slot1Y, 500)

  Else

    Shapes.Animate(LMRectangle, 25, 75, 1000)

  EndIf

  

  If LMEllX > Slot2X - 80 And LMEllX < Slot2X + 100 And LMEllY > Slot2Y - 80 And LMEllY < Slot2Y + 100 Then

    Sound.PlayClick()

    CanMoveShape = "No"

    Shapes.Animate(LMEllipse, Slot2X, Slot2Y, 500)

  Else

    Shapes.Animate(LMEllipse, 25, 200, 1000)

  EndIf

  

  If LMTriX > Slot3X - 80 And LMTriX < Slot3X + 100 And LMTriY > Slot3Y - 80 And LMTriY < Slot3Y + 100 Then

    Sound.PlayClick()

    CanMoveShape = "No"

    Shapes.Animate(LMTriangle, Slot3X, Slot3Y, 500)

  Else

    Shapes.Animate(LMTriangle, 25, 325, 1000)

  EndIf

EndSub



'KeyDown

GraphicsWindow.KeyDown = KeyDown

Sub KeyDown

  If GraphicsWindow.LastKey = "R" Then

    Sound.PlayClick()

    Shapes.Animate(LMRectangle, 25, 75, 1000)

    Shapes.Animate(LMEllipse, 25, 200, 1000)

    Shapes.Animate(LMTriangle, 25, 325, 1000)

    ClickedOnce = "No"

    Controls.SetButtonCaption(BtnShowMore, "?†’")

    Controls.SetSize(BtnShowMore, 33, 33)

  EndIf

EndSub



'ButtonClicked

Controls.ButtonClicked = ButtonClicked

Sub ButtonClicked

  If Controls.LastClickedButton = BtnShowMore Then

    Sound.PlayClick()

    If ClickedOnce = "No" Then

      Controls.SetButtonCaption(BtnShowMore, "Press 'R' to return the shapes to the default position")

      Controls.SetSize(BtnShowMore, 413, 33)

      ClickedOnce = "Yes"

    ElseIf ClickedOnce = "Yes" Then

      ClickedOnce = "No"

      Controls.SetButtonCaption(BtnShowMore, "?†’")

      Controls.SetSize(BtnShowMore, 33, 33)

    EndIf

  EndIf

EndSub



'GetShapePositions

Sub GetShapePositions

  LMRecX = Shapes.GetLeft(LMRectangle)

  LMRecY = Shapes.GetTop(LMRectangle)

  LMEllX = Shapes.GetLeft(LMEllipse)

  LMEllY = Shapes.GetTop(LMEllipse)

  LMTriX = Shapes.GetLeft(LMTriangle)

  LMTriY = Shapes.GetTop(LMTriangle)

EndSub



'GetSlotPositions

Sub GetSlotPositions

  Slot1X = Shapes.GetLeft(Slot1)

  Slot1Y = Shapes.GetTop(Slot1)

  Slot2X = Shapes.GetLeft(Slot2)

  Slot2Y = Shapes.GetTop(Slot2)

  Slot3X = Shapes.GetLeft(Slot3)

  Slot3Y = Shapes.GetTop(Slot3)

EndSub


