

'    Easy   Graf   making            2011/12/21      by   NaoChanON  



init()

data()

Plot_Data()







Sub init

  

  GraphicsWindow.top=50

  GraphicsWindow.Left=50

  GraphicsWindow.Width=1000

  GraphicsWindow.Height=600

  GraphicsWindow.Show()

  GraphicsWindow.BackgroundColor="Lightcyan"

  GraphicsWindow.BrushColor="Navy"

  GraphicsWindow.PenColor="Red"

  

  '  Draw   Rectangle  

  GTop=60

  GLeft=150

  GWidth=800

  GHeight=450

  GraphicsWindow.DrawRectangle(Gleft,Gtop,GWidth,GHeight)

  

  '   Title and X,Y-labels

  GraphicsWindow.FontSize=30

  GTitle=Shapes.AddText("  Test Graph  ")

  Shapes.Animate(GTitle,Gleft+150,Gtop-40,1000)

  LabelY=Shapes.AddText(" Y ")

  Shapes.Animate(LabelY,35,(Gtop+GHeight)/2,1500)

  Shapes.Rotate(LabelY,-90)

  LabelX=Shapes.AddText(" X ")

  Shapes.Animate(LabelX,(Gleft+Gwidth)/2-20,Gtop+GHeight+40,2000)

  

  '   X-----Axis

  GraphicsWindow.FontSize=20

  GraphicsWindow.BrushColor="Red"

  MinX=10           '  Minimum X  value  of  X-axis 

  MaxX=160          '  Maximum X  value  of  X-axis     

  XDiv=5            '  dividing  number

  For i= 0 To XDiv

    For j=GTop To GTop+Gheight-5 Step 10

          GraphicsWindow.DrawLine(Gleft+i*(Gwidth/Xdiv),J,Gleft+i*(Gwidth/Xdiv),j+5)

    EndFor 

    DivX=math.round(minX+i*((MaxX-minX)/Xdiv))

    GraphicsWindow.DrawText(Gleft+i*(Gwidth/Xdiv)-20,GTop+GHeight+10,DivX)

  EndFor

 

 '  Y---Axis

  MinY=20            '  Minimum Y  value  of  Y-axis 

  MaxY=170           '  Maximum Y  value  of  Y-axis     

  YDiv=5             '  dividing  number

  For i= YDiv To 0 Step -1

    For j=GLeft To GLeft+Gwidth-5 Step 10

      GraphicsWindow.DrawLine(j,GTop+i*(GHeight/Ydiv),j+5,GTop+i*(GHeight/Ydiv))

    EndFor

    DivY=math.Round(MaxY-i*((MaxY-minY)/Ydiv))

    GraphicsWindow.DrawText(Gleft-40,GTop+i*(GHeight/Ydiv)-10,DivY)

  EndFor

   

EndSub



Sub  Plot_Data     '  Green Circle   ;   Graphic drawing  or  shapes animating

  GraphicsWindow.PenColor="Green"

  GraphicsWindow.PenWidth=3

  For i=1 To NN

    plotx=Gleft + (PX[i]-minX)*Gwidth/(MaxX-MinX)

    ploty=GTop + Gheight- (PY[i]-minY)*Gheight/(MaxY-MinY)

    'GraphicsWindow.FillEllipse(plotx,ploty,10,10)    

    sc[i]=Shapes.AddEllipse(10,10)

    Shapes.Animate(sc[i],plotx-5,ploty-5,1000)

  EndFor

EndSub



Sub  data    '  X data  and  Y  data

  d[1]="10,30"

  d[2]="15,35"

  d[3]="20,40"

  d[4]="25,43"

  d[5]="30,50"

  d[6]="60,74"

  d[7]="80,80"

  d[8]="85,102"

  d[9]="103,114"

  d[10]="120,130"

  

  NN=10    '  data number is 10   ;  see   above 

 

  For i=1 To NN     '  X,Y   data are divided and stored here.

    midP=text.GetIndexOf(d[i],",")            'delimiter  is ","  

    PX[i]=text.GetSubText(d[i],1,midp-1)

    PY[i]=text.GetSubTextToEnd(d[i],midp+1)

  EndFor

  

EndSub














