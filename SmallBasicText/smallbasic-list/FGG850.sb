'Challenge of the month June 2013

'Physics



GraphicsWindow.Title = "Orbit Period & Velocity [Challenge of the month June 2013 (MSDN)] By Amir"

GraphicsWindow.Width = 500

GraphicsWindow.Height = GraphicsWindow.Width

cX = (GraphicsWindow.Width-25)/2

cY = (GraphicsWindow.Height+50)/2

angle = 0

anglearth = angle

pi2 = Math.Pi*2

G = 6.67*Math.Power(10,-11)

M = 5.97219*Math.Power(10,24)

speed = 6

clickFlag = 0

GraphicsWindow.MouseDown = mouseClick

GraphicsWindow.MouseUp = mouseUp

GraphicsWindow.MouseMove = sliderMove



sliderRadiasInit()

satDataDisplay()

erathAndOrbit()

sliderMove()

Sub sliderRadiasInit

  trackSlider = Shapes.AddRectangle(20,GraphicsWindow.Width)

  Shapes.Move(trackSlider,GraphicsWindow.Width-20,0)

  GraphicsWindow.BrushColor = "Red"

  sliderBar = Shapes.AddRectangle(18,18)

  Shapes.Move(sliderBar,GraphicsWindow.Width-19,GraphicsWindow.Height-19)

EndSub



Sub satDataDisplay

  txtHeigth = Shapes.AddText("Height of Satellite : 0.00 Km.")

  txtSpeed = Shapes.AddText("Speed of Satellite : 00.0 Km/s.")

  txtPeriod = Shapes.AddText("Orbit Period: 00.00 hour")

  Shapes.Move(txtSpeed,0,15)

  Shapes.Move(txtPeriod,0,30)

EndSub



Sub sliderMove

  mX = GraphicsWindow.MouseX

  mY = GraphicsWindow.MouseY

  sliderBarLeft = Shapes.GetLeft(sliderBar)

  sliderBarTop = Shapes.GetTop(sliderBar)

  If clickFlag = 1 Then

    If mY < 10 Then

      mY = 10

    ElseIf mY > GraphicsWindow.Height - 10 Then

      mY = GraphicsWindow.Height - 10

    EndIf

    Shapes.Move(sliderBar,GraphicsWindow.Width-19,mY-9)

  EndIf

  rawHeight = Math.Abs((sliderBarTop-9)-GraphicsWindow.Height +30)

  Satheight = Math.Round((rawHeight/480)*50000)

  Shapes.SetText(txtHeigth,"Height of Satellite : "+Satheight+" Km.")

  Satheight = Satheight + 6371

  Satheight = Satheight * 1000

  rad = Math.Round((rawHeight/480)*140)

  If rad < 28 Then

    rad = 28

  EndIf

  speed = 7-(rawHeight/480)*6

EndSub



Sub mouseClick

  If Mouse.IsLeftButtonDown Then

    If mX > sliderBarLeft And mX < sliderBarLeft + 18 And mY > sliderBarTop And mY < sliderBarTop + 18 Then

      clickFlag = 1

    EndIf

  EndIf

EndSub



Sub mouseUp

  clickFlag = 0

EndSub



Sub erathAndOrbit

  GraphicsWindow.DrawRectangle(1,50,GraphicsWindow.Width - 25,GraphicsWindow.Height - 55)

  GraphicsWindow.PenColor = "DarkGreen"

  GraphicsWindow.BrushColor = "Green"

  GraphicsWindow.FillEllipse(cX-30,cY-30,60,60)

  GraphicsWindow.BrushColor = "Blue"

  txtearth = Shapes.AddText("Earth")

  Shapes.Move(txtearth,cX-16,cY-9)

  GraphicsWindow.PenColor = "Black"

  GraphicsWindow.BrushColor = "Gray"

  satellite = Shapes.AddEllipse(20,20)

  Shapes.Move(satellite,cX+190,cY-10)

EndSub



While "True"

  If clickFlag = 0 Then

    period = pi2*(Math.SquareRoot(Math.Power(Satheight,3)/(G*M)))

    velocity = Math.SquareRoot((M*G)/Satheight)/1000

    Shapes.SetText(txtPeriod,"Orbit Period: "+(1/1000*Math.Round((period/3600)*1000))+" hour")

    Shapes.SetText(txtSpeed,"Satellite velocity: "+(1/100000*Math.Round(velocity*100000))+" km/sec")

  EndIf

  angle = angle + speed

  anglearth = anglearth + 2.6875

  x = cx+(rad*Math.Cos(Math.GetRadians(angle))-(rad*Math.Sin(Math.GetRadians(angle))))

  y = cy+(rad*Math.Sin(Math.GetRadians(angle))+(rad*Math.Cos(Math.GetRadians(angle))))

  Shapes.Move(satellite,x-10,y-10)

  'If clickFlag = 0 Then

    'GraphicsWindow.SetPixel(x,y,"Red")

   ' EndIf

  Shapes.Rotate(txtearth,anglearth)

  Program.Delay(20)

  If angle > 360 Then

    angle = 0

  EndIf

  If anglearth > 360 Then

    anglearth = 0

  EndIf

EndWhile


