' Date Checker v0.2

' Copyright (c) 2013 Nonki Takahashi.  All rights reserved.

'

' History:

'  v0.2 03/04/2013 Changed as Date Checker. ()

'  v0.1 14/03/2013 Created as Age Checker. (FDK603)

'

dom = "1=31;2=29;3=31;4=30;5=31;6=30;7=31;8=31;9=30;10=31;11=30;12=31;"

TextWindow.WriteLine("Date Checker v0.2")

While "True"

  error = "True"

  While error

    TextWindow.WriteLine("")

    TextWindow.Write("Date (dd/mm/yyyy)? ")

    date = TextWindow.Read()

    CheckDate()

    If error Then

      TextWindow.WriteLine(errmsg)

    Else

      TextWindow.WriteLine("Date " + date + " is valid.")

    EndIf

  EndWhile

EndWhile

Sub CheckDate

  ' param date - to check

  ' return day, month, year - if the date is valid 

  ' return error - "True" if the date is invalid

  ' return errmsg - error message if error

  If (Text.GetSubText(date, 3, 1) = "/") And (Text.GetSubText(date, 6, 1) = "/") Then

    error = "False"

  Else

    errmsg = "/ missing."

    error = "True"

  EndIf

  If error = "False" Then

    year = Text.GetSubText(date, 7, 4)

    If year + 0 <> year Then

      errmsg = "Year " + year + " is not number."

      error = "True"

    ElseIf (Math.Remainder(year, 4) = 0) And (Math.Remainder(year, 100) <> 0) Or (Math.Remainder(year, 400) = 0) Then

      dom["2"] = 29

    Else

      dom["2"] = 28

    EndIf

  EndIf

  If error = "False" Then

    If Text.GetSubText(date, 4, 1) = "0" Then

      month = Text.GetSubText(date, 5, 1)

    Else

      month = Text.GetSubText(date, 4, 2)

    EndIf

    If (month + 0 <> month) Or (month < 1) Or (12 < month) Then

      errmsg = "Month " + month + " is invalid."

      error = "True"

    EndIf

  EndIf

  If error = "False" Then

    If Text.GetSubText(date, 1, 1) = "0" Then

      day = Text.GetSubText(date, 2, 1)

    Else

      day = Text.GetSubText(date, 1, 2)

    EndIf

    If day + 0 <> day Then

      errmsg = "Day " + day + " is not number."

      error = "True"

    ElseIf day < 1 Then

      errmsg = "Day " + day + " is smaller than 1."

      error = "True"

    ElseIf dom[month] < day Then

      errmsg = "Day " + day + " is bigger than " + dom[month] + "."

      error = "True"

    EndIf

  EndIf

EndSub


