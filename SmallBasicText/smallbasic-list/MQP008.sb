'  Challenge of the month January 2014  //    Image pair game  by NaochanON 



Timer.Tick=Ontick

Timer.Interval=200

Timer.Pause()

GraphicsWindow.MouseDown=MouseDown

OPC=95   '   setopacity level  //  

get_image()

Set_image()

Mask()



Sub MouseDown

  MX= GraphicsWindow.MouseX

  MY= GraphicsWindow.MouseY

  If (Pos["X"]<MX And MX<Pos["X"]+WH["W"]*6) And (Pos["Y"]<MY And MY<Pos["Y"]+WH["H"]*6) Then 

    MCN= Math.Floor((MX-Pos["X"])/WH["W"])+1  '  selected column number 

    MRN= Math.Floor((MY-Pos["Y"])/WH["H"])+1  '  selected row number 

    Shapes.SetOpacity(Mask[MRN][MCN],0)       '  photo is visible 

    NN=NN+1

    couple[NN]=xy[MRN][MCN]                   '  store  selected  photo image number

    MRCN[NN]="X="+MCN+";Y="+MRN               '  store  selected column number and row number  

    If NN=2  then 

      if  couple[1]<>couple[2] Then 

        Shapes.SetOpacity(Mask[MRCN[1]["Y"]][MRCN[1]["X"]],opc)  '  hide photo

        Program.Delay(1000)

        Shapes.SetOpacity(Mask[MRCN[2]["Y"]][MRCN[2]["X"]],opc)  '  hide photo

      ElseIf couple[1]=couple[2] Then        '    pair images 

        correct=correct+1

        If correct=M then                    '    all pair images are gotten

          Timer.Pause()

          Sound.PlayChimeAndWait()

          Shapes.SetText(msg,"Congratulation !  You finihed!!!")

        EndIf

      endif

      couple=""

      MRCN=""

      NN=0

    EndIf

  EndIf

EndSub



Sub Ontick

  Shapes.SetText(msg,"Find image pairs!   Ellapsed time= "+Math.Floor((Clock.ElapsedMilliseconds-t0)/100)/10+" sec")

EndSub



Sub get_image 

  GraphicsWindow.Width=840

  GraphicsWindow.Height=570

  GraphicsWindow.BackgroundColor="Darkgreen"

  GraphicsWindow.FontSize=20

  GraphicsWindow.BrushColor="Yellow"

  msg= Shapes.AddText("  ")

  Shapes.Zoom(msg,2,5)

  Shapes.Move(msg,150,55)

  M=15

  WH="W=120;H=100"

  Pos="X=50;Y=30"

  For i=1 To M

    IMG_Retry:

    url=Flickr.GetRandomPicture("Cat")  '  <-----------  get photos from Flickr 

    If Text.IsSubText(IMGSum,url) or url="" Then 

      Goto IMG_Retry

    EndIf

    IMGSum=IMGSum+url+";"

    img[i]=ImageList.LoadImage(url)

    imgw[i]= ImageList.GetWidthOfImage(img[i])

    imgh[i]= ImageList.GetHeightOfImage(img[i])

    txt=txt+"?—"

    Shapes.SetText(msg,"Loading "+txt)

  EndFor

  Shapes.Zoom(msg,1,1)

  Shapes.Move(msg,150,5)

EndSub



Sub Set_image 

  WH="W=120;H=100"

  Pos="X=50;Y=30"

  For i=1 To M

    photo[2*i-1]= Shapes.AddImage(img[i])

    Shapes.Zoom(photo[2*i-1],WH["W"]/imgw[i],WH["H"]/imgh[i])

    photo[2*i]= Shapes.AddImage(img[i])

    Shapes.Zoom(photo[2*i],WH["W"]/imgw[i],WH["H"]/imgh[i])

    getrandomposition()

    Shapes.Move(photo[2*i-1],Pos["X"]+WH["W"]*(CN-1)-imgw[i]/2+WH["W"]/2,Pos["Y"]+WH["H"]*(RN-1)-imgh[i]/2+WH["H"]/2)

    getrandomposition()

    Shapes.Move(photo[2*i],Pos["X"]+WH["W"]*(CN-1)-imgw[i]/2+WH["W"]/2,Pos["Y"]+WH["H"]*(RN-1)-imgh[i]/2+WH["H"]/2)

  EndFor

EndSub



Sub getrandomposition

  retry:

  N= Math.GetRandomNumber(2*M)

  If Text.IsSubText(sum,"S"+N+"E")  Then  

    Goto retry

  EndIf

  sum=sum+"S"+N+"E"+";"

  CN=math.Remainder(N-1,6)+1   '  colomn number 

  RN=math.Floor((N-1)/6)+1     '  row  number 

  xy[RN][CN]=i

EndSub



Sub Mask

  GraphicsWindow.BrushColor="Lightgray"

  GraphicsWindow.PenColor="Black"

  For K=1 To 5

    For J=1 To 6

      Mask[k][j]= Shapes.AddRectangle(WH["W"],WH["H"])

      Shapes.Move( Mask[k][j],Pos["X"]+(j-1)*WH["W"],Pos["Y"]+(k-1)*WH["H"])

      Shapes.SetOpacity(Mask[k][j],opc)

    EndFor

  EndFor

  t0=clock.ElapsedMilliseconds 

  Timer.Resume()

EndSub

  


