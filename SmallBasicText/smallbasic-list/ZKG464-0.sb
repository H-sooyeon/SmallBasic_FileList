' Snake Game 0.1a
' Copyright (c) 2014 Nonki Takahashi.  All rights reserved.
'
' History:
'  0.1 2014-02-21 Created. (ZKG464-0)
'
GraphicsWindow.Title = "Snake Game 0.1a - Use [←] [→] keys."
Init()
CreateSnake()
CreateApple()
CreateMask()
StartSnake()
inGame = "True"
ob = "False"
While Not[ob]
  Program.Delay(200)
EndWhile
GameOver()
Sub GameOver
  ShowMask()
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.FontName = "Arial"
  fs = 60
  GraphicsWindow.FontSize = fs
  len = 367
  x = Math.Floor((gw - len) / 2)
  y = Math.Floor((gh - fs) / 2)
  msg = Shapes.AddText("GAME OVER")
  Shapes.Move(msg, x, y)
EndSub
Sub CreateMask
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "Black"
  mask = Shapes.AddRectangle(gw, gh)
  Shapes.SetOpacity(mask, 0)
EndSub
Sub ShowMask
  Shapes.SetOpacity(mask, 80)
EndSub
Sub StartSnake
  GraphicsWindow.KeyDown = OnKeyDown
  Timer.Interval = 500
  Timer.Tick = OnTick
EndSub
Sub OnTick
  MoveSnake()
EndSub
Sub OnKeyDown
  key = GraphicsWindow.LastKey
  vx = snake["vx"]
  vy = snake["vy"]
  If key = "Left" Then
    snake["vx"] = vy
    snake["vy"] = -vx
  ElseIf key = "Right" Then
    snake["vx"] = -vy
    snake["vy"] = vx
  EndIf
EndSub
Sub MoveSnake
  col = snake["head.col"]
  row = snake["head.row"]
  colLast = col
  rowLast = row
  col = col + snake["vx"]
  If col < 1 Or nCols < col Then
    ob = "True"
    Goto MSEnd
  EndIf
  snake["head.col"] = col
  row = row + snake["vy"]
  If row < 1 Or nRows < row Then
    ob = "True"
    Goto MSEnd
  EndIf
  snake["head.row"] = row
  CalcXYFromColRow()
  Shapes.Move(snake["head.obj"], x, y)
  For i = 1 To snake["length"] - 1
    col = colLast
    row = rowLast
    CalcXYFromColRow()
    Shapes.Move(snake["body" + i + ".obj"], x, y)
    colLast = snake["body" + i + ".col"]
    rowLast = snake["body" + i + ".row"]
    snake["body" + i + ".col"] = col
    snake["body" + i + ".row"] = row
  EndFor
  MSEnd:
EndSub
Sub CreateSnake
  snake["head.color"] = "DarkCyan"
  snake["body.color"] = "SeaGreen"
  snake["length"] = 2
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = snake["head.color"]
  snake["head.obj"] = Shapes.AddEllipse(size, size)
  snake["head.col"] = Math.Floor(nCols / 2)
  snake["head.row"] = Math.Floor(nRows / 2)
  col = snake["head.col"]
  row = snake["head.row"]
  CalcXYFromColRow()
  Shapes.Move(snake["head.obj"], x, y)
  GraphicsWindow.BrushColor = snake["body.color"]
  snake["body1.obj"] = Shapes.AddEllipse(size, size)
  snake["body1.col"] = snake["head.col"] - 1
  snake["body1.row"] = snake["head.row"]
  col = snake["body1.col"] 
  row = snake["body1.row"]
  CalcXYFromColRow()
  Shapes.Move(snake["body1.obj"], x, y)
  snake["vx"] = 1 
  snake["vy"] = 0
EndSub
Sub CreateApple
  apple["color"] = "Red"
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = apple["color"]
  apple["obj"] = Shapes.AddEllipse(size, size)
  apple["col"] = nCols - 2
  apple["row"] = 2
  col = apple["col"] 
  row = apple["row"]
  CalcXYFromColRow()
  Shapes.Move(apple["obj"], x, y)
EndSub
Sub CalcXYFromColRow
  ' param col, row - co-ordinate of field
  ' return x, y - co-ordinate of graphics
  x = (col - 1) * size
  y = (row - 1) * size
EndSub
Sub Init
  Not = "True=False;False=True;"
  size = 20
  nRows = 18
  nCols = 24
  gw = nCols * size
  gh = nRows * size
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.BackgroundColor = "Black"
EndSub
