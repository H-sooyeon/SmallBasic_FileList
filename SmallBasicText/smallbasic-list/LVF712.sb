'  A-HA  experience    By  NaochanON



    GraphicsWindow.MouseDown=Check

    Controls.ButtonClicked=PlayAgain

    init()                 '  Initial View

    PlayAgain()            '  Load picture & change color & count time

    

    While "True"

       Ptime()             '  count time

    endwhile

    

 

Sub PlayAgain

    sremove()              '  remove message

    Url_Photo()            '  Gets a photo from Flickr or User's file

    t0=clock.ElapsedMilliseconds

    Change_Color()         '  change color  in box size   100*60 dots     

EndSub



Sub Change_Color

  Lmargin=Math.GetRandomNumber(10)*100  '   random position -X

  Tmargin=Math.GetRandomNumber(6)*100   '   random position -Y

  For J=1 To 60 Step 2

   Ptime()

    For i=1 To 100 Step 2

      CLR= GraphicsWindow.GetPixel(Lmargin+i,Tmargin+j) ' gets pixel -color 

      NCLR=text.GetSubText(CLR,1,5)+"77"                '  changes   Blue code 

      GraphicsWindow.SetPixel(Lmargin+i,Tmargin+j,NCLR) ' change its color 

    EndFor

 EndFor

EndSub



Sub Ptime

   dtime=Math.Round((clock.ElapsedMilliseconds-t0)/1000)

   Shapes.SetText(stime,dtime+" sec")  

EndSub



Sub sremove

   Shapes.Move(smsgOK,Math.GetRandomNumber(1000),-200)

   Shapes.Move(smsgNG,Math.GetRandomNumber(1000),-200) 

EndSub



Sub check

  sremove()

  MX= GraphicsWindow.MouseX

  MY= GraphicsWindow.MouseY

  If Math.abs(MX-Lmargin)<100 And Math.abs(MY-Tmargin)<60 Then  ' Hit  

    Sound.PlayChime()

    Shapes.animate(smsgOK,MX,MY,500)

    PlayAgain()

  Else 

    Shapes.animate(smsgNG,MX,MY,500)

    Sound.PlayClickAndWait()

  EndIf

EndSub



Sub init  

  GW=1300

  GH=700

  GraphicsWindow.Left=20

  GraphicsWindow.Top=10

  GraphicsWindow.Width=GW

  GraphicsWindow.Height=GH

  GraphicsWindow.Show()

  GraphicsWindow.BrushColor="Yellow"

  GraphicsWindow.FontSize=24

  smsgOK=shapes.AddText("Congratulation! You are right!")

  Shapes.Move(smsgOK,200,-200)

  smsgNG=shapes.AddText("Boooo!  You are wrong!" )

  Shapes.Move(smsgNG,200,-200)

  next= Controls.AddButton("Play Again",20,20)

  Controls.SetSize(next,150,50)

  stime=Shapes.AddText(" ")

  Shapes.Move(stime,50,100)  

EndSub



Sub Url_Photo

   'url="http://farm4.static.flickr.com/3152/2668377616_c14dc5a823.jpg" '

   url= Flickr.GetRandomPicture("Flower")     '   

   PX=ImageList.LoadImage(url)

   GraphicsWindow.DrawResizedImage(PX,0,0,GW,GH)

   GraphicsWindow.Title=url

Endsub 


