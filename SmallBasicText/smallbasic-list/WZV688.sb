GraphicsWindow.BackgroundColor = "LightBlue"
GraphicsWindow.BrushColor = "Gray"
size = 100
ellipse = Shapes.AddEllipse(size,size/4)
maxSpeed = 4
minSpeed = 0
maxTurn = 1.5
minTurn = 0
turnAccel = 0.01
speedAccel = 0.01
friction = 0.003
beginMaxSpeed = maxSpeed
Shapes.Move(ellipse,-size/2,GraphicsWindow.Height/2-size/2)
Moving.Objecttype = ellipse
Moving.Addobject()
Moving.Objectselected = 1
GraphicsWindow.KeyDown = KeyDown
GraphicsWindow.KeyUp = KeyUp
While 1=1
  If Left Then
    Moving.Angle = Moving.Angle-turnRate
    turnRate = turnRate+turnAccel
    speed = speed-speedAccel/2
  EndIf
  If Right Then
    Moving.Angle = Moving.Angle+turnRate
    turnRate = turnRate+turnAccel
    speed = speed-speedAccel/2
  EndIf
  If Up Then
    speed = speed+speedAccel
    turnRate = turnRate-turnAccel/2
  EndIf
  If Down Then
    speed =  speed-speedAccel
    turnRate = turnRate-turnAccel/2
  EndIf
  If Space Then
    maxSpeed = beginMaxSpeed*2
  Else
    maxSpeed = beginMaxSpeed
  EndIf
  speed = speed-friction
  turnRate = turnRate-friction
  speed = Math.Max(Math.Min(speed,maxSpeed),minSpeed)
  turnRate = Math.Max(Math.Min(turnRate,maxTurn),minTurn)
  Moving.Distance = speed
  Moving.Moveandrotate()
  Program.Delay(10)
EndWhile
Sub KeyDown
  Key = GraphicsWindow.LastKey
  If Key = "Left" Then
    Left = "True"
  ElseIf Key = "Right" Then
    Right = "True"
  ElseIf Key = "Up" Then
    Up = "True"
  ElseIf Key = "Down" Then
    Down = "True"
  ElseIf Key = "Space" Then
    Space = "True"
  EndIf
EndSub
Sub KeyUp
  Key = GraphicsWindow.LastKey
  If Key = "Left" Then
    Left = "False"
  ElseIf Key = "Right" Then
    Right = "False"
  ElseIf Key = "Up" Then
    Up = "False"
  ElseIf Key = "Down" Then
    Down = "False"
  ElseIf Key = "Space" Then
    Space = "False"
  EndIf
EndSub
