'  How to win (1st player)  match sticks removing game
'    
'    Generally   1stturn & choice1st  must be    (StartMatch  -   4* math.floor((StartMatch-1)/4)  +  1 )   to win . 
'      1st Left  must  be     5   9   13   17  21  25 ......
'
'   Example
'     StartMatch   =    15 
'  1 turn 
'  choice1st=2                               left  :  13 =  4* math.floor((matchsticks-1)/4)  +  1  
'  choice2nd= 1   or  2  or  3       left  :  12  11  10
'  2 turn    
'   choice1st=4-choice2nd         left  :  9
'  choice2nd= 1   or  2  or  3     left  :  8     7     6  
'  3 turn                                   
'   choice1st=4-choice2nd         left   :  5 
'   choice2nd= 1   or  2  or  3     left  :  4   3    2
' 4 turn     ..... left <  4
'   choice1st=leftnumber-1     .....3   2   1   
'  choice2nd= 1    
'
 
  MatchNumber =12+Math.GetRandomNumber(13)  ' 13 --- 25 
  StartNumber = 4* math.floor((MatchNumber-1)/4) + 1  '   1stturn & choice1st  
  Controls.ButtonClicked = OnButtonClicked

  init_View()
  data()
  Init_Match() 
  PC_Level()
  Select_start()
 
Sub PC_choice   ' considered PC's level
  PC_Take = 0
  StartNumber = 4* math.floor((MatchNumber-1)/4) + 1 
  for k=1 to 3
    If (MatchNumber - k = StartNumber And math.Floor((MatchNumber-k)/Force)<=3 ) Then
      PC_Take = K
      EndIf
  endfor
  If MatchNumber<=3 Then 
    PC_Take = MatchNumber-1
  EndIf
  If  PC_Take = 0 Then 
    PC_Take = Math.GetRandomNumber(3)
  EndIf
  MatchNumber = MatchNumber - PC_Take
 
    If MatchNumber<1 Then
      Shapes.Zoom(ss[8],4,4)
      Shapes.Animate(ss[8],450,300,500)  'You win 
       Turtle.Speed=4
       Turtle.MoveTo(20,20)
       Program.End()
    EndIf
    '
    Shapes.move(ss[6],300,-100)      ' PC's choice
    Shapes.move(PCNmb,100,-100)      ' remove  last data 
    Shapes.Animate(ss[6],20,250,800)   ' PC's choice
    PCNmb=Shapes.AddText(PC_Take) 
    Shapes.Animate(PCNmb,170,250,800)  ' PC's choice number
    nm=PC_Take
    '
    Match_remove()                    '  remove matchs
    Choice_Start()  ' To  your choice  waiting
  EndSub
  
Sub OnButtonClicked
  name=""
  GraphicsWindow.BrushColor="Yellow"
  name=controls.LastClickedButton     'Last button
   nm=controls.GetButtonCaption(name)  ' Button's caption
  '
    if (0<nm  and nm<4 )  then 
       Your_choice()
    elseif nm="YouStart" then 
      Choice_Start()   '  
    elseif  nm="PCStart" then
      Choice_Start()
      PC_choice()
    endif
EndSub

Sub PC_Level
  Shapes.Animate(ss[12],200,20,1000)
  btn=1               '  for  PC's level  control button
  Button_Set()
  Loop:
   If nm="" Then 
     Goto Loop
   EndIf
EndSub

Sub Select_start
  Level=nm    ' PC's   level
  Force=text.GetSubText(Level,2,1)  '  remove   "L"
  GraphicsWindow.DrawText(200+BW*4,50,"Level = "+Force) 
  nm=""
  Shapes.Remove(ss[12])
  Shapes.Animate(ss[3],200,20,800)   ' you or PC start 
  btn=2             
  Button_Set()    '  for  start first and choice number control button
  Program.Delay(2000)
EndSub
 
Sub Choice_Start
    Shapes.move(ss[3],100,-100)
    Shapes.move(ss[4],100,-100)
    Shapes.Animate(ss[4],300,150,800) ' Put 1 or 2 or  3 
EndSub
  
Sub Your_choice
  MatchNumber = MatchNumber - nm     ' new matchnumber 
  Shapes.move(ss[5],100,-100)         ' your choice 
  Shapes.move(ss[last],100,-100)      ' remove  last data 
  Shapes.Animate(ss[5],20,200,500)    ' your choice 
  Shapes.Animate(ss[nm+8],170,200,500) ' 1  or  2  or  3
  last=nm+8 
  '
  Match_remove()                    ' remove matchs
  '
  If MatchNumber < 1 Then
    Shapes.Zoom(ss[7],4,4)
    Shapes.Animate(ss[7],450,300,500)    ' you lose 
     Turtle.Speed=4
     Turtle.MoveTo(20,20)
     Program.End()
   EndIf
   PC_choice()     ' To  PC choice
EndSub


Sub Match_remove    ' Turtle  removes matchs  
  For i= MatchNumber+nm To MatchNumber+1 Step -1  ' remove matchs
    Turtle.Show()
    Turtle.Speed=8
    Turtle.PenUp()    
    Match_X=150+BW+(i-1)*20 +30*(Math.floor((i-1)/5))   'Match  X  position 
    Turtle.MoveTo(Match_X,200)
    Turtle.Speed=6
    Turtle.Turn(360)
    For j=1 To 7
     Shapes.Animate(Match2[i],Match_X+50*j,200-100*j,500)
     Shapes.Animate(Match1[i],Match_X+50*j,200-100*j,500)
    EndFor
   EndFor 
  '
  Shapes.Remove(MN)
  MSG="Now Match left= "+MatchNumber
  MN=shapes.AddText(MSG)
  Shapes.Animate(MN,20,350,800)     'left matchnumber
EndSub

Sub  Button_Set 
    GraphicsWindow.BrushColor = "Blue"
    nn=0
    dm=s[btn]
    Loop2:
      end_P=Text.GetIndexOf(dm," ")
      msg=text.GetSubText(dm,1,end_P-1)  ' word 
      nn=nn+1 
      btn[nn][1] =Controls.AddButton(msg, 200+BW*(nn-1), BH+50)
      Controls.SetSize(btn[nn][1] ,BW-2,BH-2)  'Set  5 Control buttons  
      If nn=WN Then 
         Goto end1
      EndIf
      dm=text.GetSubTextToEnd(dm,end_p+1)   'gets number value 
    Goto loop2
    end1: 
EndSub

Sub Init_Match
  For i=1 To Matchnumber
    GraphicsWindow.PenColor="Yellow"
    GraphicsWindow.BrushColor="Khaki"
    Match1[i]=Shapes.AddRectangle(15,150)    
    GraphicsWindow.PenColor="Red"
    GraphicsWindow.BrushColor="Red"
    Match2[i]=Shapes.AddEllipse(15,25)
    '
    rnd_X=math.GetRandomNumber(1000)
    Shapes.Move(Match2[i],rnd_X,0)
    Shapes.Move(Match1[i],rnd_X,0)
    Match_X=150+BW+(i-1)*20 +30*(Math.floor((i-1)/5))    '  Match  X  position 
    Shapes.Animate(Match2[i],Match_X,200,800+80*i)
    Shapes.Animate(Match1[i],Match_X,225,800+80*i)
    EndFor
    GraphicsWindow.BrushColor="Yellow" 
    GraphicsWindow.DrawText(200+BW*3,50,"Match = "+MatchNumber) 
EndSub

Sub data  ' Control button  and  message  
  s[1]="L1 L2 L3 L4 L5 "
  s[2]="YouStart PCStart 1 2 3 " 
  s[3]="Select  Start button  you start or PC start "
  s[4]="Push 1 or 2 or 3 to take matches away "
  s[5]="Your choice = "
  s[6]="PC's choice = "
  s[7]=" You lose ! "
  s[8]=" You win ! "
  s[9] ="1"
  s[10]="2"
  s[11]="3"
  s[12]="Select PC's level ;  weak L1  ---  L5  strong "
  '
  For j=3 To 12
     ss[j]=shapes.AddText(s[j])
     Shapes.move(ss[j],100+50*J,-100)   '  hide shapes
  EndFor
EndSub

Sub init_View
   WN=5   '  control button number
   BW=125
   BH=45 
   GW=1.5*BW*WN
   GH=BH*12
   GraphicsWindow.clear()
   GraphicsWindow.top=0
   GraphicsWindow.Width = GW
   GraphicsWindow.Height = GH
   GraphicsWindow.Title = "Match Choice"
   GraphicsWindow.Show()
   GraphicsWindow.BackgroundColor = "Teal"
   GraphicsWindow.BrushColor = "White"
   GraphicsWindow.BrushColor ="Yellow"
   GraphicsWindow.FontSize = 20
   GraphicsWindow.FontName = "Coorie new"
   GraphicsWindow.FontItalic="True"
EndSub
  

  
  
