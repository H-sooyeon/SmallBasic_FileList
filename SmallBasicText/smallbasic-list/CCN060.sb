'Small 3D renderer for the Twenty Five Line Challenge

'by ThirdMagus



'WARNING! MAY CONTAIN LINEAR ALGEBRA!



GraphicsWindow.Title = "Small 3D renderer"

GraphicsWindow.Height = 400

GraphicsWindow.Width = 400



'The number of triangles in our model

NUMBER_OF_TRIANGLES = 4





'Model:

'Each column represents a vector. The first row holds the x-coordinate, the 2nd y, the 3rd z and the 4th row contains only '1's.

'Each group of three columns represents a triangle.

'The following model is a somewhat elongated Triangular Piramid with corners at (0,-10,0), (-7,6,1), (1,7,-7) and (5,5,5).

Model = "0=0\=0\;1\=-7\;2\=1\;3\=0\;4\=1\;5\=5\;6\=0\;7\=5\;8\=-7\;9\=5\;10\=1\;11\=-7\;;1=0\=-10\;1\=6\;2\=7\;3\=-10\;4\=7\;5\=5\;6\=-10\;7\=5\;8\=6\;9\=5\;10\=7\;11\=6\;;2=0\=0\;1\=1\;2\=-7\;3\=0\;4\=-7\;5\=5\;6\=0\;7\=5\;8\=1\;9\=5\;10\=-7\;11\=1\;;3=0\=1\;1\=1\;2\=1\;3\=1\;4\=1\;5\=1\;6\=1\;7\=1\;8\=1\;9\=1\;10\=1\;11\=1\;;"



'Color:

'Each column represents the color of a triangle.The first row holds the Red value, the 2nd Green and the 3rd Blue; all from 0 to 255.

'The order of the colors corresponds to the order of the triangles in the model specified above.

'The triangles of the model are colored (in order): Red, Green, Blue and Yellow.

Color = "0=0\=255\;1\=0\;2\=0\;3\=255\;;1=0\=0\;1\=255\;2\=0\;3\=255\;;2=0\=0\;1\=0\;2\=255\;3\=0\;;"



'Phi: the y-axis rotation of the model (in radians)

Phi = 0



'Label for the infinite loop

repeat:



'Rotation matrix:

'[cos(phi)   0  sin(phi)   0]

'[      0          1       0          0]

'[-sin(phi)  0  cos(phi)  0]

'[      0         0       0           1]

'where phi is the y-axis rotation of the model (in radians)



'Translation matrix:

'[1    0    0    0  ]

'[0    1    0    0  ]

'[0    0    1    z   ]

'[0    0    0   1   ]

'where z is the z offset of the model (how far it is behind the screen)

'z is fixed at 100 for this demo.



'Projection matrix:

'[1    0       0      0]

'[0    1       0      0]

'[0    0       1      0]

'[0    0    1/d    0]

'where d is the distance from the viewer to the screen

'd is fixed at 1000 for this demo



'Combine into single transform matrix

'Projection * Translation * Rotation =

'[cos(phi)     0         sin(phi)       0   ]

'[      0            1               0             0   ]

'[-sin(phi)    0         cos(phi)      z    ]

'[      0            0             1/d         z/d ]

Transform = "0=0\="+(Math.Cos(Phi))+"\;1\=0\;2\="+(Math.Sin(Phi))+"\;3\=0\;;1=0\=0\;1\=1\;2\=0\;3\=0\;;2=0\="+(-1*Math.Sin(Phi))+"\;1\=0\;2\="+(Math.Cos(Phi))+"\;3\="+100+"\;;3=0\=0\;1\=0\;2\="+0.001+"\;3\="+0.1+"\;;"



'Matrix multiplication: Transform * Model

For j=0 To (3 * NUMBER_OF_TRIANGLES - 1)

  For i=0 To 3

    C[i][j] = Transform[i][0] * Model[0][j] + Transform[i][1] * Model[1][j] + Transform[i][2] * Model[2][j] + Transform[i][3] * Model[3][j]

  EndFor

EndFor



'We clear the GraphicsWindow to erase the previous frame.

GraphicsWindow.Clear()



For t=0 To (NUMBER_OF_TRIANGLES - 1)

  

'Backface culling: Display only if the z-component of the normal of the triangle is negative (facing towards us.) This means triangles have to be specified in CCW order.

  If ((C[0][1+3*t]/C[3][1+3*t] - C[0][0+3*t]/C[3][0+3*t]) * (C[1][1+3*t]/C[3][1+3*t] - C[1][2+3*t]/C[3][2+3*t]) < (C[1][1+3*t]/C[3][1+3*t] - C[1][0+3*t]/C[3][0+3*t]) * (C[0][1+3*t]/C[3][1+3*t] - C[0][2+3*t]/C[3][2+3*t])) Then

'Set the fill color according to the Color matrix;

    GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(Color[0][t],Color[1][t],Color[2][t])

'Draw the triangle. We divide each coordinate by the value in the 4th row return from homogenous coordinates to regular ones. We also add 200 in order to center the image somewhat.

    GraphicsWindow.FillTriangle(C[0][0+3*t]/C[3][0+3*t]+200,C[1][0+3*t]/C[3][0+3*t]+200,C[0][1+3*t]/C[3][1+3*t]+200,C[1][1+3*t]/C[3][1+3*t]+200,C[0][2+3*t]/C[3][2+3*t]+200,C[1][2+3*t]/C[3][2+3*t]+200)

  EndIf

EndFor

'Delay between each frame. Decrease this value to increase performance.

Program.Delay(10)

'Add 0.1 radians to the angle. Add more to make it rotate faster.

Phi = Phi + 0.1

'Render the next frame

Goto repeat






