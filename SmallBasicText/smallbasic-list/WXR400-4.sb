'**************

' NyanSnake

' by GAniman

'**************



' ===============================================================================

' Initialization

' ===============================================================================



Path= Program.Directory + "/

Path= "http://ximages.net/images/

gw= 600

gh= 400

GraphicsWindow.Width=  gw

GraphicsWindow.Height= gh

GraphicsWindow.Left= (Desktop.Width  - gw)/2

GraphicsWindow.Top=  (Desktop.Height - gh)/2

GraphicsWindow.CanResize= "False

'GraphicsWindow.CanResize= "True

GraphicsWindow.BackgroundColor= "Cyan

GraphicsWindow.Title= "NyanSnake

GraphicsWindow.PenWidth= 2



skip=  1

delay= 3*skip

szBonus=  20

szHBonus= szBonus/2

x= gw/2

y= gh/2

delX= skip

delY= 0

Direction= 2

Running= "True



CreateShapes()

GraphicsWindow.KeyDown= Keyboard

Timer.Interval= 1000

Timer.Tick= Elapse



' ===============================================================================

' Main Loop

' ===============================================================================



Loop:



'gw= GraphicsWindow.Width

'gh= GraphicsWindow.Height



If Running Then

  x= x + delX

  y= y + delY

EndIf



If RainBow Then

  GraphicsWindow.PenColor= GraphicsWindow.GetRandomColor()

  GraphicsWindow.DrawEllipse(x + sizeH, y + sizeH/2, 2, 2)

EndIf



CheckBorders()

MoveSprites()

InspectFoods()

CreateSuperFood()

Program.Delay(delay)



Goto Loop



' ===============================================================================

' Subroutines

' ===============================================================================



Sub CreateShapes

  

  Nyan1=  ImageList.LoadImage(Path + "28930020147868715423.png")

  Nyan2=  ImageList.LoadImage(Path + "74412177564379571315.png")

  Pill=   ImageList.LoadImage(Path + "03515726579119112960.png")

  size=   ImageList.GetWidthOfImage(Nyan1)

  szFood= ImageList.GetWidthOfImage(Pill )

  

  If size   < 8 Then

    size=   32

  EndIf

  If szFood < 4 Then

    szFood= 20

  EndIf

  sizeH=   size/2

  szHFood= szFood/2

  

  Sprite[1]= Shapes.AddImage(Nyan1)    ' Left

  Sprite[2]= Shapes.AddImage(Nyan2)    ' Right

  Sprite[3]= Shapes.AddImage(Nyan1)    ' Down (same image as left)

  Sprite[4]= Shapes.AddImage(Nyan2)    ' Up      (same image as right)

  Shapes.Rotate(Sprite[3],270)         ' Pre-rotate it downwards

  Shapes.Rotate(Sprite[4],270)         ' Pre-rotate it upwards

  

  food= Shapes.AddImage(Pill)          ' Rainbow Food Pill

  GenerateFood()

  

  GraphicsWindow.FontSize= 20

  GraphicsWindow.BrushColor= "DarkGreen

  display= Shapes.AddText("0")

  Shapes.Move(display, gw/2,20)

  

  GraphicsWindow.BrushColor= "Red

  time= Shapes.AddText("0s")

  Shapes.Move(time, 20,5)

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub HideSprites

  

  For index= 1 To 4

    Shapes.HideShape(Sprite[index])

  EndFor

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub MoveSprites

  

  If LastDirection <> Direction Then

    HideSprites()

    Shapes.ShowShape(Sprite[Direction])

    LastDirection= Direction

  EndIf

  

  'Shapes.Move(Sprite[Direction], x,y)

  For index= 1 To 4

    Shapes.Move(Sprite[index], x,y)

  EndFor

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub GenerateFood

  

  foodX= Math.GetRandomNumber(gw - szFood)

  foodY= Math.GetRandomNumber(gh - szFood)

  Shapes.Move(food, foodX,foodY)

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub CreateSuperFood

  

  If Math.Remainder(score, 5) = 0 And BonusFood <> "True" Then    

    GraphicsWindow.PenColor=   GraphicsWindow.GetRandomColor()

    GraphicsWindow.BrushColor= GraphicsWindow.GetRandomColor()

    foodXX= Math.GetRandomNumber(gw -  szBonus)

    foodYY= Math.GetRandomNumber(gh -  szBonus)

    bonus=  Shapes.AddEllipse(szBonus, szBonus)

    Shapes.Move(bonus, foodXX,foodYY)

    BonusFood= "True

  EndIf

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub InspectFoods

  

  GraphicsWindow.Title= "(" +x+ "," +y+ ")   (" +foodX+ "," +foodY+ ")   (" +foodXX+ "," +foodYY+ ")

  

  'If Math.Abs(x-foodX) <= szFood And Math.Abs(y-foodY) <= szFood Then

  If x+size >= foodX And x <= foodX+szHFood And y+sizeH >= foodY And y <= foodY+szHFood Then

    score= score + 1

    Shapes.SetText(display,score)

    Sound.PlayChimesAndWait()

    GenerateFood()

  EndIf

  

  If BonusFood Then

    If x+size >= foodXX And x <= foodXX+szHBonus And y+sizeH >= foodYY And y <= foodYY+szHBonus Then

      score= score + 3

      Shapes.SetText(display,score)

      Sound.PlayBellRingAndWait()

      Shapes.Remove(bonus)

      BonusFood= "False

    EndIf

  EndIf

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub CheckBorders

  

  If x < -sizeH Then

    x= gw

    LastDirection= 0

  EndIf

  If x > gw + sizeH Then

    x= -sizeH

    LastDirection= 0

  EndIf

  If y < -sizeH Then

    y= gh

    LastDirection= 0

  EndIf

  If y > gh + sizeH Then

    y= -sizeH

    LastDirection= 0

  EndIf

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub Keyboard

  

  Key= GraphicsWindow.LastKey

  

  If Key = "Left"  Or key = "A" Then

    Direction= 1

    delX= -skip

    delY= 0

  EndIf

  

  If Key = "Right" Or key = "D" Then

    Direction= 2

    delX= skip

    delY= 0

  EndIf

  

  If Key = "Down"  Or key = "S" Then

    Direction= 3

    delX= 0

    delY= skip

  EndIf

  

  If Key = "Up"    Or key = "W" Then

    Direction= 4

    delX= 0

    delY= -skip

  EndIf

  

  If Key = "Escape" Then

    Sound.PlayChimeAndWait()

    Program.End()

  EndIf

  

  If Key = "Home" Or Key = "Delete" Then

    GraphicsWindow.BrushColor= GraphicsWindow.BackgroundColor

    GraphicsWindow.FillRectangle(0,0, gw,gh)

  EndIf

  

  If Key = "Back" Then

    If RainBow Then

      RainBow= "False

    Else

      RainBow= "True

    EndIf

  EndIf

  

  If Key = "Return" Then

    If Running Then

      Running= "False

      Timer.Pause()

    Else

      Running= "True

      Elapse()

      Timer.Resume()

    EndIf

  EndIf

  

EndSub

' ----------------------------------------------------------------------------------------------------------------------------------------

Sub Elapse

  

  timing= timing + 1

  Shapes.SetText(time, timing+"s")

  

EndSub



' ===============================================================================


