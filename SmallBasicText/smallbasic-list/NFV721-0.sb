Initialise()



While 0=0

  

  If keyDown And GraphicsWindow.LastKey = "Down" Then

    If GraphicsWindow.GetPixel(X, Y + size[i]) <> wall And GraphicsWindow.GetPixel(X + size[i], Y + size[i]) <> wall Then

      Y = Shapes.GetTop(sprite) + speed[i]

    EndIf

  ElseIf keyDown And GraphicsWindow.LastKey = "Up" And Y > 0 Then

    If GraphicsWindow.GetPixel(X, Y) <> wall And GraphicsWindow.GetPixel(X + size[i], Y) <> wall Then

      Y = Shapes.GetTop(sprite) - speed[i]

    EndIf

  ElseIf keyDown And GraphicsWindow.LastKey = "Right" Then

    If GraphicsWindow.GetPixel(X + size[i], Y) <> wall And GraphicsWindow.GetPixel(X + size[i], Y + size[i]) <> wall Then

      X = Shapes.GetLeft(sprite) + speed[i]

    EndIf

  ElseIf keyDown And GraphicsWindow.LastKey = "Left" Then

    If GraphicsWindow.GetPixel(X, Y) <> wall And GraphicsWindow.GetPixel(X, Y + size[i]) <> wall Then

      X = Shapes.GetLeft(sprite) - speed[i]

    EndIf

  EndIf 

  

  If X > GraphicsWindow.Width - size[i] / 2 Then

    duration = Math.Round((Clock.ElapsedMilliseconds - startClock) / 1000)

    GraphicsWindow.ShowMessage("Time taken: " + duration + " s.", "Congratulations")

    If i < 2 Then

      Configure()

      keyDown = "False"

    Else

      Program.End()

    EndIf

  EndIf

  

  Shapes.Move(sprite, X, Y)

  

  Program.Delay(20)

EndWhile



Sub Initialise

  wall = "#FF00DC"

  size = "1=8;2=16"

  speed = "1=1;2=2"

  

  Configure()

  

  GraphicsWindow.KeyDown = OnKeyDown

  GraphicsWindow.KeyUp = OnKeyUp

EndSub



Sub OnKeyDown

  keyDown = "True"

EndSub



Sub OnKeyUp

  keyDown = "False"

EndSub



Sub Configure

  i = i + 1 'Level

  url = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Maze/assets/images/maze[" + i + "].png"

  maze = ImageList.LoadImage(url)

  

  GraphicsWindow.Width = ImageList.GetWidthOfImage(maze)

  GraphicsWindow.Height = ImageList.GetHeightOfImage(maze)

  

  GraphicsWindow.Clear()

  GraphicsWindow.DrawImage(maze, 0, 0)

  

  sprite = Shapes.AddRectangle(size[i], size[i])

  X = 13

  Y = 0

  startClock = Clock.ElapsedMilliseconds

EndSub

  


