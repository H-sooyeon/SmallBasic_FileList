' mandelbrot  click-in-zoom by Rene_Miner 
' oct/15/2012 WHX270-0

gwWIDTH = 640
gwHEIGHT = 480

GraphicsWindow.Width = gwWIDTH
GraphicsWindow.Height = gwHEIGHT
GraphicsWindow.Title = "Mandelbrot - click'n zoom"
GraphicsWindow.BackgroundColor = "black"
GraphicsWindow.PenColor = "white"
GraphicsWindow.PenWidth = 1
GraphicsWindow.Show()

myCol[0] = GraphicsWindow.BackgroundColor

elapsMins = 0
elapsHours = 0

Timer.Tick = CountTime
Timer.Interval = 1000
GraphicsWindow.MouseDown = Click 

startAllOver:
alreadySelected ="true"

DEPTHMAX = 250
X_MIN = -1.1 
X_MAX = 2.1
Y_MIN = -1.22
Y_MAX = Y_MIN + (X_MAX - X_MIN) * gwHEIGHT/gwWIDTH

oneLevelDeeper:

isRunning = "true"

colorScheme = Math.GetRandomNumber(12)
' set up a nice color-palette
For i1 = 1 to 127
  If colorScheme = 1 Then 
    myRed = 100 +  0.25 * i1 
    myGreen =40 + 1.5 * i1    
    myBlue =  160 +  i1 * 0.5
  ElseIf colorScheme = 2 then 
    myRed =  200 - i1   
    myGreen = 200 -  i1  
    myBlue = 127 + i1 
  elseif colorScheme = 3 then
    myRed = 1.5 * i1  
    myGreen = 192 - i1 * 1.75
    myBlue = 128 - i1   
  elseif colorScheme = 4 then 
    myRed =   i1 * 1.5   
    myGreen = 160 - i1   
    myBlue =  2 * math.Abs(64 - i1)  
  elseif colorScheme = 5 then 
    myRed =  2 * i1 
    myGreen = 128+ i1 
    myBlue =  255 -  i1 
  elseif colorScheme = 6 then 
    myRed =  127 + i1 
    myGreen = 127 + i1  
    myBlue =   200 - i1 
  elseif colorScheme = 7 then 
    myRed =  63 + 1.5* i1 
    myGreen = 63+ 1.5* i1  
    myBlue =   255 - 2 * i1 
  elseif colorScheme = 8 then 
    myRed =  72 + i1 
    myGreen = 200 - i1  
    myBlue =   20 + 1.6 * i1 
  elseif colorScheme = 9 then 
    myRed =  60 + 1.5 *  i1 
    myGreen = 180 - 1.2 * i1  
    myBlue =   140 -  i1 
  elseif colorScheme = 10 then 
    myRed =  127 - i1 
    myGreen = 127 + i1  
    myBlue =   1.75 * i1 
  elseif colorScheme = 11 then 
    myRed =  200 - i1 
    myGreen = i1  
    myBlue =   1.75 * i1 
    elseif colorScheme = 12 then 
    myRed =  127 + i1 
    myGreen = 31 +1.75 *  i1  
    myBlue =  63 + 1.5 * i1 
  
  EndIf
  myCol[i1] = GraphicsWindow.GetColorFromRGB(myRed, myGreen, myBlue)
endfor

'start drawing
deltaX = (X_MAX - X_MIN)/ gwWIDTH
deltaY = (Y_MAX - Y_MIN)/gwHEIGHT
realVal = X_MIN
imagineVal = Y_MIN
For iY = 0 to gwHEIGHT 
  For iX = 0 To gwWIDTH 
    iter = 0 
    xVal = 0    
    yVal = 0
    Xlen = 0
    Ylen = 0
    While (iter < DEPTHMAX) And (Xlen + Ylen < 8 )
      yVal = 2 * xVal * yVal - imagineVal
      xVal = Xlen - Ylen - realVal      
      Xlen =  math.Power(xVal,2)
      Ylen = Math.Power(Yval,2)
      iter = iter + 1
    EndWhile
    If iter >= DEPTHMAX Then
      selColor = 0
    Else      
      selColor = Math.Remainder(iter, 127)
    EndIf
    realVal = realVal + deltaX 
    GraphicsWindow.SetPixel(iX, iY, myCol[selColor])
  EndFor
  realVal = X_MIN
  imagineVal = imagineVal + deltaY
EndFor
isRunning ="false"

If DEPTHMAX > 1000000 Then 
  Goto startAllOver   
EndIf 

GraphicsWindow.Title = "Mandelbrot - Depth "+ DEPTHMAX + elapsTime + " - Click desired area to zoom in"
alreadySelected = "false"
While alreadySelected = "false"
  ' just idle  
endwhile

Goto oneLevelDeeper  
'-----------------------------------------------------------
Sub countTime
  If isRunning = "true" Then 
    elapsed = elapsed + 1
    If elapsed > 59 Then
      elapsed = elapsed - 60
      elapsMins = elapsMins + 1
      If elapsMins > 59 Then 
        elapsHours = elapsHours + 1
        elapsMins = elapsMins - 60
      EndIf 
    EndIf
    elapsTime = " elapsed "
    If elapsHours > 0 then
      elapsTime = Text.Append(elapsTime, elapsHours + ":")
    endif 
    If elapsMins < 10 and elapsHours > 0 then 
      elapsTime = elapsTime +"0"
    EndIf 
    elapsTime = Text.Append(elapsTime, elapsMins +":")
    If elapsed < 10  Then 
      elapsTime = elapsTime +"0"
    EndIf 
    elapsTime = Text.Append(elapsTime, elapsed)  
    GraphicsWindow.Title = "Mandelbrot - Depth "+ DEPTHMAX + elapsTime
  Else 
    elapsed = 0
    elapsMins = 0 
    elapsHours = 0
  EndIf  
EndSub
'---------------------------------------------------------------------------
Sub Click
  
  If alreadySelected = "false" Then 
    
    xVal = GraphicsWindow.MouseX 
    If xVal < gwWIDTH * 0.25 Then 
      xVal = gwWIDTH * 0.25
    ElseIf xVal > gwWIDTH * 0.75 then 
      xVal = gwWIDTH * 0.75
    EndIf     
    yVal = GraphicsWindow.MouseY
    If yVal < gwHEIGHT * 0.25 Then 
      yVal = gwHEIGHT * 0.25
    ElseIf yVal > gwHEIGHT * 0.75 then 
      yVal = gwHEIGHT * 0.75
    EndIf 
    GraphicsWindow.DrawRectangle(xVal - gwWIDTH * 0.25, yVal - gwHEIGHT * 0.25 , gwWIDTH * 0.5, gwHEIGHT * 0.5)
    dist = Math.Abs(X_MAX - X_MIN)
    Xlen =  xVal/gwWIDTH
    X_MIN = X_MIN + Xlen * dist - 0.25 * dist
    X_MAX = X_MIN + 0.5 * dist
    
    dist = Math.Abs(Y_MAX - Y_MIN)
    Ylen =  yVal/gwHEIGHT
    Y_MIN = Y_MIN +Ylen * dist - 0.25 * dist
    Y_MAX = Y_MIN + 0.5 * dist
    
    DEPTHMAX = DEPTHMAX * 2
    
    elapsed = 0
    elapsMins = 0 
    elapsHours = 0
    alreadySelected ="true"
    
  EndIf   
EndSub
    
