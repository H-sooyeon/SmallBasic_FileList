'Frantic Clicks. Click the box to earn points. Box moves faster with each click. Ten misses and its game over
'Submitted by codingCat (Matthew L. Parets) for the 50 Line Challenge  
'Initial Setup -----------------------------------------------------------------------------------------------------
boxSize = 50                                  'build and set the box (50 easy, 25 hard, 10 crazy)
box = Shapes.AddRectangle(boxSize,boxSize)
GraphicsWindow.MouseDown = onMouseClick       'tell windows to call when mouse is clicked
onIntervalTick()                              'Move the box from the starting place
miss = 0                                      'Inital game state and score
score = 0
Timer.Tick = onIntervalTick                   'Set up timer to auto jump the box
Timer.Interval = 2000
displayScore()

Sub onMouseClick 'Called by windows when mouse is clicked -----------------------------------------
  Mx = GraphicsWindow.MouseX                  'Location of mouse cursor
  My = GraphicsWindow.MouseY
  Sx = Shapes.GetLeft(box)                    'Location of box
  Sy = Shapes.GetTop(box)
  closeX = Mx - Sx                            'How close is mouse and box
  closeY = My - Sy
  
  If (closeX > 0) And (closeY > 0) then       'Greater than zero? - In front of box
    If (closeX < boxSize) And (closeY < boxSize) Then   'Less than box size? - Inside of box
        Timer.Interval = Timer.Interval - 50  'Game speeds up
        score = score + ((100-(miss * 10)) * (2000 - Timer.Interval))
        miss = 0
        displayScore()
        onIntervalTick()
      EndIf
    EndIf
EndSub
Sub onIntervalTick 'Called by windows each time the interval elapses ----------------------------
  newX = Math.GetRandomNumber(GraphicsWindow.Width - boxSize)
  newY = Math.GetRandomNumber(GraphicsWindow.Height - boxSize)
  Shapes.Move(box,newX,newY)                  'New Location for the box
  miss = miss + 1                   
  displayScore()
  If miss > 10 Then                           'Ten misses and the game is over
    Timer.Interval = 100000000                'Stop the timer
    GraphicsWindow.ShowMessage(msg,"Game Over")
    Program.End()
  EndIf
EndSub
Sub displayScore 'Called to update the score and status on the title line of the window.---------
  msg = "Score: " + score + " - Speed: " + Timer.Interval
  For i = 1 To miss
    msg = msg + "."
  EndFor
  GraphicsWindow.Title = msg
EndSub
