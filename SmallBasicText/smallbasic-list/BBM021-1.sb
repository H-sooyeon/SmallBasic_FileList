'____________________________________________________________'



'Code Copyright Noah Buscher 2012

'Image Copyright Mojang

'Thanks to Mojang for images

'This game and/or Noah Buscher are not affliated with Minecraft

'Nor Mojang in any way



'http://social.msdn.microsoft.com/Forums/en-US/smallbasic

'/thread/e5fe83d6-8150-41a9-92b0-a48816e9b293

'BBM021-1



'____________________________________________________________'

'Sets up board to play:

GraphicsWindow.Title = "MineTag"

GraphicsWindow.CanResize = "False"

GraphicsWindow.BackgroundColor = "Green"

GraphicsWindow.BrushColor = "White"

GraphicsWindow.FontName = "Neuropol"

GraphicsWindow.FontSize = "24"

GraphicsWindow.Width  = 500

GraphicsWindow.Height = 500

gw = GraphicsWindow.Width

gh = GraphicsWindow.Height



scoreDisplay = Shapes.AddText("")  '  <--  Creates score display



path = Program.Directory + "\"

isLocal = "False"



If isLocal Then

  PlayerImg = ImageList.LoadImage(path + "nx2h4w.jpg")

  ZombieImg = ImageList.LoadImage(path + "2gvsk6r.jpg")

Else

  PlayerImg = ImageList.LoadImage("http://i47.TinyPic.com/nx2h4w.jpg")

  ZombieImg = ImageList.LoadImage("http://i49.TinyPic.com/2gvsk6r.jpg")

EndIf



Player = Shapes.AddImage(PlayerImg)

Zombie = Shapes.AddImage(ZombieImg)



spdP  = 10

spdZ  = 15

nitro = 10

size = 64

szH  = size / 2

szQ  = size / 4

counter = 5 / 1000

delay  = 100

offset = 10



GraphicsWindow.KeyDown = PlayerMove

GraphicsWindow.KeyUp   = PlayerStop



'Displays score every 5% of a second:

Timer.Interval = counter * 10000

Timer.Tick = Score

'____________________________________________________________'

Restart:

px = gw-size - offset

py = offset

zx = offset

zy = gh-size - offset



score = "0.000"

isFleeing = "True"

Timer.Resume()

'____________________________________________________________'

'Main Loop:

While isFleeing

  

  'Player's Movement:

  px = px + moveH

  py = py + moveV

  

  'Player's Screen Limit Check:

  If     px < -szH Then

    px = gw-szH

  ElseIf px > gw-szH Then

    px = -szH

  EndIf

  If     py < -szH Then

    py = gh-szH

  ElseIf py > gh-szH Then

    py = -szH

  EndIf

  

  boost = spdZ + Math.GetRandomNumber(nitro)

  

  'Enemy's Chasing:

  If zx < px Then

    zx = zx + boost

  Else

    zx = zx - boost

  EndIf

  If zy < py Then

    zy = zy + boost

  Else

    zy = zy - boost

  EndIf

  

  Shapes.Move(Player  px,py)

  Shapes.Move(Zombie  zx,zy)

  

  If zx > px-szQ And zx < px+szQ And zy > py-szQ And zy < py+szQ Then

    Timer.Pause()

    isFleeing = "False"

  EndIf

  

  Program.Delay(delay)

  

EndWhile

'____________________________________________________________'

'Game Over:

Sound.PlayMusic("T180 O2P2P8 L8GGG L2E- P24P8 L8FFF L2D R")

GraphicsWindow.ShowMessage("Your Score Was " + score , "You Lost!")

record = Math.Max(record  score)

GraphicsWindow.Title = "MineTag  ---  Record: " + record



Goto Restart

'____________________________________________________________'

Sub Score

  

  score = score + counter

  Shapes.SetText(scoreDisplay  score)

  

EndSub

'____________________________________________________________'

Sub PlayerMove

  

  key = GraphicsWindow.LastKey

  

  If     key = "A" Or key = "Left"  Then

    moveH = -spdP

  ElseIf key = "D" Or key = "Right" Then

    moveH =  spdP

  ElseIf key = "W" Or key = "Up"    Then

    moveV = -spdP

  ElseIf key = "S" Or key = "Down"  Then

    moveV =  spdP

  ElseIf key = "Tab" Then

    GraphicsWindow.BackgroundColor = GraphicsWindow.GetRandomColor()

  ElseIf key = "Escape" Then

    Sound.PlayClickAndWait()

    Program.End()

  EndIf

  

EndSub

'____________________________________________________________'

Sub PlayerStop

  

  key = GraphicsWindow.LastKey

  

  If     moveH < 0 And (key = "A" Or key = "Left")  Then

    moveH = 0

  ElseIf moveH > 0 And (key = "D" Or key = "Right") Then

    moveH = 0

  ElseIf moveV < 0 And (key = "W" Or key = "Up")    Then

    moveV = 0

  ElseIf moveV > 0 And (key = "S" Or key = "Down")  Then

    moveV = 0

  EndIf

  

EndSub

'____________________________________________________________'


