

GraphicsWindow.Width=1000

GraphicsWindow.Height=600

GraphicsWindow.BackgroundColor="Lightcyan"

init()

add_shapes()

partsposition()

Program.Delay(1000)

zooming()



Sub zooming

  dzm=0.1

  zm=0.5

  While "True"

    zm=zm+dzm

    For i=1 To Array.GetItemCount(shape)

      Shapes.Move(ell[i],c["x"]-zm*DR[i]*Math.Sin(deg[i])-4,c["y"]-zm*DR[i]*Math.cos(deg[i])-2)

      elx=shapes.GetLeft(ell[i])

      ely=shapes.Gettop(ell[i])

      If shape[i]["fn"]="rec" Or shape[i]["fn"]="el" then 

        Shapes.Move(SHP[SName][i],elx +4 - shape[i]["wd"]/2,ely+2 - shape[i]["ht"]/2)

      elseIf shape[i]["fn"]="ln" then

        Shapes.Move(SHP[SName][i],elx +4- (shape[i]["x1"]+shape[i]["x2"])/2 ,ely+2-(shape[i]["y1"]+shape[i]["y2"])/2)

      elseIf shape[i]["fn"]="tri" then

        Shapes.Move(SHP[SName][i],elx +4- (shape[i]["x1"]+shape[i]["x2"])/2 ,ely+2-(shape[i]["y1"]+shape[i]["y3"])/2)

      endif

      Shapes.Zoom (SHP[SName][i],zm,zm)

    EndFor

    Program.Delay(500)

    If zm>2 Or zm<0.5 Then 

      dzm=-dzm

    EndIf

  endwhile

EndSub





Sub partsposition

  DR[1]=0

  deg[1]=0

  c["x"]= shapes.GetLeft( SHP[SName][1])+shape[1]["wd"]/2  '.....  shape[1] center_X

  c["y"]= shapes.Gettop( SHP[SName][1])+shape[1]["ht"]/2  '.....  shape[1] center_Y

  GraphicsWindow.PenColor="Yellow"

  GraphicsWindow.PenWidth=2

  ell[1]=Shapes.AddEllipse(8,4)

  Shapes.Move(ell[1], c["x"]-4, c["y"]-2)

  For j=2 To Array.GetItemCount(shape)

    If shape[J]["fn"]="rec" Or shape[J]["fn"]="el" Then 

      dx=c["x"]-(Shapes.GetLeft(SHP[SName][j])+shape[j]["wd"]/2)

      dy=c["y"]-(Shapes.Gettop(SHP[SName][j])+shape[j]["ht"]/2)

    Elseif shape[J]["fn"]="tri" Then 

      dx=c["x"]-( Shapes.GetLeft(SHP[SName][j])+ (shape[j]["x1"]+shape[j]["x2"])/2 )

      dy=c["y"]-( Shapes.Gettop(SHP[SName][j]) + (shape[j]["y1"]+shape[j]["y3"])/2 )

    Elseif shape[J]["fn"]="ln" Then 

      dx=c["x"]-( Shapes.GetLeft(SHP[SName][j])+ (shape[j]["x1"]+shape[j]["x2"])/2 )

      dy=c["y"]-( Shapes.Gettop(SHP[SName][j]) + (shape[j]["y1"]+shape[j]["y2"])/2 )

   EndIf

    ell[j]=Shapes.AddEllipse(8,4)

    Shapes.Move(ell[j], c["x"]-4-dx, c["y"]-2-dy)

    DR[j]=Math.SquareRoot(dx*dx+dy*dy)  '  length 

    deg[j]=math.ArcTan(dx/dy)           '  angle

  EndFor

EndSub





Sub add_shapes

  M=1

  SName="house"+M

  s=scale[1]

  for i=1 To Array.GetItemCount(shape) 

    GraphicsWindow.PenWidth = shape[i]["pw"]

    GraphicsWindow.BrushColor = shape[i]["bc"] 

    GraphicsWindow.penColor = shape[i]["pc"] 

    If SHaPe[i]["fn"] = "rec"  Then

      SHP[SName][i] = Shapes.AddRectangle(SHaPe[i]["wd"]*s, SHaPe[i]["ht"]*s)

    elseIf SHaPe[i]["fn"] =  "el" Then

      SHP[SName][i] = Shapes.AddEllipse(SHaPe[i]["wd"]*s, SHaPe[i]["ht"]*s)

    ElseIf SHaPe[i]["fn"] = "tri" Then

      SHP[SName][i] = Shapes.AddTriangle(SHaPe[i]["x1"]*s, SHaPe[i]["y1"]*s, SHaPe[i]["x2"]*s, SHaPe[i]["y2"]*s, SHaPe[i]["x3"]*s, SHaPe[i]["y3"]*s)

     ElseIf SHaPe[i]["fn"] = "ln" Then

      SHP[SName][i] = Shapes.Addline(SHaPe[i]["x1"]*s, SHaPe[i]["y1"]*s, SHaPe[i]["x2"]*s, SHaPe[i]["y2"]*s)

   EndIf

    Shapes.Animate(shp[SName][i], shape[i]["x"]*s+shX[M], shape[i]["y"]*s+shY[M], 500)

  EndFor

EndSub





Sub init  '  house 

  scale[1]=1

  shX[1]=100

  shy[1]=100

  Shape[1]="fn=rec;x=100;y=100;wd=300;ht=200;bc=lightgray;pw=0"

  Shape[2]="fn=tri;x=100;y=0;x1=0;y1=100;x2=300;y2=100;x3=260;y3=0;bc=red;pw=0"

  Shape[3]="fn=ln;x=152;y=80;x1=0;y1=0;x2=240;y2=0;pc=black;pw=2"

  Shape[4]="fn=ln;x=234;y=50;x1=0;y1=0;x2=146;y2=0;pc=Green;pw=2"

  Shape[5]="fn=rec;x=330;y=15;wd=30;ht=50;bc=green;pw=0"

  Shape[6]="fn=el;x=150;y=120;wd=40;ht=60;bc=brown;pw=0"

EndSub

  


