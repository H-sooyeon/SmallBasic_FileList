' small calculator - commented version
reStart:
parameters = 0 ' we have no numbers from user now
operator = 0 ' there's no place where an operator can be found
decimalPlace = 1 ' the number gets multiplied by 10^0, 10^1, 10^2 etc.
TextWindow.WriteLine("please enter what to calculate or 'exit' to end")
userInput = Text.ConvertToLowerCase(TextWindow.Read()) ' receive mission
If userInput <> "exit" Then 
  len = Text.GetLength(userInput)
  While len > 0 ' still input left
    char = text.GetCharacterCode(Text.GetSubText(userInput, len,1)) ' pick last chars code
    If char > 47 and char< 58 then '  it's a number from 0 to 9
      If decimalPlace = 1 then ' the first for this parameter
        parameters = parameters + 1 ' so this must be new parameter
        pVal[parameters] = char - 48 ' store its first (very right) digit
      Else ' there is already a number so put it in front like that:
        pVal[parameters] = pVal[parameters] + (char - 48) * decimalPlace 
      EndIf  ' next digit will be "worth" 10 times as much 
      decimalPlace = decimalPlace * 10
    else ' it's not a number what the user typed
      decimalPlace = 1 ' reset the multiplier
      If char = 32 Then ' space does not matter
      elseif char = 42 or char = 43 or char = 45 or char = 47 then ' it's + - * or /
        operator = len ' store position
      Else ' it makes no sense what the user typed
        len = 0 ' no more charakters to go through
      EndIf   
    EndIf  
    len = len-1 ' set pointer to digit in front of current digit
  EndWhile ' continue with next digit if there is one
  If len < 0 or parameters > 2 Then ' user typed nonsense to me
    TextWindow.WriteLine("ERROR - Invalid input")
  elseif len = 0 then ' find out which operation to perform
    If text.GetSubText(userInput, operator,1) ="+" then ' and add
      TextWindow.WriteLine("Result:  " + (pVal[1] + pVal[2])) 
    elseIf text.GetSubText(userInput, operator,1) ="-" then ' subtract
      TextWindow.WriteLine("Result:  " + (pVal[2] - pVal[1])) 
    elseIf text.GetSubText(userInput, operator,1) ="*" then ' multiply
      TextWindow.WriteLine("Result:  " + (pVal[1] * pVal[2])) 
    elseIf text.GetSubText(userInput, operator,1) ="/" then ' divide
      If pVal[1] <> 0 then  ' division by 0 is not allowed
        TextWindow.WriteLine("Result:  " + (pVal[2] / pVal[1])) 
      Else ' give a complain to user
        TextWindow.WriteLine("ERROR - Division by zero!")
      EndIf   
    EndIf      
  EndIf   
  Goto reStart
EndIf
Program.End() ' taking a break
