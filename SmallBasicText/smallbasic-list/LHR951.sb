' Math Quiz
' Copyright © 2015 Nonki Takahashi.  The MIT License.
' Version 0.1
'
GraphicsWindow.Title = "Math Quiz"
Not = "True=False;False=True;"
colors = "0=#EE0000;1=#EEEE00;2=#00EE00;3=#0000EE;4=#EE00EE;"
colors = colors + "5=#CC0066;6=#CC9900;7=#66CC00;8=#0066CC;9=#9900CC;"
gw = 598
gh = 428
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.FontName = "Trebuchet MS"
fs = 50
x0 = 50
y0 = 20
GraphicsWindow.BrushColor = "#EEEEEE"
For n = 5 To 9
  GraphicsWindow.FillRectangle(x0 + (n + 0.07) * fs, y0 + 1.4 * fs, fs / 2, fs * 2.5)
EndFor
GraphicsWindow.FontSize = fs
For n = 0 To 9
  GraphicsWindow.BrushColor = colors[n]
  GraphicsWindow.DrawText(x0 + n * fs, y0, n)
  For i = 0 To n - 1
    GraphicsWindow.FillEllipse(x0 + (n + 0.2) * fs, y0 + (i + 3) * fs / 2, 10, 10)
  EndFor
EndFor
GraphicsWindow.TextInput = OnTextInput
While "True"
  a = Math.GetRandomNumber(10) - 1
  b = Math.GetRandomNumber(10) - 1
  c = a + b
  GraphicsWindow.BrushColor = colors[a]
  shpA = Shapes.AddText(a)
  Shapes.Move(shpA, x0, y0 + 13 * fs / 2)
  GraphicsWindow.BrushColor = "#666666"
  shpP = Shapes.AddText("+")
  Shapes.Move(shpP, x0 + fs, y0 + 13 * fs / 2)
  GraphicsWindow.BrushColor = colors[b]
  shpB = Shapes.AddText(b)
  Shapes.Move(shpB, x0 + fs * 2, y0 + 13 * fs / 2)
  GraphicsWindow.BrushColor = "#666666"
  shpE = Shapes.AddText("=")
  Shapes.Move(shpE, x0 + fs * 3, y0 + 13 * fs / 2)
  len = Text.GetLength(c)
  ans = ""
  For i = 1 To len
    textInput = "False"
    While Not[textInput]
      Program.Delay(200)
    EndWhile
    lastText = GraphicsWindow.LastText
    If lastText + 0 = lastText Then
      For j = 1 To Math.Min(Text.GetLength(lastText), len - i + 1)
        k = Text.GetSubText(lastText, j, 1)
        GraphicsWindow.BrushColor = colors[k]
        ans = Text.Append(ans, k)
        shpK[i] = Shapes.AddText(k)
        Shapes.Move(shpK[i], x0 + fs * (3.5 + i / 2), y0 + 13 * fs / 2)
        i = i + 1
      EndFor
    EndIf
    i = i - 1
  EndFor
  If c = ans Then
    GraphicsWindow.BrushColor = "Green"
    shpJ = Shapes.AddText("✓")
    Shapes.Move(shpJ, x0 + fs * (4 + i), y0 + 13 * fs / 2)
    Sound.PlayBellRingAndWait()
    Program.Delay(500)
  Else
    GraphicsWindow.BrushColor = "Red"
    shpJ = Shapes.AddText("✗")
    Shapes.Move(shpJ, x0 + fs * (4 + i), y0 + 13 * fs / 2)
    Program.Delay(1000)
  EndIf
  Shapes.Remove(shpA)
  Shapes.Remove(shpP)
  Shapes.Remove(shpB)
  Shapes.Remove(shpE)
  For i = 1 To len
    Shapes.Remove(shpK[i])
  EndFor
  Shapes.Remove(shpJ)
EndWhile
Sub OnTextInput
  textInput = "True"
EndSub
