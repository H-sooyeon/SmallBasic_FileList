GraphicsWindow.Width=500 'Set up the graphicswindow

GraphicsWindow.Height=600

GraphicsWindow.Top=50



Emitter=Shapes.AddTriangle(0,50,25,0,50,50) 'Add Emitter Triangle

EmitterX = 225

EmitterY = 300

Shapes.Move(Emitter,EmitterX,EmitterY)

Dragging = "False" 'set emitter Dragging to off

Ground=550



GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(230,0,0) 'Set the color for the "blood"

GraphicsWindow.PenWidth=0

BloodAmount=10

For i = 1 to BloodAmount 'Add the "Blood" sprites

  Blood[i]=shapes.AddEllipse(10,10)

endfor







While "True" 'Repeat the whole time:

  MouseControl()

  BloodMovement()

  Program.Delay(20)

endwhile



Sub MouseControl

  If Mouse.IsLeftButtonDown Then 'Drag and drop the Emitter

    MX=GraphicsWindow.MouseX

    MY=GraphicsWindow.MouseY

    If Dragging = "False" Then

      If MX >= EmitterX And MX <= EmitterX+50 And MY >= EmitterY And MY <= EmitterY+50 then

        Dragging = "True"

      endif

    Elseif Dragging = "True" Then

      EmitterX = MX-25

      EmitterY = MY-25

      Shapes.Move(Emitter,EmitterX,EmitterY)

    EndIf

  Else

    Dragging = "False"

  endif

  If Mouse.IsRightButtonDown Then 'Emitt the "blood" sprites

    For i = 1 to BloodAmount

      'BWX[i]=0.1*Math.GetRandomNumber(21)-1.1

      'BWY[i]=0.1*(Math.GetRandomNumber(30)-31)

      

      BWX[i]=0.9*Math.GetRandomNumber(6)-3

      BWY[i]=0.9*(Math.GetRandomNumber(20)-35)

      BX[i]=EmitterX+25

      BY[i]=EmitterY-5

    endfor

  endif

endsub



Sub BloodMovement 'Move the "blood" sprites around by their corresponding WX and WY

  For i = 1 To BloodAmount

    If BY[i] < Ground+i*2 Then

      If BWY[i]<50 Then

        BWY[i]=BWY[i]+2

      endif

      Blood_Drawn[i]=0

      BX[i]=BX[i]+BWX[i]

      BY[i]=BY[i]+BWY[i]

      BWX[i] = BWX[i] * 0.99

      Shapes.Move(Blood[i],BX[i]-5,BY[i]-5)

    Elseif BY[i]>=Ground+i*2 and Blood_Drawn[i]=0 Then

      GraphicsWindow.FillEllipse(BX[i],BY[i],10,10)

      Blood_Drawn[i]=1

    endif

  endfor

endsub






