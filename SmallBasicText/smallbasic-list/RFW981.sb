'Initialisation Window

gw = 800

gh = 600

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh



'Initialise Key events

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp

keyLeft = 0

keyRight = 0

keyUp = 0

keyDown = 0



'Initialise ball

ball = Shapes.AddEllipse(50,50)

ballX = 100

ballY = 100

speed = 3

Shapes.Move(ball,ballX-25,ballY-25)



'Main loop - just move the ball

While ("True")

  processKey()

  Shapes.Move(ball,ballX-25,ballY-25)

  Program.Delay(10)

EndWhile



'Key press event subroutines

'A separate Down and Up is checked for each key, this tells us the state for any key

'And isn't affected by auto-repeat delays for keys

Sub OnKeyDown

  lastKey = GraphicsWindow.LastKey

  If (lastKey = "Left") Then

    keyLeft = 1

  ElseIf (lastKey = "Right") Then

    keyRight = 1

  ElseIf (lastKey = "Up") Then

    keyUp = 1

  ElseIf (lastKey = "Down") Then

    keyDown = 1

  EndIf 

EndSub



Sub OnKeyUp

  lastKey = GraphicsWindow.LastKey

  If (lastKey = "Left") Then

    keyLeft = 0

  ElseIf (lastKey = "Right") Then

    keyRight = 0

  ElseIf (lastKey = "Up") Then

    keyUp = 0

  ElseIf (lastKey = "Down") Then

    keyDown = 0

  EndIf 

EndSub



Sub processKey

  'Move object - note it can move diagonally if 2 keys are pressed

  If (keyLeft = 1) Then

    ballX = ballX-speed

  EndIf 

  If (keyRight = 1) Then

    ballX = ballX+speed

  EndIf 

  If (keyUp = 1) Then

    ballY = ballY-speed

  EndIf 

  If (keyDown = 1) Then

    ballY = ballY+speed

  EndIf

  

  'Check for hitting edges

  If (ballX < 0) Then

    ballX = ballX+gw

  EndIf

  If (ballX > gw) Then

    ballX = ballX-gw

  EndIf

  If (ballY < 0) Then

    ballY = ballY+gh

  EndIf

  If (ballY > gh) Then

    ballY = ballY-gh

  EndIf

EndSub


