'  Challenge of the Month - October 2014   'moon lander'    by  NaochanON  PNR896-0

'  Rocket.png and fire.png are  downloaded from  here. 

' https://docs.google.com/file/d/0B8mzGh2PVjd-LVp5Umw2QVJYUDg/edit   <-----  Rocket.png

' https://docs.google.com/file/d/0B8mzGh2PVjd-T2MwZFgycGFaRjg/edit   <-----  Fire.png



init()

GraphicsWindow.KeyDown=onkeydown

GraphicsWindow.KeyUp=onkeyup



While NN<5 

  NN=NN+1

  While TF="True"

    Vx=Vx+DX[Lastkey]

    Vy=Vy+DY[Lastkey]

    Shapes.Move(Rocket[1],shapes.GetLeft(Rocket[1])+Vx,shapes.Gettop(Rocket[1])+Vy)

    Shapes.Zoom(Rocket[2],1,multi)

    Shapes.Move(Rocket[2],shapes.GetLeft(Rocket[1])+(imgW[1]-imgW[2])/2,shapes.Gettop(Rocket[1])+imgH[1]+imgH[2]*((multi-1)/2))

    SHPX=shapes.GetLeft(Rocket[1])

    SHPY=shapes.Gettop(Rocket[1])

    landing()

    If TF="True" Then 

      Clashed() 

    EndIf

    Program.Delay(100)

  EndWhile

  Program.Delay(1500)

  Shapes.Move(Rocket[1],50+math.GetRandomNumber(800),-10)

  Shapes.Zoom(Rocket[2],1,multi)

  Shapes.Move(Rocket[2],shapes.GetLeft(Rocket[1])+(imgW[1]-imgW[2])/2,shapes.Gettop(Rocket[1])+imgH[1]+imgH[2]*((multi-1)/2))

  Vy=1

  Vx=0.1  

  TF="True"

  TTL="    Now  Landing  "

endwhile



Sub Clashed

  For i=1 To 26

    If LNX[i-1]<SHPX And (shpX+imgW[1]/2)<LNX[i] Then 

      angle=(LNY[i]-LNY[i-1])/(LNX[i]-LNX[i-1])

      lineLY= angle*shpX + LNY[i]-angle*LNX[i]

      lineRY= angle*(shpX+imgW[1]) + LNY[i]-angle*LNX[i]

      If (angle>0 and lineLY<SHPY+imgH[1]) Or  (angle<0 and lineRY<SHPY+imgH[1]) or shpx<-imgW[1] or 900<shpx Or math.max(LNY[i],LNY[i-1])<(shpy+imgH[1]) then 

        TTL="   You Clashed!!!!"

        TF="False"

      EndIf

    EndIf

  EndFor

  Shapes.SetText(msg,NN+"   Vx=  "+Vx+"   Vy=  "+Vy+TTL)

EndSub



Sub landing

  For j=1 To 7

    If LNX[PXY[j]]<(shpX+imgW[1]/2) And (shpX+imgW[1]/2)<LNX[PXY[j]+1] and LNY[PXY[j]+1]<SHPY+imgH[1] Then 

      SumPoints=SumPoints+points[j]

      TTL="    Well Landed  !!!!"+Text.GetCharacter(10)+"    points= "+SumPoints

      TF="False"

    EndIf

  EndFor

  Shapes.SetText(msg,NN+"   Vx=  "+Vx+"   Vy=  "+Vy+TTL)

endsub



Sub onkeydown

  lastkey= GraphicsWindow.LastKey

  If lastkey="Up" Then 

    multi=multi-DY["Up"]

    DX["Up"]= DX["NULL"]

  EndIf

  DX["NULL"]=DX[Lastkey]

EndSub



Sub onkeyup

  multi=0.2

  Lastkey="NULL"

EndSub



Sub init

  GraphicsWindow.BackgroundColor="Black"

  GraphicsWindow.Width=900

  GraphicsWindow.Height=600

  GraphicsWindow.PenColor="Yellow"

  GraphicsWindow.BrushColor="Yellow"

  LNX="0=0;1=43;2=88;3=133;4=182;5=224;6=263;7=283;8=323;9=369;10=397;11=429;12=466;13=500;14=554;15=569;16=628;17=649;18=664;19=687;20=752;21=777;22=833;23=850;24=886;25=900;26=933"

  LNY="0=371;1=371;2=346;3=445;4=460;5=460;6=433;7=500;8=500;9=415;10=475;11=475;12=314;13=445;14=476;15=490;16=490;17=414;18=454;19=495;20=495;21=451;22=397;23=429;24=429;25=351;26=371"

  For I=1 To 26

    Lines[i]=Shapes.AddLine(LNX[i-1],LNY[i-1],LNX[i],LNY[i])

  EndFor

  PXY="1=0;2=4;3=7;4=10;5=15;6=19;7=23"

  points="1=10;2=20;3=30;4=50;5=5;6=5;7=40"

  GraphicsWindow.FontSize=16

  For j=1 To 7

    PT[j]=Shapes.AddText(points[j])

    Shapes.Move(PT[j],(LNX[PXY[j]]+LNX[PXY[j]+1])/2-10,LNY[PXY[j]])  

  EndFor

  path="1=Rocket;2=Fire"

  For i=1 to 2

    img[i]=ImageList.LoadImage(Program.Directory+"\"+path[i]+".png")

    Rocket[i]=shapes.AddImage(img[i])

    imgW[i]=ImageList.GetWidthOfImage(img[i])

    imgH[i]=ImageList.GetheightOfImage(img[i])

  endfor

  Shapes.Move(Rocket[1],50+math.GetRandomNumber(800),-10)

  multi=0.5

  DX="Right=0.1;Left=-0.1"

  DY="Right=0.1;Left=0.1;Up=-0.2;NULL=0.1"

  Vy=1

  Vx=0.1  

  Lastkey="Right"

  TF="True"

  TTL="    Now  Landing  "

  GraphicsWindow.BrushColor="Yellow"

  GraphicsWindow.FontSize=20

  msg= Shapes.AddText(" ")

  Shapes.Move(msg,300,60)

EndSub






