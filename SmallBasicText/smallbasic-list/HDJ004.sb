

' Random Rectangles 1440 x 900

'Microsoft Small Basic v0.9

' 20/06/2010



' The window is designed to fit on my 1440 x 900 monitor.

'If it's too large for yours, or if you're really lucky, too small,

'change the values of gx and gy, but keep them in multiples

'of 32 otherwise it won't look right. 

'Adjust the values of wx and wy to position the window on the

'screen.



gx = 1408       '  Window width  - multiple of 32

gy =  768       '  Window height - multiple of 32

wx =   27       '  Window top

wy =    8       '  Window left

ax =    1       '  Minimum x position

ay =    1       '  Minimum y position

cx = gx/16      '  Maximum x position  gx/16

cy = gy/16      '  Maximum y position gy/16

bx = cx/2-4     '  Middle x position  cx/2-4

by = cy/2-4     '  Middle y position cy/2-4



GraphicsWindow.Top    = wx

GraphicsWindow.Left   = wy

GraphicsWindow.Width  = gx

GraphicsWindow.Height = gy

GraphicsWindow.Title  = "Microsoft Small Basic v0.9 - Random Rectangles"

r = Math.GetRandomNumber(50)+100

g = Math.GetRandomNumber(50)+100

b = Math.GetRandomNumber(50)+100

GraphicsWindow.BackgroundColor = GraphicsWindow.GetColorFromRGB(r,g,b)

GraphicsWindow.Show()



While 1 = 1

  DrawFourRectangles()

EndWhile



Sub DrawFourRectangles

  

  fx = Math.GetRandomNumber(bx)

  fy = Math.GetRandomNumber(by)

  

  rx = fx

  ry = fy

  rw = Math.GetRandomNumber(bx-fx)+3

  rh = Math.GetRandomNumber(by-fy)+3

  

  r = Math.GetRandomNumber(150)+100

  g = Math.GetRandomNumber(150)+100

  b = Math.GetRandomNumber(150)+100

  

  DrawOneRectangle()

  rx = cx-fx-rw

  DrawOneRectangle()

  ry = cy-fy-rh

  DrawOneRectangle()

  rx = fx

  DrawOneRectangle()

  

EndSub



Sub DrawOneRectangle

  

  px = rx*16

  py = ry*16

  pw = rw*16

  ph = rh*16

  

  GraphicsWindow.PenColor = "Black"

  GraphicsWindow.DrawRectangle(px,py,pw,ph)

  GraphicsWindow.DrawRectangle(px+1,py+1,pw-2,ph-2)

  GraphicsWindow.DrawRectangle(px+16,py+16,pw-32,ph-32)

  GraphicsWindow.DrawRectangle(px+17,py+17,pw-34,ph-34)

  GraphicsWindow.PenColor = GraphicsWindow.GetColorFromRGB(r,g,b)

  

  For i = 2 To 15

    GraphicsWindow.DrawRectangle(px+i,py+i,pw-i*2,ph-i*2)

  EndFor

  

EndSub


