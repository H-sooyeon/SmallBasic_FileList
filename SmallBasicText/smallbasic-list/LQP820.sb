'Create GW
gw = 600
gh = 600
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh

'Create game boxes
radius = 20
numBox = 5
GraphicsWindow.BrushColor = "Blue"
For i = 1 To numBox
  enemy[i] = Shapes.AddRectangle(2*radius,2*radius)
  enemyPosX[i] = Math.GetRandomNumber(gw)
  enemyPosY[i] = Math.GetRandomNumber(gh)
  enemyVelX[i] = Math.GetRandomNumber(5) - 3
  enemyVelY[i] = Math.GetRandomNumber(5)
EndFor
GraphicsWindow.BrushColor = "Red"
player = Shapes.AddRectangle(2*radius,2*radius)
playerPosX = gw/2
playerPosY = gh/2
speed = 3 ' Player move speed

'key control
GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp
keyLeft = 0
keyRight = 0
keyUp = 0
keyDown = 0

'Infinite game loop
While ("True")
  For i = 1 To numBox
    'Update enemy positions
    enemyPosX[i] = enemyPosX[i]+enemyVelX[i]
    enemyPosY[i] = enemyPosY[i]+enemyVelY[i]
    'Check for off screen
    If (enemyPosY[i] > gw +radius) Then
      enemyPosY[i] = -radius
    EndIf
    'Bounce off sides
    If (enemyPosX[i] < radius Or enemyPosX[i] > gw-radius) Then
      enemyVelX[i] = -enemyVelX[i]
    EndIf
    'Update position (X,Y) is shape center
    Shapes.Move(enemy[i],enemyPosX[i]-radius,enemyPosY[i]-radius)
  EndFor
  'Move player with keys
  processKey()
  Shapes.Move(player,playerPosX-radius,playerPosY-radius)
  'Collision detection
  collision()
  Program.Delay(20)
EndWhile

'SUBROUTINES

'Key press event subroutines
'A separate Down and Up is checked for each key, this tells us the state for any key
'And isn't affected by auto-repeat delays for keys
Sub OnKeyDown
  lastKey = GraphicsWindow.LastKey
  If (lastKey = "Left") Then
    keyLeft = 1
  ElseIf (lastKey = "Right") Then
    keyRight = 1
  ElseIf (lastKey = "Up") Then
    keyUp = 1
  ElseIf (lastKey = "Down") Then
    keyDown = 1
  EndIf 
EndSub

Sub OnKeyUp
  lastKey = GraphicsWindow.LastKey
  If (lastKey = "Left") Then
    keyLeft = 0
  ElseIf (lastKey = "Right") Then
    keyRight = 0
  ElseIf (lastKey = "Up") Then
    keyUp = 0
  ElseIf (lastKey = "Down") Then
    keyDown = 0
  EndIf 
EndSub

Sub processKey
  'Move object - note it can move diagonally if 2 keys are pressed
  If (keyLeft = 1) Then
    playerPosX = playerPosX-speed
  EndIf 
  If (keyRight = 1) Then
    playerPosX = playerPosX+speed
  EndIf 
  If (keyUp = 1) Then
    playerPosY = playerPosY-speed
  EndIf 
  If (keyDown = 1) Then
    playerPosY = playerPosY+speed
  EndIf
EndSub

Sub collision
  For i = 1 To numBox
    distX = Math.Abs(playerPosX - enemyPosX[i])
    distY = Math.Abs(playerPosY - enemyPosY[i])
    If (distX < 2*radius And distY < 2*radius) Then
      Sound.PlayClickAndWait()
    EndIf
  EndFor
EndSub
