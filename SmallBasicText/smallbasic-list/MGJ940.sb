' SmallBasic Version 1.0
' Funktion: LDDateDiff
' Autor: Pappa Lapub
' Herkunft: 
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev
' Kommentar: Berechnet Differenz zw. 2 Daten in Tagen
'  
' Variablen: 
'  
' --------------------------------------------------------------------------------
GW = 190
GH = 115
TBH = 60      ' Taskbar Höhe
GraphicsWindow.Top = Desktop.Height-GH-37 - TBH
GraphicsWindow.Left = Desktop.Width-GW-14
GraphicsWindow.Width = GW
GraphicsWindow.Height = GH
GraphicsWindow.Title = "DiffTage"
GraphicsWindow.BackgroundColor = "Gainsboro"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontSize = 11

Dat1 = Controls.AddTextBox(10,10)
Controls.SetSize(Dat1,75,22)
Dat2 = Controls.AddTextBox(105,10)
Controls.SetSize(Dat2,75,22)

DatSel1 = Controls.AddButton("Datum1",22,40)
DatSel2 = Controls.AddButton("Datum2",116,40)

GraphicsWindow.DrawText(10,86,"Diff [Tage]:")
Dat3 = Controls.AddTextBox(85,82)
Controls.SetSize(Dat3,50,22)

Plus = Controls.AddButton("",140,82)
Controls.SetSize(Plus,9,9)
LDShapes.SetImage(Plus,Program.Directory + "\img\Plus.png")
Minus = Controls.AddButton("",140,94)
Controls.SetSize(Minus,9,9)
LDShapes.SetImage(Minus,Program.Directory + "\img\Minus.png")

Clr = Controls.AddButton("C",156,81)
Controls.SetSize(Clr,24,24)

Controls.ButtonClicked = OnButtonClick

Sub OnButtonClick
  GraphicsWindow.BrushColor = "Gainsboro"
  GraphicsWindow.DrawText(90,15,Text.GetCharacter(9608))  ' Voller Block "█"
  GraphicsWindow.BrushColor = "Black"
  
  If Controls.LastClickedButton = Clr Then
    For N = 1 To 3
      Controls.SetTextBoxText(Text.Append("TextBox",N),"")
    EndFor
    
  ElseIf Controls.LastClickedButton = Plus Then
    If Controls.GetTextBoxText(Dat1) <> "" And Controls.GetTextBoxText(Dat3) <> "" Then
      Res = LDDateTime.Add(Controls.GetTextBoxText(Dat1),Controls.GetTextBoxText(Dat3))
      Controls.SetTextBoxText(Dat2, Text.GetSubText(Res,1,Text.GetIndexOf(Res," ")-1))
    Else
      GraphicsWindow.ShowMessage("Eingaben für 'Datum1' UND 'Diff' erforderlich!","Fehlende Angabe(n)")
    EndIf
  ElseIf Controls.LastClickedButton = Minus Then
    If Controls.GetTextBoxText(Dat1) <> "" And Controls.GetTextBoxText(Dat3) <> "" Then
      Res = LDDateTime.Add(Controls.GetTextBoxText(Dat1),-1 * Controls.GetTextBoxText(Dat3))
      Controls.SetTextBoxText(Dat2, Text.GetSubText(Res,1,Text.GetIndexOf(Res," ")-1))
    Else
      GraphicsWindow.ShowMessage("Eingaben für 'Datum1' UND 'Diff' erforderlich!","Fehlende Angabe(n)")
    EndIf
    
  Else
    Datum = LDDialogs.Calendar(Clock.Date)
    DatumTag = Text.GetSubText(Datum,1,Text.GetIndexOf(Datum," ")-1)
    If Controls.LastClickedButton = DatSel1 Then
      'Datum1 = Text.GetSubText(Datum,1,Text.GetIndexOf(Datum," ")-1)
      'Controls.SetTextBoxText(Dat1,Datum1)
      Controls.SetTextBoxText(Dat1,DatumTag)
    Else
      'Datum2 = Text.GetSubText(Datum,1,Text.GetIndexOf(Datum," ")-1)
      'Controls.SetTextBoxText(Dat2,Datum2)
      Controls.SetTextBoxText(Dat2,DatumTag)
    EndIf
    If Controls.GetTextBoxText(Dat1) <> "" And Controls.GetTextBoxText(Dat2) <> "" Then
      'Controls.SetTextBoxText(Dat3, Math.Abs(LDDateTime.Subtract(Datum1,Datum2)))
      Diff = LDDateTime.Subtract(Controls.GetTextBoxText(Dat1),Controls.GetTextBoxText(Dat2))
      Controls.SetTextBoxText(Dat3, Math.Abs(Diff))
      If Diff > 0 Then
        GraphicsWindow.DrawText(90,15,">")
      Else
        GraphicsWindow.DrawText(90,15,"<")
      EndIf
    EndIf
    
  EndIf
EndSub
