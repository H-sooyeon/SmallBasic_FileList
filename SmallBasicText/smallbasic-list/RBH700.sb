'Author Matthew L. Parets
'Euler Project 28 - Diagonal sums of a spiral.
'Build a 1001x1001 spiral in the fashion of  5x5 example below. 
'Goal: Find the sum of the diagonals of the square.
'This solution tracks the sums as the code follows the sprial
'This eliminates the need for an array, and the code takes
'half as long to find the solution

'11   12   13  14   15       21  22  23 24  25  
'21  22  23  24  25       20   7    8   9   10
'31  32  33  34  35  =  19   6    1    2   11   =  101
'41  42  43  44  45      18   5    4    3  12
'51  52  53  54  55      17  16  15  14  13

'Positions 1 and 2 are special cases. Otherwise we count the value at the start
'and end of every horizontal line. No storage is neccessary

'Size of the spiral grid
MaxRow = 1001
MaxCol = 1001

'Initialize - Set the Center
Row = Math.Ceiling(MaxRow / 2)
Col = Math.Ceiling(MaxCol / 2)
val = 1

'Track the direction and distance to move through grid
hdir = 1
hdist = 1
vdir = 1
vdist = 1

sum = 0
top:

'The first position of each line is always added
sum = sum + val 

'Horizaontal distance
For i = 1 to hdist
  val = val + 1
  Col = Col + hdir
    
  If Math.Remainder(val,1000) = 0 Then
    TextWindow.Write(".")
  EndIf
EndFor
'Last postion is always counted
'  (First postion is special cased to simplify algorithm)
If hdist <> 1 Then 
  If vdir > 0 then 'Moving right, value is current postion
    sum = sum + val
  Else 'Moving left, value at previous postion
    sum = sum + (val - 1)
  EndIf  
EndIf

'The spiral will always end at the end of the top row
If Row > MaxRow or Col > MaxCol then
  Goto end
endif

'The next row will go in the opposite direction and be one index larger
hdir = hdir * -1
hdist = hdist + 1

'Vertical distance
For i = 1  to vdist
  val = val + 1
  Row = Row + vdir
EndFor

'The next column will go in the opposite direction and be one index larger
vdir = vdir * -1
vdist = vdist + 1

Goto top

end:
TextWindow.WriteLine(" ")
TextWindow.WriteLine("The Answer: " + sum)
    
    
