DEPTHMAX = 200
X_MAX = 2.1
X_MIN = -0.7
Y_MAX = 0.7
Y_MIN = -1.4
StpSize = 1
gwWIDTH = 320
gwHEIGHT = 320
GraphicsWindow.Width = gwWIDTH
GraphicsWindow.Height = gwHEIGHT
GraphicsWindow.Show()
' set up a color-palette
myCol[0] = "black" 
For i1 = 1 to 31
  myCol[i1] = GraphicsWindow.GetRandomColor()
endfor
'start drawing
deltaX = (X_MAX - X_MIN)/ gwWIDTH
deltaY = (Y_MAX - Y_MIN)/gwHEIGHT
realVal = X_MIN
imagineVal = Y_MIN
For iY = 0 to gwHEIGHT Step StpSize
  For iX = 0 To gwWIDTH Step StpSize
    iterate = 0 
    xVal = 0    
    yVal = 0
    Xlen = 0
    Ylen = 0
    While (iterate < DEPTHMAX) And (Xlen + Ylen < 8 )
      yVal = 2 * xVal * yVal - imagineVal
      xVal = Xlen - Ylen - realVal      
      Xlen =  math.Power(xVal,2)
      Ylen = Math.Power(Yval,2)
      iterate = iterate + 1
    EndWhile
    If iterate >= DEPTHMAX Then
      selColor = 0
    Else      ' since there's no MOD...
      selColor = iterate -  (31 * Math.Floor( iterate/31))
    EndIf
    realVal = realVal + deltaX * StpSize
    GraphicsWindow.SetPixel(iX, iY, myCol[selColor])
  EndFor
  realVal = X_MIN
  imagineVal = imagineVal + deltaY * StpSize
EndFor
ExitDrawFractal:
While GraphicsWindow.LastKey <>"Escape"
endwhile
Program.End()
