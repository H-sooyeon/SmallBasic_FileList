GraphicsWindow.Width =800
GraphicsWindow.Height=1000
GraphicsWindow.Left=20
GraphicsWindow.Top =10
LDUtilities.ShowErrors="False
runn=2
GraphicsWindow.BrushColor="Black
GraphicsWindow.MouseMove=MWWW
c2dmd=0

stl=1
LDEvents.MouseWheel=mscrr
m300=600'----------------------------------maxprglines
kcd=0'--------------------------------------screen keyb scancode
ctc=1'--------------------------------------ctrls index
LDUtilities.FixFlickr()'-----------for vb<1.1
prg=LDArray.Create(m300)'-----------prg list array

For x=1 to m300
  LDArray.SetValue (prg,x,0)
endfor
csgn=LDText.Split("K L E X S G C"," ")'----------cursors: K)eyword L)etter C)aps  eX)tended-green kw E)xtra-red kw S)ymbol  G)raphic 
md=1
grtx[1]="SIN COS TAN INT RND STR$ CHR$ ELLIP RECT TAB "
grtx[2]="READ RESTORE DATA SGN ABS SQR VAL LEN USR _ "
grtx[3]="LN EXP LPRT LLIST BIN INK$ PI "
rdxt[1]="ASIN ACOS ATN SHAPE MERGE WEB CUB2 IN OUT LOGO "
rdxt[2]="FLCKR HSB COLR FONT REPLC CIRCLE VAL$ SCRN$ INC _ "
rdxt[3]="BEEP INK PAPER UNDRL SMLCAP ITAL BOLD _ _ "
rdxt[4]="SUB FN LINE OPN# CLOS# MOVE ERASE POINT DIR DELSHP "
cmat[1]=LDText.Split("PLOT DRAW 'REM RUN HELP RETURN IF INPUT CUBE PRINT NEW SAVE DIM FOR GOTO CALL LOAD LIST LET `` COPY CLEAR CONT CLS BORDER NEXT PAUSE `: '_ 1 2 3 4 5 6 7 8 9 0 '"," ")
cmat[2]=LDText.Split("q w e r t y u i o p a s d f g h j k l `` z x c v b n m `: `_ 1 2 3 4 5 6 7 8 9 0 `<"," ")
df1=""
bsx0=0
bsy0=0
bsx=300
bsy=300
cedge=50'-----------3D cube endgesize
sllw=0.5
san=60
fc=0'-------------------"for"deepness nest level

'----------------------------making keyword array list
For x=1 to 3
  df1=df1+grtx[x] 
endfor
cmat[3]=LDText.Split(df1," ")

df2=""
For x=1 to 4
  df2=df2+rdxt[x] 
endfor

cmat[4]=LDText.Split(df2," ")
clst=cmat[1]+cmat[3]+cmat[4]
'------------------------------------------------end making kwds
ki=1
t_="true
f_="false
sst=1
acc=ldtext.Split("|@#$%^&() /*+-=_?!;"," ")'---------FnKeys

ttx=0'--------------------txt list current X pos.
tty=50'-------------------txt list current Y pos.
args=0'-------------------sub calls paramarray
wc=12.5'------------------prg. list charwidth
hc_=20'---------------------chars height pix.
cclr="Yellow"'-----------cursor base paper
nolst=0'--------------------blocks prg listing
ipc="Black "'-------------ink color
nl=Text.GetCharacter(13)+Text.GetCharacter(10) '----------newline
ppc="#cccccc"'----------paper clr ,light gray

clrs=LDText.Split("BLUE Red Magenta Green Cyan Yellow White Black Gray Black"," ")'----------zx spectrum default clrs
clrs[0]=ppc
GraphicsWindow.BackgroundColor=ppc
GraphicsWindow.BrushColor="Black
GraphicsWindow.FillRectangle(0,600,800,400)

drwkbd()
scfnt()
LDShapes.ShapeEvent=shppw'---------------scrn keys shape event handler
GraphicsWindow.KeyDown=kdd'-------------phys. kbd handler 
makecursor()

Timer.Tick=ttm'-------------------------------cursor blink sub
Timer.Interval=750'--------------------------cursor blink interval

donw()'-------------------------------------------animation history effect
dosample()'-------------------------------------optional sample prg.
runn=0
While 1=1'--------===============================MAIN LOOP================================
  
  Program.Delay(5)
  If runn=1 Then'----------------------if run mode then start prg
    runprg()
    runn=0
  endif
  
  If kcd>0 Then'------------------------if screeen keyb press made, handle keys
    
    If kcd=20 then'----------------------enter key 
      ww=""
      Timer.Pause ()
      ccmd()
      If runn=0 then
        evc2 ()
      endif
      crst ()
      Goto xx
    ElseIf kcd=40 then
      ww=""
      inp=text.GetSubText (inp,1,text.GetLength (inp)-1)
      If inp="" then 
        md=1
        
      endif
      Goto hh
    ElseIf kcd=29 then'-------spc key
      ww=" "
      Goto hh
    ElseIf kcd=28 then'----------------------symb key press
      ww=""
      md=md+1
      If md>5 then 
        md=1
      endif
      Goto xx
    endif 
    ww=cmat[md][kcd]
    If Text.IsSubText ("1234567890",ww)  then
      'nixx
    else
      If md=1 then 
        ww=" "+ww+" "
      endif  
      md=2'-----------------------------------after keyword entered switch to chars mode 
      
    endif
    hh:
    lnt=text.GetLength (ww)
    
    inp=Text.Append (inp,ww)
    drcln()'------------------------------cleaning cmdline
    xx:
    kcd=0'-----------------------------scrn.key handled, ready for next press
  endif
endwhile'---------------------------========================================ENDLOOP================

Sub scfnt
  GraphicsWindow.FontName="Calibri
  GraphicsWindow.FontSize=20
  GraphicsWindow.FontBold=T_
  GraphicsWindow.FontItalic=f_
EndSub 

Sub mscrr
  If runn=0 Then
    
    stpp=LDEvents.LastMouseWheelDelta*3
    If stpp<0 And Text.IsSubText (prrs,"'ProgEnd.") Then
      stpp=0
    endif
    
    stl=stl- stpp
    
    If stl<1 Then 
      stl=1
    endif  
    nolst=0
    refscr()
  Endif
endsub 

Sub dosample'-----------------------sample prg list--------------------
  ldcall.Function2 ("lset",5,"BORDER 6\INK 7\CLS 2")
  LDArray.SetValue(prg,25,"25:PRINT Turtle demo by SpectBasic")
  LDArray.SetValue(prg,27,"27:LOGO sh Sp10 m550;200 r150")
  LDArray.SetValue(prg,30,"30:FOR 1 26 1")
  LDArray.SetValue(prg,32,"32:LOGO F50 R15")
  LDArray.SetValue(prg,40,"40:NEXT")
  ldcall.Function2 ("lset",69,"LOGO hd")
  ldcall.Function2 ("lset",70,"STOP")
  ldcall.Function2 ("lset",35,"CUB2") 
  ldcall.Function2 ("lset",62,"LOGO F50 R15")
  ldcall.Function2 ("lset",65,"CUB2 0 T") 
  ldcall.Function2 ("lset",110,"FOR 1 250 55")
  ldcall.Function2 ("lset",120,"FOR 1 250 55")
  ldcall.Function2 ("lset",130,"FOR 1 250 55")
  ldcall.Function2 ("lset",140,"CUBE %F2 %F3 %F1 %F1")
  LDCall.Function2 ("lset",150,"NEXT")
  LDCall.Function2 ("lset",160,"NEXT")
  LDCall.Function2 ("lset",170,"NEXT")
  LDCall.Function2 ("lset",m300-1,"'ProgEnd.")
  LDArray.SetValue(prg,200,"200:PRINT Done.")
  refscr ()
EndSub 

Sub lset
  sl=args[1]
  sln=ldtext.Split(args[2],"\")
  For cz=1 To Array.GetItemCount (sln)
    LDArray.SetValue(prg,sl,sl+":"+sln[cz])
    sl=sl+5
  endfor
endsub

Sub makecursor'-----------------------------------------makes cursor shapes and edit lines
  kw=Shapes.AddText ("K")
  cml1=Shapes.AddText ("")
  cml2=Shapes.AddText ("")
  LDShapes.BrushColour (cml1,"White")
  LDShapes.PenColour (cml1,"Blue")
  LDShapes.BrushColour (kw,"Black")
  LDShapes.PenColour (kw,"White")
  LDShapes.Font(kw,"Arial",18,T_,F_)
  LDShapes.Font(cml1,"Arial",18,T_,F_)
  LDShapes.Font(cml2,"Arial",18,T_,F_)
  
  Shapes.Move(kw,70,530)
  Shapes.Move(cml1,70,530)
  Shapes.Move(cml2,70,530)
  
endsub


Sub drcln'-----------------------------------------------------draws cmd line txt
  tts=LDText.Split (inp," ")'--------splits line by spaces
  wlw=""
  For m=1 To Array.GetItemCount(tts)
    If Text.StartsWith(tts[m],"#") Then'---------keyword entered
      mm=text.GetSubTextToEnd (tts[m],2)
      If mm<0 then
        tts[m]=cmat[3][-mm]'----------kw set #2
      ElseIf mm>50 then
        tts[m]=cmat[4][mm-50]'--------keyw set #3
      else
        tts[m]=cmat[1][mm]'-----------kw set #1
      endif
    endif
    wlw=wlw+tts[m]+" "
  endfor
  tt= Shapes.GetTop (kw)
  Shapes.SetText(cml1,wlw)
  dss=ldShapes.Width(ldtext.Trim (cml1)) 
  Shapes.Move(kw,70+dss+10,tt)
  Shapes.Move(cml2,70+dss+15+10,tt)
  
EndSub



Sub runprg'---------------------------------------------------running prg
  prgf=LDArray.CopyToSBArray(prg)
  prr=""
  if cntr=1 then
    cntr=0
    rrx= cntt 
  else
    rrx=1
  endif
  while rrx<= Array.GetItemCount(prgf)
    If prgf[rrx]="0" then
      'nxx
      rrx=rrx+1 
    else
      pt=text.GetIndexOf (prgf[rrx],":")+1
      cm=text.GetSubTextToEnd ( prgf[rrx],pt)
      cln=rrx
      evprgln()
    endif
    
  EndWhile  
  If hltt=1 Then
    GraphicsWindow.Title="D Break. Cont continues..."
    hltt=0
  else
    GraphicsWindow.Title="0 Ok. All done)
  Endif
EndSub 

Sub ttm'-------------------------------------------timer tick cursor flashing
  tc=tc+1
  Shapes.SetText (kw,csgn[md])
  ipc1="Black
  If md=3 Then 
    ipc1="Green
  elseif md=4 then
    ipc1="Red
    endif
  If Math.Remainder(tc,2)=0 then
    LDShapes.BrushColour (kw,IPC1)
    LDShapes.PenColour (kw,cclr)
  Else
    LDShapes.BrushColour (kw,cclr)
    LDShapes.PenColour (kw,IPC1)
  endif  
endsub 


Sub kdd'---------------------------------------------phisical kbd handling*************************************************
  lk= GraphicsWindow.LastKey 
  GraphicsWindow.Title=lk
  ww=lk
  If Text.StartsWith (lk,"D") And Text.GetLength (lk)=2 Then '----------regukar nuber keys
    ww=text.GetSubTextToEnd (lk,2)
  elseIf Text.StartsWith (lk,"F") And Text.GetLength (lk)=2 Then'--------func keys
    cf=text.GetSubTextToEnd (lk,2)
    ww=text.GetSubText(acc[sst],cf,1)   
  elseif lk="OemQuestion" then
    ww="?"
    
  elseif lk="F11" then
    sst=1
    md=md-1
    If md<1 then
      md=1
    EndIf 
  elseif lk="F12" then
    sst=2
    md=md+1
    mxd=Array.GetItemCount (csgn )
    If md>mxd then
      md=mxd
    EndIf   
  elseIf Text.StartsWith (lk,"NumP") then'------------------numpad keys
    
    ww=text.GetSubTextToEnd (lk,Text.GetLength(lk))
    'TextWindow.WriteLine (ww)  
  endif
  
  If lk="Space" Then 
    ww=" "
    
  elseIf lk="Escape" and text.GetLength(inp)<4 Then' ----------------edits line
    inp=LDArray.GetValue  (prg,inp)
    num=text.GetSubText(inp,1,5)                 '--------------replaces : to space in order to make line editable!))
    num=LDText.Replace(num,":"," ")                
    inp=num+text.GetSubTextToEnd(inp,6)
    ww=""
  elseIf lk="Back" Then
    ww=""
    inp=text.GetSubText (inp,1,text.GetLength (inp)-1)
    If inp="" then 
      md=1
      
    endif
    
  elseIf lk="Return" Then
    Timer.Pause ()
    ccmd()
    If runn=0 then
      evc2 ()
    Else 
      Goto rrr
    endif
    If nodo=0 then
      If inp="" then
        For x=1 to 20
          LDShapes.Move  (cml1,100+x*40,550)
          Program.Delay(22)
        Endfor 
      else
        For x=1 to 20
          LDShapes.Move  (cml1,100-x*2,550-x*20)
          Program.Delay(22)
        Endfor 
      endif
      
    endif
    rrr:
    nodo=0
    crst()
  endif
  
  If Text.GetLength(ww)=1 or ww="" then
    
    inp=Text.Append (inp,ww)
    drcln()
  endif
EndSub 

Sub crst'--------------------------------reset cmdline after enter key
  Shapes.SetText(cml1,"") 
  Shapes.Move(cml1,70,530)
  
  md=1
  Shapes.SetText (kw,"K")
  inp=""
  Shapes.Move(kw,70,530)
  If runn=0 then
    refscr()
  endif
  
  Shapes.SetText(cml2,"")
  Timer.Resume ()
  
EndSub 

Sub refscr'------------------------------------------------------refresh screen
  GraphicsWindow.BrushColor=ppc
  GraphicsWindow.FillRectangle (70,40,660,550)
  
  If nolst=0 then
    prgf=0
    prgf=LDArray.CopyToSBArray(prg)
    prrs=""
    sl=0
    For x=stl To Array.GetItemCount(prgf)
      If prgf[x]="0" Then 
        'nicc
      else
        prrs=prrs+prgf[x]+nl
        sl=sl+1
        If sl>18 then
          Goto skk
        endif
      endif
    endfor 
    skk:
    ' If Text.IsSubText (prr,"'") Then
    '  GraphicsWindow.BrushColor ="Green
    '  GraphicsWindow.FontItalic=t_ 
    'Else
    GraphicsWindow.BrushColor=Ipc
    GraphicsWindow.FontItalic =f_ 
    '  endif 
    GraphicsWindow.DrawBoundText (75,45,650,prrs)
  endif
  nolst=0
endsub


Sub shppw'---------------------------------------------screen keybd shape press handling
  lsh=ldshapes.LastEventShape 
  lst= LDShapes.LastEventType 
  If lst="MouseDown" Then
    Shapes.SetOpacity (lsh,50)
    GraphicsWindow.Title=lsh
    kcd=text.GetSubTextToEnd (lsh,10)
    'TextWindow.WriteLine (kcd) 
  Else
    Shapes.SetOpacity (lsh,5)
  endif
EndSub 

Sub nwarr'------------------------------------------------new prg array
  
  LDArray.Delete(prg)
  prg=LDArray.Create(m300)
  For x=1 to m300
    LDArray.SetValue (prg,x,0)
  endfor
EndSub 

Sub ccmd'----------------------------------------------------------eval cmdline
  cm=ldtext.Trim(Text.ConvertToUpperCase(inp))
  runn=0
  If cm="NEW" Then 
    nwarr()
    stl=1
    inp=""
    runn=0
    donw()
  elseIf cm="CONT" Then
    GraphicsWindow.Title="Continuing...
    runn=1
    cntr=1
    nolst=1
    ww=""
  elseIf cm="LOAD" Then
    fll=LDDialogs.OpenFile ("txt","i:\")
    
' The following line could be harmful and has been automatically commented.
'     If LDFile.Exists(fll) then 
      
' The following line could be harmful and has been automatically commented.
'       infl=File.ReadContents(fll)
      inlns=ldtext.Split (infl,nl)
      nwarr()
      For x=1 to Array.GetItemCount(inlns) 
        ln=ldtext.Split(inlns[x],":")     
        LDArray.SetValue(prg,ln[1],ln[1]+":"+ln[2])
      endfor
      
    endif
  elseIf cm="HELP" Then
    showhelp()
  elseIf cm="?LOGO" Then
    showlogohlp()
  elseIf cm="?CUBE" Then
    shwcubhlp()
  elseIf cm="SAVE" Then
    fll=LDDialogs.SaveFile ("txt","i:\")
    If fll="" then
      'nicc
    Else
      prgf=LDArray.CopyToSBArray(prg)
      prr=""
      For x=1 To Array.GetItemCount(prgf)
        If prgf[x]="0" then
          'nxx
        else
          prr=prr+ (prgf[x])+nl
        endif
      endfor 
    endif
    
' The following line could be harmful and has been automatically commented.
'     File.WriteContents (fll,prr)
    GraphicsWindow.Title="SAVE Ok!"
  elseIf cm="LLIST" Then 
    inp=""
    prgf=LDArray.CopyToSBArray(prg)
    prr=""
    For x=1 To Array.GetItemCount(prgf)
      If prgf[x]="0" then
        'nxx
      else
        textwindow.WriteLine (prgf[x])
      endif
    endfor 
    
  elseIf Text.StartsWith (cm,"LIST") Then 
    GraphicsWindow.Title="OK 0:1"
    nnn=LDText.Split(cm,"|")
    stl=nnn[2]
    'TextWindow.WriteLine (stl)
    ww=""
    nodo=1
    crst()
    nolst=0
    scfnt ()
    refscr()
    nolst=1
    
  elseIf cm="CLS" Then 
    GraphicsWindow.Title="OK 0:1"
    ww=""    
    nolst=1
    refscr ()
    nolst=1
    crst()
    nolst=1
  elseIf Text.StartsWith (cm,"RUN") Then 
    GraphicsWindow.Title="Running...
    runn=1
    nnn=LDText.Split(cm,"|")
    cntt=nnn[2]
    If cntt>0 then
      cntr=1
    endif
    nolst=1
    ww=""
  endif
endsub

Sub shwcubhlp
  
  hlg="CUBE x y z clh"+nl
  hlg=hlg+"CUB2 clh"+nl
  
  hlg=hlg+"x,y,z = 3d coords of cubecentre"+nl
  hlg=hlg+"2d last coords by PLOT/DRAW for CUB2 cmd."+nl
  hlg=hlg+"clh = color hue (0/360=red, 60=Yllw, 120=grn, "+nl
  hlg=hlg+"180=cy, 240=blue, 300=magenta"+nl
  
  GraphicsWindow.ShowMessage("CUBE/CUB2 params: " + nl + hlg, "Information")
EndSub 

Sub showhelp
  
  hlg="BORDER PAPER INK CLS LIST GOTO RUN STOP CONT"+nl
  hlg=hlg+"PLOT DRAW 'REM HELP CUBE CUB2 LOGO LET"+nl
  hlg=hlg+"FOR NEXT SAVE LOAD ?LOGO ?CUBE NEW LLIST LPRINT"+nl
  hlg=hlg+"FLCKR WEB CIRCLE PAUSE
  GraphicsWindow.ShowMessage("Operative cmds: " + nl + hlg, "Information")
  endsub
  

Sub showlogohlp
  hlg="Fxxx=forward by xxx px, RT/LT=turn right/left 90 deg."+nl
  hlg=hlg+"Rxxx /Lxxx =turn right/lft by xxx deg."+nl
  hlg=hlg+"Annn = set heading to nnn deg."+nl
  hlg=hlg+"Mxxx;yyy= move to (xxx,yyy)"+nl
  hlg=hlg+"SH/HD= shw/hide turtle"+nl
  hlg=hlg+"SPnn= turtle speed 1..10"
  
  GraphicsWindow.ShowMessage("LOGO cmds: " + nl + hlg, "Information")
EndSub 

Sub plin'--------------------------------------------line splitting
  nxtl=0
  If ttx+text.GetLength(args[1])*wc>660 Then
    ls=Math.Floor ((660-ttx)/13)
    pl1=text.GetSubText(args[1],1,ls)
    pl2=text.GetSubTexttoend(args[1],ls+1)
    nxtl=1
  Else 
    pl1=args[1]
  endif
endsub

Sub wrarr
  ari=Array.GetAllIndices(varr)
  ' TextWindow.WriteLine("***************")
  For jx=1 to Array.GetItemCount(varr)
    TextWindow.Write(ari[jx]+"<=")
    TextWindow.WriteLine(varr[ari[jx]])
  endfor 
EndSub 

Sub replvars
  inn=ldtext.Split (args[1]," ")
  For fd=2 To Array.GetItemCount(inn)
    if Text.IsSubText (inn[fd],"&") Then
      vn=text.ConvertToLowerCase (text.GetSubTextToEnd (inn[fd],2))
      inn[fd]=varr[vn]
    EndIf
  EndFor 
  ress=""
  For fd=1 To Array.GetItemCount(inn)
    ress=ress+inn[fd]+" "
  EndFor 
endsub


Sub evprgln'-------------------------------------evals prg cmds
  If runn=1 then
    For q=1 To 6 '------nest level 6 loops 
      cm=LDText.Replace(cm,"%F"+q,fv[q])
    endfor
  endif
  ldcall.Function ("replvars",cm)
  cm=ress
  lm=text.GetIndexOf(cm+"'","'")
  lcc=text.GetSubText (cm,1,lm)
  ltx=ldtext.Split(lcc," ")
  
  icl=array.GetItemCount (ltx)
  If ltx[1]="NEXT" then
    
    fv[fc]=fv[fc]+fs[fc]
    If fv[fc]<=fx[fc] then  
      rrx=flin[fc]
    Else 
      rrx=rrx+1
      fc=fc-1
    endif
    
  else
    
    If ltx[1]="BORDER" or ltx[1]="#B" Then
      GraphicsWindow.BackgroundColor=clrs[ltx[2]]
      inp=""
    elseIf ltx[1]="FOR" then
      fc=fc+1
      flin[fc]=cln+1
      fv[fc]=ltx[2]
      fx[fc]=ltx[3]
      fs[fc]=ltx[4]
      If fs[fc]=0 then
        fs[fc]=1
      endif
    elseIf ltx[1]="STOP" then
      hltt=1
      cntt=rrx+1
      rrx=999
    elseIf ltx[1]="LOGO" then
      For lg=2 to icl
        LDCall.Function ("logmv",ltx[lg])
      endfor
      
    elseIf ltx[1]="WEB" then
      www=ltx[2]
      If www="" then
        www="www.smallbasic.com"
      endif
      wd=ltx[3]
      ht=ltx[4]
      If wd*ht=0 then
        wd=600
        ht=400
      endif
      wbb[ctc]=LDControls.AddBrowser (wd,ht,www)
      wbt[ctc]="Web:"+www
      Shapes.Move (wbb[ctc],70,40)
      
      ctc=ctc+1
      
    elseIf ltx[1]="FLCKR" then
      iml=Flickr.GetRandomPicture("Water")
      GraphicsWindow.DrawResizedImage (iml,0,0,800,600)
    elseIf ltx[1]="INC" then
      vn = text.ConvertToLowerCase (ltx[2])
      varr[vn] = FCExtensions.MathEval( varr[vn]) + FCExtensions.MathEval( ltx[3])
      wrarr()  
    elseIf ltx[1]="LET" then
      nf=2
      rpp:
      varr[text.ConvertToLowerCase (ltx[nf])]=ltx[nf+1]
      If ltx[nf+2]="|" then
        nf=nf+3
        Goto rpp
      endif
      
    elseIf ltx[1]="PLOT" then
      plx=ltx[2]
      ply=ltx[3]
      GraphicsWindow.BrushColor=ipc
      GraphicsWindow.FillEllipse(plx,ply,2,2)
      
    elseIf ltx[1]="DRAW" then
      plx1=ltx[2]
      ply1=ltx[3]
      dww=ltx[4]
      If dww<1 then
        dww=1
      endif 
      GraphicsWindow.PenColor=ipc
      GraphicsWindow.DrawLine (plx,ply,plx1,ply1)
      plx=plx1
      ply=ply1
      
      
    elseIf ltx[1]="GOTO" then
      rrx=ltx[2]-1
    elseIf ltx[1]="CUBE" then
      LDCall.Function4 ("DrawCube",ltx[2],ltx[3],ltx[4],ltx[5])
    elseIf ltx[1]="CUB2" then
      c2dmd=1
      LDCall.Function4 ("DrawCube",plx,ply,ltx[2],ltx[3])
      c2dmd=0
      
    elseIf ltx[1]="CIRCLE" then
      
      GraphicsWindow.BrushColor=ipc  
      rr=ltx[2]*2
      GraphicsWindow.FillEllipse(plx-ltx[2],ply-ltx[2],rr,rr)   
    elseIf ltx[1]="LPRINT" then
      If ltx[3]="|" then 
        TextWindow.WriteLine (ltx[2])
      Else
        TextWindow.Write (ltx[2])
        
      endif
      
    elseIf ltx[1]="PRINT" then
      If ltx[3]="|" then 
        ttx=0
        tty=tty+hc_
      endif
      GraphicsWindow.FontName ="Lucida Console"
      GraphicsWindow.FontSize =20
      GraphicsWindow.FontBold =T_
      ldcall.Function ( "plin",ltx[2])
      GraphicsWindow.DrawText (ttx+70,tty, pl1)
      ' TextWindow.WriteLine (ltx[2])
      ttx=ttx+text.GetLength(pl1)*wc
      
      If ttx>660 or nxtl=1 then
        ttx=0
        tty=tty+hc_
        If nxtl=1 then
          GraphicsWindow.DrawText (ttx+70,tty, pl2)
          ttx=ttx+text.GetLength(pl2)*wc
        endif
        nxtl=0 
      endif
      
      inp=""
    elseIf ltx[1]="PAUSE" then
      Program.Delay(ltx[2]) 
    elseIf ltx[1]="PAPER" or ltx[1]="#P" Then
      ppc=clrs[ltx[2]]
      inp=""
    elseIf ltx[1]="CLS" Then 
      If ltx[2]>0 then
        GraphicsWindow.BrushColor=clrs[ltx[2]]
      Else
        GraphicsWindow.BrushColor=ppc
      endif
      GraphicsWindow.FillRectangle (70,40,660,550)
      GraphicsWindow.BrushColor=Ipc
      
      ttx=0
      tty=50
    elseIf ltx[1]="INK" or ltx[1]="#I" Then
      ipc=clrs[ltx[2]]
      if ltx[2]>4 and ltx[2]<8 then 
        cclr="Black
      Else
        cclr="White
      endif
      
      inp=""
      
    endif
    rrx=rrx+1
  endif   
  
EndSub 

Sub logmv'---------------------------------------------------------------logo part*******************************
  lc=text.ConvertToLowerCase (args[1])
  If Text.StartsWith(lc,"f") Then
    Turtle.Move (Text.GetSubTextToEnd (lc,2))
  elseIf Text.StartsWith(lc,"a") Then
    Turtle.Angle = (Text.GetSubTextToEnd (lc,2))
    
  elseIf Text.StartsWith(lc,"rt") Then
    Turtle.TurnRight ()
    
  elseIf Text.StartsWith(lc,"lt") Then
    Turtle.TurnLeft()
  elseIf Text.StartsWith(lc,"r") Then
    Turtle.Angle = Turtle.Angle+(Text.GetSubTextToEnd (lc,2))
  elseIf Text.StartsWith(lc,"l") Then
    Turtle.Angle = Turtle.Angle-(Text.GetSubTextToEnd (lc,2))   
  elseIf Text.StartsWith(lc,"u") Then
    Turtle.PenUp ()
  elseIf Text.StartsWith(lc,"d") Then
    Turtle.PenDown  ()
  elseIf Text.StartsWith(lc,"sh") Then
    LDShapes.ResetTurtle ()
    Turtle.Show  ()
  elseIf Text.StartsWith(lc,"hd") Then
    Turtle.hide  ()
    LDShapes.RemoveTurtleLines()
    
  elseIf Text.StartsWith(lc,"sp") Then
    Turtle.Speed  = (Text.GetSubTextToEnd (lc,3))
  elseIf Text.StartsWith(lc,"m") Then
    Trx= LDText.Split (Text.GetSubTextToEnd (lc,2),";")
    Turtle.MoveTo(Trx[1],Trx[2]) 
    Turtle.Angle=0
    
  endif
  plx=turtle.X 
  ply=turtle.y
EndSub 

Sub donw'------------------------------------------------------------new cmd-reset
  Timer.Pause ()
  cclr="White"
  ppc=clrs[0]
  nolst=1
  refscr()
  Shapes.SetText(cml1,"")
  Shapes.SetText(cml2,"")
  
  GraphicsWindow.FontName="Calibri
  GraphicsWindow.FontSize=33
  GraphicsWindow.FontBold=t_
  GraphicsWindow.BrushColor="Black
  GraphicsWindow.DrawText(300,200,"Spectrum revival") 
  GraphicsWindow.DrawText(300,230,"1982-2015")
  GraphicsWindow.BackgroundColor=clrs[0]
  brc= LDText.Split("Red Yellow Green Cyan"," ") 
  brr= LDShapes.BrushGradient(brc,"DU")
  bpp[1][1]=650
  bpp[1][2]=600
  bpp[2][1]=750
  bpp[2][2]=600
  bpp[3][1]=800
  bpp[3][2]=550
  bpp[4][1]=800
  bpp[4][2]=450
  
  LDShapes.BrushGradientPolygon(brr,bpp)
  nn=Shapes.AddRectangle  (660,550)
  Shapes.Move (nn,70,40)
  ipc="Black
  ppc=clrs[0]
  LDShapes.BrushColour(nn,"Black")
  For x=100 To 1 Step -1
    Shapes.setOpacity(nn,x)
    Program.Delay(5)
  endfor
  Shapes.HideShape(nn) 
  ldShapes.move (nn,1000,1000)
  Program.Delay(1500)
  scfnt ()
  GraphicsWindow.BrushColor="Black
  Timer.Resume ()
endsub

Sub evc2
  If LDText.FindAll (inp," ")=0   Then
    
    ni=inp
    If ni>0 and ni<m300-1 Then
      LDArray.SetValue (prg,ni,0)
      For x=1 to 10
        LDShapes.Move  (cml1,100-x*12,550)
        Program.Delay(22)
      Endfor
      nodo=1
    endif
    inp=""
  Else
    ' evprgln()
    ltx=ldtext.Split(cm," ")
    
    If ltx[1]=0 Then
      inp=""
    Else
      If ltx[1]>m300-2 Then
        ltx[1]=m300-2
      endif
      pzz= LDText.FindAll(clst,ltx[2]) 
      'TextWindow.WriteLine(pzz)
      If pzz=0 Then 
        ltx[2]="' rem:"+ltx[2]
      endif
      lll=ltx[1]+":"
      For x=2 To Array.GetItemCount(ltx)
        'TextWindow.WriteLine(ltx[x])
        If ltx[x]=0 then
          
          'nicc
        ElseIf ltx[x]="0$" then
          lll=lll+"0 "
        else
          lll=lll+ltx[x]+" "
        EndIf
      endfor
      
      LDArray.SetValue(prg,ltx[1],lll)
    endif
  endif
endsub

Sub kyy'---------------------------------draws keys and set key shape event ----------------
  LDShapes.BrushGradientRoundedRectangle(brr,args[1],args[2],args[3],args[4],5)
  kyb[ki]=Shapes.AddRectangle(args[3],args[4])
  Shapes.Move (kyb[ki],args[1],args[2])
  LDShapes.SetShapeEvent (kyb[ki])
  Shapes.SetOpacity (kyb[ki],10)
  If ki=10 Then
    LDDialogs.ToolTip (kyb[10],"Type ?LOGO for help")
  elseIf ki=9 Then
    LDDialogs.ToolTip (kyb[9],"Type ?CUBE for help")
  elseIf ki=4 Then
    LDDialogs.ToolTip(kyb[ki],"Press RUN+enter to start prg")
  elseIf ki=11 Then
    LDDialogs.ToolTip(kyb[ki],"Press NEW+enter to start a new prg")
  endif
  ki=ki+1
endsub

Sub drwkbd'-----------------------------------------------------------scrn keybd draw--------------------
  GraphicsWindow.FontName="ARIAL
  GraphicsWindow.FontSize=11
  GraphicsWindow.BrushColor ="Green
  bcc=ldtext.Split("White Gray DarkGray"," ")
  brr= LDShapes.BrushGradient (bcc,"DD")
  txl=ldtext.Split(grtx[1]," ")
  For x=0 To 9
    LDCall.Function4 ("kyy",50+x*73,750,50,40)  '---qwe
    GraphicsWindow.DrawText (50+x*73,730,txl[x+1])
  EndFor
  GraphicsWindow.BrushColor ="Black
  GraphicsWindow.FontSize=26
  
  For x=0 To 9
    GraphicsWindow.DrawText (55+x*73,750,Text.GetSubText ("QWERTYUIOP",x+1,1))
  EndFor
  GraphicsWindow.FontSize=11
  
  GraphicsWindow.BrushColor ="Green
  txl=ldtext.Split(grtx[2]," ")
  For x=0 To 9
    LDCall.Function4 ("kyy",70+x*73,830,50,40) '--asd 
    GraphicsWindow.DrawText (73+x*73,812,txl[x+1])
  EndFor 
  
  GraphicsWindow.BrushColor ="Black
  GraphicsWindow.FontSize=26
  
  For x=0 To 9
    GraphicsWindow.DrawText (75+x*73,830,Text.GetSubText ("ASDFGHJKL",x+1,1))
  EndFor
  GraphicsWindow.FontSize=11
  
  GraphicsWindow.BrushColor ="Green
  
  txl=ldtext.Split(grtx[3]," ")
  For x=0 To 8
    LDCall.Function4 ("kyy",110+x*73,915,50,40) '---zxc 
    GraphicsWindow.DrawText (115+x*73,900,txl[x+1])
  EndFor 
  
  GraphicsWindow.BrushColor ="Black
  GraphicsWindow.FontSize=26
  
  For x=0 To 9
    GraphicsWindow.DrawText (114+x*73,915,Text.GetSubText ("ZXCVBNM ",x+1,1))
  EndFor
  GraphicsWindow.FontSize=11
  
  GraphicsWindow.BrushColor ="White
  txl=ldtext.Split("PLOT DRAW REM RUN HELP! RET. IF INPUT CUBE PRINT"," ")
  meastx()
  For x=0 To 9
    GraphicsWindow.DrawText (94+x*73-mx[x+1],774,txl[x+1])
  EndFor
  
  txl=ldtext.Split("NEW SAVE DIM FOR GOTO GSUB LOAD LIST LET ENTER"," ")
  meastx()
  For x=0 To 9
    GraphicsWindow.DrawText (114+x*73-mx[x+1],853,txl[x+1])
  EndFor
  
  txl=ldtext.Split("COPY CLR CONT CLS BORD. NEXT PAUSE SYMB SPC"," ")
  meastx()
  For x=0 To 8
    GraphicsWindow.DrawText (155+x*73-mx[x+1],940,txl[x+1])
  EndFor
  
  GraphicsWindow.BrushColor ="Red
  txl=ldtext.Split(rdxt[1]," ")
  
  For x=0 To 9
    GraphicsWindow.DrawText (57+x*73,793,txl[x+1])
  EndFor
  
  txl=ldtext.Split(rdxt[2]," ")
  
  For x=0 To 8
    GraphicsWindow.DrawText (77+x*73,873,txl[x+1])
  EndFor
  
  txl=ldtext.Split(rdxt[3]," ")
  
  For x=0 To 6
    GraphicsWindow.DrawText (117+x*73,960,txl[x+1])
  EndFor
  
  txl=ldtext.Split(rdxt[4]," ")
  
  For x=0 To 9
    GraphicsWindow.DrawText (29+x*73,710,txl[x+1])
  EndFor
  
  txl=ldtext.Split("! @ # $ % & ' ( ) _"," ")
  
  For x=0 To 10
    LDCall.Function4 ("kyy",26+x*73,660,50,40)  '---123
    GraphicsWindow.DrawText (54+x*73,684,txl[x+1])
  EndFor
  
  GraphicsWindow.BrushColor ="Black
  GraphicsWindow.FontSize=26
  
  For x=0 To 10
    GraphicsWindow.DrawText (30+x*73,670,Text.GetSubText ("1234567890‚Üê",x+1,1))
  EndFor
  GraphicsWindow.FontSize=11
  GraphicsWindow.BrushColor ="Red
  
  txl=ldtext.Split("HLT NOT < > \ ^ - + ="," ")
  meastx()
  For x=0 To 8
    GraphicsWindow.DrawText (118+x*73-mx[x+1],833,txl[x+1])
  EndFor
  nw=text.GetCharacter(34)
  txl=ldtext.Split("[ ] TRG { } AND OR AT ; "+nw," ")
  meastx()
  For x=0 To 9
    GraphicsWindow.DrawText (99+x*73-mx[x+1],753,txl[x+1])
  EndFor
  
endsub

Sub mwww
  GraphicsWindow.Title= GraphicsWindow.MouseX +" : "+GraphicsWindow.MouseY 
EndSub 

Sub meastx'----------------------------------------------measure txt width
  mx=0
  For f=1 To Array.GetItemCount(txl)
    ia=LDText.FindAll(txl[f],"I")
    ic=array.GetItemCount (ia)
    is=text.GetLength(txl[f])-ic
    mx[f]=ic*4+is*8
    
  EndFor 
EndSub 

Sub DrawCube'---------------------------------------3dcubes
  dmt=ldtext.Split ("1 1 1"," ")
  If c2dmd=1 Then
    
    edge = Math.SquareRoot(2 / 3) * cedge
    x=args[1]
    y=args[2]
    cci=args[3]
    If args[4]="T" Then 
      dmt=ldtext.Split ("0 0 1"," ")
    endif
    ppx=0
    ppy=0
    x1 = ppx
    y1 = ppy
  else
    ppx1= args[1]
    ppy1= args[2]
    ppz1= args[3]
    ppx=bsx0
    ppy=bsy0
    ppz=0
    
    edge = Math.SquareRoot(2 / 3) * cedge 
    cci= args[4]
    x1 = ppx
    y1 = ppy
    ppx=ppx1
    ppy=ppy1
    ppz=ppz1
    Conv3Dto2D()
  endif
  
  
  cw=1
  clw1[1]=LDColours.HSLtoRGB(cci,0.4,sllw)
  clw1[2]=LDColours.HSLtoRGB(cci,0.4,sllw*1.2)
  clw1[3]=LDColours.HSLtoRGB(cci,0.4,sllw*1.4)
  clw2[1]=LDColours.HSLtoRGB(cci,0.5,sllw)
  clw2[2]=LDColours.HSLtoRGB(cci,0.5,sllw*1.2)
  clw2[3]=LDColours.HSLtoRGB(cci,0.5,sllw*1.4)
  clw3[1]=LDColours.HSLtoRGB(cci,0.7,sllw)
  clw3[2]=LDColours.HSLtoRGB(cci,0.7,sllw*1.3)
  clw3[3]=LDColours.HSLtoRGB(cci,0.7,sllw*1.5)
  
  brsh[1]=LDShapes.BrushGradient (clw1,"DD")
  brsh[2]=LDShapes.BrushGradient (clw2,"DU")
  brsh[3]=LDShapes.BrushGradient (clw3,"H")
  
  
  For a = san To 360+san Step 60
    _a = Math.GetRadians(a)
    x2 = bsx0 - edge * Math.Sin(_a)
    y2 = bsy0 - edge * Math.Cos(_a)
    
    If a>san Then
      If a=360 then 
        cc=1
      Else 
        cc=math.Remainder (a,360)  / 60
      endif
      
      If math.Remainder (a/60-1,2)=0 then
        plly[1][1]=x1+50+x
        plly[1][2]=y1-30+y
        plly[2][1]=x2+50+x
        plly[2][2]=y2-30+y
        plly[3][1]=x3+50+x
        plly[3][2]=y3-30+y
        plly[4][1]=ox3+50+x
        plly[4][2]=oy3-30+y 
        If dmt[cw]=1 then
          ldShapes.brushGradientPolygon(brsh[cw], plly)
        endif
        
        cw=cw+1
        pi=pi+1 
      endif
    EndIf
    
    ox3=x3
    oy3=y3
    x3 = x2
    y3 = y2
  EndFor
endsub

Sub Conv3Dto2D'-----------------------------------------3d to 2d cnv
  x = bsx - Math.SquareRoot(1 / 2) *ppx
  x = x + Math.SquareRoot(1 / 2) * ppy
  y = bsy + Math.SquareRoot(1 / 6) * ppx
  y = y + Math.SquareRoot(1 / 6) * ppy
  y = y - Math.SquareRoot(2 / 3) * ppz
EndSub
