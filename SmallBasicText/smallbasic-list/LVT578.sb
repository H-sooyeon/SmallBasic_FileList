' Sort 0.1

' Copyright (c) 2013 Nonki Takahashi.  All right reserved.

'

' History:

'  2013/02/04 Created.

'

GraphicsWindow.Title = "Sort 0.1"

GraphicsWindow.BackgroundColor = "LightGray"

GraphicsWindow.BrushColor = "Black"

CRLF = Text.GetCharacter(13) + Text.GetCharacter(10)

InitArray()

GetArrayToBuf()

GraphicsWindow.DrawText(10, 10, "Original Array")

original = Controls.AddMultiLineTextBox(10, 30)

Controls.SetSize(original, 295, 355)

Controls.SetTextBoxText(original, buf)

GraphicsWindow.DrawText(315, 10, "Sorted Array")

sorted = Controls.AddMultiLineTextBox(315, 30)

Controls.SetSize(sorted, 295, 355)

Controls.AddButton("Sort", 10, 400)

Controls.ButtonClicked = OnButtonClicked

Sub Compare

  ' param txt[i1]

  ' param txt[i2]

  ' returns eq ="True" if txt[i1] = txt[i2]

  ' returns gt = "True" if txt[i1] > txt[i2]

  ' returns lt = "True" if txt[i1] < txt[i2]

  eq = "False"

  gt = "False"

  lt = "False"

  txt1 = Text.ConvertToLowerCase(txt[i1])

  txt2 = Text.ConvertToLowerCase(txt[i2])

  len1 = Text.GetLength(txt1)

  len2 = Text.GetLength(txt2)

  len = Math.Min(len1, len2)

  For p = 1 To len

    c1 = Text.GetCharacterCode(Text.GetSubText(txt1, p, 1))

    c2 = Text.GetCharacterCode(Text.GetSubText(txt2, p, 1))

    If c1 > c2 Then

      gt = "True"

      p = len + 1 ' break

    ElseIf c1 < c2 Then

      lt = "True"

      p = len + 1 ' break

    Else

      c1 = Text.GetCharacterCode(Text.GetSubText(txt[i1], p, 1))

      c2 = Text.GetCharacterCode(Text.GetSubText(txt[i2], p, 1))

      If c1 > c2 Then

        gt = "True"

        p = len + 1 ' break

      ElseIf c1 < c2 Then

        lt = "True"

        p = len + 1 ' break

      EndIf

    EndIf

  EndFor

  If gt = "False" And lt = "False" Then

    If len1 > len2 Then

      gt = "True"

    ElseIf len1 < len2 Then

      lt = "True"

    Else

      eq = "True"

    EndIf

  EndIf

EndSub

Sub GetArrayFromTextBox

  txt = ""

  i = 0

  buf = Controls.GetTextBoxText(original)

  While Text.GetLength(buf) > 0

    p = Text.GetIndexOf(buf, CRLF)

    i = i + 1

    If p = 0 Then

      txt[i] = Text.GetSubTextToEnd(buf, 1)

      buf = ""

    ElseIf p = 1 Then

      txt[i] = ""

      buf = Text.GetSubTextToEnd(buf, p + 2)

    Else

      txt[i] = Text.GetSubText(buf, 1, p - 1)

      buf = Text.GetSubTextToEnd(buf, p + 2)

    EndIf

  EndWhile

EndSub

Sub GetArrayToBuf

  buf = ""

  index = Array.GetAllIndices(txt)

  count = Array.GetItemCount(txt)

  For i = 1 To index[count]

    buf = buf + txt[i] + CRLF

  EndFor

EndSub

Sub GetSortedArrayToBuf

  buf = ""

  i = 0

  While order[i] <> ""

    i = order[i]

    buf = buf + txt[i] + CRLF

  EndWhile

EndSub

Sub InitArray

  txt = "1=Z;2=t;3=q;4=aa;6=C;7=b;8=a;9=A;"

EndSub

Sub OnButtonClicked

  GetArrayFromTextBox()

  Sort()

  GetSortedArrayToBuf()

  Controls.SetTextBoxText(sorted, buf)

EndSub

Sub Sort

  ' param txt[] - array to sort

  ' param order[] - index list for sorted array

  order = ""

  index = Array.GetAllIndices(txt)

  count = Array.GetItemCount(txt)

  order[0] = 1

  For i1 = 2 To index[count]

    i2 = 0

    While order[i2] <> ""

      lasti2 = i2

      i2 = order[i2]

      Compare()

      If lt Then  ' insert

        order[lasti2] = i1

        order[i1] = i2

        i2 = -1 ' break

      EndIf

    EndWhile

    If i2 <> -1 Then  ' append

      order[i2] = i1

    EndIf

  EndFor

EndSub

      


