' Star Sign
' Copyright © 2014 Nonki Takahashi.  The MIT License.
' Last update 2014-12-18
' Version (pilot)
'
TextWindow.Title = "Star Sign"
debug = "False"
Init()
While "True"
  GetDate()
  WriteStarSign()
EndWhile
Sub GetDate
  invalid = "True"
  While invalid
    TextWindow.Write("Birthday(mm-dd)? ")
    date = TextWindow.Read()
    GetMonthAndDay()
    If 1 <= month And month <= 12 Then
      If debug Then
        TextWindow.WriteLine("Month " + month + " is valid.")
      EndIf
      If 1 <= day And day <= dom[month] Then
        If debug Then
          TextWindow.WriteLine("Day " + day + " is valid.")
        EndIf
        invalid = "False"
      Else
        If debug Then
          TextWindow.WriteLine("Day " + day + " is invalid.")
          TextWindow.WriteLine("dom[month]=" + dom[month])
        EndIF
      EndIf
    EndIf
    If invalid Then
      TextWindow.ForegroundColor = "Yellow"
      TextWindow.WriteLine("Date " + month + "-" + day + " is invalid.")
      TextWindow.ForegroundColor = "Gray"
    EndIF
  EndWhile
EndSub
Sub GetDays
  ' param month, day
  ' return days
  days = 0
  For m = 1 To month - 1
    days = days + dom[m]
  EndFor
  days = days + day
EndSub
Sub GetMonthAndDay
  ' param date
  ' return month, day
  month = Text.GetSubText(date, 1, 2) * 1
  day = Text.GetSubText(date, 4, 2) * 1
EndSub
Sub Init
  zodiac["Aries"] = "sign=♈;ja=おひつじ座;from=03-21;to=04-19;"
  zodiac["Taurus"] = "sign=♉;ja=おうし座;from=04-20;to=05-20;"
  zodiac["Gemini"] = "sign=♊;ja=ふたご座;from=05-21;to=06-21;"
  zodiac["Cancer"] = "sign=♋;ja=かに座;from=06-21;to=07-22;"
  zodiac["Leo"] = "sign=♌;ja=しし座;from=07-23;to=08-22;"
  zodiac["Virgo"] = "sign=♍;ja=おとめ座;from=08-23;to=09-22;"
  zodiac["Libra"] = "sign=♎;ja=てんびん座;from=09-23;to=10-23;"
  zodiac["Scorpio"] = "sign=♏;ja=さそり座;from=10-24;to=11-22;"
  zodiac["Sagittarius"] = "sign=♐;ja=いて座;from=11-23;to=12-23;"
  zodiac["Capricorn"] = "sign=♑;ja=やぎ座;from=12-24;to=01-19;"
  zodiac["Aquarius"] = "sign=♒;ja=みずがめ座;from=01-20;to=02-18;"
  zodiac["Pisces"] = "sign=♓;ja=うお座;from=02-19;to=03-20;"
  dom = "1=31;2=29;3=31;4=30;5=31;6=30;7=31;8=31;9=30;10=31;11=30;12=31;"
  nZodiac = Array.GetItemCount(zodiac)
  index = Array.GetAllIndices(zodiac)
  For z = 1 To nZodiac
    date = zodiac[index[z]]["from"]
    GetMonthAndDay()
    GetDays()
    zodiac[index[z]]["fromDays"] = days
    date = zodiac[index[z]]["to"]
    GetMonthAndDay()
    GetDays()
    zodiac[index[z]]["toDays"] = days
    If debug Then
      TextWindow.WriteLine(zodiac[index[z]])
    EndIf
  EndFor
EndSub
Sub WriteStarSign
  ' param month, day
  GetDays()
  nZodiac = Array.GetItemCount(zodiac)
  index = Array.GetAllIndices(zodiac)
  match = "False"
  For z = 1 To nZodiac
    fromDays = zodiac[index[z]]["fromDays"]
    toDays = zodiac[index[z]]["toDays"]
    If debug Then
      TextWindow.WriteLine("index[z]=" + index[z])
      TextWindow.WriteLine("fromDays=" + fromDays)
      TextWindow.WriteLine("toDays=" + toDays)
      TextWindow.WriteLine("days=" + days)
    EndIf
    If fromDays < toDays Then
      If (fromDays <= days) And (days <= toDays) Then
        match = "True"
      EndIf
    Else
      If ((fromDays <= days) And (days <= 366)) Or ((1 <= days) And (days <= toDays)) Then
        match = "True"
      EndIf
    EndIf
    If match Then
      TextWindow.WriteLine(index[z])
      z = nZodiac ' break
    EndIf
  EndFor
EndSub
