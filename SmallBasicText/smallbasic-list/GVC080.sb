'==================================================

'INITIALISE

'==================================================



gw = 600

gh = 600

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

'GraphicsWindow.PenWidth = 0

boxSize = 200

viewerDistance = 100



nBox = 5

For i = 1 To nBox

  box[i] = Shapes.AddRectangle(boxSize,boxSize)

  x = 100+Math.GetRandomNumber(gw-200)

  y = 100+Math.GetRandomNumber(gh-200)

  Shapes.Move(box[i],x-boxSize/2,y-boxSize/2)

  boxDistance[i] = Math.GetRandomNumber(200)

EndFor

viewBox()



up =0

down = 0

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp



'==================================================

'MAIN LOOP

'==================================================



While ("True")

  If (up = 1) Then

    viewerDistance = viewerDistance-1

    viewBox()

  EndIf

  If (down = 1) Then

    viewerDistance = viewerDistance+1

    viewBox()

  EndIf

  Program.Delay(10)

EndWhile



'==================================================

'SUBROUTINES

'==================================================



Sub viewBox

  viewerDistance = Math.Min(500,Math.Max(0,viewerDistance)) 'Limit viewerDistance to be between 0 and 500

  For i = 1 To nBox

    actualDistance = viewerDistance+boxDistance[i] 'Total distance to box

    scaleBox = boxSize/actualDistance 'Linear scaling with distance

    Shapes.Zoom(box[i],scaleBox,scaleBox)

    Shapes.SetOpacity(box[i],100-viewerDistance/5) 'Further boxes are lighter

  EndFor

  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor

  GraphicsWindow.FillRectangle(0,0,gw,gh)

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.DrawText(20,20,"Viewer Distance is "+viewerDistance)

EndSub



'==================================================

'EVENT SUBROUTINES

'==================================================



Sub OnKeyDown

  If (GraphicsWindow.LastKey = "Up") Then

    up = 1

  ElseIf (GraphicsWindow.LastKey = "Down") Then

    down = 1

  EndIf

EndSub



Sub OnKeyUp

  If (GraphicsWindow.LastKey = "Up") Then

    up = 0

  ElseIf (GraphicsWindow.LastKey = "Down") Then

    down = 0

  EndIf

EndSub




