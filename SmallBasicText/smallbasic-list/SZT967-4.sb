'  absolu     teleprompter   like movie endroll?      SZT967-4

GUI()
Controls.ButtonClicked=onclicked

Sub onclicked
  eff=Controls.GetButtonCaption(Controls.LastClickedButton)
  If eff="DEMARRAGE DU SCROLL" Then
    fenetrescroll()
  elseif eff="DELETE" then
    Controls.SetTextBoxText(texte1,"")              
  elseif eff="RETOUR TEXTE" then   
    GUI()                       '  initial  window
  elseif eff="<" or eff=">" then 
    LetterSize()
  elseif eff="+" or eff="-" then 
    SpeedUpDown()
  elseif eff="↑" or eff="↓"   then 
    ScrollUpDown()
    PlayScroll()
  elseif eff="ǁ" then   
    Scrollpause()
  EndIf
EndSub

Sub PlayScroll
  SCRLTop=Shapes.GetTop(scrolltext)
  While 800>SCRLTop  And  SCRLTop>-100 And Scroll="true"
    GraphicsWindow.Title=" Scroll is  ....  "+SCUpDown+"  direction ,   Speed is "+ SPUpDown+" : "+UD
    SCRLTop=Shapes.GetTop(scrolltext)
    Shapes.Zoom(scrolltext,SZ/12,SZ/12)
    Shapes.move(scrolltext,50+700*(SZ-12)/12/2,SCRLTop-ud*PM)
    Program.Delay(2)
  endwhile
  GraphicsWindow.Title=" Scroll End !!!  "
EndSub

Sub Lettersize
  If eff=">" Then 
    SZ=SZ+1
    If SZ>48 Then 
      SZ=48
    EndIf
  ElseIf eff="<" then 
    SZ=SZ-1
    If SZ<10 Then 
      SZ=10
    EndIf
  EndIf
  Shapes.Zoom(scrolltext,SZ/12,SZ/12)
  Shapes.move(scrolltext,50+700*(SZ-12)/12/2,SCRLTop-ud*PM)
EndSub

sub ScrollUpDown
  If eff="↑" Then 
    SCUpDown="Up"
    PM=1 
  ElseIf eff="↓" then 
    SCUpDown="Down"
    PM=-1
  EndIf 
  Scroll="true"
EndSub

Sub SpeedUpDown
  If eff="+" Then  
    SPUpDown="Up"
    UD=UD+0.1
  ElseIf eff="-" then 
    SPUpDown="Down"
    UD=UD-0.1
    If UD<0.1    then 
      UD=0.1
    EndIf
  EndIf
  GraphicsWindow.Title=" Speed = "+UD
EndSub

sub ScrollPause
  Scroll="false"
  SCRLTop=Shapes.GetTop(scrolltext)
EndSub

'------------------------------------------------------------
sub GUI
  'Restart :
  GraphicsWindow.Clear()
  '------------------------------------------------------------
  GraphicsWindow.Width = 1280 
  GraphicsWindow.Height = 600
  GraphicsWindow.Left =(Desktop.Width / 2) - (GraphicsWindow.Width / 2)
  GraphicsWindow.Top =(Desktop.Height / 2) - (GraphicsWindow.Height / 2)
  GraphicsWindow.CanResize = "False"
  GraphicsWindow.BackgroundColor = "black"
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.FontSize = 30
  GraphicsWindow.BrushColor = "WHITE"
  GraphicsWindow.DrawText(10,10,"ENTREZ LE TEXTE")
  'Controls.ButtonClicked=onclicked
  GraphicsWindow.BrushColor = "black"
  GraphicsWindow.FontName = "arial"
  GraphicsWindow.FontSize = 12
  texte1=Controls.AddMultiLineTextBox(10,50)     '   <-----   Multilenetextbox  is suitable
  Controls.SetSize(texte1,700,500)
  '----------------------------  this is only test  -------------------------
  data()
  Controls.SetTextBoxText(texte1,initdata)
  '--------------------------------------------------------------------------
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.FontSize = 28
  GraphicsWindow.BrushColor = "blue"
  GraphicsWindow.DrawText(800,50,"ENTREZ LA VITESSE DE DEFILEMENT")
  GraphicsWindow.DrawText(940,80,"DE 1 <---> 10")
  V1=Controls.AddTextBox(1000,120)
  Controls.SetSize(V1,50,40)
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.BrushColor = "green"
  GraphicsWindow.FontSize = 30
  DEM=Controls.AddButton("DEMARRAGE DU SCROLL",860,300)
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "red"
  Bdel=Controls.AddButton("DELETE",300,560)
EndSub


Sub fenetrescroll
  ' GraphicsWindow.Clear()
  '--------------------------------------------------------------------------
  GraphicsWindow.BrushColor = "white"
  GraphicsWindow.FontName = "arial"
  GraphicsWindow.FontSize = 12
  InitY=600                            '  initial  Y-Position 
  CRLF= Text.GetCharacter(13)+Text.GetCharacter(10)
  GetText= Controls.GetTextBoxText(texte1)+CRLF
  scrolltext=Shapes.addText(Gettext)   '  set each line to shapes 
  Shapes.Move(scrolltext,50,InitY)     '  move to  initial position 
  SCRLTop=Shapes.GetTop(scrolltext)
  SZ=30                                '   initial  zoom up size
  Lettersize()                         '   zoom up  size 12 →  30
  '------------------------------------------------------------------------------ 
  PM=1
  Scroll="true"                      '  scroll enabled   
  SCUpDown="Up"                      '  scroll direction
  UD=Controls.GetTextBoxText(V1)*0.1   '  Up down speed
  If UD=0 Then 
    UD=0.2
  EndIf
  '----------------------------------  erase  first window  ----------------------------------------
  GraphicsWindow.brushColor = "black"
  GraphicsWindow.FillRectangle(0,0,1280,600)
  Controls.Remove(texte1)
  Controls.Remove(v1)
  Controls.Remove(DEM)
  Controls.Remove(Bdel)
  '---------------------------------  make next window  -----------------------------------------
  GraphicsWindow.BrushColor = "white"
  GraphicsWindow.FontName = "arial"
  GraphicsWindow.FontSize = 40
  GraphicsWindow.DrawText(10,10,"" + texte1)
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.FontSize = 40
  GraphicsWindow.BrushColor = "black"
  slideup=Controls.AddButton("↑",1200,100)
  pause=Controls.AddButton("ǁ",1200,180)
  slidedown=Controls.AddButton("↓",1200,260)
  GraphicsWindow.BrushColor = "green"
  enlarg=Controls.AddButton(">",1200,340)
  ensmall=Controls.AddButton("<",1160,340)
  vup=Controls.AddButton("+",1200,20)
  vdown=Controls.AddButton("-",1160,20)
  GraphicsWindow.FontName = "DS-Digital"
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "BLUE"
  retour=Controls.AddButton("RETOUR TEXTE",1120,500)
endsub

Sub data    '  this is only  test data 
  CRLF= Text.GetCharacter(13)+Text.GetCharacter(10)
  initdata="Hello, I would try to make some sort of teleprompter. "+CRLF
  initdata=initdata+"There is a first page with room for copy text and lay out as you want. The ability to completely erase the text."+CRLF 
  initdata=initdata+"The ability to return a base speed of between 1 and 10. "+CRLF
  initdata=initdata+"From there, you can launch the second page which contains text that will scroll. "+CRLF
  initdata=initdata+"On the second page, you can see the text bigger (pause), with a menu on the right. "+CRLF
  initdata=initdata+"In the menu, there is a play button, pause, stop, + and - buttons to adjust the speed of scrolling."+CRLF
  initdata=initdata+"The direction arrows to the direction of scrolling text. "+CRLF
  initdata=initdata+"And a button to return to the first page of text (which must remain in memory) for adjusting the layout, in order to improve readability. "+CRLF
EndSub

