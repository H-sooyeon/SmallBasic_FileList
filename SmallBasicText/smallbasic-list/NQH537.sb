gw = 600

gh = 600

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.BackgroundColor = "LightBlue"

GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor

GraphicsWindow.PenColor = GraphicsWindow.BackgroundColor

GraphicsWindow.FillRectangle(0,0,gw,gh) 'The Background colour doesn't seem to register with the GetPixel so we draw filled rectangle

GraphicsWindow.PenColor = "Black" '#000000

GraphicsWindow.BrushColor = "Black"

For i = 1 To 10

  GraphicsWindow.FillEllipse(Math.GetRandomNumber(gw)-25,Math.GetRandomNumber(gh)-25,50,50)

EndFor



image = ImageList.LoadImage("http://www.valeofberkeley.co.uk/media/Forward%20Arrow.png")

imageW = ImageList.GetWidthOfImage(image)

imageH = ImageList.GetHeightOfImage(image)

arrow = Shapes.AddImage(image)

GraphicsWindow.PenColor = "Red"

GraphicsWindow.BrushColor = "#00000000" 'Transparent

hotSpot = Shapes.AddEllipse(10,10)

angle = 0

pi = Math.Pi



While ("True")

  'Move and rotate arrow

  If (Mouse.IsLeftButtonDown) Then

    angle = angle-1

  EndIf

  If (Mouse.IsRightButtonDown) Then

    angle = angle+1

  EndIf

  xM = GraphicsWindow.MouseX

  yM = GraphicsWindow.MouseY

  Shapes.Move(arrow,xM-imageW/2,yM-imageH/2)

  Shapes.Rotate(arrow,angle)

  'Move and rotate hot spot

  xH = xM+118*Math.Cos(angle*pi/180)+3*Math.Sin(angle*pi/180)

  yH = yM+118*Math.Sin(angle*pi/180)-3*Math.Cos(angle*pi/180)

  Shapes.Move(hotSpot,xH-5,yH-5)

  'Detect hot spot over black ellipse

  If (xH > 0 And xH < gw And yH > 0 And yH < gh) Then 'Crash if outside the GW

    color = GraphicsWindow.GetPixel(xH,yH)

    'TextWindow.WriteLine(color)

    If (color = "#000000") Then 'Black

      Sound.PlayClickAndWait()

    EndIf

  EndIf

  Program.Delay(20)

EndWhile


