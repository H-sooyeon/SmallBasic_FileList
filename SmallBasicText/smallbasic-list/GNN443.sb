GraphicsWindow.BackgroundColor="tan

GraphicsWindow.Title = "Draw Circle Game 

GraphicsWindow.FontName="Lucida console

GraphicsWindow.FontSize=16

GraphicsWindow.FontBold="false

args=0

Init()

While "True"

  DrawCircle()

  If n>5 Then

    Result()

  EndIf

  Clear()

EndWhile



Sub Clear

  GraphicsWindow.BrushColor = "tan

  GraphicsWindow.FillRectangle(0, 0, gw, gh)

  n = 0

  x = ""

  y = ""

EndSub



Sub DrawCircle

  buttonClicked = "False"

  GraphicsWindow.BrushColor = "Black"

  While Not[buttonClicked]

    If mouseMove And Mouse.IsLeftButtonDown Then

      If GraphicsWindow.MouseY < gh - 10 Then

        n = n + 1

        x[n] = GraphicsWindow.MouseX

        y[n] = GraphicsWindow.MouseY

        GraphicsWindow.FillEllipse(x[n] - 2, y[n] - 2, 4, 4)

      EndIf

      mouseMove = "False"

    EndIf

  EndWhile

EndSub



Sub Init

  Not = "False=True;True=False

  CR = Text.GetCharacter(13)

  LF = Text.GetCharacter(10)

  dx = "1=1;2=0;3=-1;4=0

  dy = "1=0;2=1;3=0;4=-1

  gw = 598

  gh = 428

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

  GraphicsWindow.BrushColor = "Black"

  bbb=Controls.AddButton("Check", gw - 60, gh - 30)

  Controls.ButtonClicked = OnButtonClicked

  GraphicsWindow.MouseMove = OnMouseMove

EndSub



Sub OnButtonClicked

  buttonClicked = "True"

EndSub



Sub OnMouseMove

  mouseMove = "True"

EndSub



Sub Result

  txt = "nodes: " + n + CR + LF

  sx = 0

  sy = 0

  For i = 1 To n

    sx = sx + x[i]

    sy = sy + y[i]

  EndFor

  ax = Math.Floor(sx / n)

  ay = Math.Floor(sy / n)

  txt = txt + "Σx: " + ldcall.Function2("fmt" sx 6) + " | avr(x): " + ax + CR + LF

  txt = txt + "Σy: " + ldcall.Function2("fmt" sy 6) + " | avr(y): " + ay + CR + LF

  

  GraphicsWindow.BrushColor = "Red"

  GraphicsWindow.FillEllipse(ax - 2, ay - 2, 4, 4)

  sr = 0

  For i = 1 To n

    rx = x[i] - ax

    ry = y[i] - ay

    r[i] = Math.SquareRoot(rx * rx + ry * ry)

    sr = sr + r[i]

  EndFor

  sr = Math.Floor(sr)

  ar = Math.Floor(sr / n)

  txt = txt + "Σr: " + ldcall.Function2("fmt" sr 6) + " | avr(r): " + ar + CR + LF

  GraphicsWindow.PenColor = "Red"

  GraphicsWindow.DrawEllipse(ax - ar, ay - ar, 2 * ar, 2 * ar)

  se = 0

  For i = 1 To n

    e = Math.Abs(r[i] - ar)

    se = se + e

  EndFor

  se = Math.Floor(se)

  txt = txt + "Σe: " + se + CR + LF

  txt = txt + "score [n*avr(r)/Σe] = " + Math.Floor(n * ar / se)

  GraphicsWindow.BrushColor="darkblue

  GraphicsWindow.DrawBoundText(10, gh - 110 gw - 60 txt)

  Controls.SetButtonCaption(bbb "New")

  Wait()

EndSub



sub fmt

  aa=args[1]

  ll=args[2]

  return=Text.GetSubTextToEnd(Math.Power(10 ll)+aa 2)

EndSub





Sub Wait

  buttonClicked = "False"

  While Not[buttonClicked]

    Program.Delay(20)

  EndWhile

  Controls.SetButtonCaption(bbb "Check")

EndSub




