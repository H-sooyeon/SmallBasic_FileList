'     made by      EpicProgrammer1000     RHW257

txtmsg()      '   changed to  subroutine
gui()         '   changed to  subroutine
entry()       '   changed to  subroutine
startgame()
NewPlane()
NN=1
Run="true"

Timer.Tick = cloudanimation
Timer.Interval = 10

While Run="true"                      '   changed to  while loop
  MX= GraphicsWindow.MouseX           '     
  MY= GraphicsWindow.mouseY           ' 
  rdt=Shapes.GetTop(reddot)           '  reddot Y position   
  apt=Shapes.GetTop (airplane)        '  airplane  Y  position 
  If  0<MX and MX<80 and  290<MY And MY<290+80 Then ' joystick    X=10  Y=300  W=80 
    dx=MX-(10+80/2)                  '  distance from red dot   -X   
    dy=(290+80/2)-MY                 '  distance from red dot   -Y     
    sdx=sdx+dx                       '  sum of dx   
    sdy=sdy+dy                       '  sum of dy   
    APX=sdx*0.1                      '  airplane  moving -X direction    
    APY=APY+sdy*0.01                '   airplane  moving -Y direction       
    Shapes.Move (airplane,APX, APY)    '     
    Shapes.Rotate (airplane,dy-2)      '     
  EndIf
  GraphicsWindow.Title = "Chase's Flight Simulator   "+ APX+"  :  "+(430-APY)+ " : "+sdy   '  <------------------   
  If 0<APX And Math.abs(430-APY)<20 And sdy<60 Then   '   '  <------------------   landing 
    run="False"
  EndIf
  Program.Delay (50)
endwhile

sub  NewPlane
  'R = Math.GetRandomNumber(100)
  ' Shapes.Move (airplane2,800,R)
  'Import the airplane picture from the Web
  airplane = Shapes.AddImage (pgd+"img_3.png")' "http://www.veryicon.com/icon/png/Transport/Brilliant%20Transportation/airplane.png")
  Shapes.Zoom (airplane,.5,.5)
  'resize the airplane
  'add 1 more cloud
  cloud3 = Shapes.AddImage (pgd+"img_13.png")' "http://i967.photobucket.com/albums/ae160/mykyrIacos_bucket/TUBES%20NUAGES/Jillcreation-overlay-cloud.png")
  Shapes.Zoom (cloud3,.5,.5)
  'start of Loop of Moving airplane and also other stuff
endsub

'animation of Clouds,Trees, and Mountains,etc.
Sub cloudanimation 
  If run="False" Then    '  to stop  background running
    NN=NN+.1
    Timer.Interval = 10*NN   '  background running  slow down 
  EndIf
  Shapes.Move (cloud1,Shapes.GetLeft(cloud1) - 5 - N,0)
  Shapes.Move (cloud2,Shapes.GetLeft(cloud2) - 6 - N,10)
  Shapes.Move (tree1,Shapes.GetLeft(tree1) - 4 - N,250)
  Shapes.Move (tree2,Shapes.GetLeft(tree2) - 7 - N,170)
  Shapes.Move (mountains,Shapes.GetLeft(mountains) - .1,-200)
  Shapes.Move (cloud3,Shapes.GetLeft(cloud3) - 7 - N,-200)
  Shapes.Move (house,Shapes.GetLeft(house) - 8 - N,450)
  Shapes.Move (grasstexture,Shapes.GetLeft(grasstexture) - 5 - N+dx/100,-100) 
  Shapes.Move (landline,Shapes.GetLeft(landline) - 8 - N,550)
  If Shapes.GetTop(airplane) = 200 Then
    N = 0
    Shapes.Rotate (airplane,0)
  EndIf
  If Shapes.GetTop (airplane) < 200 Then
    N = 2
  EndIf
  If Shapes.GetLeft(cloud1) < -300 Then
    Shapes.Move (cloud1,1200,0)
  EndIf
  If Shapes.GetLeft(cloud2) < -300 Then
    Shapes.Move (cloud2,1200,10)
  EndIf
  If Shapes.GetLeft(tree1) < -200 Then
    Shapes.Move (tree1,1200,200)
  EndIf
  If Shapes.GetLeft(cloud3) < -800 Then
    Shapes.Move (cloud3,1200,-200)
  EndIf
  If Shapes.GetLeft(tree2) < -300 Then
    Shapes.Move (tree2,1200,170)
  EndIf
  If Shapes.GetLeft(mountains) < -2000 Then
    GraphicsWindow.BrushColor = "White"
    Shapes.Move (mountains,0,-200)
  EndIf
  If Shapes.GetLeft(house) < -400 Then
    Shapes.Move (house,1200,200)
  EndIf
  If Shapes.GetLeft(grasstexture) < -80 Then
    Shapes.Move(grasstexture,0,-100)
  EndIf
  If Shapes.GetLeft(landline) < -1000 Then    '  <------------------  added  landing line 
    Shapes.Move (landline,1200,550)
  EndIf
  'Move the Circle Joystick to the Mouse Coordinates
  If MX<100   And  300<MY And  MY<370 Then    '  MX <----- graphicswindow.mouseX
    Shapes.Move (jS,MX,MY)
  EndIf
  If run="False" and NN>5 And  math.Abs(Shapes.Getleft(airplane)-Shapes.GetLeft(landline)-700/2)<700/2 Then 
    Timer.Pause()                    '   stop   background running
    Shapes.SetText(smsg,"Congratulation!  you could landing successfully."  )  ' message
    Shapes.Move(smsg,0,-100)
    Shapes.Animate(smsg,500,450,3000)
    Sound.PlayChimeAndWait()
  EndIf
EndSub

Sub gui
  'Show the Window For Sim
  pgd= Program.Directory+"\"              '  <----------------------------   image folder
  GraphicsWindow.Show()
  Mouse.HideCursor()
  GraphicsWindow.Title = "Chase's Flight Simulator"
  GraphicsWindow.Width = 1200
  GraphicsWindow.Height = 700
  GraphicsWindow.top=50
  GraphicsWindow.BackgroundColor = "Blue"
  planebackground = Shapes.AddImage (pgd+"img_1.jpg" )'"http://us.123rf.com/400wm/400/400/rtimages/rtimages0802/rtimages080200031/2575002-old-single-propeller-fighter-plane-flying-above-the-clouds.jpg")
  Shapes.Zoom(planebackground,.5,.5)  '  Plane without plopeller
  Shapes.Move (planebackground,-200,-400)
  Circle = Shapes.AddImage(pgd+"img_2.png" ) '"http://www.newmarketpartners.co.uk/images/propeller.png")  ' propeller
  Shapes.Move(Circle,250,120)
  Shapes.Zoom (Circle,1.4,1.4)
  GraphicsWindow.BrushColor = "Red"
  GraphicsWindow.FontSize=20                  '    <--------------------------  added  
  start = Shapes.AddText("Load Simulator")
  Shapes.Move (start,170,300)
  exit = Shapes.AddText ("Exit Program")
  Shapes.Move (exit,600,300)
  GraphicsWindow.BrushColor = "Red"
  GraphicsWindow.PenWidth = 0
  cursor = Shapes.AddRectangle(10,10)
EndSub

sub entry 
  Sound.Play (pgd+"radial.wav") '  "http://avanimation.avsupport.com/sound/Radial.wav")   
  gamestart="False"            '   
  While gamestart="False"      '   changed to  while loop
    MX= GraphicsWindow.MouseX  '
    MY= GraphicsWindow.mouseY  '
    Shapes.Move(cursor,MX,MY)
    If (160 < MX and MX < 280) and (290<MY  And MY < 350)  and mouse.IsLeftButtonDown then
      Shapes.Remove(Circle)
      Shapes.Remove(planebackground)
      Shapes.Remove(Start)
      Shapes.Remove(exit)
      Shapes.Remove(cursor)
      gamestart="True"         '  
    endif
    If (500< MX and MX < 700) and  (290<MY and  MY< 320) and Mouse.IsLeftButtonDown then
      Program.End()
    endif
    o = o + 5                  '  <-----------------       1  --->  5  
    Shapes.Rotate(Circle,o)
    Program.Delay(1.5)
  EndWhile
endsub

sub startgame
  GraphicsWindow.BackgroundColor = "Blue"
  GraphicsWindow.BrushColor = "Green"
  'add clouds and trees  from the internet and put them at the top of the screen
  sky = Shapes.AddImage (pgd+"img_4.jpg")'   "http://www.truehdwallpaper.com/wp-content/uploads/2011/02/Light-Blue-Sky.jpeg")
  grasstexture = Shapes.AddImage (pgd+"img_5.jpg")'  "http://free-textures.got3d.com/natural/free-grass-textures/images/free-grass-texture-017.jpg")
  mountains = Shapes.AddImage (pgd+"img_6.png")'  "http://www.kevinwalton.co.uk/images/mountain.png")
  cloud1 = Shapes.AddImage (pgd+"img_7.png")'  "http://scri8e.com/stars/PNG_Clouds/CL04.png")
  cloud2 = Shapes.AddImage (pgd+"img_8.png")' "http://3.bp.blogspot.com/_LApxKDsDWI0/SsLnCxtP67I/AAAAAAAAB64/qi8Dn-Hx11c/s320/CL05.png")
  tree1 = Shapes.AddImage (pgd+"img_9.png")' "http://i33.photobucket.com/albums/d63/Ven0mSeven/tree.png")
  tree2 = Shapes.AddImage (pgd+"img_10.png")' "http://i562.photobucket.com/albums/ss61/andi_misty/tree.png")
  ' airplane2 = Shapes.AddImage (pgd+"img_11.png")' "http://multiplus3d.weebly.com/uploads/3/7/7/3/3773098/airplane.png")
  house = Shapes.AddImage (pgd+"img_12.png")' "http://www.saidwin.com/Images/parallaxTree.png")
  
  Shapes.Zoom (house,.2,.2)'Zoom Tree 2
  Shapes.Zoom (sky,4,5)
  Shapes.Zoom (tree2,.1,.1)
  ' Shapes.Zoom (airplane2,.4,.4)
  Shapes.Zoom (grasstexture,2,.5)
  GraphicsWindow.BrushColor = "White"
  Joysticksquare = Shapes.AddRectangle(80,80)
  Landline = Shapes.AddRectangle(700,20)         ' <-----------------------   landing line 
  GraphicsWindow.BrushColor = "Green" 'add joystick
  jS = Shapes.AddRectangle(20,20)     'add  joystick overlay
  Shapes.SetOpacity(js,50)
  GraphicsWindow.PenColor = "Red"
  reddot = Shapes.AddRectangle (5,5) 'Add red dot
  'Movement
  'move the land rectangle to the bottom of the screen
  Shapes.Move (house,0,300)
  Shapes.Move (jS,50,340)
  Shapes.Move (reddot,50,340)
  Shapes.Move (Joysticksquare,10,300)
  Shapes.Move (grasstexture,0,-100)
  Shapes.Move (Landline,400,550)               ' <-----------------------  
  'Zoom
  Shapes.Zoom (mountains,1,1)
  Shapes.Zoom (tree1,.2,.2)
  GraphicsWindow.brushColor = "Red"
  smsg= Shapes.AddText(" ")                    ' <-----------------------  
EndSub

Sub txtmsg      
  'Land It Game Chase Morell 20012 
  TextWindow.ForegroundColor = "Green"
  TextWindow.WriteLine ("Welcome to Chase's Flight Simulator")
  TextWindow.WriteLine ("-")
  TextWindow.WriteLine ("How To Play")
  TextWindow.WriteLine ("Use The Mouse To adjust the altitude of the plane.")
  TextWindow.WriteLine ("The Controls are relative to the red dot at the center of the joystick")
  TextWindow.WriteLine ("Mouse over the Dot; plane goes down and vice versa.")
  TextWindow.WriteLine ("It is possible to land the plane")
  TextWindow.WriteLine ("Chase Morell 2012")
EndSub
