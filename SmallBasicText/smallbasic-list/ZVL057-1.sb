' Football 0.3b

' Copyright (c) 2013 Nonki Takahashi.  All rights reserved.

'

' History:

'  0.3b 2013/03/05 Enabled to play and added semaphore. (ZVL057-1)

'  0.2a 2013/03/05 Enabled score and paddle. (ZVL057-0)

'  0.1a 2013/03/05 Graphics created. (ZVL057)

'

GraphicsWindow.Title = "Football 0.3b"

GraphicsWindow.BackgroundColor = "#003300"

Num_Init()

VRAM_Init()

CenterLine_Init()

Score_Init()

Sound_Init()  ' preload sounds

player = 1

Ball_Init()

Timer.Interval = 10

Timer.Tick = OnTick

GraphicsWindow.KeyDown = OnKeyDown

While "True"

  If beep Then

    Sound.PlayAndWait(urlBeep)

    beep = "False"

  EndIF

  If ping Then

    Sound.PlayAndWait(urlPing)

    ping = "False"

  EndIF

  Program.Delay(10)

EndWhile

Sub OnTick

  If semaphore Then

    Goto ot_exit

  Else

    semaphore = "True"

  EndIf

  Ball_Update()

  semaphore = "False"

  ot_exit:

EndSub

Sub Sound_Init

  urlPing = "http://www.nonkit.com/smallbasic.files/se_sad05.wav"

  Sound.PlayAndWait(urlPing)  ' test for preload

  urlBeep = "http://www.nonkit.com/smallbasic.files/se_sad08.wav"

  Sound.PlayAndWait(urlBeep)  ' test for preload

EndSub

Sub Paddle_Down

  ' param player - 1 or 2

  If paddleRow[player] < lowRow Then

    iUp = (paddleRow[player] - 1) * 40 + paddleCol[player][1]

    iLow = (paddleRow[player] + 4) * 40 + paddleCol[player][1]

    Shapes.SetOpacity(vram[iUp]["obj"], 0)

    vram[iUp]["on"] = "False"

    Shapes.SetOpacity(vram[iLow]["obj"], 100)

    vram[iLow]["on"] = "True"

    iUp = (paddleRow[player] - 1) * 40 + paddleCol[player][2]

    iLow = (paddleRow[player] + 4) * 40 + paddleCol[player][2]

    Shapes.SetOpacity(vram[iUp]["obj"], 0)

    vram[iUp]["on"] = "False"

    Shapes.SetOpacity(vram[iLow]["obj"], 100)

    vram[iLow]["on"] = "True"

    paddleRow[player] = paddleRow[player] + 1

  EndIf

EndSub

Sub Paddle_Up

  ' param player - 1 or 2

  If upRow < paddleRow[player] Then

    iUp = (paddleRow[player] - 2) * 40 + paddleCol[player][1]

    iLow = (paddleRow[player] + 3) * 40 + paddleCol[player][1]

    Shapes.SetOpacity(vram[iLow]["obj"], 0)

    vram[iLow]["on"] = "False"

    Shapes.SetOpacity(vram[iUp]["obj"], 100)

    vram[iUp]["on"] = "True"

    iUp = (paddleRow[player] - 2) * 40 + paddleCol[player][2]

    iLow = (paddleRow[player] + 3) * 40 + paddleCol[player][2]

    Shapes.SetOpacity(vram[iLow]["obj"], 0)

    vram[iLow]["on"] = "False"

    Shapes.SetOpacity(vram[iUp]["obj"], 100)

    vram[iUp]["on"] = "True"

    paddleRow[player] = paddleRow[player] - 1

  EndIf

EndSub

Sub OnKeyDown

  If semaphore Then

    Goto okd_exit

  Else

    semaphore = "True"

  EndIf

  key = GraphicsWindow.LastKey

  If key = "S" Then     ' player 1 paddle up

    player = 1

    Paddle_Up()

  ElseIf key = "D" Then ' player 1 paddle down

    player = 1

    Paddle_Down()

  ElseIf key = "K" Then ' player 2 paddle up

    player = 2

    Paddle_Up()

  ElseIf key = "L" Then ' player 2 paddle down

    player = 2

    Paddle_Down()

  EndIf

  semaphore = "False"

  okd_exit:

EndSub

Sub Ball_Init

  ' param player - 1 or 2

  colBall = centerCol

  rowBall = centerRow

  If player = 1 Then

    vCol = 0.1

    vRow = Math.Round(Math.GetRandomNumber(10) - 5) / 50

  ElseIf player = 2 Then

    vCol = -0.1

    vRow = Math.Round(Math.GetRandomNumber(10) - 5) / 50

  EndIf

EndSub  

Sub Ball_Update

  ' param colBall, rowBall - ball position (real number)

  ' param iBall - ball position in VRAM

  ' param vCol, vRow - velocity of ball [pixel/tick]

  iBallLast = iBall

  colBall = colBall + vCol

  rowBall = rowBall + vRow

  iBall = (Math.Round(rowBall) - 1) * 40 + Math.Round(colBall)

  If iBall <> iBallLast Then

    If iBallLast > 0 Then

      obj = vram[iBallLast]["obj"]

      Shapes.SetOpacity(obj, 0)

    EndIf

    obj = vram[iBall]["obj"]

    Shapes.SetOpacity(obj, 100)

    col = Math.Round(colBall)

    row = Math.Round(rowBall)

    If 0 < vRow Then

      If row = bottomRow - 1 Then

        vRow = -vRow

        ping = "True"

      EndIf

    ElseIf vRow < 0 Then

      If row = topRow + 1 Then

        vRow = -vRow

        ping = "True"

      EndIf

    EndIf

    If col < goalCol[1] Then

      player = 2

      s = score[player]["score"] + 1

      Score_Set()

      player = 1

      Ball_Init()

      beep = "True"

      Program.Delay(2000)

    ElseIf goalCol[2] < col Then

      player = 1

      s = score[player]["score"] + 1

      Score_Set()

      player = 2

      Ball_Init()

      beep = "True"

      Program.Delay(2000)

    ElseIf 0 < vCol Then

      If col = paddleCol[2][1] - 1 And paddleRow[2] <= row And row <= paddleRow[2] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = paddleCol[1][2] - 1 And paddleRow[1] <= row And row <= paddleRow[1] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = paddleCol[2][2] - 1 And paddleRow[2] <= row And row <= paddleRow[2] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = goalCol[2] - 1 And topRow + 1 <= row And row <= topRow + 5 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = goalCol[2] - 1 And bottomRow - 5 <= row And row <= bottomRow - 1 Then

        vCol = -vCol

        ping = "True"

      EndIf

    ElseIf vCol < 0 Then

      If col = paddleCol[1][1] + 1 And paddleRow[1] <= row And row <= paddleRow[1] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = paddleCol[2][1] + 1 And paddleRow[2] <= row And row <= paddleRow[2] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = paddleCol[1][2] + 1 And paddleRow[1] <= row And row <= paddleRow[1] + 4 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = goalCol[1] + 1 And topRow + 1 <= row And row <= topRow + 5 Then

        vCol = -vCol

        ping = "True"

      ElseIf col = goalCol[1] + 1 And bottomRow - 5 <= row And row <= bottomRow - 1 Then

        vCol = -vCol

        ping = "True"

      EndIf

    EndIf

  EndIf

EndSub

Sub Num_Init

  num[0][1] = "***"

  num[0][2] = "* *"

  num[0][3] = "* *"

  num[0][4] = "* *"

  num[0][5] = "***"

  num[1][1] = "  *"

  num[1][2] = "  *"

  num[1][3] = "  *"

  num[1][4] = "  *"

  num[1][5] = "  *"

  num[2][1] = "***"

  num[2][2] = "  *"

  num[2][3] = "***"

  num[2][4] = "*  "

  num[2][5] = "***"

  num[3][1] = "***"

  num[3][2] = "  *"

  num[3][3] = "***"

  num[3][4] = "  *"

  num[3][5] = "***"

  num[4][1] = "* *"

  num[4][2] = "* *"

  num[4][3] = "***"

  num[4][4] = "  *"

  num[4][5] = "  *"

  num[5][1] = "***"

  num[5][2] = "*  "

  num[5][3] = "***"

  num[5][4] = "  *"

  num[5][5] = "***"

  num[6][1] = "***"

  num[6][2] = "*  "

  num[6][3] = "***"

  num[6][4] = "* *"

  num[6][5] = "***"

  num[7][1] = "***"

  num[7][2] = "  *"

  num[7][3] = "  *"

  num[7][4] = "  *"

  num[7][5] = "  *"

  num[8][1] = "***"

  num[8][2] = "* *"

  num[8][3] = "***"

  num[8][4] = "* *"

  num[8][5] = "***"

  num[9][1] = "***"

  num[9][2] = "* *"

  num[9][3] = "***"

  num[9][4] = "  *"

  num[9][5] = "***"

  num[" "][1] = "   "

  num[" "][2] = "   "

  num[" "][3] = "   "

  num[" "][4] = "   "

  num[" "][5] = "   "

EndSub

Sub Score_Init

  For player = 1 To 2

    score[player]["score"] = 0

    s = score[player]["score"]

    len = Text.GetLength(s)

    If len = 1 Then

      s = " " + s

    EndIf

    For o = 2 To 1 Step -1

      d = Text.GetSubText(s, 3 - o, 1)

      For i = 1 To 15

        j = (o - 1) * 15 + i

        col = Math.Remainder(i - 1, 3) + 1

        row = Math.Floor((i - 1) / 3) + 1

        x = x0 + (sizeX + 1) * (10 + 12 * (player - 1) + 4 * (2 - o) + col - 1)

        y = y0 + (sizeY + 2) * (2 + row - 1)

        obj = Shapes.AddRectangle(sizeX, sizeY)

        score[player]["obj" + j] = obj

        Shapes.Move(obj, x, y)

        If Text.GetSubText(num[d][row], col, 1) = " " Then

          Shapes.SetOpacity(obj, 0)

        EndIf

      EndFor

    EndFor

  EndFor

EndSub

Sub CenterLine_Init

  col = centerCol

  For row = 3 To 21 Step 2

    y = y0 + (row - 1) * (sizeY + 2)

    i = (row - 1) * 40 + col

    x = x0 + (col - 1) * (sizeX + 1)

    obj = Shapes.AddRectangle(sizeX, sizeY)

    Shapes.Move(obj, x, y)

  EndFor

EndSub

Sub Score_Set

  ' param player - 1 or 2

  ' param s - score

  score[player]["score"] = s

  len = Text.GetLength(s)

  If len = 1 Then

    s = " " + s

  EndIf

  For o = 2 To 1 Step -1

    d = Text.GetSubText(s, 3 - o, 1)

    For i = 1 To 15

      j = (o - 1) * 15 + i

      col = Math.Remainder(i - 1, 3) + 1

      row = Math.Floor((i - 1) / 3) + 1

      obj = score[player]["obj" + j]

      If Text.GetSubText(num[d][row], col, 1) = " " Then

        Shapes.SetOpacity(obj, 0)

      Else

        Shapes.SetOpacity(obj, 100)

      EndIf

    EndFor

  EndFor

EndSub

Sub VRAM_Init

  init[0]  = "----+----1----+----2----+----3----+----4"

  init[1]  = " *************************************  "

  init[2]  = " *                                   *  "

  init[3]  = " *                                   *  "

  init[4]  = " *                                   *  "

  init[5]  = " *                                   *  "

  init[6]  = " *                                   *  "

  init[7]  = "                                        "

  init[8]  = "                                        "

  init[9]  = "                                        "

  init[10] = "           *                       *    "

  init[11] = "   *       *               *       *    "

  init[12] = "   *       *               *       *    "

  init[13] = "   *       *               *       *    "

  init[14] = "   *       *               *       *    "

  init[15] = "   *                       *            "

  init[16] = "                                        "

  init[17] = "                                        "

  init[18] = " *                                   *  "

  init[19] = " *                                   *  "

  init[20] = " *                                   *  "

  init[21] = " *                                   *  "

  init[22] = " *                                   *  "

  init[23] = " *************************************  "

  init[24] = "                                        "

  centerCol = 20

  centerRow = 12

  goalCol[1] = 2  ' left end

  goalCol[2] = 38 ' right end

  topRow = 1      ' top end

  bottomRow = 23  ' bottom end

  goalUpRow = 7   ' upper row of goal

  goalLowRow = 17 ' lower row of goal

  upRow = 6       ' upper end for paddle

  lowRow = 14     ' lower end for paddle

  paddleCol[1][1] = 4

  paddleCol[1][2] = 28

  paddleRow[1] = 11

  paddleCol[2][1] = 12

  paddleCol[2][2] = 36

  paddleRow[2] = 10

  sizeX = 12

  sizeY = 15

  x0 = (GraphicsWindow.Width - (sizeX + 1) * 40) / 2

  y0 = (GraphicsWindow.Height - (sizeY + 2) * 24) / 2

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.PenColor = "#335c33"

  GraphicsWindow.BrushColor = "#ccd6cc"

  For row = 1 To 24

    y = y0 + (row - 1) * (sizeY + 2)

    For col = 1 To 40

      i = (row - 1) * 40 + col

      x = x0 + (col - 1) * (sizeX + 1)

      obj = Shapes.AddRectangle(sizeX, sizeY)

      vram[i]["obj"] = obj

      If Text.GetSubText(init[row], col, 1) = " " Then

        vram[i]["on"] = "False"

        Shapes.SetOpacity(obj, 0)

      Else

        vram[i]["on"] = "True"

      EndIf

      Shapes.Move(obj, x, y)

    EndFor

  EndFor

EndSub


