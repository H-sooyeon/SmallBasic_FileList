'-----------------------------------------------------------------------------------------------------------
'--------------------------Armored Assault---------------------------------------------------------------
'--------------------------Developed By ----------------------------------------------------------------
'--------------------------Sean Sullivan-----------------------------------------------------------------
'===========Copyright DOSMANN INC.=================================
'-----------------------Started Jan, 2014----------------------------------------------------------------
'------------------------------------------------------------------------------------------------------------
'------------------------------------------------------------------------------------------------------------


' The following line could be harmful and has been automatically commented.
' ThisUsername = LDEncryption.AESDecrypt(File.ReadLine(Program.Directory + "/Settings.txt",1),"TankWars")
LDNetwork.SetGameData("TankWars" + IOTCPClient.IP,ThisUsername)
DataStuff = LDNetwork.GetGameData("TankWarsBeta")
NumofServers = LDNetwork.GetGameData("TankWarsBetaNum")
GraphicsWindow.DrawResizedImage(Program.Directory + "/Background.jpg",0,0,GraphicsWindow.Width,GraphicsWindow.Height)
GraphicsWindow.BrushColor = "White"
GraphicsWindow.Title = "Armored Assault"
LDUtilities.Icon = Program.Directory + "/Icon.ico"
ServerListings = Shapes.AddImage(Program.Directory + "/ServerListing.png")
Shapes.Move(ServerListings,5,5)
Shapes.SetOpacity(ServerListings,50)
Shapes.Move(Shapes.AddText("Name                        IP                     Port")15,13)
DrawServers()
GraphicsWindow.BrushColor = "Black"
BRefresh = Controls.AddButton("Refresh list",500,400)
GraphicsWindow.BrushColor = "White"
Controls.ButtonClicked = OnButtonClicked
LDUtilities.ExitOnClose = "False"
LDUtilities.GWClosing = OnGWClose
IOTCPClient.OnMessageRecieved = OnMessageRecieved
GraphicsWindow.BrushColor = "Black"

While CanStartGame <> "Yes"
  Program.Delay(50)
EndWhile
Username[ThisPC] = ThisUsername


IOTCPClient.SendMessage("type=init;username=" + Username[ThisPC] + ";client=" + ThisPC)


GraphicsWindow.Width = 781
GraphicsWindow.Height = 600
GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.MouseDown = TankSelection
LDUtilities.ShowErrors = "False"
LDShapes.AnimationInterval = 40

Img = ImageList.LoadImage(Program.Directory + "/Sand.png")
For i10 = 1 to 2000 Step 1000
  For i11 = 1 To 2000 Step 500
    '  Shapes.Move(Shapes.AddImage(Img),i10,i11)
    GraphicsWindow.DrawImage(Img,i10,i11)
  EndFor
EndFor

GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp

'GraphicsWindow.KeyDown = OnKeyDown

SetupLevel()
ClientKey = ""
NumOfPlayers = ThisPC

Mouse.HideCursor()
MouseCursorDir = Program.Directory + "\crosshair.png"
MouseCursor =  Shapes.AddImage(MouseCursorDir)




Progdir = Program.Directory + "\"

TankWeapon = LDNetwork.GetGameData("TankWarsWeapons") '<-- this get a array of the availible weapons. eg.

'TankWeapon[2]["Name"] = "Level 1 Laser"
'TankWeapon[2]["Animation"] = "Animations/Anim.png" '<--- hit wall animation
'TankWeapon[2]["AnimCountx"] = 4
'TankWeapon[2]["AnimCounty"] = 4
'TankWeapon[2]["Projectile"] = "weapons/Lazer.png"
'TankWeapon[2]["ProjectileAnix"] = 1
'TankWeapon[2]["ProjectileAniy"] = 1
'TankWeapon[2]["Damage"] = 10
'TankWeapon[2]["Velocity"] = 50
'TankWeapon[2]["ROF"] = 100
'TankWeapon[2]["Homeing"] = "False"
'TankWeapon[2]["Icon"] =  "weapons/Lazer.png"






SpawnTime = 5
MaxSpeed = 15
AccelerationInc = 0.020
DecelerationInc = 0.020
TurningSpeed = 3
BallCount = 10
Delta = 1
power = 100
Gravity = 0 '<--- Change this to 1500 to use it well
PlayerNum = 1
wkey = 0
dkey = 0
skey = 0
akey = 0
Firelast = Clock.ElapsedMilliseconds

SecectorCircle = Shapes.AddImage(Progdir + "Select.png")
Shapes.Move(SecectorCircle,5000,5000)

For q = 1 To NumOfPlayers
  Tankx[q] = TankInitPos[q]["x"]
  Tanky[q] = TankInitPos[q]["y"]
  TankExp[q] = LDShapes.AddAnimatedImage(Program.Directory + "/Animations/anim1.png","False",8,6)
  LDShapes.AnimationSet(TankExp[q],48)
  UsernameText[q] = Shapes.AddText(Username[q])
  
  Var = LDNetwork.GetGameData("TankWars" + Username[q]) '<--- this gets the current weapon of the user
  CurrentProj[q] = Var["weapon"]
  
  
  Tank[q] = Shapes.AddImage(Program.Directory + "/Tank.png")
  Turret[q] = Shapes.AddImage(Program.Directory + "/" + TankWeapon[CurrentProj[q]]["Turret"])
  
Endfor



For w = 1 to BallCount
  For dd = 1 to NumOfPlayers
    
    
    Exp[dd][w] = LDShapes.AddAnimatedImage(Progdir + TankWeapon[CurrentProj[dd]]["Animation"],"False",TankWeapon[CurrentProj[dd]]["AnimCountx"],TankWeapon[CurrentProj[dd]]["AnimCounty"])
    
    LDShapes.AnimationSet(Exp[dd][w],16)
  EndFor
EndFor

LDScrollBars.Add(1680,1200)
LDScrollBars.Visibility = "False"

'Timer.Tick = MainLoop
'Timer.Interval = 10

While 1 = 1
  Program.Delay(20)
  If Tanky[ThisPC] < 4000 Then
    LDScrollBars.VerticalScroll = Tanky[ThisPC] - 300
    LDScrollBars.HorizontalScroll = Tankx[ThisPC] - 400
  EndIf
  For q = 1 To NumOfPlayers
    Shapes.Move(UsernameText[q],Tankx[q],Tanky[q] - 15)
    If TankDead[q] = "True" Then
      If (Clock.ElapsedMilliseconds - TankDieat) > (SpawnTime *1000) Then
        TankDead[q] = "False"
        Tankx[q] = TankInitPos[q]["x"]
        Tanky[q] = TankInitPos[q]["y"]
        ' TankHealth[q] = TankHealthInit
      EndIf
    EndIf  
    
    
    
    If TankDead[ThisPC] = "True" Then
      If q = ThisPC Then
        Shapes.Move(Tank[q],Tankx[q],Tanky[q])
        Shapes.Move(Turret[q],Tankx[q] + 6,Tanky[q] - 28)
        CheckBulletColision()
        MoveBalls()
        
        Goto Skip
      EndIf
    EndIf
    
    If SelectedTank <> "" Then
      Shapes.Move(SecectorCircle,Tankx[SelectedTank] - 15,Tanky[SelectedTank] - 10)
    Endif
    
    Shapes.Move(Tank[q],Tankx[q],Tanky[q])
    Shapes.Move(Turret[q],Tankx[q] + 6,Tanky[q] - 28)
    Shapes.Rotate(Tank[q],Angle[q] + 90)
    MoveBalls()
    GetTurretAngle()
    RotateTurret()
    SetTime()
    GetAngle()
    GetTankpos()
    CheckColision()
    CheckBulletColision()
    Skip:
  EndFor
  
  If Mouse.IsLeftButtonDown Then
    OnMouseDown()
  EndIf
  
EndWhile


'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++SUBROUTINES+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


Sub SetTime
  Time = Time + Dtime[q]
  If q = ThisPC Then
    If LDUtilities.KeyDown("w") Then
      If Dtime[q] < .002 Then
        Dtime[q] = Dtime[q] + AccelerationInc
      EndIf
    EndIf     
    If LDUtilities.KeyDown("w") <> "True" Then
      If Dtime[q] > 0 Then
        Dtime[q] = Dtime[q] - DecelerationInc
      EndIf
    EndIf
    
    
    
    
    If LDUtilities.KeyDown("s") Then
      If Dtime[q] > -0.001 Then
        Dtime[q] = Dtime[q] - (AccelerationInc / 2)
      EndIf
    EndIf     
    If LDUtilities.KeyDown("s") <> "True" Then
      If Dtime[q] < 0 Then
        Dtime[q] = Dtime[q] + DecelerationInc
      EndIf
    EndIf
    
    
    
  Else   '<------------------------------------------------------^
    If ClientKey[q]["w"] = "Down" Then
      If Dtime[q] < .002 Then
        Dtime[q] = Dtime[q] + AccelerationInc
      EndIf
    EndIf     
    If ClientKey[q]["w"] = "Up" Then
      If Dtime[q] > 0 Then
        Dtime[q] = Dtime[q] - DecelerationInc
      EndIf
    EndIf
    
    
    
    If ClientKey[q]["s"] = "Down" Then
      If Dtime[q] > -0.001 Then
        Dtime[q] = Dtime[q] - (AccelerationInc / 2)
      EndIf
    EndIf     
    If ClientKey[q]["s"] = "Up" Then
      If Dtime[q] < 0 Then
        Dtime[q] = Dtime[q] + DecelerationInc
      EndIf
    EndIf
    
    
    
    
  EndIf
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub OnMouseMove
  Mx = GraphicsWindow.MouseX
  My = GraphicsWindow.Mousey
  Shapes.Move(MouseCursor,Mx - 15,My - 15) '<-- No Idea why it is 15 not 30, but it works
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub SetupLevel
  LevelPath = Program.Directory + "/Levels/" + CurrentMap + ".txt"
  For ly = 1 To 40
' The following line could be harmful and has been automatically commented.
'     Level[1][ly] = File.ReadLine(LevelPath,ly)
  EndFor
  For Levx = 1 To Text.GetLength(Level[1][1])
    For Levy = 1 To 40
      If Text.GetSubText(Level[1][Levy],Levx,1) = "p" Then
        Shapes.Move(Shapes.AddImage(Program.Directory + "/Brick.Png"),Levx * 30 - 30,Levy * 30 - 30)
      EndIf
      If LDUtilities.IsNumber(Text.GetSubText(Level[1][Levy],Levx,1)) Then
        TankInitPos[Text.GetSubText(Level[1][Levy],Levx,1)]["x"] = Levx * 30 - 30
        TankInitPos[Text.GetSubText(Level[1][Levy],Levx,1)]["y"] = Levy * 30 - 30
        
      EndIf
    EndFor
  EndFor
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub GetAngle
  If q = ThisPC Then
    If LDUtilities.KeyDown("a") Then
      Angle[q] = Angle[q] - TurningSpeed
    ElseIf LDUtilities.KeyDown("d") Then
      Angle[q] = Angle[q] + TurningSpeed
    EndIf
    
    
  Else
    
    If ClientKey[q]["a"] = "Down" Then
      Angle[q] = Angle[q] - TurningSpeed
    ElseIf ClientKey[q]["d"] = "Down" Then
      Angle[q] = Angle[q] + TurningSpeed
    EndIf
    
  EndIf
  If Angle[q] >= 360 Then
    Angle[q] = 0
  EndIf
  
  If Angle[q] < 0 Then
    Angle[q] = 359 - Angle[q]
  EndIf
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub CheckColision
  
  RealDtime[q] = Dtime[q]
  RealAngle = Angle[q]
  
  Time = Time + Dtime[q]
  If q = ThisPC Then
    If LDUtilities.KeyDown("w") Then
      If Dtime[q] < .002 Then
        Dtime[q] = Dtime[q] + AccelerationInc
      EndIf
    EndIf     
    If LDUtilities.KeyDown("w") <> "True" Then
      If Dtime[q] > 0 Then
        Dtime[q] = Dtime[q] - DecelerationInc
      EndIf
    EndIf
  Else
    
    If ClientKey[q]["w"] = "Down" Then
      If Dtime[q] < .002 Then
        Dtime[q] = Dtime[q] + AccelerationInc
      EndIf
    EndIf     
    If clientKey[q]["w"] = "Up" Then
      If Dtime[q] > 0 Then
        Dtime[q] = Dtime[q] - DecelerationInc
      EndIf
    EndIf
    
    
  EndIf
  Array2 = Zock77Math.Fire(Tankx[q],Tanky[q],0,Time,MaxSpeed,Angle[q])
  TankxCheck[q] = Array2["x"]
  TankyCheck[q] = Array2["y"]
  TankRoundx = Math.Round((TankxCheck[q] - 15) / 30 + 1)
  TankRoundy = Math.Round((TankyCheck[q] - 15) / 30 + 1)
  TankxCheck2[q] = Array2["x"] + 35
  TankyCheck2[q] = Array2["y"] + 35
  TankRoundx2 = Math.Round((TankxCheck2[q] - 15) / 30 + 1)
  TankRoundy2 = Math.Round((TankyCheck2[q] - 15) / 30 + 1)
  Result = Text.GetSubText(Level[1][TankRoundy],TankRoundx,1)
  Result2 = Text.GetSubText(Level[1][TankRoundy2],TankRoundx2,1)
  If Result = "p" Or Result2 = "p" Then
    Angle[q] = Zock77Math.RoundtoNearest(Angle[q],90)
    Dtime[q] = 0
  Else
    Dtime[q] = RealDtime[q]
  EndIf
  Time = 0
EndSub
'
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub Fire
  
  ShotsFired[FiringTank] = ShotsFired[FiringTank] + Delta
  If Ball[FiringTank][ShotsFired[FiringTank]] = "" Then '<--- If it has not already spawned, then add it
    
    If TankWeapon[CurrentProj[FiringTank]]["ProjectileAnix"] = 1 And TankWeapon[CurrentProj[FiringTank]]["ProjectileAniy"] =1 Then
      Ball[FiringTank][ShotsFired[FiringTank]] = Shapes.AddImage(Progdir + TankWeapon[CurrentProj[FiringTank]]["Projectile"])
    Else
      Ball[FiringTank][ShotsFired[FiringTank]] = LDShapes.AddAnimatedImage(Progdir + TankWeapon[CurrentProj[FiringTank]]["Projectile"],"True",TankWeapon[CurrentProj[FiringTank]]["ProjectileAnix"],TankWeapon[CurrentProj[FiringTank]]["ProjectileAniy"])
    EndIf
  EndIf
  If ShotsFired[FiringTank] > BallCount Then '<--- If the number of shots fired is more than the number of balls that can possible be on the screen, Then set it to One
    ShotsFired[FiringTank] = 1
  EndIf
  Missile_Dead[FiringTank][ShotsFired[FiringTank]] = "False"
  If TankWeapon[CurrentProj[FiringTank]]["Homeing"] = "True" Then
    vx[FiringTank][ShotsFired[FiringTank]] = Math.GetDegrees(Math.Cos(Math.GetRadians(TurretAngle[FiringTank] - 90))) * (TankWeapon[CurrentProj[FiringTank]]["Velocity"] / 750)
    vy[FiringTank][ShotsFired[FiringTank]] = Math.GetDegrees(Math.Sin(Math.GetRadians(TurretAngle[FiringTank] - 90))) * (TankWeapon[CurrentProj[FiringTank]]["Velocity"] / 750)
    Missilex[FiringTank][ShotsFired[FiringTank]]=FireTankx
    Missiley[FiringTank][ShotsFired[FiringTank]]=FireTanky
    Tankat[FiringTank][ShotsFired[FiringTank]]=TankSelected[FiringTank]
    ' TextWindow.WriteLine("Tankat = " + Tankat[FiringTank][ShotsFired[FiringTank]])
  Else
    
    Startx[FiringTank][ShotsFired[FiringTank]] = FireTankx
    Starty[FiringTank][ShotsFired[FiringTank]] = FireTanky
    AngleFire[FiringTank][ShotsFired[FiringTank]] = TurretAngle[FiringTank]'<--- Set the Firing Angle
    TimeStart[FiringTank][ShotsFired[FiringTank]] = Clock.ElapsedMilliseconds / 1000
  EndIf
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub GetTurretAngle
  AngleRad = Math.ArcTan((GraphicsWindow.MouseY - Tanky[ThisPC]) / (GraphicsWindow.MouseX - TankX[ThisPC]))
  TurretAngle[ThisPC] = Math.GetDegrees(AngleRad) + 90
  If (GraphicsWindow.MouseX - TankX[ThisPC]) = 0 or (GraphicsWindow.MouseX - TankX[ThisPC]) < 0 Then
    TurretAngle[ThisPC] = TurretAngle[ThisPC] - 180
  EndIf
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub MoveBalls
  For i = 1 To BallCount
    If Missile_Dead[q][i] = "True" Then
      Shapes.Move(ball[q][i],6000,6000)
      TimeStart[q][i] = TimeStart[q][i] - 500 '<--- Fixes non-homing
      Missilex[q][i]= 6000 '<--- Fixes homing
      Missiley[q][i]=6000 '<--- Fixes homing
    Else
      If TankWeapon[CurrentProj[q]]["Homeing"] = "True" Then
        
        '=================================================================HOMEING STUFF
        Missile_speed = TankWeapon[CurrentProj[q]]["Velocity"] / 10
        If Tankat[q][i] <> "" And  Tankx[Tankat[q][i]] < GraphicsWindow.Width And Tanky[Tankat[q][i]] < GraphicsWindow.Height Then
          Missile_turning = TankWeapon[CurrentProj[q]]["TurnRate"]
          Targetx =Tankx[Tankat[q][i]]
          Targety = Tanky[Tankat[q][i]]
          dx[q][i]=Targetx-Missilex[q][i]
          dy[q][i]=Targety-Missiley[q][i]
          distance[q][i]=Math.SquareRoot((dx[q][i]*dx[q][i])+(dy[q][i]*dy[q][i]))
          dx[q][i]=dx[q][i]/distance[q][i]
          dy[q][i]=dy[q][i]/distance[q][i]
          vx[q][i]=vx[q][i]+(dx[q][i]*Missile_turning)
          vy[q][i]=vy[q][i]+(dy[q][i]*Missile_turning)
          
          velocity[q][i]=Math.SquareRoot((vx[q][i]*vx[q][i])+(vy[q][i]*vy[q][i]))
          if (velocity[q][i]>Missile_speed) Then
            vx[q][i]=(vx[q][i]*Missile_speed)/velocity[q][i]
            vy[q][i]=(vy[q][i]*Missile_speed)/velocity[q][i]
          EndIf
        EndIf
        Missilex[q][i]=Missilex[q][i]+vx[q][i]
        Missiley[q][i]=Missiley[q][i]+vy[q][i]
        Offsety = TankWeapon[CurrentProj[q]]["ProjectileOffsety"]
        Offsetx = TankWeapon[CurrentProj[q]]["ProjectileOffsetx"]
        
        
        Shapes.Move(ball[q][i],Missilex[q][i] + Offsetx,Missiley[q][i] + Offsety)
        
        
        Xchange = Missilex[q][i] - OldX[q][i]
        Ychange = Missiley[q][i] - OldY[q][i]
        If Xchange < 0 Then
          Rot = Math.GetDegrees(Math.ArcTan(Ychange/Xchange))  - 180
        Else
          Rot = Math.GetDegrees(Math.ArcTan(Ychange/Xchange))
        EndIf
        Shapes.Rotate(Ball[q][i],rot)
        OldY[q][i] = Missiley[q][i]
        OldX[q][i] = Missilex[q][i]
      Else  '<-----------------------------------------------------------------V---Not homeing
        TimeFire[q][i] = (Clock.ElapsedMilliseconds / 1000) - TimeStart[q][i]
        
        Firestart = Zock77Math.GetCircle(25,Startx[q][i] + 12,Starty[q][i] + 20,AngleFire[q][i] - 90)
        xyArray = Zock77Math.Fire(Firestart["x"],Firestart["y"],Gravity,TimeFire[q][i],TankWeapon[CurrentProj[q]]["Velocity"],AngleFire[q][i] - 90)
        Offsety = TankWeapon[CurrentProj[q]]["ProjectileOffsety"]
        Offsetx = TankWeapon[CurrentProj[q]]["ProjectileOffsetx"]
        
        Shapes.Move(Ball[q][i],xyArray["x"]+ Offsetx,xyArray["y"] + Offsety)
        Xchange = xyArray["x"] - OldX[q][i]
        Ychange = xyArray["y"] - OldY[q][i]
        If Xchange < 0 Then
          Rot = Math.GetDegrees(Math.ArcTan(Ychange/Xchange))  - 180
        Else
          Rot = Math.GetDegrees(Math.ArcTan(Ychange/Xchange))
        EndIf
        Shapes.Rotate(Ball[q][i],rot)
        OldY[q][i] = xyArray["y"]
        OldX[q][i] = xyArray["x"]
      EndIf
    EndIf
    
  EndFor
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub RotateTurret
  Shapes.Rotate(Turret[q], TurretAngle[q]) 
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub GetTankpos
  Array = Zock77Math.Fire(Tankx[q],Tanky[q],0,Time,MaxSpeed,Angle[q])
  Tankx[q] = Array["x"]
  Tanky[q] = Array["y"]
  Time = 0
EndSub
'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Sub OnKeyDown
  Key = GraphicsWindow.LastKey
  If Key <> OldKey Then 
    Info = "KEYDO " + Key
    SendInfo()
  EndIf
  OldKey = Key
  KeyUp = ""
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------
Sub OnKeyUp
  If GraphicsWindow.LastKey <> KeyUp Then
    KeyUp = GraphicsWindow.LastKey
    OldKey = ""
    Info = "KEYUP " + KeyUp
    SendInfo()
  EndIf
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------
Sub SendInfo
  '1 W is down
  '2W is Up
  '3 D is down
  '4 D is up
  '5 S is down
  '6 S is up
  '7 A is down
  '8 A is Up
  
  If Info = "KEYDO W" Then
    wkey = 1
  ElseIf Info = "KEYUP W" Then
    wkey = 0
  ElseIf Info = "KEYDO D" Then
    dkey = 1
  ElseIf Info = "KEYUP D" Then
    dkey = 0
  ElseIf Info = "KEYDO S" Then
    skey = 1
  ElseIf Info = "KEYUP S" Then
    skey = 0
  ElseIf Info = "KEYDO A" Then
    akey = 1
  ElseIf Info = "KEYUP A" Then
    akey = 0
  EndIf
  ' 'TextWindow.WriteLine(Info)
  
  DatatoSend = Text.Append(wkey,dkey)
  DatatoSend = Text.Append(DatatoSend,skey)
  DatatoSend = Text.Append(DatatoSend,akey)
  Var = "type=pos;x=" + Math.Round(Tankx[ThisPC]) + ";y=" + Math.Round(Tanky[ThisPC]) + ";ang=" + Math.Round(Angle[ThisPC]) + ";key=" + DatatoSend + ";client=" + ThisPC + ";dtime=" + Dtime[ThisPC]
  IOTCPClient.SendMessage(Var)
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------
Sub OnMessageRecieved
  
  NumOfMesRec = NumOfMesRec + 1
  If NumOfMesRec = 1 Then 
    If Text.GetSubText(IOTCPClient.LastMessageData,1,21) = "You are client number" Then
      CanStartGame = "Yes"
      ThisPC = Text.GetSubText(IOTCPClient.LastMessageData,23,1)
      
      
      Var = Text.GetSubTextToEnd(IOTCPClient.LastMessageData,25)
      
      
      CurrentMap = Var["level"]
      
      For i = 1 To ThisPC - 1
        Username[i] = Var["username" + i]
        
      EndFor
      
      
      
      
    EndIf
  Else
    Data = IOTCPClient.LastMessageData
    If Text.GetSubText(Data,1,1) = ThisPC Then
      'Do Nothing
    Else
      DoStuff()
    EndIf
    
    If Text.GetSubText(IOTCPClient.LastMessageData,1,15) = "Client Connect " Then
      Occ()
    EndIf
    
  EndIf
  
  
  'TextWindow.WriteLine(IOTCPClient.LastMessageData)
  
  
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------
Sub Occ
  NumClients = NumClients + 1
  Var2 = Text.GetSubTextToEnd(IOTCPClient.LastMessageData,16)
  ClientNum[Var2] = NumClients
  ClientIP[NumClients] = Var2
  Username[NumOfPlayers] = LDNetwork.GetGameData("TankWars" + ClientIP[NumClients])
  DataUser = LDNetwork.GetGameData("TankWars" + Username[NumOfPlayers])
  
  
  ' Username[NumClients] = Var2["usr"]
  'TextWindow.WriteLine(ClientIP[NumClients])
  'TextWindow.WriteLine("Client number " + NumClients + " Joined")
  NumOfPlayers = NumOfPlayers + 1
  CurrentProj[NumOfPlayers] = DataUser["weapon"]
  Nop = NumOfPlayers
  
  Tankx[Nop] = TankInitPos[Nop]["x"]
  Tanky[Nop] = TankInitPos[Nop]["y"]
  UsernameText[Nop] = Shapes.AddText(Username[Nop])
  Tank[Nop] = Shapes.AddImage(Program.Directory + "/Tank.png")
  Turret[Nop] = Shapes.AddImage(Program.Directory + "/" + TankWeapon[CurrentProj[Nop]]["Turret"])
  TankExp[Nop] = LDShapes.AddAnimatedImage(Program.Directory + "/Animations/anim1.png","False",8,6)
  LDShapes.AnimationSet(TankExp[Nop],48)
  For w22 = 1 to BallCount
    Exp[nop][w22] = LDShapes.AddAnimatedImage(Progdir + TankWeapon[CurrentProj[Nop]]["Animation"],"False",TankWeapon[CurrentProj[Nop]]["AnimCountx"],TankWeapon[CurrentProj[Nop]]["AnimCounty"])
    LDShapes.AnimationSet(Exp[Nop][w22],16)
  EndFor
  
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------




Sub DoStuff
  
  VarTemp = IOTCPClient.LastMessageData
  'LDtext can't handle = and ;
  Varnew =Text.Replace(Vartemp,";","w") '<--- so I convert ; to w
  Varnew =Text.Replace(Varnew,"=","q")  '<--- and = to q
  Varnew = LDText.Split(Varnew,"type")    'then I split it
  
  For i9 = 1 To Array.GetItemCount(Varnew)
    Var = "type" + Text.Replace(Text.Replace(Varnew[i9],"w",";"),"q","=") '<--- then, once they are split, I convert them back.
    If TankDead[Var["client"]] <> "True" Then
      If Var["type"] = "pos" Then
        k = Var["client"]
        KeyClient = Var["key"]
        Tankx[k] = Var["x"]
        Tanky[k] = Var["y"]
        Angle[k] = Var["ang"]
        Dtime[k] = Var["dtime"]
        
        
        Array1 = "0=Up;1=Down;"
        Array2 = "1=w;2=d;3=s;4=a;"
        For i4 = 1 To 4
          KeyVar[Array2[i4]] = Array1[Text.GetSubText(KeyClient,i4,1)]
          ClientKey[k][Array2[i4]] = KeyVar[Array2[i4]] 
        EndFor
      EndIf
      
      If Var["type"] = "death" Then
        TankDead[Var["client"]] = "True"
        StartSpawncount()
        Shapes.Move(TankExp[Var["client"]],Tankx[Var["client"]]-49,Tanky[Var["client"]]-49)
        Shapes.Rotate(TankExp[Var["client"]], Angle[Var["client"]] + 90)
        LDShapes.AnimationSet(TankExp[Var["client"]],1)
        GraphicsWindow.DrawImage(Program.Directory + "/scorch_mark" + Math.Round(Math.GetRandomNumber(2)) +".png",Tankx[Var["client"]]-49,Tanky[Var["client"]]-49)
        Tankx[Var["client"]] = 5000
        Tanky[Var["client"]] = 5000
        
      EndIf
      
      If Var["type"] = "init" Then
        Username[Var["client"]] = Var["username"]
        DataUser = LDNetwork.GetGameData("TankWars" + Username[Var["client"]])
        CurrentProj[Var["client"]] = DataUser["weapon"]
      EndIf
      
      
      If Var["client"] <> ThisPC Then
        If Var["type"] = "fire" Then
          FiringTank = Var["client"]
          FireTankx = Var["x"]
          FireTanky = Var["y"]
          TurretAngle[FiringTank] = Var["ang"]
          TankSelected[FiringTank] = Var["ts"]
          Distx = Math.Abs(Tankx[Var["client"]] - FireTankx)
          Disty = Math.Abs(Tanky[Var["client"]] - FireTanky)
          
          ' If the tank is too far off, then fix its position
          If Distx > 25 Then
            Tankx[Var["client"]] = FireTankx 
          EndIf
          If Disty > 25 Then
            Tanky[Var["client"]] = FireTanky  
          EndIf
          
          Fire()
        EndIf
      EndIf
    EndIf
  EndFor
EndSub
' --------------------------------------------------------------------------------------------------------------------------------------------------------
Sub OnButtonClicked
  If Controls.LastClickedButton <> BRefresh Then
    For i8 = 1 To NumofServers
      If Controls.LastClickedButton = "Button" + i8 Then
        Connect =  IOTCPClient.Connect(DataStuff[i8]["IP"],DataStuff[i8]["Port"])
        If Connect = "SUCCESS" Then
          GraphicsWindow.Clear()
          GraphicsWindow.ShowMessage("Successfully connected!!","")
        Else
          GraphicsWindow.ShowMessage("Connection Failed.","")
        EndIf
      EndIf
    EndFor
  Else
    DrawServers()
  EndIf
EndSub

Sub OnGWClose
  LDProcess.Start(Program.Directory + "/Armored Assault.exe","")
  Program.End()
EndSub

Sub OnMouseDown
  If Clock.ElapsedMilliseconds - Firelast > TankWeapon[CurrentProj[ThisPC]]["ROF"] Then
    FiringTank = ThisPC
    FireTankx = Tankx[ThisPC]
    FireTanky = Tanky[ThisPC]     
    TankSelected[ThisPC] = SelectedTank
    Fire()
    Var = "type=fire;x=" + Math.Round(Tankx[ThisPC]) + ";y=" + Math.Round(Tanky[ThisPC]) + ";ang=" + Math.Round(TurretAngle[ThisPC]) + ";client=" + ThisPC + ";ts=" + SelectedTank
    IOTCPClient.SendMessage(Var)
    Firelast = Clock.ElapsedMilliseconds
  EndIf
EndSub

Sub CheckBulletColision
  For i = 1 To BallCount
    If TankWeapon[CurrentProj[q]]["Homeing"] = "True" Then
      tempX = Missilex[q][i]
      tempY = Missiley[q][i]
      TankRoundx = Math.Round((tempX - 15) / 30 + 1)
      TankRoundy = Math.Round((tempY - 15) / 30 + 1)
      Result = Text.GetSubText(Level[1][TankRoundy],TankRoundx,1)          
    Else
      TimeFire[q][i] = (Clock.ElapsedMilliseconds / 1000) - TimeStart[q][i]
      Firestart = Zock77Math.GetCircle(25,Startx[q][i] + 12,Starty[q][i] + 20,AngleFire[q][i] - 90)
      
      xyArray5 = Zock77Math.Fire(Firestart["x"],Firestart["y"],Gravity,TimeFire[q][i],TankWeapon[CurrentProj[q]]["Velocity"],AngleFire[q][i] - 90)
      TankRoundx = Math.Round((xyArray5["x"] - 15) / 30 + 1)
      TankRoundy = Math.Round((xyArray5["y"] - 15) / 30 + 1)
      Result = Text.GetSubText(Level[1][TankRoundy],TankRoundx,1)
      tempX = xyArray5["x"]
      tempY = xyArray5["y"]
    EndIf
    
    For t1 = 1 To NumOfPlayers
      If q <> t1 Then '<--- q is who owns the bullet. t1 is who its checking. So as long as the owner isn't being checked, we are good.
        If tempX > Tankx[t1] And tempX < Tankx[t1] + 30 And tempY > Tanky[t1] And tempY < TankY[t1] + 30 Then
          Missile_Dead[q][i] = "True"
        EndIf
      EndIf
    EndFor
    
    If Result = "p" Then
      Missile_Dead[q][i] = "True"
      Shapes.Move(Exp[q][i], tempX - 34,tempY - 32)
      
      LDShapes.AnimationSet(Exp[q][i],1)
      
    EndIf
  EndFor
EndSub

Sub TankSelection
  If Mouse.IsRightButtonDown Then
    
    Mx = GraphicsWindow.MouseX
    My = GraphicsWindow.MouseY
    For u2 = 1 To NumOfPlayers
      Var = LDShapes.GetAllShapesAt(Mx,My)
      For u3 = 1 to Array.GetItemCount(Var)
        If Var[u3] = Tank[u2] Then
          SelectedTank = u2
          u2 = NumOfPlayers '<-- exit the For statement
        EndIf
      EndFor
    EndFor
  EndIf
EndSub

Sub StartSpawncount
  TankDieat = Clock.ElapsedMilliseconds
EndSub

Sub DrawServers
  Servers = LDNetwork.GetGameData("TankWarsServers")
  pos = 0
  For i8 = 1 to 20
    For z = 1 To 3
      Shapes.Remove(Text[i8][z])
    EndFor
    Shapes.Remove(Button[i8])
  EndFor
  For i7 = 1 To 20
    If Servers[i7] = "closed" Then
      pos = pos + 1
      GraphicsWindow.BrushColor = "White"
      ' GraphicsWindow.DrawBoundText(10,10 + pos*30,75,DataStuff[i7]["Server"]) 
      ' GraphicsWindow.DrawText(100,10 + pos*30,DataStuff[i7]["IP"]) 
      Text[i7][1] = Shapes.AddText(DataStuff[i7]["Server"])
      Text[i7][2] = Shapes.AddText(DataStuff[i7]["IP"])
      Text[i7][3] = Shapes.AddText(DataStuff[i7]["Port"])
      Shapes.Move(Text[i7][1],15,10 + pos*30)
      Shapes.Move(Text[i7][2],100,10 + pos*30)
      Shapes.Move(Text[i7][3],210,10 + pos*30)
      ' GraphicsWindow.DrawText(210,10 + pos*30,DataStuff[i7]["Port"]) 
      GraphicsWindow.BrushColor = "Black"
      Button[i7] = Controls.AddButton("Join",300,(pos*30) + 4)
      Controls.SetSize(Button[i7],50,30)
    EndIf
  EndFor
EndSub
