gw = 600

gh = 600

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.BackgroundColor = "#555500"



makeRiver()

makeFire()



While ("True")

  start = Clock.ElapsedMilliseconds

  

  'Move waves

  For i = 1 To waveCount

    waveX[i] = waveX[i]+Math.GetRandomNumber(waveSpeed)

    waveY[i] = waveY[i]+(Math.GetRandomNumber(11)-6)/10*waveJiggle

    If (waveX[i] > gw) Then

      waveX[i] = -waveWidth

    EndIf

    If (waveY[i] > gh Or waveY[i] < gh-riverWidth) Then

      waveY[i] = gh-riverWidth+Math.GetRandomNumber(riverWidth)

    EndIf

    Shapes.Move(waves[i],waveX[i],waveY[i])

    o = waveOpacityRange+Math.GetRandomNumber(waveOpacityRange)

    Shapes.SetOpacity(waves[i],o)

    z = (1-waveZoomRange/2)+(Math.GetRandomNumber(11)-1)/10*waveZoomRange

    Shapes.Zoom(waves[i],z,z) 

  EndFor

  

  'Move flames

  For i = 1 To flameCount

    flameX[i] = flameX[i]+(Math.GetRandomNumber(11)-6)/10*fileJiggle

    flameY[i] = flameY[i]+(Math.GetRandomNumber(11)-6)/10*fileJiggle

    If (flameX[i] > gw/2+fireWidth/2 Or flameX[i] < gw/2-fireWidth/2) Then

      flameX[i] = gw/2+Math.GetRandomNumber(fireWidth)-fireWidth/2

    EndIf

    If (flameY[i] > gh/2+fireHeight/2 Or flameY[i] < gh/2-fireHeight/2) Then

      flameY[i] = gw/2+Math.GetRandomNumber(fireHeight)-fireHeight/2

    EndIf

    Shapes.Move(flames[i],flameX[i]-flameWidth/2,flameY[i]-flameHeight/2)

    o = waveOpacityRange+Math.GetRandomNumber(waveOpacityRange)

    Shapes.SetOpacity(flames[i],o)

    z = (1-waveZoomRange/2)+(Math.GetRandomNumber(11)-1)/10*waveZoomRange

    Shapes.Zoom(flames[i],z,z) 

  EndFor



delay = 10-(Clock.ElapsedMilliseconds-start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile



'River

Sub makeRiver

  waveCount  = 100

  riverWidth = 100

  waveWidth = 150

  waveHeight = 6

  waveOpacityRange = 50

  waveZoomRange = 0.5

  waveSpeed = 20

  waveJiggle = 3

  GraphicsWindow.PenWidth = 0

  GraphicsWindow.BrushColor = "#99DDFF"

  GraphicsWindow.FillRectangle(0,gh-0.95*riverWidth,gw,0.95*riverWidth)

  

  For i = 1 To waveCount

    R = 10+Math.GetRandomNumber(20)

    G = 100+Math.GetRandomNumber(155)

    B = 200+Math.GetRandomNumber(55)

    GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(R,B,B)

    w = waveWidth+Math.GetRandomNumber(waveWidth/2)

    h = waveHeight+Math.GetRandomNumber(waveHeight/2)

    waves[i] = Shapes.AddEllipse(w,h)

    waveX[i] = Math.GetRandomNumber(gw+waveWidth)-waveWidth

    waveY[i] = gh-riverWidth+Math.GetRandomNumber(riverWidth)

    Shapes.Move(waves[i],waveX[i],waveY[i])

  EndFor

EndSub



'Fire

Sub makeFire

  flameCount = 50

  flameWidth = 5

  flameHeight = 50

  fireWidth = 50

  fireHeight = 80

  fileJiggle = 3

  For i = 1 To flameCount

    R = 200+Math.GetRandomNumber(55)

    G = 0+Math.GetRandomNumber(50)

    B = 50+Math.GetRandomNumber(50)

    GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(R,B,B)

    w = flameWidth+Math.GetRandomNumber(flameWidth/2)

    h = flameHeight+Math.GetRandomNumber(flameHeight/2)

    flames[i] = Shapes.AddEllipse(w,h)

    flameX[i] = gw/2+Math.GetRandomNumber(fireWidth)-fireWidth/2

    flameY[i] = gh/2+Math.GetRandomNumber(fireHeight)-fireHeight/2

    Shapes.Move(flames[i],flameX[i]-flameWidth/2,flameY[i]-flameHeight/2)

  EndFor

  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor

  Shapes.Move(Shapes.AddRectangle(200,50),gw/2-100,gh/2+fireHeight)

  GraphicsWindow.BrushColor = "#EE000000"

  rock = Shapes.AddEllipse(fireWidth,20)

  Shapes.Move(rock,gw/2-fireWidth,gh/2+fireHeight-10)

  rock = Shapes.AddEllipse(fireWidth,20)

  Shapes.Move(rock,gw/2-fireWidth/2,gh/2+fireHeight-10)

  rock = Shapes.AddEllipse(fireWidth,20)

  Shapes.Move(rock,gw/2,gh/2+fireHeight-10)

EndSub


