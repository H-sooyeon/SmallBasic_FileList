' Moon Phase - June 2015

' Copyright Â© 2012-2015 Nonki Takahashi.  The MIT License.

' Last update 2015-06-04

' Program ID RPM049-2

' Version 0.4 - changed for Silverlight again

' Reference

' [1] ?™??‚‹?‚¼?ƒŸ ?œˆé½¢æ–¹ç¨‹å¼ http://www.suguru.jp/www.monjirou.net/rika/moon/age_of_the_moon.html

'

GraphicsWindow.Title = "June 2015"

gw = 598

gh = 428

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.BackgroundColor = "Black"

lp = 0    ' lunar phase (days)

yy = 2015

mm = 6

dd = 1

wk = 2    ' Monday

days = 30

lp = Math.Remainder((yy - 2000) * 11 + mm + dd - 8, 30) ' [1]

maxlp = 29.53

x0 = 40   ' left

y0 = 60   ' top

d = 46    ' diameter of moon

GraphicsWindow.PenWidth = 0

wn = "1=SUN;2=MON;3=TUE;4=WED;5=THU;6=FRI;7=SAT;"

GraphicsWindow.BrushColor = "LightGray"

GraphicsWindow.FontSize = 20

GraphicsWindow.FontName = "Trebuchet MS"

y = y0 - 44

For w = 1 To 7

  x = x0 + Math.Remainder((w - 1), 7) * (d * 1.8)

  GraphicsWindow.DrawText(x, y, wn[w])

EndFor

For day = 1 To days

  x = x0 + Math.Remainder((day - 2 + wk), 7) * (d * 1.8)

  y = y0 + Math.Floor((day - 2 + wk) / 7) * (d * 1.6)

  a = 0         ' area of crescent [%]

  If lp > 0 Then

    GraphicsWindow.BrushColor = "White"

    GraphicsWindow.FillEllipse(x, y, d, d)

    a = 100         ' area of crescent [%]

  EndIF

  GraphicsWindow.BrushColor = "Black"

  If 0 < lp And lp < maxlp / 2 Then

    GraphicsWindow.FillRectangle(x, y, d / 2, d)

    a = a - 50      ' area of crescent [%]

  ElseIf maxlp / 2 < lp And lp < maxlp Then

    GraphicsWindow.FillRectangle(x + d / 2, y, d / 2, d)

    a = a - 50      ' area of crescent [%]

  EndIF

  If 0 < lp And lp < maxlp / 4 Then

    s = Math.Cos(lp / maxlp * 2 * Math.Pi)

    GraphicsWindow.BrushColor = "Black"

    GraphicsWindow.FillEllipse(x + d * (1 - s) / 2, y, d * s, d)

    a = a - 50 * s  ' area of crescent [%]

  ElseIf maxlp / 4 < lp And lp < maxlp / 2 Then

    s = Math.Sin((lp / maxlp - 1 / 4) * 2 * Math.Pi)

    GraphicsWindow.BrushColor = "White"

    GraphicsWindow.FillEllipse(x + d * (1 - s) / 2, y, d * s, d)

    a = a + 50 * s  ' area of crescent [%]

  ElseIf maxlp / 2 < lp And lp < maxlp * 3 / 4 Then

    s = Math.Cos((lp / maxlp - 1 / 2) * 2 * Math.Pi)

    GraphicsWindow.BrushColor = "White"

    GraphicsWindow.FillEllipse(x + d * (1 - s) / 2, y, d * s, d)

    a = a + 50 * s  ' area of crescent [%]

  ElseIf maxlp * 3 / 4 < lp And lp < maxlp Then

    s = Math.Sin((lp / maxlp - 3 / 4) * 2 * Math.Pi)

    GraphicsWindow.BrushColor = "Black"

    GraphicsWindow.FillEllipse(x + d * (1 - s) / 2, y, d * s, d)

    a = a - 50 * s  ' area of crescent [%]

  EndIf

  GraphicsWindow.BrushColor = "White"

  el = Shapes.AddEllipse(d, d)

  Shapes.Move(el, x, y)

  Shapes.SetOpacity(el, 30)

  GraphicsWindow.BrushColor = "LightGray"

  GraphicsWindow.FontSize = 20

  GraphicsWindow.DrawText(x - d * 0.6, y -  d * 0.4, day)

  Program.Delay(300)

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.FontSize = 12

  GraphicsWindow.DrawText(x + d * 0.1, y -  d * 0.3, lp)

  GraphicsWindow.BrushColor = "Black"

  pc = Shapes.AddText((Math.Floor(a * 10) / 10) + "%")

  Shapes.Move(pc, x + d * 0.1, y + d * 0.35)

  lp = lp + 1

  If lp >= maxlp Then

    lp = lp - maxlp

  EndIf

EndFor


