'Create some springs
numSpring = 5
For i = 1 To numSpring
  length[i] = 100 'Unstretched length
  fixedPosX[i] = Math.GetRandomNumber(500)
  fixedPosY[i] = Math.GetRandomNumber(500)
EndFor

'Create a weight
GraphicsWindow.Width = 600
GraphicsWindow.Height = 600
weightX = 50+Math.GetRandomNumber(500) 'Position
weightY = 50+Math.GetRandomNumber(500)
weightU = 0 'Velocity
weightV = 0
radius = 30
damping  = 0.01 'Some damping to the velocity
weight = Shapes.AddEllipse(2*radius,2*radius)

While ("True")
  'Calculate the force (Hooke's Law) and it is summed for each spring
  forceX = 0
  forceY = 0
  For i = 1 To numSpring
    vecX = weightX-fixedPosX[i]
    vecY = weightY-fixedPosY[i]
    len = Math.SquareRoot(vecX*vecX+vecY*vecY)
    forceX = forceX - vecX/len * (len-length[i])
    forceY = forceY - vecY/len * (len-length[i])
  EndFor
  'Update the velocity and positio of the weight
  weightU = (1-damping)*(weightU+forceX/20)
  weightV = (1-damping)*(weightV+forceY/20)
  weightX = weightX+weightU
  weightY = weightY+weightV
  'Do the update drawing
  Shapes.Move(weight,weightX-radius,weightY-radius)
  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor
  GraphicsWindow.FillRectangle(-10,-10,GraphicsWindow.Width+20,GraphicsWindow.Height+20)
  GraphicsWindow.BrushColor = "Black"
  For i = 1 To numSpring
    GraphicsWindow.DrawLine(fixedPosX[i],fixedPosY[i],weightX,weightY)
  EndFor
  Program.Delay(20)
EndWhile
