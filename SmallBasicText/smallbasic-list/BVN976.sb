GraphicsWindow.backgroundcolor="black"
GraphicsWindow.width=1300
GraphicsWindow.height=800
startx=20
side=35
bingo="BINGO"
numCards=6
For i=0 to numCards-1
  x=startx
  startingX[i]=x 'upper left corner of the card
  y=50
  createCard()
  allCards[i]=card
  showCard()   
  startx=startx+side*6  
endfor
GraphicsWindow.BrushColor="magenta"
x=60
y=250
player=1
For i=1 to numCards
GraphicsWindow.DrawText(x,y,"PLAYER "+i)
x=x+side*6
endfor
createBingoBalls()
ballCount=0
GraphicsWindow.brushcolor="Black"
button=Controls.AddButton("CALL NEXT NUMBER", 20,300)
Controls.ButtonClicked=nextNumber
bbX=20
bbY=375
bbDiam=30

Sub createCard
  For col=0 To 4
    createColumn()
  endfor  
endsub

sub createColumn
  For row=0 To 4
    If row=2 And col=2 then 'center square
      card[row][col]=0
    Else
      num=math.GetRandomNumber(15)+15*col
      duplicate()
      While dup="True"
        num=math.GetRandomNumber(15)+15*col       
        duplicate()      
      endwhile     
      card[row][col]=num     
      endif   
   endfor 
 endsub
 
Sub duplicate
  dup="False"
  For r=0 To 4
    If num=card[r][col] then
      dup="True"
    EndIf
  endfor 
endsub

Sub showCard
  GraphicsWindow.fontbold="True"
  GraphicsWindow.fontsize=18
  GraphicsWindow.fontname="Arial"  
  For row=0 To 4
    For col=0 To 4
      drawSquare()
      x=x+side+2
    EndFor
    x=startx
    y=y+side+2
    endfor
  bX=x+10
  bY=25
  GraphicsWindow.brushcolor="yellow"  
  For b=1 To 5
    GraphicsWindow.DrawText(bX,bY,text.GetSubText(bingo,b,1))
    bX=bX+side+2
    endfor
  endsub
  
sub drawSquare  
      cardNum=card[row][col]
      GraphicsWindow.brushcolor="white"  
      GraphicsWindow.FillRectangle(x,y,side,side)          
      graphicswindow.brushcolor="black"     
      If card[row][col]>=10 then        
        GraphicsWindow.DrawText(x+9,y+9,cardNum)        
      Else
        GraphicsWindow.DrawText(x+16,y+9,cardNum)      
        endif
        'center square red
      If row=2 And col=2 then
            GraphicsWindow.brushcolor="red"  
            GraphicsWindow.FillRectangle(x,y,side,side)    
        endif   
      endsub
      
    Sub createBingoBalls      
      For i=0 To 74
        If i+1<=15 Then
          letter="B"
        Else
          If i+1<=30 Then
            letter="I"
          Else
            If i+1<=45 Then
              letter="N"
            Else
              If i+1<=60 then
                letter="G"
              Else
                letter="O"
              EndIf
            EndIf
          EndIf
        EndIf
        bb[i]=letter+(i+1)      
 endfor
endsub

 Sub nextNumber
   'call a random number
   bbNum=math.GetRandomNumber(75)-1  'index of  bb
   chooseBingoBall()
   While chosen="True"
     bbNum=math.GetRandomNumber(75)-1  'index of  bb
     chooseBingoBall()     
   endwhile  
   ball=bb[bbNum] 
   ballCount=ballCount+1
   If ballCount=75 Then
     Shapes.Remove(button)
     endif
   'mark chosen
   bb[bbNum]="*"   
   showBall()
   if Math.Remainder(ballCount,25)=0 Then 'new row
     bbY=bbY+40
     bbX=20
     else
       bbX=bbX+bbDiam+10
     endif
     checkCards()   'check which (if any) cards contain the number  
   endsub
   
 Sub chooseBingoBall
  chosen="False"
  If bb[bbNum]="*" Then
    chosen="True"
   endif
 endsub
 
 Sub showBall
   GraphicsWindow.brushcolor="cyan"
   GraphicsWindow.FillEllipse(bbX,bbY,bbDiam,bbDiam)
   GraphicsWindow.brushcolor="black"
   GraphicsWindow.fontsize=12
   graphicswindow.drawtext(bbX+5,bbY+10,ball)
 endsub
 
 Sub checkCards
  'see if bingo number called is contained in one or more cards
  ' get the column
  let=text.GetSubText(ball,1,1)
  column=text.GetIndexOf(bingo,let)-1 
  For c=0 To numCards-1    
    card=allCards[c]    
    checkOneCard()   
  endfor           
  If ballCount>=4 Then
       checkForWinners()
       endif    
 endsub
     
Sub checkOneCard
  foundNumber="False"
  For rr=0 To 4   
    If let+card[rr][column]=ball Then  
      startX=startingX[c]    
      card[rr][column]="*"+card[rr][column] 'mark called     
      allCards[c][rr][column]=card[rr][column]
      drawCalledSquare()   
    endif
  endfor
endsub

Sub drawCalledSquare   
  GraphicsWindow.fontbold="True"
  GraphicsWindow.fontsize=18
  GraphicsWindow.fontname="Arial"  
  cardNum=text.GetSubTextToEnd(card[rr][column],2)  
  'GraphicsWindow.brushcolor="cyan"  
  GraphicsWindow.brushcolor="white"
  x=startX+column*(side+2)
  y=50+rr*(side+2)
  GraphicsWindow.FillRectangle(x,y,side,side) 
  GraphicsWindow.brushcolor="cyan"
  GraphicsWindow.FillEllipse(x,y,side,side)
  GraphicsWindow.brushcolor="black"
  GraphicsWindow.pencolor="black"
  GraphicsWindow.DrawEllipse(x,y,side,side)
  If cardNum >=10 then        
   GraphicsWindow.DrawText(x+9,y+9,cardNum)        
  Else
   GraphicsWindow.DrawText(x+16,y+9,cardNum)      
  endif
endsub

Sub checkForWinners
  'For each card. check rows, cols, 2 diagonals for marked (* in first character) squares
  winMessage=""
  For c=0 To numCards-1
    win="False"
    card=allCards[c]
    'printCard()
    checkRows()
    checkCols()
    checkDiag1() 'upper left to lower right
    checkDiag2()
    If win="True" Then 
      GraphicsWindow.BackgroundColor="green"
      Controls.HideControl(button)
      GraphicsWindow.brushcolor="black"
      GraphicsWindow.fontsize=30
      GraphicsWindow.DrawText(40,600,winMessage)     
      endif
    endfor
    endsub
  
  Sub checkRows
    For rr=0 To 4
      checkOneRow()
      endfor    
    endsub
    
  Sub checkOneRow    
    count=0
    For column=0 To 4      
      firstChar=text.GetSubText(card[rr][column],1,1)      
      If firstChar="*" Then       
        count=count+1
      endif
    endfor
    if count=4 and rr=2 or count=5 Then      
        winMessage=winMessage+" "+"PLAYER "+(c+1)+" WINS!! "
        win="True"
        endif    
  endsub
      
Sub checkCols
    For cc=0 To 4
      checkOneCol()
    endfor    
endsub
  
Sub checkOneCol     
    count=0
    For row=0 To 4     
      firstChar=text.GetSubText(card[row][cc],1,1)      
      If firstChar="*" Then        
        count=count+1
      endif
      endfor
      if count=4 and cc=2 or count=5 Then        
        winMessage=winMessage+" "+"PLAYER "+(c+1)+" WINS!! "
        win="True"
        endif    
      endsub
      
Sub checkDiag1
    count=0
      For num=0 To 4
      firstChar=text.GetSubText(card[num][num],1,1)      
      If firstChar="*" Then        
        count=count+1
      endif
      endfor
      if count=4  Then       
        winMessage=winMessage+" "+"PLAYER "+(c+1)+" WINS!! "
        win="True"
        endif    
      endsub
      
Sub checkDiag2
    count=0
      For num=0 To 4
      firstChar=text.GetSubText(card[num][4-num],1,1)      
      If firstChar="*" Then        
        count=count+1
      endif
      endfor
      if count=4  Then       
        winMessage=winMessage+" "+"PLAYER "+(c+1)+" WINS!! "
        win="True"
        endif    
  endsub     
      
  
