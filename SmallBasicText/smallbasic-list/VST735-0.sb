'http://smallbasic.com/program/?VST735-0

'Drop Down Menu System - (under construction)  by Jibba Jabba, January 2014



Initialise()



While "True"  

  UpdateProgram()

  UpdateScreen() 

  FrameRate()  

EndWhile



Sub UpdateProgram

  start = Clock.ElapsedMilliseconds

  

  If mouseMove Then    

    mseX = GraphicsWindow.MouseX

    mseY = GraphicsWindow.MouseY 

    

    nextMenuFocus = 0

    If mseY <= menuHeight And mseX < menuWidth * Array.GetItemCount(menu) Then   'Update Next Menu Focus        

      For i = Array.GetItemCount(menu) To 1 Step -1

        left = Shapes.GetLeft(menuFocus[i])

        If mseX >= left And mseX <= left + menuWidth Then

          nextMenuFocus = i

        EndIf      

      EndFor      

    EndIf

    

    If nextMenuFocus <> currentMenuFocus Then  'Change Current Menu Focus     

      previousMenuFocus = currentMenuFocus

      currentMenuFocus = nextMenuFocus      

    EndIf    

    mouseMove = "False"   

    

    

  ElseIf mouseDown Then    

    If currentMenuFocus > 0 Then      

      isMenuSelected = -isMenuSelected        

    EndIf    

    mouseDown = "False"

    

  EndIf

EndSub



Sub UpdateScreen   

  Shapes.SetOpacity(menuFocus[previousMenuFocus], 0)

  Shapes.SetOpacity(menuFocus[currentMenuFocus], menuFocusOpacity[isMenuSelected])

EndSub



Sub FrameRate

  delay = 1000 / fps - (Clock.ElapsedMilliseconds - start)

  If delay > 0 Then

    Program.Delay(delay)

  Else

    TextWindow.WriteLine("delay = " + delay)

  EndIf

EndSub



Sub Initialise

  'DEBUG

  TextWindow.Top = 20

  TextWindow.Left = 20

  

  'Main Loop

  fps = 50  '*6  drags > 300fps

  

  'MENU 

  'Data Table

  menu = "1=View;2=Prog;3=Help"  

  subMenu[1] = "1=Home;2=Game;3=The Leaders Board" 

  subMenu[2] = "1=Debug;2=Close" 

  subMenu[3] = "1=How to Play;2=About Match Picture"   

  

  'Get Properties

  gw = GraphicsWindow.Width

  gh = GraphicsWindow.Height

  

  'Set Parameters

  menuHeight = 25

  menuWidth = 55

  menuTextOffset = 8

  currentMenuFocus = 0

  isMenuSelected = -1  'toggle init OFF

  menuFocusOpacity = "1=100;-1=30"

  

  'Add Menu Bar   - Sub {redraw for new window size}

  GraphicsWindow.BrushColor = "Peru"

  GraphicsWindow.FillRectangle(0, 0, gw, menuHeight)

  

  'Add Menu

  GraphicsWindow.FontName = "Segoe Print"

  GraphicsWindow.FontSize = 13

  GraphicsWindow.PenWidth = 0

  

  For i = Array.GetItemCount(menu) To 1 Step -1

    GraphicsWindow.BrushColor = "DeepPink"

    menuFocus[i] = Shapes.AddRectangle(menuWidth, menuHeight) 

    Shapes.SetOpacity(menuFocus[i], 0)   

    GraphicsWindow.BrushColor = "PaleGreen"  

    menuText[i] = Shapes.AddText(menu[i])    

    Shapes.Move(menuText[i], menuWidth * (i-1) + menuTextOffset, 0)

    Shapes.Move(menuFocus[i], menuWidth * (i-1), 0)    

  EndFor

  

  'Register Events

  GraphicsWindow.MouseMove = OnMouseMove

  GraphicsWindow.MouseDown = OnMouseDown

EndSub



Sub OnMouseMove

  mouseMove = "True"

EndSub



Sub OnMouseDown

  mouseDown = "True"

EndSub


