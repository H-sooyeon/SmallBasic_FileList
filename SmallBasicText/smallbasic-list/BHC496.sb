'Events
Controls.ButtonClicked = OnButtonClicked
LDControls.FileDropped = OnFileDropped
LDControls.MediaPlayerEnded = OnMediaPlayerEnded
Timer.Tick = OnTick
Timer.Interval = 100

Initialisation()

Playing = 1
While Playing = 1
  
EndWhile 
Program.End()


Sub Initialisation
  GraphicsWindow.Top = 1
  GraphicsWindow.Left = 1
  gw = 720
  gh = 640
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  player = LDControls.AddMediaPlayer(gw,gh-100)
  volume = 0.5
  LDControls.MediaPlayerVolume(player,volume)
  
  play = Controls.AddButton("Play",4,gh-40)
  stop = Controls.AddButton("Stop",47,gh-40)
  pause = Controls.AddButton("Pause",90,gh-40)
  volumeDown = Controls.AddButton("Vol-",141,gh-40)
  volumeUp = Controls.AddButton("Vol+",180,gh-40)
  FullDisplay = Controls.AddButton("FD",225,gh-40)
  NormalDisplay = Controls.AddButton("ND",255,gh-40)
  Off = Controls.AddButton("Off",290,gh-40)
  applySpeed = Controls.AddButton("Apply",650,gh-80)
  applyTime = Controls.AddButton("Apply",650,gh-40)
  
  speed = Controls.AddTextBox(485,gh-80)
  time = Controls.AddTextBox(485,gh-40)
  fileInfo = Shapes.AddText("Drop a media file HERE")
  speedInfo = Shapes.AddText("Playback Speed")
  timeInfo = Shapes.AddText("Time ("+")")
  Shapes.Move(fileInfo,10,gh-75)
  Shapes.Move(speedInfo,325,gh-75)
  Shapes.Move(timeInfo,325,gh-35)
  Controls.SetTextBoxText(speed,"1")
  Controls.SetTextBoxText(time,"0")
  LDControls.AllowDrop(fileInfo)
  LDShapes.AnimateOpacity(fileInfo,1000,0)
  gwf = Desktop.Width - 30
  ghf = Desktop.Height - 80
  'komma[1] = "."                                        number with "."
  komma[1] = ","
EndSub 'Initialisation

Sub OnButtonClicked
  button = Controls.LastClickedButton
  If (button = play) Then
    LDControls.MediaPlayerPlay(player)
  ElseIf (button = stop) Then
    LDControls.MediaPlayerStop(player)
  ElseIf (button = pause) Then
    LDControls.MediaPlayerPause(player)
  ElseIf (button = applySpeed) Then
    LDControls.MediaPlayerSpeed(player,Controls.GetTextBoxText(speed))
  ElseIf (button = applyTime) Then
    LDControls.MediaPlayerSeek(player,1000*Controls.GetTextBoxText(time))
  ElseIf (button = volumeUp) Then
    volume = volume + 0.1
    If (volume > 1) Then
      volume = 1
    EndIf
    LDControls.MediaPlayerVolume(player,volume)
  ElseIf (button = volumeDown) Then
    volume = volume - 0.1
    If (volume < 0) Then
      volume = 0
    EndIf
    LDControls.MediaPlayerVolume(player,volume)
  ElseIf (button = FullDisplay) Then
    GraphicsWindow.Width = gwf
    GraphicsWindow.Height = ghf
    LDControls.MediaPlayerStretch(player, "Fill") 
  ElseIf (button = NormalDisplay) Then
    gw = 720
    gh = 640
    GraphicsWindow.Width = gw
    GraphicsWindow.Height = gh
    LDControls.MediaPlayerStretch(player, "None") 
  ElseIf (button = Off) Then  
    Playing = 0
  EndIf
EndSub 

Sub OnMediaPlayerEnded
  LDControls.MediaPlayerStop(player)
EndSub

Sub OnFileDropped
  If (LDControls.LastDropShape = fileInfo) Then
    files = LDControls.LastDropFiles
    LDControls.MediaPlayerLoad(player,files[1])
    LDControls.MediaPlayerPlay(player)
  EndIf
EndSub

Sub OnTick
  pp1 = LDText.Split(LDControls.MediaPlayerPosition(player), komma)
  po = Math.Round(pp1[1]/1000)
  pp2 = LDText.Split(LDControls.MediaPlayerPlayTime(player), komma)
  pl = Math.Round(pp2[1]/1000)
  Shapes.SetText(timeInfo, "Time ("+po+" of "+pl+" sec)")
EndSub
  
