CreatCards()
While ("True")
  HideCards()
  Program.Delay(1000)
  ShuffleCards()
  DealCards()
  Program.Delay(2000)
EndWhile

'Create cards
Sub CreatCards
  GraphicsWindow.FontSize = 25
  GraphicsWindow.Title = "Small Basic Cards"
  GraphicsWindow.BackgroundColor = "Green"
  GraphicsWindow.FontName = "Arial"
  GraphicsWindow.FontBold = "True"
  GraphicsWindow.Width = (2.5*13+1)*GraphicsWindow.FontSize 'Scale window to fit 13 spaced cards
  'Unicode for suits http://en.wikipedia.org/wiki/Playing_cards_in_Unicode
  suit[1] = Text.GetCharacter(9827) 'Club
  suit[2] = Text.GetCharacter(9830) 'Diamond
  suit[3] = Text.GetCharacter(9829) 'Heart
  suit[4] = Text.GetCharacter(9824) 'Spade
  For i = 1 To 4
    If (i = 2 Or i = 3) Then
      GraphicsWindow.BrushColor = "Red"
    Else
      GraphicsWindow.BrushColor = "Black"
    EndIf
    cardsOrdered[(i-1)*13+1] = Shapes.AddText("A"+suit[i])
    For j = 2 To 10
      cardsOrdered[(i-1)*13+j] = Shapes.AddText(j+suit[i])
    EndFor
    cardsOrdered[(i-1)*13+11] = Shapes.AddText("J"+suit[i])
    cardsOrdered[(i-1)*13+12] = Shapes.AddText("Q"+suit[i])
    cardsOrdered[(i-1)*13+13] = Shapes.AddText("K"+suit[i])
  EndFor
EndSub

'Hide all created cards off screen ready to be dealt
Sub HideCards
  For i = 1 To Array.GetItemCount(cardsOrdered)
    Shapes.Animate(cardsOrdered[i], GraphicsWindow.Width/2, GraphicsWindow.Height+100,1000)
  EndFor
EndSub

'Suffle cards to Stack
Sub ShuffleCards
  cards = cardsOrdered ' A copy to work with
  While (Array.GetItemCount(cards) > 0)
    cardIndices = Array.GetAllIndices(cards) 'Ordered indices of remaining cards
    numCard = Array.GetItemCount(cardIndices) 'Number of remaining cards
    iCard = Math.GetRandomNumber(numCard) 'Get a random card
    Stack.PushValue("shuffled",cards[cardIndices[iCard]]) 'Add selected card to stack
    cards[cardIndices[iCard]] = "" 'Remove selected card
  EndWhile
EndSub

'Deal shuffled cards from Stack
Sub DealCards
  x = 20
  y = 10
  speed = 8
  While (Stack.GetCount("shuffled") > 0)
    card = Stack.PopValue("shuffled")
    Shapes.Animate(card,x,y,1000/speed)
    Program.Delay(1.5*1000/speed)
    x = x + 2.5*GraphicsWindow.FontSize
    If (x > GraphicsWindow.Width - 2.5*GraphicsWindow.FontSize) Then
      x = 20
      y = y+GraphicsWindow.FontSize
    EndIf
  EndWhile
EndSub
