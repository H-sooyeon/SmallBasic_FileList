' SmallBasic Version 1.2
' Program: Counter
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/78d690e1-8369-42d3-b962-0c1203004676/did-you-find-any-trivia-about-small-basic
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev
' Comment: http://ru.maisfontes.com/?fnome=Erbos+Draco
'  http://www.fontspace.com/total-fontgeek-dtf-ltd/erbos-draco-monospaced-nbp
' Variables: 
' ToDo: 
'===============================================================================
fntDir = "C:\path\to\directory\"

fnt = fntDir +"#Erbos Draco 1st NBP"  ' dark BG
'fnt = fntDir +"#Erbos Draco 1st NBP, Digital-7 Mono"  ' or CompositeFont
'fnt =  fntDir +"#Erbos Draco 1st Open NBP"  '  with FontSize 36 , Digital-7 Mono" ' white BG

def = "1=10;2=20;3=1000;"       ' initial values "min;max;interval"  (n, max, intv)
maxVal = 9999                   ' Alt: define nDigits -> maxVal = Math.Power(10, nDigits+1) - 1
For n = 1 To Text.GetLength(maxVal)-1 ' Math.Floor(Math.Log(maxVal)) ' nDigits-1
  pre = Text.Append(pre, "0")
EndFor
err = "'Max' value has to be > 'Min' value and maximal "+ maxVal +"!"


GraphicsWindow.CanResize = ""
GraphicsWindow.Width = 340
GraphicsWindow.Height = 104
GraphicsWindow.Title = "Counter"
LDGraphicsWindow.TopMost = "True"
'LDGraphicsWindow.Icon = "C:\Program Files (x86)\Microsoft\Small Basic\Projects\Icons\Counter.ico"
GraphicsWindow.BackgroundColor = "#505050"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontName = fnt
GraphicsWindow.FontBold = ""
GraphicsWindow.FontSize = 72  ' 36 with font "#Erbos Draco 1st Open NBP"
shp = Shapes.AddText(Text.GetSubTextToEnd(Text.Append(pre,def[1]), Text.GetLength(def[1]))) ' def[1] = n
LDShapes.BrushColour(shp, "Lime")
Shapes.Move(shp, 10,10)

GraphicsWindow.FontSize = 11
GraphicsWindow.FontName = "Tahoma"
tbMin = Controls.AddTextBox(150,10)
tbMax = Controls.AddTextBox(200,10)
tbIntv = Controls.AddTextBox(200,40)
For n = 1 To 3
  Controls.SetSize("TextBox"+n, 40,21)
  LDShapes.SetProperty("TextBox"+n, "TextAlignment","Right")
  Controls.SetTextBoxText("TextBox"+n, def[n])
EndFor
GraphicsWindow.DrawText(193,13, "-")
GraphicsWindow.DrawText(250,13, "Min - Max values")
GraphicsWindow.DrawText(250,43, "Interval [ms]")

dur = Shapes.AddText("")  '(dt)
LDShapes.SetSize(dur, 40,21)
LDShapes.SetProperty(dur, "TextAlignment", "Right")
Shapes.Move(dur, 200,73)
GraphicsWindow.DrawText(250,73, "Duration [sec]")   ' Timespan
OnTextTyped()       ' calculate & show duration dt

GraphicsWindow.FontSize = 18
GraphicsWindow.FontBold = "True"
btn = Controls.AddButton("Start", 10,60)
LDShapes.BrushColour(btn, "PaleGreen")
Controls.SetSize(btn, 128,34)


Controls.ButtonClicked = OnButtonClick
Controls.TextTyped = OnTextTyped
GraphicsWindow.KeyDown = OnKeyDown
Timer.Tick = OnTimerTick


' ////////////////////  EVENTs  \\\\\\\\\\\\\\\\\\\\
Sub OnButtonClick
  n = Controls.GetTextBoxText(tbMin)
  max = Controls.GetTextBoxText(tbMax)
  If n < max And max <= maxVal Then
    LDShapes.SetProperty(btn, "IsEnabled","False")
    Controls.SetButtonCaption(btn, ". . .")
    Shapes.SetText(shp, Text.GetSubTextToEnd(Text.Append(pre,n), Text.GetLength(n)))
    Timer.Interval = Controls.GetTextBoxText(tbIntv)
  Else
    GraphicsWindow.ShowMessage(err, "Check input VALUES")
  EndIf
EndSub

Sub OnTextTyped
  n = Controls.GetTextBoxText(tbMin)
  max = Controls.GetTextBoxText(tbMax)
  intv = Controls.GetTextBoxText(tbIntv)
  dt = (max - n) * intv / 1000
  Shapes.SetText(dur, dt)
EndSub

Sub OnKeyDown
  If GraphicsWindow.LastKey = "Escape" Then
    Program.End()
  EndIf
EndSub

Sub OnTimerTick
  n = n + 1
  nn = Text.GetSubTextToEnd(Text.Append(pre,n), Text.GetLength(n))
  Shapes.SetText(shp, nn)
  If n >= max Then
    'n = 0
    Timer.Pause()
    LDShapes.SetProperty(btn, "IsEnabled","True")
    Controls.SetButtonCaption(btn, "Start")
  EndIf
EndSub

