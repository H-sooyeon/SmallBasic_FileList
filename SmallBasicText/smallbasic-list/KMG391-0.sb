' Combo Box Sample

' Version 0.1b

' Copyright Â© 2015 Nonki Takahashi.  The MIT License.

' Last update 2015-10-24

' Program ID KMG391-0

' 

GraphicsWindow.Title = "Combo Box Sample 0.1b"

GraphicsWindow.BackgroundColor = "LightGray"

Controls_Init()

item = "1=\=;2=>;3=<;4=>\=;5=<\=;6=<>;"

left = 10

top = 10

Controls_AddComboBox()

item = "1=And;2=Or;"

left = 10

top = 40

Controls_AddComboBox()

While "True"

  If Controls_triggered Then

    Controls_Proc()

  Else

    Program.Delay(200)

  EndIf

EndWhile

Sub Controls_AddComboBox

  ' param item - array of items

  ' param left - the x co-ordinate of the combo box

  ' param top - the y co-ordinate of the combo box

  ' return cbox - the combo box

  ' version 0.1a

  nCBox = nCBox + 1

  fs = GraphicsWindow.FontSize

  cbProp[nCBox]["height"] = fs * 1.8

  nItem = Array.GetItemCount(item)

  cbProp[nCBox]["nItem"] = nItem

  cbProp[nCBox]["item"] = item

  widthMax = 0

  For i = 1 To nItem

    len = Text.GetLength(item[i])

    width = 0

    For j = 1 To len

      width = width + Math.Floor(luw[Text.GetSubText(item[i], j, 1)] * fs / 100)

    EndFor

    If widthMax < width Then

      widthMax = width

    EndIf

  EndFor

  tboxWidth = widthMax + fs

  cbProp[nCBox]["width"] = tboxWidth

  cbProp[nCBox]["x"] = left

  cbProp[nCBox]["y"] = top

  cbProp[nCBox]["tbox"] = Controls.AddTextBox(left, top)

  Controls.SetSize(cbProp[nCBox]["tbox"], tboxWidth, cbProp[nCBox]["height"])

  cbProp[nCBox]["btn"] = Controls.AddButton("?–¾", left + tboxWidth - 1, top)

  Controls.SetSize(cbProp[nCBox]["btn"], fs * 1.8, cbProp[nCBox]["height"])

  cbox = "ComboBox" + nCBox

  Controls.ButtonClicked = Controls_OnButtonClicked

  GraphicsWindow.MouseMove = Controls_OnMouseMove

  GraphicsWindow.MouseDown = Controls_OnMouseDown

EndSub

Sub Controls_Proc

  ' version 0.1a

  For i = 1 To nCBox

    If Controls.LastClickedButton = cbProp[i]["btn"] Then

      GraphicsWindow.PenColor = "LightGray"

      GraphicsWindow.PenWidth = 1

      GraphicsWindow.BrushColor = "White"

      rect = Shapes.AddRectangle(cbProp[i]["width"], cbProp[i]["height"] * cbProp[i]["nItem"])

      Shapes.Move(rect, cbProp[i]["x"], cbProp[i]["y"] + cbProp[i]["height"] - 1)

      GraphicsWindow.BrushColor = "Black"

      For j = 1 To cbProp[i]["nItem"]

        it[j] = Shapes.AddText(cbProp[i]["item"][j])

        Shapes.Move(it[j], cbProp[i]["x"] + cbProp[i]["height"] * 0.3, cbProp[i]["y"] + (j + 0.2) * cbProp[i]["height"])

      EndFor

      Controls_mouseDown = "False"

      While Not[Controls_mouseDown]

        If Controls_mouseMove Then

          Controls_Select()

          Controls_mouseMove = "False"

        Else

          Program.Delay(200)

        EndIf

      EndWhile

      Controls_Select()

      Shapes.Remove(cbSelect)

      cbSelect = ""

      For j = 1 To cbProp[i]["nItem"]

        Shapes.Remove(it[j])

      EndFor

      Shapes.Remove(rect)

      If selected <> 0 Then

        Controls.SetTextBoxText(cbProp[i]["tbox"],cbProp[i]["item"][selected])

      EndIf

    EndIf

  EndFor

  Controls_triggered = "False"

EndSub

Sub Controls_Init

  ' version 0.1a

  Not = "False=True;True=False;"

  GraphicsWindow.FontName = "Lucida UI"

  GraphicsWindow.BrushColor = "Black"

  WQ = Text.GetCharacter(34)

  ' Lucida UI font width [px] table while the size (height) 100 [px]

  luw = " =60;!=65;" + WQ + "=82;#=92;$=90;%=119;&=117;'=62;(=69;)=69;"

  luw = luw + "*=78;+=103;,=59;-=73;.=59;/=77;0=90;1=90;2=90;3=90;4=90;"

  luw = luw + "5=90;6=90;7=90;8=90;9=90;:=59;\;=59;<=103;\==103;>=103;"

  luw = luw + "?=76;@=128;A=86;B=94;C=80;D=94;E=86;F=71;G=94;H=93;I=61;"

  luw = luw + "J=61;K=88;L=61;M=124;N=93;O=94;P=94;Q=94;R=72;S=76;T=71;"

  luw = luw + "U=93;V=87;W=112;X=88;Y=86;Z=80;[=69;\\=76;]=69;^=103;_=74;"

  luw = luw + "`=64;{=69;|=65;}=69;~=103;Â =60;Â¡=65;Â¢=90;Â£=90;Â¤=88;Â¥=90;"

  luw = luw + "Â¦=65;Â§=81;Â¨=79;Â©=120;Âª=73;Â«=90;Â¬=103;Â­=32;Â®=120;Â¯=74;Â°=70;"

  luw = luw + "Â±=103;Â²=73;Â³=73;Â´=63;Âµ=94;Â¶=83;Â·=59;Â¸=54;Â¹=72;Âº=78;Â»=90;"

  luw = luw + "Â¼=128;Â½=129;Â¾=130;Â¿=76;?=86;Ã=86;Ã‚=86;Ãƒ=86;Ã„=86;Ã…=86;Ã†=115;"

  luw = luw + "Ã‡=80;Ãˆ=86;Ã‰=86;ÃŠ=86;Ã‹=86;ÃŒ=61;Ã=61;Ã=61;Ã=61;Ã=92;Ã‘=93;Ã’=93;"

  luw = luw + "Ã“=93;Ã”=93;Ã•=93;Ã–=93;Ã—=103;Ã˜=94;Ã™=93;Ãš=93;Ã›=93;Ãœ=93;Ã=86;"

  luw = luw + "Ã=94;ÃŸ=95;Ã·=103;Ã¿=86;"

  Controls_triggered = "False"

EndSub

Sub Controls_OnButtonClicked

  ' version 0.1a

  Controls_triggered = "True"

EndSub

Sub Controls_OnMouseMove

  ' version 0.1a

  Controls_mx = GraphicsWindow.MouseX

  Controls_my = GraphicsWindow.MouseY

  Controls_mouseMove = "True"

EndSub

Sub Controls_OnMouseDown

  ' version 0.1a

  Controls_mx = GraphicsWindow.MouseX

  Controls_my = GraphicsWindow.MouseY

  Controls_mouseDown = "True"

EndSub

Sub Controls_Select

  ' version 0.1a

  selected = 0

  If cbProp[i]["x"] <= Controls_mx And Controls_mx <= cbProp[i]["x"] + cbProp[i]["width"] Then

    For j = 1 To cbProp[i]["nItem"]

      y1 = cbProp[i]["y"] + j * cbProp[i]["height"]

      y2 = y1 + cbProp[i]["height"]

      If y1 <= Controls_my And Controls_my <= y2 Then

        GraphicsWindow.BrushColor = "#663399FF"

        GraphicsWindow.PenWidth = 0

        If cbSelect = "" Then

          cbSelect = Shapes.AddRectangle(cbProp[i]["width"], cbProp[i]["height"])

        EndIf

        Shapes.Move(cbSelect, cbProp[i]["x"], y1 - 1)

        selected = j

      EndIf

    EndFor

  EndIf

EndSub


