'Mapping Sample
'**************************************************************************************
'Details available @ http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Map/
'Import: QXX945-0  Runs locally only!  Updated November 26, 2014
'**************************************************************************************

Initialise()

'==============================================================
'MAIN LOOP

While 0=0
  'UPDATE PROGRAM
  If mouseMove Then    
    mouseX = GraphicsWindow.MouseX
    mouseY = GraphicsWindow.MouseY
    pixel = GraphicsWindow.GetPixel(mouseX, mouseY)
    
    If pixel <> "#000000" And mouseX <= ImageList.GetWidthOfImage(map) Then      
      Shapes.ShowShape(contextHelpFrame)
      Shapes.ShowShape(contextHelp)
    Else
      Shapes.HideShape(contextHelpFrame)
      Shapes.HideShape(contextHelp)
    EndIf
    
    mouseMove = "False"
  EndIf
  
  'UPDATE SCREEN
  Shapes.Zoom(contextHelpFrame, legend[pixel]["scaleX"], 1)
  Shapes.SetText(contextHelp, legend[pixel]["name"])
  Shapes.Move(contextHelpFrame, mouseX + 10, mouseY + 10)
  Shapes.Move(contextHelp, mouseX + 3 + 10, mouseY + 3 + 10)
  
  Program.Delay(20)
EndWhile

'===================================================================
'SUBROUTINE(S)

Sub Initialise
  'SET CONSTANTS
  FONTSIZE = GraphicsWindow.FontSize
  BRUSHCOLOR = GraphicsWindow.BrushColor
  LF = Text.GetCharacter(10)
  
  'SET LEGEND VALUES
  legend["#FF1493"] = "name=Lairmairrener;scaleX=2.25" 'deep pink  
  legend["#00FF00"] = "name=Tommeginne;scaleX=2.2"   'lime
  legend["#00FFFF"] = "name=Tyerremotepanner;scaleX=3"  'cyan
  legend["#006400"] = "name=Pyemmairre;scaleX=2" 'darkgreen
  legend["#800000"] = "name=Paredarerme;scaleX=2.15" 'maroon
  legend["#808080"] = "name=where Hobart is;scaleX=2.6"  'gray
  legend["#0000FF"] = "name=Nuenonne;scaleX=1.7" 'blue
  legend["#FFD700"] = "name=Toogee;scaleX=1.28"  'gold
  legend["#800080"] = "name=Peerapper;scaleX=1.8"  'purple
  legend["#F4A460"] = "name=haven't told us yet;scaleX=3.05"  'sandybrown   
  
  'PRE SETUP MESSAGE
  GraphicsWindow.Title = "Loading..."
  GraphicsWindow.DrawText(20, GraphicsWindow.Height /2, "D/loading Map, please wait")
  
  'LOAD ASSET(S)
  map = ImageList.LoadImage("http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Map/assets/images/map-of-tasmania.png")
  
  'VALIDATE LOAD
  If ImageList.GetWidthOfImage(map) = 0 Then
    GraphicsWindow.ShowMessage("Check your connection and/or try again."+LF+"End Program?", "File Failed to Download")
    Program.End()
  EndIf  
  
  'WINDOW SETTINGS
  GraphicsWindow.Title = "Mapping Sample"
  GraphicsWindow.Width = ImageList.GetWidthOfImage(map) * 2.5
  GraphicsWindow.Height = ImageList.GetHeightOfImage(map) + 20
  GraphicsWindow.CanResize = "False"
  
  'ADD CONTEXT SENSITIVE SHAPES
  GraphicsWindow.BrushColor = "LightGray"
  GraphicsWindow.PenWidth = 0.5  
  contextHelpFrame = Shapes.AddRectangle(40, 20)
  
  GraphicsWindow.BrushColor = "Black"  
  contextHelp = Shapes.AddText("") 
  
  Shapes.SetOpacity(contextHelpFrame, 85) 
  Shapes.HideShape(contextHelpFrame)
  Shapes.HideShape(contextHelp)
  
  'DRAW SCREEN
  GraphicsWindow.FontSize = 20 
  GraphicsWindow.BrushColor = BRUSHCOLOR
  GraphicsWindow.DrawText(ImageList.GetWidthOfImage(map) + 50, 10, "Indigenous Map" + LF + "of Tasmania")
  GraphicsWindow.FontSize = FONTSIZE
  GraphicsWindow.DrawText(ImageList.GetWidthOfImage(map) + 50, 120, "Move the mouse over the map" +LF+ "to reveal name of country")
  GraphicsWindow.DrawImage(map, 10, 10)  
  
  'REGISTER EVENT(S)
  GraphicsWindow.MouseMove = OnMouseMove
EndSub

'==============================================================================
'EVENT HANDLER(S)

Sub OnMouseMove
  mouseMove = "True"
EndSub
