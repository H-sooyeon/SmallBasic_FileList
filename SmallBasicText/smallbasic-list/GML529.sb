'Initial Prototype 2 -- by Matthew L. Parets aka codingCat
'High score initial entery system. This version
'breaks down the inital entry loop making the routine
're-enterant.  This allows other systems to remain active. 
'To show the Effect I am spinning a dash in one corner. 

GraphicsWindow.Show()
GraphicsWindow.FontSize = 20
dash = Shapes.AddText("-")
angle = 0
GraphicsWindow.BackgroundColor = "black"
exiting = "False"
GraphicsWindow.KeyDown = OnKeyDown

InitialsEntryBuild()
Program.Delay(1000)

initialEntryShow()
While InitEntry = "True"
  EnterInitials()
  SpinDash()
  Program.Delay(50)
endwhile
InitialEntryHide()

Sub SpinDash
  angle = (Math.Remainder(angle+10,360))
  Shapes.Rotate(dash,angle)
EndSub

Sub InitialsEntryBuild
  initials = ""
  curWid = 50
  curHei = 65
  promptx = (GraphicsWindow.Width / 2) - (((curWid + 5) * 1) / 2)
  prompty = (GraphicsWindow.Height / 2) - 0
  
  GraphicsWindow.BackgroundColor = "black"
  GraphicsWindow.FontName = "Tahoma"
  GraphicsWindow.BrushColor = "white"
  GraphicsWindow.FontSize = 20
  titleinitials1 = Shapes.AddText("Enter initials for the")
  GraphicsWindow.FontSize = 30
  titleinitials2 = Shapes.AddText("Leader Board")
  Shapes.Move(titleinitials1, 25, prompty - 25)
  Shapes.HideShape(titleinitials1)
  Shapes.Move(titleinitials2, 25, prompty - 0)
  Shapes.HideShape(titleinitials2)
  
  cclrnam[1] = "cyan"
  cclrnam[2] = "magenta"
  cclrnam[3] = "yellow"
  For cci = 1 To Array.GetItemCount(cclrnam)
    GraphicsWindow.PenColor = cclrnam[cci]
    GraphicsWindow.BrushColor = cclrnam[cci]
    cursor[cci] = Shapes.AddRectangle(curWid,curHei)
    Shapes.HideShape(cursor[cci])
  EndFor
EndSub

Sub initialEntryShow
  curcnt = Array.GetItemCount(cursor)
  
  Shapes.ShowShape(titleinitials1)
  Shapes.ShowShape(titleinitials2)
  
  GraphicsWindow.FontName = "Courier New"
  GraphicsWindow.FontBold = "True"
  GraphicsWindow.FontSize = 70
  cur = 1
  pressed = "False"
  pos = 0
  InitEntry = "true"
EndSub

sub InitialEntryHide
  GraphicsWindow.BrushColor = "black"
  For ii = 1 To Text.GetLength(initials)
    GraphicsWindow.FillRectangle(promptx + ((curWid + 5) * (pos-ii)),prompty+5,curWid,curHei)  
  EndFor
  For ii = 1 To curcnt
    Shapes.HideShape(cursor[ii])
  EndFor
  Shapes.HideShape(titleinitials1)
  Shapes.HideShape(titleinitials2)
endsub


Sub EnterInitials
  'Prompt for initials when a high score has been earned
  Shapes.HideShape(cursor[cur])
  cur = cur + 1
  If cur > curcnt Then
    cur = 1
  EndIf
  
  If pressed = "True" Then 
    key = GraphicsWindow.LastKey
    If key = "Return" then
      InitEntry = "False"
    EndIf
    If key = "Space" then
      key = " "
    endif
    If Text.GetLength(key) = 2 and Text.GetSubText(key,1,1) = "D" then
      key = Text.GetSubTextToEnd(key,2)
    endif
    If (key = "Back" Or key = "Left") and pos > 0 then
      GraphicsWindow.BrushColor = "black"
      GraphicsWindow.FillRectangle(promptx + ((curWid + 5) * (pos-1)),prompty+5,curWid,curHei)
      initials = Text.GetSubText(initials,1,Text.GetLength(initials)-1)
      pos = pos - 1
    ElseIf pos < 3 then
      If Text.GetLength(key) = 1 then
        keycode = Text.GetCharacterCode(key)
        If keycode >= 97 And keycode <= 122 then
          keycode = keycode - 32
        EndIf
        If keycode = 32 or (keycode >= 65 and keycode <= 90) or (keycode >= 48 and keycode < 57) then
          key = Text.GetCharacter(keycode)
          initials = initials + key
          GraphicsWindow.BrushColor = "darkgray"
          GraphicsWindow.DrawText(promptx + (((curWid + 5) * pos+1)) + 2 + 2,prompty + 2,key)
          GraphicsWindow.BrushColor = "white"
          GraphicsWindow.DrawText(promptx + (((curWid + 5) * pos+1)) + 2,prompty,key)
          pos = pos + 1
        endif
      EndIf
    endif
    pressed = "False"
  EndIf
  
  Shapes.Move(cursor[cur],promptx + ((curWid + 5) * pos),prompty+5)
  Shapes.ShowShape(cursor[cur])
endsub

















Sub OnKeyDown
  'Take note of the fact that a key has been pressed
  pressed = "True"
EndSub
