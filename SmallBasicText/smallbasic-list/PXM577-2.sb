' Happy New Year 2017
' Version 1.8
' Copyright © 2016 Nonki Takahashi.  The MIT License.
' Program ID PXM577-2
'
GraphicsWindow.Title = "Happy New Year 2017"
bg = "YellowGreen"
GraphicsWindow.BackgroundColor = bg
threepoint = "True"
debug = "False"
Init()
CountDown()
InitHen()
xmaxHen = xmax
zmaxHen = zmax
InitChick()
bx = xmaxHen + xmax + 4
by = 0
bz = zmaxHen - zmax
DrawBlock()
bx = xmaxHen + 2
by = 0
bz = zmaxHen - zmax
DrawBlock()
InitHen()
bx = 0
by = 0
bz = 0
DrawBlock()
GraphicsWindow.FontName = "Bradley Hand ITC"
GraphicsWindow.FontSize = 40
GraphicsWindow.BrushColor = "Green"
GraphicsWindow.FontBold = "False"
GraphicsWindow.DrawText(100, 40, "Happy New Year 2017")

Sub CountDown
  year = Clock.Year
  If year < 2017 Then
    GraphicsWindow.BrushColor = "Green"
    GraphicsWindow.PenWidth = 0
    mask = Shapes.AddRectangle(gw, gh)
    GraphicsWindow.BrushColor = "White"
    GraphicsWindow.FontName = "Consolas"
    GraphicsWindow.FontSize = 100
    txt = Shapes.AddText("")
    Shapes.Move(txt, 80, 120)
    sec2017 = (((31 - 1) * 24 + 24) * 60 + 0) * 60 + 0
    sec = (((Clock.Day - 1) * 24 + Clock.Hour) * 60 + Clock.Minute) * 60 + Clock.Second
    secRemain = sec2017 - sec
    While year < 2017
      ShowCountDown()
      Program.Delay(200)
      sec = (((Clock.Day - 1) * 24 + Clock.Hour) * 60 + Clock.Minute) * 60 + Clock.Second
      secRemain = sec2017 - sec
      year = Clock.Year
    EndWhile
    Shapes.Remove(txt)
    Shapes.Remove(mask)
  EndIf
EndSub

Sub Init
  UNDEFINED = "N/A"
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  xo = 0.7 * gw
  yo = 0.55 * gh
  u = 20
  ru = u * Math.SquareRoot(2 / 3)
  a60 = Math.GetRadians(60)
  a120 = Math.GetRadians(120)
  If threepoint Then
    rv = 2 * gw
    vx["x"] = xo + rv * Math.Sin(-a60)
    vx["y"] = yo - rv * Math.Cos(-a60)
    vy["x"] = xo + rv * Math.Sin(a60)
    vy["y"] = yo - rv * Math.Cos(a60)
    vz["x"] = xo
    vz["y"] = yo + rv
  Else
    rv = "∞"
  EndIf
  Colors_Init()
  palette = "O=Orange;Y=Yellow;B=Black;R=Red;W=White;"
EndSub

Sub InitHen
  xmax = 11
  ymax = 6
  zmax = 12
  
  block[12][6] = "    O       "
  block[12][5] = "    OOO     "
  block[12][4] = "    O       "
  block[12][3] = "            "
  block[12][2] = "    O       "
  block[12][1] = "    OOO     "
  block[12][0] = "    O       "
  
  block[11][6] = "            "
  block[11][5] = "    O       "
  block[11][4] = "            "
  block[11][3] = "            "
  block[11][2] = "            "
  block[11][1] = "    O       "
  block[11][0] = "            "
  
  block[10][6] = "            "
  block[10][5] = "    O       "
  block[10][4] = "            "
  block[10][3] = "            "
  block[10][2] = "            "
  block[10][1] = "    O       "
  block[10][0] = "            "
  
  block[9][6] = "            "
  block[9][5] = "    W       "
  block[9][4] = "            "
  block[9][3] = "            "
  block[9][2] = "            "
  block[9][1] = "    W       "
  block[9][0] = "            "
  
  block[8][6] = "    WW      "
  block[8][5] = "  WWWWWW    "
  block[8][4] = "  WWWWWW    "
  block[8][3] = "  WWWWWW    "
  block[8][2] = "  WWWWWW    "
  block[8][1] = "  WWWWWW    "
  block[8][0] = "    WW      "
  
  block[7][6] = "   WWWW     "
  block[7][5] = "  WWWWWW    "
  block[7][4] = "  WWWWWW    "
  block[7][3] = "  WWWWWW    "
  block[7][2] = "  WWWWWW    "
  block[7][1] = "  WWWWWW    "
  block[7][0] = "   WWWW     "
  
  block[6][6] = "  WWWWW     "
  block[6][5] = "  WWWWWW    "
  block[6][4] = "  WWWWWW    "
  block[6][3] = "  WWWWWW    "
  block[6][2] = "  WWWWWW    "
  block[6][1] = "  WWWWWW    "
  block[6][0] = "  WWWWW     "
  
  block[5][6] = "            "
  block[5][5] = "  WWWWWW    "
  block[5][4] = "  WWWWWww   "
  block[5][3] = " wWWWWWww   "
  block[5][2] = "  WWWWWww   "
  block[5][1] = "  WWWWWW    "
  block[5][0] = "            "
  
  block[4][6] = "            "
  block[4][5] = "            "
  block[4][4] = "      WWW   "
  block[4][3] = "WW    WWWR  "
  block[4][2] = "      WWW   "
  block[4][1] = "            "
  block[4][0] = "            "
  
  block[3][6] = "            "
  block[3][5] = "            "
  block[3][4] = "       WWW  "
  block[3][3] = "       WWR  "
  block[3][2] = "       WWW  "
  block[3][1] = "            "
  block[3][0] = "            "
  
  block[2][6] = "            "
  block[2][5] = "            "
  block[2][4] = "       WBW  "
  block[2][3] = "       WWWOO"
  block[2][2] = "       WBW  "
  block[2][1] = "            "
  block[2][0] = "            "
  
  block[1][6] = "            "
  block[1][5] = "            "
  block[1][4] = "       WWW  "
  block[1][3] = "       WWRR "
  block[1][2] = "       WWW  "
  block[1][1] = "            "
  block[1][0] = "            "
  
  block[0][6] = "            "
  block[0][5] = "            "
  block[0][4] = "            "
  block[0][3] = "        RR  "
  block[0][2] = "            "
  block[0][1] = "            "
  block[0][0] = "            "
EndSub

Sub InitChick
  xmax = 5
  ymax = 4
  zmax = 6
  
  block[6][4] = "      "
  block[6][3] = "  OO  "
  block[6][2] = "      "
  block[6][1] = "  OO  "
  block[6][0] = "      "
  
  block[5][4] = "      "
  block[5][3] = "  O   "
  block[5][2] = "      "
  block[5][1] = "  O   "
  block[5][0] = "      "
  
  block[4][4] = "      "
  block[4][3] = " YYY  "
  block[4][2] = " YYY  "
  block[4][1] = " YYY  "
  block[4][0] = "      "
  
  block[3][4] = " YY   "
  block[3][3] = " YYY  "
  block[3][2] = " YYY  "
  block[3][1] = " YYY  "
  block[3][0] = " YY   "
  
  block[2][4] = "      "
  block[2][3] = " YYYY "
  block[2][2] = "YYYYY "
  block[2][1] = " YYYY "
  block[2][0] = "      "
  
  block[1][4] = "      "
  block[1][3] = "  YBY "
  block[1][2] = "  YYYO"
  block[1][1] = "  YBY "
  block[1][0] = "      "
  
  block[0][4] = "      "
  block[0][3] = "  YYY "
  block[0][2] = "  YYY "
  block[0][1] = "  YYY "
  block[0][0] = "      "
EndSub

Sub DrawBlock
  ' param bx, by, bz - base point
  ' param xmax, ymax, zmax - size of block array
  param = ""
  For _z = zmax To 0 Step -1
    param["z"] = _z + bz
    For _y = ymax To 0 Step -1
      param["y"] = _y + by
      For _x = xmax To 0 Step -1
        param["x"] = _x + bx
        color = Text.GetSubText(block[_z][_y], (xmax + 1) - _x, 1)
        If color <> " " Then
          param["color"] = palette[color]
          DrawVoxel()
        EndIf
      EndFor
    EndFor
  EndFor
EndSub

Sub CalcColors
  color = param["color"]
  If color = "Transparent" Then
    transparent = "True"
    color = "Black"
  Else
    transparent = "False"
  EndIf
  Color_NameToColor()
  colorTop = color
  rate = 0.2
  Color_Blacken()
  colorLeft = color
  color = colorTop
  rate = 0.4
  Color_Blacken()
  colorRight = color
  If transparent Then
    colorTop = "#66" + Text.GetSubTextToEnd(colorTop, 2)
    colorLeft = "#66" + Text.GetSubTextToEnd(colorLeft, 2)
    colorRight = "#66" + Text.GetSubTextToEnd(colorRight, 2)
  EndIf
EndSub

Sub CalcVertex
  ' Calcurate vertex between line p1-p2 and line p3-p4
  a = p1["x"] * p2["y"] - p1["y"] * p2["x"]
  b = p1["y"] - p2["y"]
  c = p1["x"] - p2["x"]
  d = p3["x"] * p4["y"] - p3["y"] * p4["x"]
  e = p3["y"] - p4["y"]
  f = p3["x"] - p4["x"]
  If b = 0 Then
    p["y"] = a / c
    p["x"] = (f * p["y"] - d) / e
  ElseIf c = 0 Then
    p["x"] = -a / b
    p["y"] = (e * p["x"] + d) / f
  Else
    p["x"] = ((a * f) - (c * d)) / ((c * e) - (b * f))
    p["y"] = (a + b * p["x"]) / c
  EndIF
EndSub

Sub DrawCuboid
  Stack.PushValue("local", param)
  xmin = param["x"]
  ymin = param["y"]
  zmin = param["z"]
  xmax = param["width"] + xmin - 1
  ymax = param["height"] + ymin - 1
  zmax = param["depth"] + zmin - 1
  param = "color=" + param["color"] + ";"
  For _z = zmax To zmin Step -1
    param["z"] = _z
    For _y = ymax To ymin Step -1
      param["y"] = _y
      For _x = xmax To xmin Step -1
        param["x"] = _x
        DrawVoxel()
      EndFor
    EndFor
  EndFor
  param = Stack.PopValue("local")
EndSub

Sub DrawVoxel
  CalcColors()
  If rv = "∞" Then
    x0 = xo + ru * Math.Sin(a60) * param["y"] + ru * Math.Sin(-a60) * param["x"]
    y0 = yo - ru * Math.Cos(a60) * param["y"] + ru * param["z"] - ru * Math.Cos(-a60) * param["x"]
    GraphicsWindow.BrushColor = colorTop
    x1 = Math.Round(x0)
    y1 = Math.Round(y0 - ru)
    x2 = Math.Round(x0 + ru * Math.Sin(-a60))
    y2 = Math.Round(y0 - ru * Math.Cos(-a60))
    x3 = Math.Round(x0 + ru * Math.Sin(a60))
    y3 = Math.Round(y0 - ru * Math.Cos(a60))
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(x0 + ru * Math.Sin(-a60))
    y1 = Math.Round(y0 - ru * Math.Cos(-a60))
    x2 = Math.Round(x0 + ru * Math.Sin(a60))
    y2 = Math.Round(y0 - ru * Math.Cos(a60))
    x3 = Math.Round(x0)
    y3 = Math.Round(y0)
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    GraphicsWindow.BrushColor = colorLeft
    x1 = Math.Round(x0 + ru * Math.Sin(-a60))
    y1 = Math.Round(y0 - ru * Math.Cos(-a60))
    x2 = Math.Round(x0)
    y2 = Math.Round(y0)
    x3 = Math.Round(x0)
    y3 = Math.Round(y0 + ru)
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(x0 + ru * Math.Sin(-a60))
    y1 = Math.Round(y0 - ru * Math.Cos(-a60))
    x2 = Math.Round(x0 + ru * Math.Sin(-2 * a60))
    y2 = Math.Round(y0 - ru * Math.Cos(-2 * a60))
    x3 = Math.Round(x0)
    y3 = Math.Round(y0 + ru)
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    GraphicsWindow.BrushColor = colorRight
    x1 = Math.Round(x0 + ru * Math.Sin(a60))
    y1 = Math.Round(y0 - ru * Math.Cos(a60))
    x2 = Math.Round(x0)
    y2 = Math.Round(y0)
    x3 = Math.Round(x0)
    y3 = Math.Round(y0 + ru)
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(x0 + ru * Math.Sin(a60))
    y1 = Math.Round(y0 - ru * Math.Cos(a60))
    x2 = Math.Round(x0 + ru * Math.Sin(2 * a60))
    y2 = Math.Round(y0 - ru * Math.Cos(2 * a60))
    x3 = Math.Round(x0)
    y3 = Math.Round(y0 + ru)
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
  Else
    GraphicsWindow.BrushColor = colorTop
    sx = param["x"] + 1
    sy = param["y"] + 1
    sz = param["z"]
    Map2D()
    pxy = p
    sx = param["x"] + 1
    sy = param["y"]
    sz = param["z"]
    Map2D()
    px = p
    sx = param["x"]
    sy = param["y"] + 1
    sz = param["z"]
    Map2D()
    py = p
    sx = param["x"]
    sy = param["y"]
    sz = param["z"]
    Map2D()
    po = p
    x1 = Math.Round(pxy["x"])
    y1 = Math.Round(pxy["y"])
    x2 = Math.Round(px["x"])
    y2 = Math.Round(px["y"])
    x3 = Math.Round(py["x"])
    y3 = Math.Round(py["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(px["x"])
    y1 = Math.Round(px["y"])
    x2 = Math.Round(py["x"])
    y2 = Math.Round(py["y"])
    x3 = Math.Round(po["x"])
    y3 = Math.Round(po["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    GraphicsWindow.BrushColor = colorLeft
    sx = param["x"] + 1
    sy = param["y"]
    sz = param["z"] + 1
    Map2D()
    pxz = p
    sx = param["x"]
    sy = param["y"]
    sz = param["z"] + 1
    Map2D()
    pz = p
    x1 = Math.Round(px["x"])
    y1 = Math.Round(px["y"])
    x2 = Math.Round(pxz["x"])
    y2 = Math.Round(pxz["y"])
    x3 = Math.Round(pz["x"])
    y3 = Math.Round(pz["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(px["x"])
    y1 = Math.Round(px["y"])
    x2 = Math.Round(po["x"])
    y2 = Math.Round(po["y"])
    x3 = Math.Round(pz["x"])
    y3 = Math.Round(pz["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    GraphicsWindow.BrushColor = colorRight
    sx = param["x"]
    sy = param["y"] + 1
    sz = param["z"] + 1
    Map2D()
    pyz = p
    x1 = Math.Round(py["x"])
    y1 = Math.Round(py["y"])
    x2 = Math.Round(po["x"])
    y2 = Math.Round(po["y"])
    x3 = Math.Round(pz["x"])
    y3 = Math.Round(pz["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
    x1 = Math.Round(py["x"])
    y1 = Math.Round(py["y"])
    x2 = Math.Round(pyz["x"])
    y2 = Math.Round(pyz["y"])
    x3 = Math.Round(pz["x"])
    y3 = Math.Round(pz["y"])
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
  EndIf
EndSub

Sub Map2D
  ' param sx, sy, sz ��� 0
  ' return p["x"],  p["y"]
  k = (rv - ru) / (rv + ru)
  If sx = 0 Then
    rx = 0
  Else
    rx = Math.Power(1 + k, Math.Log(sx) / Math.Log(2)) * ru
  EndIf
  If sy = 0 Then
    ry = 0
  Else
    ry = Math.Power(1 + k, Math.Log(sy) / Math.Log(2)) * ru
  EndIf
  If sz = 0 Then
    rz = 0
  Else
    rz = Math.Power(1 + k, Math.Log(sz) / Math.Log(2)) * ru
  EndIf
  If debug Then
    TextWIndow.WriteLine("sz=" + sz) 
    TextWIndow.WriteLine("logsz=" + Math.Log(sz)) 
    TextWindow.WriteLine("rx=" + rx)
    TextWindow.WriteLine("ry=" + ry)
    TextWindow.WriteLine("rz=" + rz)
  EndIf
  _px["x"] = xo + rx * Math.Sin(-a60)
  _px["y"] = yo - rx * Math.Cos(-a60)
  If debug Then
    GraphicsWindow.PenColor = "Black"
    GraphicsWindow.DrawLine(xo, yo, _px["x"], _px["y"])
  EndIf
  _py["x"] = xo + ry * Math.Sin(a60)
  _py["y"] = yo - ry * Math.Cos(a60)
  If debug Then
    GraphicsWindow.DrawLine(xo, yo, _py["x"], _py["y"])
  EndIf
  _pz["x"] = xo
  _pz["y"] = yo + rz
  If debug Then
    GraphicsWindow.DrawLine(xo, yo, _pz["x"], _pz["y"])
  EndIf
  p1 = _px
  p2 = vy
  p3 = _py
  p4 = vx
  CalcVertex()
  If debug Then
    GraphicsWindow.DrawLine(_px["x"], _px["y"], p["x"], p["y"])
    GraphicsWindow.DrawLine(_py["x"], _py["y"], p["x"], p["y"])
  EndIf
  _pxy = p
  p1 = _px
  p2 = vz
  p3 = _pz
  p4 = vx
  CalcVertex()
  If debug Then
    GraphicsWindow.DrawLine(_px["x"], _px["y"], p["x"], p["y"])
    GraphicsWindow.DrawLine(_pz["x"], _pz["y"], p["x"], p["y"])
  EndIf
  _pxz = p
  p1 = _pxz
  p2 = vy
  p3 = _pxy
  p4 = vz
  CalcVertex()
  If debug Then
    GraphicsWindow.DrawLine(_pxy["x"], _pxy["y"], p["x"], p["y"])
    GraphicsWindow.DrawLine(_pxz["x"], _pxz["y"], p["x"], p["y"])
    GraphicsWindow.DrawLine(p["x"] - 5, p["y"] - 5, p["x"] + 5, p["y"] + 5)
    GraphicsWindow.DrawLine(p["x"] - 5, p["y"] + 5, p["x"] + 5, p["y"] - 5)
    bc = GraphicsWindow.BrushColor
    GraphicsWindow.BrushColor = "Black"
    GraphicsWindow.DrawText(p["x"], p["y"], "(" + sx + "," + sy + "," + sz + ")")
    GraphicsWindow.BrushColor = bc
  EndIf
EndSub

Sub ShowCountDown
  ' param secRemain
  sec = Math.Remainder(secRemain, 60)
  secRemain = secRemain - sec
  min = Math.Remainder(secRemain / 60, 60)
  secRemain = secRemain - min * 60
  hour = secRemain / 3600
  If hour < 10 Then
    time = Text.Append(0, hour) + ":"
  Else
    time = hour + ":"
  EndIf
  If min < 10 Then
    time = time + 0 + min + ":"
  Else
    time = time + min + ":"
  EndIf
  If sec < 10 Then
    time = time + 0 + sec
  Else
    time = time + sec
  EndIf
  Shapes.SetText(txt, time)
EndSub

Sub Color_HSLtoRGB
  ' Color | Convert HSL to RGB
  ' param hue - [0, 360) or UNDEFINED
  ' param lightness - [0, 1]
  ' param saturation - [0, 1]
  ' return r, g, b - RGB values 0..255
  If lightness <= 0.5 Then
    n2 = lightness * (1 + saturation)
  Else
    n2 = lightness + saturation - lightness * saturation
  EndIf
  n1 = 2 * lightness - n2
  If saturation = 0 Then
    r = Math.Round(lightness * 255)
    g = Math.Round(lightness * 255)
    b = Math.Round(lightness * 255)
  Else
    h = hue + 120
    Color_Value()
    r = value
    h = hue
    Color_Value()
    g = value
    h = hue - 120
    Color_Value()
    b = value
  EndIf
EndSub

Sub Color_Blacken
  ' Color | Blacken given color
  ' param color - given color
  ' param rate - 0..1
  ' return color - color blackened
  Color_NameToColor()
  Color_ColorToRGB()
  r = Math.Floor(r * (1 - rate))
  g = Math.Floor(g * (1 - rate))
  b = Math.Floor(b * (1 - rate))
  color = GraphicsWindow.GetColorFromRGB(r, g, b)
EndSub

Sub Color_Whiten
  ' Color | Whiten given color
  ' param color - given color
  ' param rate - 0..1
  ' return color - color whitened
  Color_NameToColor()
  Color_ColorToRGB()
  r = Math.Floor(r * (1 - rate) + 255 * rate)
  g = Math.Floor(g * (1 - rate) + 255 * rate)
  b = Math.Floor(b * (1 - rate) + 255 * rate)
  color = GraphicsWindow.GetColorFromRGB(r, g, b)
EndSub

Sub Color_ColorToRGB
  ' Color | Convert color to RGB values
  ' param color - "#rrggbb" (hexadecimal values)
  ' return r, g, b - RGB values 0..255
  sR = Text.GetSubText(color, 2, 2)
  sG = Text.GetSubText(color, 4, 2)
  sB = Text.GetSubText(color, 6, 2)
  hex = sR
  Math_Hex2Dec()
  r = dec
  hex = sG
  Math_Hex2Dec()
  g = dec
  hex = sB
  Math_Hex2Dec()
  b = dec
EndSub

Sub Color_NameToColor
  ' Color | Convert color name to color
  ' param color - color name
  ' returns color -"#rrggbb"
  If Text.StartsWith(color, "#") Then
    color = Text.ConvertToUpperCase(color)
  Else
    color = Text.ConvertToLowerCase(color)
    color = colors[color]
  EndIf
EndSub

Sub Color_Value
  ' Color | Function value
  ' param n1, n2
  ' param h - [-120, 480)
  ' return value - 0..255
  If h >= 360 Then
    h = h - 360
  EndIF
  If h < 0 Then
    h = h + 360
  EndIF 
  If h < 60 Then
    v = n1 + (n2 - n1) * h / 60
  ElseIf h < 180 Then
    v = n2
  ElseIf h < 240 Then
    v = n1 + (n2 - n1) * (240 - h) / 60
  Else
    v = n1
  EndIf
  value = Math.Round(v * 255)
EndSub

Sub Color_ColortoGray
  ' Color | Convert RGB to Gray
  ' param color - "#rrggbb"
  ' return brightness - (0, 1)
  ' return gray - "#rrggbb"
  Color_NameToColor()
  Color_ColorToRGB()
  min = Math.Min(Math.Min(r, g), b)
  level = min + Math.Round(((r - min) * 2 + (g - min) * 4 + (b - min) * 1 ) / 7)
  brightness = Math.Round(level / 255 * 10000) / 10000
  gray = GraphicsWindow.GetColorFromRGB(level, level, level)
EndSub

Sub Color_RGBtoHSL
  ' Color | Convert RGB to HSL
  ' param r, g, b - RGB values 0..255
  ' return hue - [0, 360) or UNDEFINED
  ' return lightness - (0, 1)
  ' return saturation - (0, 1)
  ' r = r / 255 ' occurs Math.Max() bug
  r = Math.Round(r / 255 * 10000) / 10000
  ' g = g / 255 ' occurs Math.Max() bug
  g = Math.Round(g / 255 * 10000) / 10000
  ' b = b / 255 ' occurs Math.Max() bug
  b = Math.Round(b / 255 * 10000) / 10000
  max = Math.Max(r, g)
  max = Math.Max(max, b)
  min = Math.Min(r, g)
  min = Math.Min(min, b)
  lightness = (max + min) / 2
  If max = min Then ' r = g = b
    saturation = 0
    hue = UNDEFINED
  Else
    If lightness <= 0.5 Then
      saturation = (max - min) / (max + min)
    Else
      saturation = (max - min) / (2 - max - min)
    EndIf
    rc = (max - r) / (max - min)
    gc = (max - g) / (max - min)
    bc = (max - b) / (max - min)
    If r = max Then     ' between Yellow and Magenta
      hue = bc - gc
    ElseIf g = max Then ' between Cyan and Yellow
      hue = 2 + rc - bc
    ElseIf b = max Then ' between Magenta and Cyan
      hue = 4 + gc - rc
    Else
      TextWindow.WriteLine("Error:")
      TextWindow.WriteLine("max=" + max)
      TextWindow.WriteLine("r=" + r + ",sR=" + sR)
      TextWindow.WriteLine("g=" + g + ",sG=" + sG)
      TextWindow.WriteLine("b=" + b + ",sB=" + sB)
    EndIf
    hue = hue * 60
    If hue < 0 Then
      hue = hue + 360
    EndIf
  EndIf
EndSub

Sub Color_GrayFromLightness
  ' Color | Gray from lightness
  ' param lightness - 0..1
  ' return gray - "#rrggbb"
  iGray = Math.Round(lightness * 255)
  gray = GraphicsWindow.GetColorFromRGB(iGray, iGray, iGray)
EndSub

Sub Colors_Init
  colors["aliceblue"]="#F0F8FF"
  colors["antiquewhite"]="#FAEBD7"
  colors["aqua"]="#00FFFF"
  colors["aquamarine"]="#7FFFD4"
  colors["azure"]="#F0FFFF"
  colors["beige"]="#F5F5DC"
  colors["bisque"]="#FFE4C4"
  colors["black"]="#000000"
  colors["blanchedalmond"]="#FFEBCD"
  colors["blue"]="#0000FF"
  colors["blueviolet"]="#8A2BE2"
  colors["brown"]="#A52A2A"
  colors["burlywood"]="#DEB887"
  colors["cadetblue"]="#5F9EA0"
  colors["chartreuse"]="#7FFF00"
  colors["chocolate"]="#D2691E"
  colors["coral"]="#FF7F50"
  colors["cornflowerblue"]="#6495ED"
  colors["cornsilk"]="#FFF8DC"
  colors["crimson"]="#DC143C"
  colors["cyan"]="#00FFFF"
  colors["darkblue"]="#00008B"
  colors["darkcyan"]="#008B8B"
  colors["darkgoldenrod"]="#B8860B"
  colors["darkgray"]="#A9A9A9"
  colors["darkgreen"]="#006400"
  colors["darkkhaki"]="#BDB76B"
  colors["darkmagenta"]="#8B008B"
  colors["darkolivegreen"]="#556B2F"
  colors["darkorange"]="#FF8C00"
  colors["darkorchid"]="#9932CC"
  colors["darkred"]="#8B0000"
  colors["darksalmon"]="#E9967A"
  colors["darkseagreen"]="#8FBC8F"
  colors["darkslateblue"]="#483D8B"
  colors["darkslategray"]="#2F4F4F"
  colors["darkturquoise"]="#00CED1"
  colors["darkviolet"]="#9400D3"
  colors["deeppink"]="#FF1493"
  colors["deepskyblue"]="#00BFFF"
  colors["dimgray"]="#696969"
  colors["dodgerblue"]="#1E90FF"
  colors["firebrick"]="#B22222"
  colors["floralwhite"]="#FFFAF0"
  colors["forestgreen"]="#228B22"
  colors["fuchsia"]="#FF00FF"
  colors["gainsboro"]="#DCDCDC"
  colors["ghostwhite"]="#F8F8FF"
  colors["gold"]="#FFD700"
  colors["goldenrod"]="#DAA520"
  colors["gray"]="#808080"
  colors["green"]="#008000"
  colors["greenyellow"]="#ADFF2F"
  colors["honeydew"]="#F0FFF0"
  colors["hotpink"]="#FF69B4"
  colors["indianred"]="#CD5C5C"
  colors["indigo"]="#4B0082"
  colors["ivory"]="#FFFFF0"
  colors["khaki"]="#F0E68C"
  colors["lavender"]="#E6E6FA"
  colors["lavenderblush"]="#FFF0F5"
  colors["lawngreen"]="#7CFC00"
  colors["lemonchiffon"]="#FFFACD"
  colors["lightblue"]="#ADD8E6"
  colors["lightcoral"]="#F08080"
  colors["lightcyan"]="#E0FFFF"
  colors["lightgoldenrodyellow"]="#FAFAD2"
  colors["lightgray"]="#D3D3D3"
  colors["lightgreen"]="#90EE90"
  colors["lightpink"]="#FFB6C1"
  colors["lightsalmon"]="#FFA07A"
  colors["lightseagreen"]="#20B2AA"
  colors["lightskyblue"]="#87CEFA"
  colors["lightslategray"]="#778899"
  colors["lightsteelblue"]="#B0C4DE"
  colors["lightyellow"]="#FFFFE0"
  colors["lime"]="#00FF00"
  colors["limegreen"]="#32CD32"
  colors["linen"]="#FAF0E6"
  colors["magenta"]="#FF00FF"
  colors["maroon"]="#800000"
  colors["mediumaquamarine"]="#66CDAA"
  colors["mediumblue"]="#0000CD"
  colors["mediumorchid"]="#BA55D3"
  colors["mediumpurple"]="#9370DB"
  colors["mediumseagreen"]="#3CB371"
  colors["mediumslateblue"]="#7B68EE"
  colors["mediumspringgreen"]="#00FA9A"
  colors["mediumturquoise"]="#48D1CC"
  colors["mediumvioletred"]="#C71585"
  colors["midnightblue"]="#191970"
  colors["mintcream"]="#F5FFFA"
  colors["mistyrose"]="#FFE4E1"
  colors["moccasin"]="#FFE4B5"
  colors["navajowhite"]="#FFDEAD"
  colors["navy"]="#000080"
  colors["oldlace"]="#FDF5E6"
  colors["olive"]="#808000"
  colors["olivedrab"]="#6B8E23"
  colors["orange"]="#FFA500"
  colors["orangered"]="#FF4500"
  colors["orchid"]="#DA70D6"
  colors["palegoldenrod"]="#EEE8AA"
  colors["palegreen"]="#98FB98"
  colors["paleturquoise"]="#AFEEEE"
  colors["palevioletred"]="#DB7093"
  colors["papayawhip"]="#FFEFD5"
  colors["peachpuff"]="#FFDAB9"
  colors["peru"]="#CD853F"
  colors["pink"]="#FFC0CB"
  colors["plum"]="#DDA0DD"
  colors["powderblue"]="#B0E0E6"
  colors["purple"]="#800080"
  colors["red"]="#FF0000"
  colors["rosybrown"]="#BC8F8F"
  colors["royalblue"]="#4169E1"
  colors["saddlebrown"]="#8B4513"
  colors["salmon"]="#FA8072"
  colors["sandybrown"]="#F4A460"
  colors["seagreen"]="#2E8B57"
  colors["seashell"]="#FFF5EE"
  colors["sienna"]="#A0522D"
  colors["silver"]="#C0C0C0"
  colors["skyblue"]="#87CEEB"
  colors["slateblue"]="#6A5ACD"
  colors["slategray"]="#708090"
  colors["snow"]="#FFFAFA"
  colors["springgreen"]="#00FF7F"
  colors["steelblue"]="#4682B4"
  colors["tan"]="#D2B48C"
  colors["teal"]="#008080"
  colors["thistle"]="#D8BFD8"
  colors["tomato"]="#FF6347"
  colors["turquoise"]="#40E0D0"
  colors["violet"]="#EE82EE"
  colors["wheat"]="#F5DEB3"
  colors["white"]="#FFFFFF"
  colors["whitesmoke"]="#F5F5F5"
  colors["yellow"]="#FFFF00"
  colors["yellowgreen"]="#9ACD32"
EndSub

Sub Math_Hex2Dec
  ' Math | Convert hexadecimal to decimal
  ' param hex
  ' return dec
  dec = 0
  len = Text.GetLength(hex)
  For ptr = 1 To len
    dec = dec * 16 + Text.GetIndexOf("123456789ABCDEF", Text.GetSubText(hex, ptr, 1))
  EndFor
EndSub
    
