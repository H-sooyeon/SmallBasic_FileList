Sub init

  Mouse.HideCursor()

  

  width = Desktop.Width

  height = Desktop.Height

  

  GraphicsWindow.BackgroundColor = "black"

  GraphicsWindow.BrushColor = "white"

  GraphicsWindow.Height = height

  GraphicsWindow.Width = width

  GraphicsWindow.Top = 0

  GraphicsWindow.Left = -8

  

  G = 1 'https://en.wikipedia.org/wiki/Gravitational_constant you can read about this

  T = 0.5 'this is time. if you make value bigger that simulation work faster

  count = 15 'this is object count

EndSub



init()



createRandomMap()



While "true"

  If Mouse.IsLeftButtonDown = "true" Then

    GraphicsWindow.Clear()

    

    For cellN = 1 To count

      Shapes.Remove(cell[cellN]["shape"])

    EndFor

    

    createRandomMap()

    

    init()

  EndIf

  

  For cellN = 1 To count

    cell[cellN]["ax"] = 0

    cell[cellN]["ay"] = 0

    

    For cellN2 = 1 To count

      If cellN2 <> cellN Then

        r = Math.SquareRoot(Math.Abs(Math.Power(cell[cellN]["x"] - cell[cellN2]["x"], 2) + Math.Power(cell[cellN]["y"] - cell[cellN2]["y"], 2)))

        

        cell[cellN]["ax"] = cell[cellN]["ax"] + (G * cell[cellN2]["mass"] * (cell[cellN2]["x"] - cell[cellN]["x"]) / Math.Power(r, 3))

        cell[cellN]["ay"] = cell[cellN]["ay"] + (G * cell[cellN2]["mass"] * (cell[cellN2]["y"] - cell[cellN]["y"]) / Math.Power(r, 3))

      EndIf

    EndFor

    

    cell[cellN]["ax"] = cell[cellN]["ax"] / cell[cellN]["mass"] * 100

    cell[cellN]["ay"] = cell[cellN]["ay"] / cell[cellN]["mass"] * 100

    

    cell[cellN]["vx"] = cell[cellN]["vx"] + T * cell[cellN]["ax"]

    cell[cellN]["vy"] = cell[cellN]["vy"] + T * cell[cellN]["ay"]

    

    cellNew[cellN]["x"] = cell[cellN]["x"] + T * cell[cellN]["vx"]

    cellNew[cellN]["y"] = cell[cellN]["y"] + T * cell[cellN]["vy"]

    

    Shapes.Move(cell[cellN]["shape"], cellNew[cellN]["x"], cellNew[cellN]["y"])

  EndFor

  

  

  For cellN = 1 To count

    cell[cellN]["x"] = cellNew[cellN]["x"]

    cell[cellN]["y"] = cellNew[cellN]["y"]

  EndFor

  

  Program.Delay(1)

EndWhile



Sub createRandomMap  

  For cellN = 1 To count

    cell[cellN]["x"] = Math.GetRandomNumber(width)

    cell[cellN]["y"] = Math.GetRandomNumber(height)

    

    cell[cellN]["mass"] = Math.GetRandomNumber(10000) + 10

    

    cell[cellN]["size"] = cell[cellN]["mass"] / 1000 + 10

    

    cell[cellN]["vy"] = (Math.GetRandomNumber(3) - 2) * Math.GetRandomNumber(5)

    cell[cellN]["vx"] = (Math.GetRandomNumber(3) - 2) * Math.GetRandomNumber(5)

    

    cell[cellN]["shape"] = Shapes.AddEllipse(cell[cellN]["size"], cell[cellN]["size"])

  EndFor

EndSub


