' Ferris Wheel
' Version 0.1
' Copyright Â© 2016 Nonki Takahashi.  The MIT License.
' Challenge 2016-05
'
title = "Ferris Wheel"
GraphicsWindow.Title = title
ems = Clock.ElapsedMilliseconds
Form()
ms = 100
Timer.Interval = ms
Timer.Tick = OnTick
Sub OnTick
  da = da + 2 * Math.Pi / (60000 / ms)
  op = (1 - Math.Cos(da)) / 2
  Shapes.SetOpacity(sky, op * 100)
  For i = 1 To n
    a = i * 2 * Math.Pi / n + da
    x = x1 + r * Math.Sin(a)
    y = y1 - r * Math.Cos(a) + r2
    Shapes.Move(gondola[i], x - (r2 + w / 2), y - (r2 + w / 2))
  EndFor
  For i = 1 To n / 2
    a = i * 2 * Math.Pi / n + da
    Shapes.Rotate(beam[i], Math.GetDegrees(a))
  EndFor
EndSub
Sub Form
  gw = 598
  gh = 428
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.BackgroundColor = "SkyBlue"
  x1 = gw / 2
  y1 = gh / 2
  x2 = x1 - 50
  y2 = gh - 15
  x3 = x1 + 50
  y3 = y2
  r = 160
  n = 12  ' even number
  r2 = 20
  w = 4
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "Black"
  sky = Shapes.AddRectangle(gw, gh)
  Shapes.SetOpacity(sky, 0)
  GraphicsWindow.BrushColor = "DimGray"
  ground = Shapes.AddRectangle(gw, gh - y2)
  Shapes.Move(ground, 0, y2) 
  GraphicsWindow.PenWidth = w
  GraphicsWindow.PenColor = "#CC0000"
  GraphicsWindow.BrushColor = "#00000000"
  For i = 1 To n
    gondola[i] = Shapes.AddEllipse(2 * r2, 2 * r2)
    a = i * 2 * Math.Pi / n
    x = x1 + r * Math.Sin(a)
    y = y1 - r * Math.Cos(a) + r2
    Shapes.Move(gondola[i], x - (r2 + w / 2), y - (r2 + w / 2))
  EndFor
  GraphicsWindow.PenColor = "White"
  Shapes.AddTriangle(x1, y1, x2, y2, x3, y3)
  wheel = Shapes.AddEllipse(2 * (r + w / 2), 2 * (r + w / 2))
  Shapes.Move(wheel, x1 - (r + w / 2), y1 - (r + w / 2))
  sqrt2 = Math.SquareRoot(2)
  For i = 1 To n / 2
    a = i * 2 * Math.Pi / n
    beam[i] = Shapes.AddLine(0, 0, 0, 2 * r)
    Shapes.Move(beam[i], x1, y1 - r)
    Shapes.Rotate(beam[i], Math.GetDegrees(a))
  EndFor
EndSub
