FillArray()



cpu = 0

SortByName()

Output()

SortByStraße()

Output()

SortByOrt()

Output()



TextWindow.ForegroundColor = "Yellow"

TextWindow.Write("Number of elements in each sort (x3 sorts) ")

TextWindow.ForegroundColor = "Red"

TextWindow.WriteLine(DataSize)

TextWindow.ForegroundColor = "Yellow"

TextWindow.Write("Average elapsed time per sort (ms)         ")

TextWindow.ForegroundColor = "Red"

TextWindow.WriteLine(Math.Floor(cpu/3+0.5))

TextWindow.ForegroundColor = "Gray"

TextWindow.WriteLine("")



Sub FillArray

  multSize = Math.Floor(100000/3)

  DataSize = 3*multSize

  Data = LDArray.Create(DataSize)

  Sort = LDArray.Create(DataSize)

  SortIndex = LDArray.Create(DataSize)

  TC = Text.GetCharacter(255)

  

  For i = 1 To multSize 'Random numbers added to give the sort something to do

    A["Name"] = "Meyer, Anne"+Math.GetRandomNumber(1000000)

    A["Straße"] = "Northstreet"+Math.GetRandomNumber(1000000)

    A["Ort"] = "London"+Math.GetRandomNumber(1000000)

    LDArray.SetValue(Data,(i-1)*3+1,A)

    

    A["Name"] = "Android, Jan"+Math.GetRandomNumber(1000000)

    A["Straße"] = "Weststreet"+Math.GetRandomNumber(1000000)

    A["Ort"] = "Belfast"+Math.GetRandomNumber(1000000)

    LDArray.SetValue(Data,(i-1)*3+2,A)

    

    A["Name"] = "Nobian, Marcus"+Math.GetRandomNumber(1000000)

    A["Straße"] = "Brigdestreet"+Math.GetRandomNumber(1000000)

    A["Ort"] = "New York"+Math.GetRandomNumber(1000000)

    LDArray.SetValue(Data,(i-1)*3+3,A)

  EndFor

EndSub



Sub SortByName

  var = "Name"

  SortData()

EndSub



Sub SortByOrt

  var = "Ort"

  SortData()

EndSub



Sub SortByStraße

  var = "Straße"

  SortData()

EndSub



Sub SortData

  start = Clock.ElapsedMilliseconds

  For i = 1 To DataSize

    A = LDArray.GetValue(Data,i)

    LDArray.SetValue(Sort,i,A[var])

  EndFor

  LDArray.SortIndex(Sort,SortIndex)

  cpu = cpu + Clock.ElapsedMilliseconds - start

EndSub



Sub Output

  TextWindow.ForegroundColor = "Yellow"

  TextWindow.Write("Sort by ")

  TextWindow.ForegroundColor = "Red"

  TextWindow.WriteLine(var)

  For i = 1 To Math.Min(5,DataSize)

    OutputLine()

  EndFor

  TextWindow.WriteLine("...")

  For i = Math.Max(1,DataSize-4) To DataSize

    OutputLine()

  EndFor

  TextWindow.WriteLine("")

EndSub



Sub OutputLine

  j = LDArray.GetValue(SortIndex,i)

  A = LDArray.GetValue(Data,j)

  

  If (var = "Name") Then

    TextWindow.ForegroundColor = "Cyan"

  Else

    TextWindow.ForegroundColor = "Gray"

  EndIf

  TextWindow.CursorLeft = 0

  TextWindow.Write(A["Name"])

  

  If (var = "Straße") Then

    TextWindow.ForegroundColor = "Cyan"

  Else

    TextWindow.ForegroundColor = "Gray"

  EndIf

  TextWindow.CursorLeft = 28

  TextWindow.Write(A["Straße"])

  

  If (var = "Ort") Then

    TextWindow.ForegroundColor = "Cyan"

  Else

    TextWindow.ForegroundColor = "Gray"

  EndIf

  TextWindow.CursorLeft = 56

  TextWindow.Write(A["Ort"])

  

  TextWindow.ForegroundColor = "Gray"

  TextWindow.WriteLine("")

EndSub

  


