' Challenge of the month  March 2013    Like pong gme .        by NaochanON  

'    If    Player (or PC)  get 5 Points ,  You ( or PC)  wins.       

'    arrow key version.     You can select your Level               JHJ100



GraphicsWindow.KeyDown=Playermoving

Shapes_Init()

Shapes_Add()

InitData()





While  Gameover<>"true"

  Moving="true"

  RNDBallPosition()

  While Moving="true"

    NN=NN+0.005

    defence_moving()

    PCMoving()

    ball_moving()

    If NN>10 then 

      NN=10

    endif

    Program.Delay(25-NN-LVL)     '  speed Up  related to Level

  endwhile

  Program.Delay(1000)

  NN=0

endwhile

endmessage()                               '    Game Over



sub PlayerMoving

  PLT= Shapes.GetTop(SHP[10]["obj"])

  XLR= Text.GetCharacterCode(GraphicsWindow.LastKey)  ' Left arrow ;76    Right arrow ;82

  MX=MX+(XLR-79)*3

  If MX<WL  Then 

    MX=WL+PLW/2+3

  ElseIf MX+PLW/2>WR then 

    MX=WR-3-PLW/2

  EndIf

  Shapes.Move(SHP[10]["obj"],MX-PLW/2,PLT)

EndSub



Sub PCMoving

  PCSX=shapes.GetLeft(SHP[9]["obj"])         '  PC slider

  Distance=(BL+BW/2)-(GLX+GLW/2)

  chaseSpeed=Distance*((PLpoint+1)/( PLpoint-LVL+11.5))     '  chasing speed changes related  to PLPoint & Level 

  If BYY<0  and math.Abs(Distance)<3*GLW/4  Then

    Shapes.Move(SHP[9]["obj"],GLX+GLW/4+chaseSpeed,Shapes.GetTop(SHP[9]["obj"]))

  EndIf

endsub



Sub ball_moving

  If Moving="true" Then 

    BL=Shapes.GetLeft(SHP[8]["obj"])     '    ball left position

    BT=Shapes.Gettop(SHP[8]["obj"])      '    ball top position                      

    PCSX=shapes.GetLeft(SHP[9]["obj"])   '   PC slider position

    PLSX=shapes.GetLeft(SHP[10]["obj"])  '  Player slider position

    DFX=Shapes.GetLeft(SHP[7]["obj"])    '  defence  X position

    DFY=shapes.GetTop(SHP[7]["obj"])     '  defence  Y position

    If (GPCY-GLH)< BT and (BT+BW)<(GPPY+GLH) And  BL<=WL Then                   '    left wall  

      BXX=BXY["X"]

    ElseIf  (GPCY-GLH)<BT and (BT+BW)<(GPPY+GLH) And WR<=(BL+BW) then           '  right  wall 

      BXX=-BXY["X"]

    ElseIf  ((GPCY-GLH)<BT and BT<=GPCY and  BL+BW/2<=GLX)  then                '   PC  Upper wall     Left  

      BYY=BXY["Y"]+1

    ElseIf  ((GPCY-GLH)<BT and BT<=GPCY and GLX2<=(BL+BW/2) ) then              '   PC  Upper wall    Right  

      BYY=BXY["Y"]+1

    elseIf  (GPPY<=(BT+BW) and (BT+BW)<(GPPY+GLH) and  BL+BW/2<=GLX )  Then     '   player   Lower wall     Left

      BYY=-(BXY["Y"]+1)

    ElseIf  (GPPY<=(BT+BW) and (BT+BW)<(GPPY+GLH) and GLX2<=(BL+BW/2)) then     '   player   Lower wall     Right

      BYY=-(BXY["Y"]+1)

    ElseIf BYY>0 and (DFX-BW<=BL and BL<=(DFX+DFW)) and (DFY<=(BT+BW) and  (BT+BW)<(DFY+DFH/2)  )  then  '   at defence from PC  

      BYY=-(BXY["Y"]+1)

    ElseIf BYY<0 and (DFX-BW<=BL and BL<=(DFX+DFW)) and ((DFY+DFH/2)<BT and BT<=(DFY+DFH))  then         '   at defence from Player  

      BYY=BXY["Y"]+1

    elseif  BYY<0 and (PCSX-BW<=BL and BL<(PCSX+PCW)) and  (PCT+PCH/2<BT and BT<PCT+PCH ) then           '  PC  slider returns  ball

      BYY=BXY["Y"]+1

    elseif BYY>0 and (PLSX-BW<=BL and BL<(PLSX+PLW)) and  (PLT<BT+BW and BT+BW<PLT+PLH/2 ) then          '  Player slider returns ball    

      BYY=-(BXY["Y"]+1)

    elseif  BT>500 or  BT<0 then      ' if  ball run throgh  Player's Goal !!!    PC Wins 

      WhichWins()

    EndIf

    Shapes.Move(SHP[8]["obj"],BL+BXX,BT+BYY)

  EndIf

EndSub



Sub endmessage

  If  PCpoint>PLpoint Then 

    winmsg="You loose !!!! "

    Sound.PlayBellRing()

  Else 

    winmsg="You Win !!!! "

    Sound.PlayChime()

  EndIf

  wmsg=Shapes.AddText(winmsg)

  Shapes.Move(wmsg,130,225) 

EndSub



Sub WhichWins

  Moving="false"

  If BT<0  Then 

    PLpoint=PLpoint+1

  elseif BT>500 Then 

    PCpoint=PCpoint+1

  endif  

  Shapes.SetText(smsg," PC : "+PCpoint+"      Player : "+PLpoint)

  If PLPoint=5 Or PCPoint=5 Then        '    If You (PC) get 5 points 

    GameOver="true"

  EndIf 

EndSub



Sub defence_moving

  DFX=Shapes.GetLeft(SHP[7]["obj"])    '  defence  X position

  DFY=shapes.GetTop(SHP[7]["obj"])     '  defence  Y position

  If DFX<WL Then 

    dPM=-dPM

  ElseIf WR<DFX+DFW then 

    dPM=-dPM

  EndIf

  Shapes.Move(SHP[7]["obj"],DFX-dPM,DFY)

EndSub



Sub Shapes_Init

  X0 = 100   ' x offset

  Y0 = 100   ' y offset

  SHP[1] = "func=rect;x=1;y=1;width=70;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;"      '  Upper left

  SHP[2] = "func=rect;x=198;y=0;width=70;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;"    '  Upper right

  SHP[3] = "func=rect;x=1;y=10;width=12;height=351;bc=#FFFFFF;pc=#FFFFFF;pw=2;"    '  left wall

  SHP[4] = "func=rect;x=1;y=361;width=70;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;"    '  lower left

  SHP[5] = "func=rect;x=198;y=361;width=70;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;"  '  lower right

  SHP[6] = "func=rect;x=256;y=11;width=12;height=351;bc=#FFFFFF;pc=#FFFFFF;pw=2;"  '  Right wall

  SHP[7] = "func=rect;x=94;y=185;width=50;height=12;bc=#EE0515;pc=#EE0515;pw=2;"   '  center  defence

  SHP[8] =  "func=ell;x=130;y=10;width=16;height=16;bc=#209F09;pc=#209F09;pw=2;"   '   ball 

  SHP[9] = "func=rect;x=94;y=36;width=60;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;"    '  PC's   slider

  SHP[10] = "func=rect;x=110;y=326;width=60;height=12;bc=#FFFFFF;pc=#FFFFFF;pw=2;" '  your slider

EndSub



Sub SHaPes_Add

  GraphicsWindow.CanResize="false"

  GraphicsWindow.Width=500

  GraphicsWindow.Height=500

  GraphicsWindow.BackgroundColor="Black"

  s=1

  For i = 1 To Array.GetItemCount(SHP)

    GraphicsWindow.PenWidth = SHP[i]["pw"]

    GraphicsWindow.PenColor = SHP[i]["pc"]

    GraphicsWindow.BrushColor = SHP[i]["bc"]

    If SHP[i]["func"] = "rect" Then

      SHP[i]["obj"] = Shapes.AddRectangle(SHP[i]["width"], SHP[i]["height"])

    ElseIf SHP[i]["func"] = "ell" Then

      SHP[i]["obj"] = Shapes.AddEllipse(SHP[i]["width"], SHP[i]["height"])

    ElseIf SHP[i]["func"] = "tri" Then

      SHP[i]["obj"] = Shapes.AddTriangle(SHP[i]["x1"], SHP[i]["y1"], SHP[i]["x2"], SHP[i]["y2"], SHP[i]["x3"], SHP[i]["y3"])

    ElseIf SHP[i]["func"] = "line" Then

      SHP[i]["obj"] = Shapes.AddLine(SHP[i]["x1"], SHP[i]["y1"], SHP[i]["x2"], SHP[i]["y2"])

    EndIf

    Shapes.Move(SHP[i]["obj"], X0 + SHP[i]["x"], Y0 + SHP[i]["y"])

    Shapes.Rotate(SHP[i]["obj"], SHP[i]["angle"])

  EndFor

  '----------------------------------------------------------------------------------------------------

  SelectButton()

  selectLevel()

EndSub



Sub InitData

  '----------------------------------------------------------------------------------------------------

  dPM=1        '  defence  moving

  '----------------------------------------------------------------------------------------------------

  WL=Shapes.GetLeft(SHP[3]["obj"])+SHP[3]["width"]    '  Wall left position

  WR=Shapes.GetLeft(SHP[6]["obj"])                      '  wall right position

  BW=SHP[8]["width"]                                  '  ball width & height   

  GPCY=Shapes.Gettop(SHP[1]["obj"])+SHP[1]["height"]  '  Goal PC position Y

  GLH=SHP[1]["height"]                                '  Goal  wall height

  DFW=SHP[7]["width"]                                 '  defence width 

  DFH=SHP[7]["height"]                                '  defence  height

  GPPY=Shapes.Gettop(SHP[4]["obj"])                     '  Goal Player position Y

  GLX=Shapes.Getleft(SHP[1]["obj"])+SHP[1]["width"]   '  goal position X //PC &  Player  same value

  GLX2=Shapes.Getleft(SHP[2]["obj"])                    '  goal position X2 //  PC &  Player  same value

  GLW=GLX2-GLX                                          '  goal width

  PCT=Shapes.Gettop(SHP[9]["obj"])                      '  PC's slider  Top

  PCW=SHP[9]["width"]                                 '  PC's slider  width

  PCH=SHP[9]["height"]                                '  PC's  slider  Height

  PLW=SHP[10]["width"]                                '  Player's slider width

  PLH=SHP[10]["height"]                               '  Player's slider Height

  MX= Shapes.GetLeft(SHP[10]["obj"])                    '   initial  Player's slider position

  Moving="true"                                         '   Initial Moving = true

EndSub



Sub RNDBallPosition

  BXY="X=1;Y=4"  '   ball speed  

  BYY=BXY["Y"]

  BXX=BXY["X"]

  Shapes.Move(SHP[8]["obj"],WL+Math.GetRandomNumber(200),GPCY+50+Math.GetRandomNumber(100))

EndSub



Sub SelectButton 

  GraphicsWindow.BrushColor="Red"

  GraphicsWindow.FontSize=14

  For i=1 To 5

    btn[i]=Controls.AddButton(i,50+i*40,5)

  endfor

  GraphicsWindow.FontSize=30

  GraphicsWindow.BrushColor="White"

  smsg=Shapes.addText(" Click and select Level; "+text.GetCharacter(10)+" Low 1 --- 5 High ")

  Shapes.Move(smsg,50,35)

EndSub



Sub selectLevel

  LVL=""

  While LVL=""

    LVL= controls.GetButtonCaption(Controls.LastClickedButton)

  endwhile

  For i=1 to 5

    Controls.Remove(btn[i])

  endfor  

  MX= X0+SHP[10]["X"]*s                   '   initial  Player's slider position

  Shapes.Move(SHP[10]["obj"],MX-PLW/2,Shapes.GetTop(SHP[10]["obj"]))

  GraphicsWindow.Title=" Get 5 points to Win to PC !!!   Your Level= "+LVL

  Shapes.setText(smsg," PC : 0     Player : 0 ")

  Mouse.HideCursor()

  Program.Delay(500)

EndSub


