init()
load_images()
get_Size()
draw_back_hands()
init_vars()

Timer.Interval = 1000
Timer.Tick = rotate
Timer.Resume()

Sub rotate
  second_angle= (Clock.Second*6)-90
  minute_angle = (Clock.Minute*6)-90
  hrs = Clock.Hour
  If hrs > 12 Then
    hrs = hrs - 12
  EndIf
  hour_angle = (hrs*30)-90
  Shapes.Rotate(hand_sec,second_angle)
  Shapes.Rotate(hand_min,minute_angle)
  Shapes.Rotate(hand_hr,hour_angle)
  move()
EndSub

Sub move
  rot_second = Math.GetRadians(second_angle)
  offsetXsec = shiftXsec*Math.Cos(rot_second) - shiftYsec*Math.Sin(rot_second)
  offsetYsec = shiftXsec*Math.Sin(rot_second) + shiftYsec*Math.Cos(rot_second)
  Shapes.Move(hand_sec,xc+shiftXsec-offsetXsec,yc+shiftYsec-offsetYsec+2)
  rot_minute = Math.GetRadians(minute_angle)
  offsetXmin = shiftXmin*Math.Cos(rot_minute) - shiftYmin*Math.Sin(rot_minute)
  offsetYmin = shiftXmin*Math.Sin(rot_minute) + shiftYmin*Math.Cos(rot_minute)
  Shapes.Move(hand_min,xc+shiftXmin-offsetXmin,yc+shiftYmin-offsetYmin+2)
  rot_hour = Math.GetRadians(hour_angle)
  offsetXhr = shiftXhr*Math.Cos(rot_hour ) - shiftYhr*Math.Sin(rot_hour )
  offsetYhr = shiftXhr*Math.Sin(rot_hour ) + shiftYhr*Math.Cos(rot_hour )
  Shapes.Move(hand_hr,xc+shiftXhr-offsetXhr,yc+shiftYhr-offsetYhr+2)
EndSub

Sub load_images
  sec_arm = ImageList.LoadImage(Program.Directory+"\"+"sec-arm.PNG")
  min_arm = ImageList.LoadImage(Program.Directory+"\"+"min-arm.PNG")
  hr_arm = ImageList.LoadImage(Program.Directory+"\"+"hour-arm.PNG")
  back = ImageList.LoadImage(Program.Directory+"\"+"back.GIF")
EndSub

Sub get_Size
  gww = GraphicsWindow.Width
  gwh = GraphicsWindow.Height
  xc = gww/2
  yc = gwh/2
  sec_height = ImageList.GetHeightOfImage(sec_arm)
  sec_width= ImageList.GetWidthOfImage(sec_arm)
  min_height = ImageList.GetHeightOfImage(min_arm)
  min_width= ImageList.GetWidthOfImage(min_arm)
  hr_height = ImageList.GetHeightOfImage(hr_arm)
  hr_width= ImageList.GetWidthOfImage(hr_arm)
EndSub

Sub draw_back_hands
  GraphicsWindow.DrawResizedImage(back,0,0,gww,gwh)
  hand_sec = Shapes.AddImage(sec_arm)
  Shapes.Move(hand_sec,xc,yc)
  hand_min = Shapes.AddImage(min_arm)
  Shapes.Move(hand_min,xc,yc)
  hand_hr = Shapes.AddImage(hr_arm)
  Shapes.Move(hand_hr,xc,yc)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.PenWidth = 0
  centre = Shapes.AddEllipse(20,20)
EndSub

Sub init_vars
  shiftXsec = -sec_width/2
  shiftYsec = 0
  shiftXmin = -min_width/2
  shiftYmin = 0
  shiftXhr = -hr_width/2
  shiftYhr = 0
  Shapes.Move(centre,xc+sec_width/2-10+shiftXsec,yc+sec_height/2-10+shiftYsec)
EndSub

Sub init
  GraphicsWindow.Title = "Analogical Clock Version 2.0 [Amir Rke]"
  GraphicsWindow.Width = 440
  GraphicsWindow.Height = 440
  GraphicsWindow.CanResize = 0
EndSub
