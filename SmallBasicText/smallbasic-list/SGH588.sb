startRuntimeLoginLoop()



Sub initConstants

  ''' Small OS Version Information

  thisVersion = "2.0 Beta"

  

  ''' Small OS Exception Handling Mehanism

  exceptionThrown = "False"

  exceptionName = ""

  exceptionDescription1 = ""

  exceptionDescription2 = ""

  exceptionID = 0

  

  ''' Small OS Audio File References

  actionSoundFile = "C:\SmallOS\Action.wav"

  startUpSoundFile = "C:\SmallOS\Startup.wav"

  loggedInSoundFile = "C:\SmallOS\LoggedIn.wav"

  exceptionSoundFile = "C:\SmallOS\Exception.wav"

  

  ''' Current User Information

  pUserName = ""

  pPassword = ""

  pIsAdmin = "False"

  pIsNetworkEnabled = "False"

  pWrongUserNameOrPassword = ""

  pSecurityLevel = 0

  

  ''' Command Interpreter Reset Mehanism

  command = ""

  continueRuntimeLoop = "True"

  preMessage = ""

  postRestart = "False"

  postShutDown = "False"

  loggedOf = "True"

  postException = "False"

  

  ''' Frame drawer Information

  frameHeight = 12

  

  ''' Internal Message System

  imsTitle = ""

  imsDescription = ""

  imsButtonTitle = ""

  imsBackgroundColor = "Red"

  

  ''' Command Security

  securityLevel = 0

  

endsub



Sub startRuntimeLoginLoop

  ''' For first settings

  initConstants()

  

  ''' Show the startup information

  startIntroScreen()

  

  'Start the runtime login loop.

  While postRestart <> "True" And postShutDown <> "True" And postException <> "True"

    tryToLogin()

    

    If pUserName <> "" Then

      defaultBackgroundColor = FCSettings.Get(pUserName + "BackgroundColor")

      If defaultBackgroundColor = "" then

        defaultBackgroundColor = "Black"

      endif

      defaultForegroundColor = FCSettings.Get(pUserName + "ForegroundColor")

      If defaultForegroundColor = "" then

        defaultForegroundColor = "Gray"

      EndIf 

      startRuntimeLoop()

    endif

    

  endwhile

  

  If postShutDown then

    systemShutdown()

  endif

  

  If postRestart then

    systemRestart()

    startRuntimeLoginLoop ()

  endif

  

  If postException then

    systemException()

  endif

  

endsub







Sub systemException

  TextWindow.BackgroundColor = "DarkRed"

  TextWindow.ForegroundColor = "White"

  frameHeight = 12

  drawFrame()

  

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 4  

  TextWindow.Write("An error had occurred.")

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 6

  TextWindow.Write("Name: <" + exceptionName + ">   ID: " + exceptionID)

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 8

  TextWindow.Write("Description:")

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 9

  TextWindow.Write(exceptionDescription1)

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 10

  TextWindow.Write(exceptionDescription2)

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 12

  TextWindow.Write("The system will quit now.")

  

  TextWindow.PauseWithoutMessage()

  systemShutdown()

endsub







Sub systemRestart

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "White"

  frameHeight = 3

  drawFrame()

  i = 0

  While i < 3

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Restarting.  ")

    Program.Delay(200)

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Restarting.. ")

    Program.Delay(200)

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Restarting...")

    Program.Delay(200)

    i = i + 1

  endwhile

endsub







Sub systemShutdown

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "White"

  frameHeight = 3

  drawFrame()

  i = 0

  While i < 3

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Shutting down.  ")

    Program.Delay(200)

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Shutting down.. ")

    Program.Delay(200)

    TextWindow.CursorLeft = 8

    TextWindow.CursorTop = 4

    TextWindow.WriteLine("Shutting down...")

    Program.Delay(200)

    i = i + 1

  endwhile

  

  Program.End()

endsub







Sub startIntroScreen

  

  ''' Don't user custom settings - for stability

  TextWindow.BackgroundColor = "Black"

  TextWindow.ForegroundColor = "Gray"

  TextWindow.Clear()

  Program.Delay(200)

  TextWindow.WriteLine("Welcome to Small OS")

  Program.Delay(50)

  TextWindow.WriteLine("Version " + thisVersion)

  Program.Delay(50)

  TextWindow.WriteLine("")

  Program.Delay(100)

  TextWindow.Write("Loading settings from: " + FCSettings.SettingFile)

  Program.Delay(100)

  TextWindow.WriteLine(" Done.")

  

  defaultBackgroundColor = "Black"

  defaultForegroundColor = "Gray"

  

  TextWindow.WriteLine("")

  TextWindow.WriteLine("Press RETURN to log in...")

  TextWindow.WriteLine("Press RETURN while holding 'SHIFT' to shutdown.")

  

  TextWindow.PauseWithoutMessage()

  

  If FCKeyboard.IsShiftPressed then

    postShutDown = "True"

  endif

  

  If FCKeyboard.IsControlPressed then  ''' DEBUG

    postRestart = "True"

  endif

  

endsub







Sub drawFrame

  Program.Delay(200)

  TextWindow.Clear()

  Program.Delay(200)

  

  TextWindow.CursorLeft = 4

  TextWindow.CursorTop = 2

  TextWindow.Write("+------------------------------------------------------------+")

  

  i = 0

  While i < frameHeight

    TextWindow.CursorLeft = 4

    TextWindow.CursorTop = 3 + i

    TextWindow.Write("|")

    i = i + 1

  EndWhile 

  

  i = 0

  While i < frameHeight

    TextWindow.CursorLeft = 65

    TextWindow.CursorTop = 3 + i

    TextWindow.Write("|")

    i = i + 1

  EndWhile 

  

  TextWindow.CursorLeft = 4

  TextWindow.CursorTop = frameHeight + 3

  TextWindow.Write("+------------------------------------------------------------+")

endsub







Sub tryToLogin

  drawLoginWindow()

  

  pWrongUserNameOrPassword = ""

  c = "True"

  

  If pUserName = "Admin" and pPassword = FCSettings.Get("AdminPassword") then

    pIsAdmin = "True"

    pIsNetworkEnabled = "True"

    pSecurityLevel = 2

    c = "False"

  endif

  

  If pUserName = "Standard" and pPassword = FCSettings.Get("StandardPassword") then

    pIsAdmin = "False"

    pIsNetworkEnabled = "True"

    pSecurityLevel = 1

    c = "False"

  endif

  If FCSettings.Get("GuestAccountEnabled") = "" then

    If pUserName = "Guest" and pPassword = "" then

      pIsAdmin = "False"

      pIsNetworkEnabled = "False"

      pSecurityLevel = 0

      c = "False"

    endif

  Else

    If pUserName = "Guest" and pPassword = "" then

      pUserName = ""

      pPassword = ""

      pIsAdmin = "False"

      pIsNetworkEnabled = "False"

      pSecurityLevel = 0

      c = "False"

      pWrongUserNameOrPassword = "Guest Account Disabled!"

    endif

  endif

  

  If c = "True" then

    pWrongUserNameOrPassword = "Wrong Name or Password!"

    pUserName = ""

    pPassword = ""

    pIsAdmin = "False"

    pIsNetworkEnabled = "False"

    pSecurityLevel = 0

  endif

endsub



Sub drawLoginWindow

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "White"

  frameHeight = 18

  drawFrame()

  TextWindow.ForegroundColor = "White"

  TextWindow.CursorLeft = 4

  TextWindow.CursorTop = 17

  TextWindow.Write("+------------------------------------------------------------+")

  Program.Delay(200)

  

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 4  

  TextWindow.Write("Log into Small OS.")

  

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 5  

  TextWindow.ForegroundColor = "Red"

  TextWindow.Write(pWrongUserNameOrPassword)

  TextWindow.ForegroundColor = "White"

  

  TextWindow.CursorLeft = 12

  TextWindow.CursorTop = 7  

  TextWindow.Write("    Name:")

  

  TextWindow.CursorLeft = 12

  TextWindow.CursorTop = 9  

  TextWindow.Write("Password:")

  

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 12  

  TextWindow.Write("When typed credentials, press 'RETURN'.")

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 14  

  TextWindow.ForegroundColor = "Gray"

  TextWindow.Write("To shutdown, hold 'SHIFT' while pressing 'RETURN'.")

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 15  

  TextWindow.Write("This allows You to SAFELY turn off Your computer.")

  

  TextWindow.ForegroundColor = "White"

  TextWindow.CursorLeft = 8

  TextWindow.CursorTop = 19  

  TextWindow.Write("Small OS Authentication")

  

  TextWindow.ForegroundColor = "Yellow"

  TextWindow.BackgroundColor = "DarkGray"

  TextWindow.CursorLeft = 27

  TextWindow.CursorTop = 7

  TextWindow.Write("                    ")

  

  TextWindow.CursorLeft = 27

  TextWindow.CursorTop = 9

  TextWindow.Write("                    ")

  

  TextWindow.CursorLeft = 28

  TextWindow.CursorTop = 7

  Program.Delay(200)

  pUserName = TextWindow.Read()

  If FCKeyboard.IsShiftPressed then

    systemShutdown()

  endif

  

  TextWindow.CursorLeft = 28

  TextWindow.CursorTop = 9

  pPassword = TextWindow.Read()

  TextWindow.CursorLeft = 28

  TextWindow.CursorTop = 9

  TextWindow.ForegroundColor = "Gray"

  TextWindow.WriteLine("******************")

  TextWindow.ForegroundColor = "Yellow"

  If FCKeyboard.IsShiftPressed then

    systemShutdown()

  endif

  

  TextWindow.CursorLeft = 28

  TextWindow.CursorTop = 10

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "Gray"

  Program.Delay(200)

  TextWindow.Write("Verifing...")

  TextWindow.ForegroundColor = "White"

  Program.Delay(150)

endsub



sub startRuntimeLoop

  continueRuntimeLoop = "True"

  

  TextWindow.BackgroundColor = defaultBackgroundColor

  TextWindow.ForegroundColor = defaultForegroundColor

  TextWindow.Clear()

  

  While continueRuntimeLoop

    securityLevel = -1

    

    TextWindow.ForegroundColor = "DarkGray"

    If preMessage = "" Then

      TextWindow.Write(">")

    Else

      TextWindow.Write(preMessage + ".>")

    endif

    

    preMessage = ""

    

    TextWindow.ForegroundColor = defaultForegroundColor

    command = TextWindow.Read()

    

    ValidateCommand() 

    

    If pSecurityLevel >= securityLevel Then

      command = Text.ConvertToLowerCase(command)

    Else

      command = "nopermissions"

    endif

    If Text.GetLength(command) > 0 then

      DoCommand()

    endif

    

  EndWhile

EndSub 



Sub ValidateCommand

  availableCommands = FCExtensions.GetAllSubs()

  If Array.ContainsValue(availableCommands, "isvalid_" + command) then

    FCExtensions.Call("isvalid_" + command)

  endif

endsub



Sub DoCommand

  didCommand = "False"

  

  availableCommands = FCExtensions.GetAllSubs()

  If Array.ContainsValue(availableCommands, "command_" + command) then

    FCExtensions.Call("command_" + command)

    didCommand = "True"

  endif

  

  If didCommand = "False" Then

    preMessage = "Command '" + command + "' not valid!"

  endif

  

  

EndSub 



Sub command_nopermissions

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 4

  TextWindow.ForegroundColor = "Red"

  TextWindow.WriteLine("You don't have requested permissions to execute this command.")

endsub



'---------------------------------------------------------------------------------------------------

'----------------------- COMMANDS START HERE -----------------------------------------

'---------------------------------------------------------------------------------------------------



Sub command_lock

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "Gray"

  

  frameHeight = 12

  drawFrame()

  

  TextWindow.CursorLeft = 28

  TextWindow.CursorTop = 7  

  TextWindow.Write("Computer locked")

  

  TextWindow.CursorLeft = 14

  TextWindow.CursorTop = 9  

  TextWindow.Write("Press 'CTRL' + 'ALT' + 'RETURN' to unlock.")

  

  TextWindow.CursorLeft = 0

  TextWindow.CursorTop = 0

  

  smallContinue = "True"

  

  While smallContinue 

    TextWindow.PauseWithoutMessage()

    

    If FCKeyboard.IsControlPressed And FCKeyboard.IsAltPressed Then

      smallContinue = "False"

    endif

  EndWhile 

  

  TextWindow.BackgroundColor = defaultBackgroundColor

  TextWindow.ForegroundColor = defaultForegroundColor

  TextWindow.Clear()

EndSub 

Sub isvalid_lock

  securityLevel = 1

endsub





Sub command_logoff

  initConstants()

  continueRuntimeLoop = "False"

endsub

Sub isvalid_logoff

  securityLevel = 0

endsub





Sub command_shutdown

  postShutDown = "True"

  continueRuntimeLoop = "False"

EndSub

Sub isvalid_shutdown

  securityLevel = 0

endsub





Sub command_restart

  postRestart = "True"

  continueRuntimeLoop = "False"

EndSub 

Sub isvalid_restart

  securityLevel = 0

endsub





Sub command_me

  preMessage = "I am " + pUserName

endsub

Sub isvalid_me

  securityLevel = 0

endsub





Sub command_hello

  preMessage = "hello, " + pUserName

endsub

Sub isvalid_hello

  securityLevel = 0

endsub





Sub command_setdefaults

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("Background color:  (type nothing for default - black)")

  TextWindow.CursorLeft = 6

  tempBgColor = TextWindow.Read()

  If tempBgColor = "" then

    tempBgColor = "Black"

  endif

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("Foreground color:  (type nothing for default - gray)")

  TextWindow.CursorLeft = 6

  tempFrColor = TextWindow.Read()

  If tempFrColor = "" then

    tempFrColor = "Gray"

  endif

  

  FCSettings.Set(pUserName + "BackgroundColor", tempBgColor)

  FCSettings.Set(pUserName + "ForegroundColor", tempFrColor)

  

  TextWindow.CursorLeft = 6

  TextWindow.CursorTop = TextWindow.CursorTop + 2

  TextWindow.WriteLine("The changes will take effect when You restart.")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("Press 'SHIFT' when pressing 'RETURN' to restart now.")

  

  TextWindow.PauseWithoutMessage()

  If FCKeyboard.IsShiftPressed Then

    postRestart = "True"

    continueRuntimeLoop = "False"

  endif

endsub

Sub isvalid_setdefaults

  securityLevel = 1

endsub





Sub command_time

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("The time is:")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine(Clock.Hour + ":" + Clock.Minute + "/" + Clock.Second + "   " + Clock.WeekDay + ", " + Clock.Day + "/" + Clock.Month + "/" + Clock.Year)

  TextWindow.WriteLine("")

endsub

Sub isvalid_time

  securityLevel = 0

endsub





Sub command_clear

  TextWindow.BackgroundColor = defaultBackgroundColor 

  TextWindow.ForegroundColor = defaultForegroundColor 

  TextWindow.Clear()

endsub

Sub isvalid_clear

  securityLevel = 0

endsub





Sub command_help

  TextWindow.WriteLine("")

  i = 0

  While i < Array.GetItemCount(availableCommands)

    If Text.StartsWith(availableCommands[i], "command_") Then

      TextWindow.CursorLeft = 6

      TextWindow.WriteLine(text.GetSubTextToEnd(availableCommands[i], 9))

    endif

    

    i=i+1

  EndWhile 

  TextWindow.WriteLine("")

endsub

Sub isvalid_help

  securityLevel = 0

endsub





Sub command_settings

' The following line could be harmful and has been automatically commented.

'   c = File.ReadContents(FCSettings.SettingFile)

  TextWindow.WriteLine("")

  TextWindow.WriteLine(c)

  TextWindow.WriteLine("")

EndSub 

Sub isvalid_settings

  securityLevel = 2

endsub





Sub command_resetsettings

  FCSettings.ClearAll()

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("Settings cleared.")

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("System has to restart. Press 'RETURN' to restart computer.")

  TextWindow.WriteLine("")

  

  TextWindow.PauseWithoutMessage()

  postRestart = "True"

  continueRuntimeLoop = "False"

  

endsub

Sub isvalid_resetsettings

  securityLevel = 2

endsub





Sub command_changepassword

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("Old password:")

  TextWindow.CursorLeft = 6

  tempPassword = TextWindow.Read()

  if tempPassword <> FCSettings.Get(pUserName + "Password") Then

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("Incorrect Password!")

  Else

    TextWindow.CursorLeft = 6

    TextWindow.CursorTop = TextWindow.CursorTop - 1

    TextWindow.WriteLine("**********************************")

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("New password:")

    TextWindow.CursorLeft = 6

    newPassword = TextWindow.Read()

    TextWindow.CursorLeft = 6

    TextWindow.CursorTop = TextWindow.CursorTop - 1

    TextWindow.WriteLine("**********************************")

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("New password (retype):")

    TextWindow.CursorLeft = 6

    newPassword2 = TextWindow.Read()

    If newPassword <> newPassword2 then

      TextWindow.CursorLeft = 4

      TextWindow.WriteLine("Incorrect Password!")

    Else

      pPassword = newPassword 

      FCSettings.set(pUserName + "Password", newPassword)

      TextWindow.CursorLeft = 4

      TextWindow.WriteLine("Password changed successfully!")

    endif

  endif

  

endsub

Sub isvalid_changepassword

  If FCSettings.Get("AllowStandardToChangePassword") = "" then

    securityLevel = 1

  Else

    securityLevel = 2

  endif

endsub



Sub command_resetpassword

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("Reset Password of 'Standard' Account.")

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("'Admin' password:")

  TextWindow.CursorLeft = 6

  tempPassword = TextWindow.Read()

  if tempPassword <> FCSettings.Get(pUserName + "Password") Then

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("Incorrect Password!")

  Else

    FCSettings.Set("StandardPassword", "")

    TextWindow.WriteLine("")

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("'Standard' Account's password reset!")

  endif

endsub

Sub isvalid_resetpassword

  securityLevel = 2

endsub



Sub command_setpassword

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("Set New Password for 'Standard' Account.")

  TextWindow.CursorLeft = 4

  TextWindow.WriteLine("'Admin' password:")

  TextWindow.CursorLeft = 6

  tempPassword = TextWindow.Read()

  if tempPassword <> FCSettings.Get(pUserName + "Password") Then

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("Incorrect Password!")

  Else

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("New Password For 'Standard':")

    TextWindow.CursorLeft = 6

    newPassword = TextWindow.Read()

    FCSettings.Set("StandardPassword", newPassword)

    TextWindow.WriteLine("")

    TextWindow.CursorLeft = 4

    TextWindow.WriteLine("'Standard' Account's new password set!")

  endif

endsub

Sub isvalid_setpassword

  securityLevel = 2

endsub



Sub command_allowpasswordchange

  FCSettings.Set("AllowStandardToChangePassword", "")

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("'Standard' Account Can Now Change Password.")

  TextWindow.WriteLine("")

endsub

Sub isvalid_allowpasswordchange

  securityLevel = 2

endsub



Sub command_disallowpasswordchange

  FCSettings.Set("AllowStandardToChangePassword", "False")

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("'Standard' Account CAN'T Now Change Password.")

  TextWindow.WriteLine("")

endsub

Sub isvalid_disallowpasswordchange

  securityLevel = 2

endsub



Sub command_enableguestaccount

  FCSettings.Set("GuestAccountEnabled", "")

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("'Guest' Account Enabled. The Password Remains Blank ''.")

  TextWindow.WriteLine("")

endsub

Sub isvalid_enableguestaccount

  securityLevel = 2

endsub



Sub command_disableguestaccount

  FCSettings.Set("GuestAccountEnabled", "False")

  TextWindow.WriteLine("")

  TextWindow.CursorLeft = 6

  TextWindow.WriteLine("'Guest' Account Disabled.")

  TextWindow.WriteLine("")

endsub

Sub isvalid_disableguestaccount

  securityLevel = 2

endsub


