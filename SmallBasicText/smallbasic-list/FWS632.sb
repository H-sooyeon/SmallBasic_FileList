'  Run Soldier Dude    ----    By Krueg -- June 2012
'         ARROWS to move
'      SPACE or UP to jump
'          ESCAPE to quit

'  Credit goes to GoToLoop, NaochanON, Zock77, and litedev for helping to make this program better.
'         Thank You for your suggestions!

debug = "unTrue"
SetupVariables()
SetupWindow()
SetupImages()
StartGameHere:
ShowIntro()
StartLevel:
LoadLevel()
ShowHealthBar()
GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp   = OnKeyUp

MainLoop: '================================================ MainLoop
While health > 0
  fps = Clock.ElapsedMilliseconds + delay   'Calculate the time to wait for frames per second
  '---------------------------------------------------------------------------------Calculate the jump direction and speed
  If Jump Then
    ManY = ManY + JumpCurentSpeed           'Adjust ManY for the jump
    If JumpCurentSpeed < JumpPower Then     'The man will only fall this fast
      JumpCurentSpeed = JumpCurentSpeed + JumpInc     'Add for gravity
    EndIf
  EndIf
  If ManY > gh-64 Then                      'The man landed on the bottom of the screen
    HitSpike()
  ElseIf ManY < 32 Then                     'The man hit the roof
    JumpCurentSpeed = .4
  EndIf
  
  '---------------------------------------------------------------------------------Falling off platforms, check for stars, hearts, and spikes
  If ManY = pfy5-64 And Jump = "" Then      'Check conditions of the bottom row of platforms
    If (pf5[pfx] > 0 And pf5[pfx] < 50) Or (pf5[pfx-1] > 0 And pf5[pfx-1] < 50) Or (pf5[pfx-2] > 0 And pf5[pfx-2] < 50) Or (pf5[pfx-3] > 0 And pf5[pfx-3] < 50) Or (pf5[pfx-4] > 0 And pf5[pfx-4] < 50) Then
    Else                                    
      Jump = "True"
      JumpCurentSpeed = JumpInc
    EndIf
    If pf5[pfx] > 50 And pf5[pfx] < 70 Then 'Did the man hit a star?
      Sound.PlayMusic(TuneStar)
      Shapes.Move(star[pf5[pfx]],-32,pfy5)
      pf5[pfx] = ""
      Score = Score + 10
      StarCount = StarCount + 1
    ElseIf pf5[pfx] > 70 And pf5[pfx] < 80 Then 'Did the man hit a heart?
      Sound.PlayMusic(TuneHeart)
      Shapes.Move(heart[pf5[pfx]],-32,pfy5)
      pf5[pfx] = ""
      health = health + 1
      ShowHealthBar()
    ElseIf pf5[pfx] > 80 And pf5[pfx] < 100 Then 'Did the man hit a spike?
      HitSpike()
    ElseIf pf5[pfx] = 109 Then              'Did the man hit a 1up?
      Sound.PlayMusic(Tune1Up)
      Shapes.Move(icon[pf5[pfx]],-32,pfy5)
      pf5[pfx] = ""
      lives = lives + 1
      If lives > 5 Then
        lives = 5
      EndIf
      ShowHealthBar()
    ElseIf pf5[pfx-1] = 119 Then            'Did the man hit the exit?
      Sound.PlayMusic("O5 C8 C8 E4")
      Program.Delay(1000)
      ResetImages()
      level = level + 1
      Goto StartLevel
    EndIf
  ElseIf ManY = pfy4-64 And Jump = "" Then  'Check conditions of the fourth row of platforms
    If (pf4[pfx] > 0 And pf4[pfx] < 50) Or (pf4[pfx-1] > 0 And pf4[pfx-1] < 50) Or (pf4[pfx-2] > 0 And pf4[pfx-2] < 50) Or (pf4[pfx-3] > 0 And pf4[pfx-3] < 50) Or (pf4[pfx-4] > 0 And pf4[pfx-4] < 50) Then
    Else
      Jump = "True"
      JumpCurentSpeed = JumpInc
    EndIf
    If pf4[pfx] > 50 And pf4[pfx] < 70 Then 'Did the man hit a star?
      Sound.PlayMusic(TuneStar)
      Shapes.Move(star[pf4[pfx]],-32,pfy4)
      pf4[pfx] = ""
      Score = Score + 10
      StarCount = StarCount + 1
    ElseIf pf4[pfx] > 70 And pf4[pfx] < 80 Then 'Did the man hit a heart?
      Sound.PlayMusic(TuneHeart)
      Shapes.Move(heart[pf4[pfx]],-32,pfy4)
      pf4[pfx] = ""
      health = health + 1
      ShowHealthBar()
    ElseIf pf4[pfx] > 80 And pf4[pfx] < 100 Then 'Did the man hit a spike?
      HitSpike()
    EndIf
  ElseIf ManY = pfy3-64 And Jump = "" Then  'Check conditions of the third row of platforms
    If (pf3[pfx] > 0 And pf3[pfx] < 50) Or (pf3[pfx-1] > 0 And pf3[pfx-1] < 50) Or (pf3[pfx-2] > 0 And pf3[pfx-2] < 50) Or (pf3[pfx-3] > 0 And pf3[pfx-3] < 50) Or (pf3[pfx-4] > 0 And pf3[pfx-4] < 50) Then
    Else
      Jump = "True"
      JumpCurentSpeed = JumpInc
    EndIf
    If pf3[pfx] > 50 And pf3[pfx] < 70 Then 'Did the man hit a star?
      Sound.PlayMusic(TuneStar)
      Shapes.Move(star[pf3[pfx]],-32,pfy3)
      pf3[pfx] = ""
      Score = Score + 10
      StarCount = StarCount + 1
    ElseIf pf3[pfx] > 70 And pf3[pfx] < 80 Then 'Did the man hit a heart?
      Sound.PlayMusic(TuneHeart)
      Shapes.Move(heart[pf3[pfx]],-32,pfy3)
      pf3[pfx] = ""
      health = health + 1
      ShowHealthBar()
    ElseIf pf3[pfx] > 80 And pf3[pfx] < 100 Then 'Did the man hit a spike?
      HitSpike()
    EndIf
  ElseIf ManY = pfy2-64 And Jump = "" Then  'Check conditions of the second row of platforms
    If (pf2[pfx] > 0 And pf2[pfx] < 50) Or (pf2[pfx-1] > 0 And pf2[pfx-1] < 50) Or (pf2[pfx-2] > 0 And pf2[pfx-2] < 50) Or (pf2[pfx-3] > 0 And pf2[pfx-3] < 50) Or (pf2[pfx-4] > 0 And pf2[pfx-4] < 50) Then
    Else
      Jump = "True"
      JumpCurentSpeed = JumpInc
    EndIf
    If pf2[pfx] > 50 And pf2[pfx] < 70 Then 'Did the man hit a star?
      Sound.PlayMusic(TuneStar)
      Shapes.Move(star[pf2[pfx]],-32,pfy2)
      pf2[pfx] = ""
      Score = Score + 10
      StarCount = StarCount + 1
    ElseIf pf2[pfx] > 70 And pf2[pfx] < 80 Then 'Did the man hit a heart?
      Sound.PlayMusic(TuneHeart)
      Shapes.Move(heart[pf2[pfx]],-32,pfy2)
      pf2[pfx] = ""
      health = health + 1
      ShowHealthBar()
    ElseIf pf2[pfx] > 80 And pf2[pfx] < 100 Then 'Did the man hit a spike?
      HitSpike()
    EndIf
  ElseIf ManY = pfy1-64 And Jump = "" Then  'Check conditions of the first row of platforms
    If (pf1[pfx] > 0 And pf1[pfx] < 50) Or (pf1[pfx-1] > 0 And pf1[pfx-1] < 50) Or (pf1[pfx-2] > 0 And pf1[pfx-2] < 50) Or (pf1[pfx-3] > 0 And pf1[pfx-3] < 50) Or (pf1[pfx-4] > 0 And pf1[pfx-4] < 50) Then
    Else
      Jump = "True"
      JumpCurentSpeed = JumpInc
    EndIf
    If pf1[pfx] > 50 And pf1[pfx] < 70 Then 'Did the man hit a star?
      Sound.PlayMusic(TuneStar)
      Shapes.Move(star[pf1[pfx]],-32,pfy1)
      pf1[pfx] = ""
      Score = Score + 10
      StarCount = StarCount + 1
    ElseIf pf1[pfx] > 70 And pf1[pfx] < 80 Then 'Did the man hit a heart?
      Sound.PlayMusic(TuneHeart)
      Shapes.Move(heart[pf1[pfx]],-32,pfy1)
      pf1[pfx] = ""
      health = health + 1
      ShowHealthBar()
    ElseIf pf1[pfx] > 80 And pf1[pfx] < 100 Then 'Did the man hit a spike?
      HitSpike()
    EndIf
  EndIf
  
  '---------------------------------------------------------------------------------Landing on platforms
  If JumpCurentSpeed > 0 And ManY < pfy5-32 And ManY > pfy5-64 Then
    If (pf5[pfx] > 0 And pf5[pfx] < 50) Or (pf5[pfx-1] > 0 And pf5[pfx-1] < 50) Or (pf5[pfx-2] > 0 And pf5[pfx-2] < 50) Or (pf5[pfx-3] > 0 And pf5[pfx-3] < 50) Or (pf5[pfx-4] > 0 And pf5[pfx-4] < 50) Then
      Jump = ""
      ManY = pfy5-64
    EndIf
  ElseIf JumpCurentSpeed > 0 And ManY < pfy4-32 And ManY > pfy4-64 Then
    If (pf4[pfx] > 0 And pf4[pfx] < 50) Or (pf4[pfx-1] > 0 And pf4[pfx-1] < 50) Or (pf4[pfx-2] > 0 And pf4[pfx-2] < 50) Or (pf4[pfx-3] > 0 And pf4[pfx-3] < 50) Or (pf4[pfx-4] > 0 And pf4[pfx-4] < 50) Then
      Jump = ""
      ManY = pfy4-64
    EndIf
  ElseIf JumpCurentSpeed > 0 And ManY < pfy3-32 And ManY > pfy3-64 Then
    If (pf3[pfx] > 0 And pf3[pfx] < 50) Or (pf3[pfx-1] > 0 And pf3[pfx-1] < 50) Or (pf3[pfx-2] > 0 And pf3[pfx-2] < 50) Or (pf3[pfx-3] > 0 And pf3[pfx-3] < 50) Or (pf3[pfx-4] > 0 And pf3[pfx-4] < 50) Then
      Jump = ""
      ManY = pfy3-64
    EndIf
  ElseIf JumpCurentSpeed > 0 And ManY < pfy2-32 And ManY > pfy2-64 Then
    If (pf2[pfx] > 0 And pf2[pfx] < 50) Or (pf2[pfx-1] > 0 And pf2[pfx-1] < 50) Or (pf2[pfx-2] > 0 And pf2[pfx-2] < 50) Or (pf2[pfx-3] > 0 And pf2[pfx-3] < 50) Or (pf2[pfx-4] > 0 And pf2[pfx-4] < 50) Then
      Jump = ""
      ManY = pfy2-64
    EndIf
  ElseIf JumpCurentSpeed > 0 And ManY < pfy1-32 And ManY > pfy1-64 Then
    If (pf1[pfx] > 0 And pf1[pfx] < 50) Or (pf1[pfx-1] > 0 And pf1[pfx-1] < 50) Or (pf1[pfx-2] > 0 And pf1[pfx-2] < 50) Or (pf1[pfx-3] > 0 And pf1[pfx-3] < 50) Or (pf1[pfx-4] > 0 And pf1[pfx-4] < 50) Then
      Jump = ""
      ManY = pfy1-64
    EndIf
  EndIf
  
  'If pfx < 0 Then                          'Routine to wrap the playfield.
  '  pfx = pfx + pfw - 25                   'The first 25 steps in the array
  'ElseIf pfx > pfw Then                    'must match the last 25 steps
  '  pfx = pfx - pfw + 25                   'in the SetupPlayField routine.
  'EndIf    
  '---------------------------------------------------------------------------------Show playfield from array
  For i = 1 To 30
    pfxt = pfx-18+i
    t = i * 32 - 160 + bgx
    pftest = pf1[pfxt]                      'Show playfield line 1
    If pftest <> "" Then
      If pftest < 50 Then
        Shapes.Move(brick[pftest],t,pfy1)
      ElseIf pftest > 50 and pftest < 70 Then
        Shapes.Move(star[pftest],t,pfy1-32)
      ElseIf pftest > 70 and pftest < 80 Then
        Shapes.Move(heart[pftest],t,pfy1-32)
      ElseIf pftest > 80 and pftest < 100 Then
        Shapes.Move(spike[pftest],t,pfy1-32)
      EndIf
    EndIf
    pftest = pf2[pfxt]                      'Show playfield line 2
    If pftest <> "" Then
      If pftest < 50 Then
        Shapes.Move(brick[pftest],t,pfy2)
      ElseIf pftest > 50 and pftest < 70 Then
        Shapes.Move(star[pftest],t,pfy2-32)
      ElseIf pftest > 70 and pftest < 80 Then
        Shapes.Move(heart[pftest],t,pfy2-32)
      ElseIf pftest > 80 and pftest < 100 Then
        Shapes.Move(spike[pftest],t,pfy2-32)
      EndIf
    EndIf
    pftest = pf3[pfxt]                      'Show playfield line 3
    If pftest <> "" Then
      If pftest < 50 Then
        Shapes.Move(brick[pftest],t,pfy3)
      ElseIf pftest > 50 and pftest < 70 Then
        Shapes.Move(star[pftest],t,pfy3-32)
      ElseIf pftest > 70 and pftest < 80 Then
        Shapes.Move(heart[pftest],t,pfy3-32)
      ElseIf pftest > 80 and pftest < 100 Then
        Shapes.Move(spike[pftest],t,pfy3-32)
      EndIf
    EndIf
    pftest = pf4[pfxt]                      'Show playfield line 4
    If pftest <> "" Then
      If pftest < 50 Then
        Shapes.Move(brick[pftest],t,pfy4)
      ElseIf pftest > 50 and pftest < 70 Then
        Shapes.Move(star[pftest],t,pfy4-32)
      ElseIf pftest > 70 and pftest < 80 Then
        Shapes.Move(heart[pftest],t,pfy4-32)
      ElseIf pftest > 80 and pftest < 100 Then
        Shapes.Move(spike[pftest],t,pfy4-32)
      EndIf
    EndIf
    pftest = pf5[pfxt]                      'Show playfield line 5 this is the ground
    If pftest <> "" Then
      If pftest < 50 Then
        Shapes.Move(ground[pftest],t,pfy5)
      ElseIf pftest > 50 and pftest < 70 Then
        Shapes.Move(star[pftest],t,pfy5-32)
      ElseIf pftest > 70 and pftest < 80 Then
        Shapes.Move(heart[pftest],t,pfy5-32)
      ElseIf pftest > 80 and pftest < 100 Then
        Shapes.Move(spike[pftest],t,pfy5-32)
      ElseIf pftest > 100 and pftest < 110 Then
        Shapes.Move(icon[pftest],t,pfy5-32)
      ElseIf pftest > 110 and pftest < 120 Then
        Shapes.Move(icon[pftest],t,pfy5-99)
      EndIf
    EndIf
  EndFor
  '----------------------------------------------------------------------------Calculate and move background and roofline
  bgx = bgx + bgInc                         'Add the increment to the background X position
  If bgx < - 32 Then                        'Background images repeat every 32 pixels
    bgx = bgx + 32
    pfx = pfx + 1                           'Move right on the playfield array
  ElseIf bgx > 0 Then
    bgx = bgx - 32
    pfx = pfx - 1                           'Move left on the playfield array
  EndIf
  Shapes.Move(background,bgx,bgy)
  Shapes.Move(roofline,bgx,bgy)
  Shapes.Move(bottom,bgx,gh-32)
  '----------------------------------------------------------------------------Generate animation for the man
  ManSlide = ManSlide + ManIncIs
  If ManSlide > 4.9   Then
    ManSlide =  1
  EndIf  
  ManSlideFloor = Math.Floor(ManSlide)
  If ManDirLast <> ManDir Or ManSlideFloorLast <> ManSlideFloor Then
    Shapes.Move(Man[ManDir][ManSlideFloor],ManX,ManY)
    Shapes.ShowShape(man[ManDir][ManSlideFloor])
    Shapes.HideShape(man[ManDirLast][ManSlideFloorLast])
  EndIf
  If Jump Then
    Shapes.Move(man[ManDir][ManSlideFloor],ManX,ManY)
  EndIf
  ManDirLast = ManDir
  ManSlideFloorLast = ManSlideFloor
  GraphicsWindow.Title = "RUN Little Soldier Dude, RUN! ---- Score = " + Score + " ---- Stars = " + StarCount + " ---- Lives = " + lives + " ---- Level = " + level
  While Clock.ElapsedMilliseconds < fps     'Wait here to create a consistant frames per second
  EndWhile
  If debug Then
    DebugData()
  EndIf
EndWhile                                    'End of loop for health
'------------------------------------------------------------------------------Player lost a life
Sound.PlayMusic(TuneDie)
ResetImages()
lives = lives - 1
If lives = 0 Then
  Goto GameOver
EndIf
health = 7
LoadLevel()
ShowHealthBar()
Goto MainLoop

Sub ShowHealthBar '======================================== ShowHealthBar
  If health > 7 Then
    health = 7
  EndIf
  Shapes.Move(HealthBar[health],20,40)
  If health <> lasthealth Then
    Shapes.Move(HealthBar[lasthealth],-160,40)
    lasthealth = health
  EndIf
  'If lives <> lastlives Then
  Shapes.Move(LifeIcon[lives],200 40)
  'Shapes.Move(LifeIcon[lastlives],200,40)
  'lastlives = lives
  'EndIf
EndSub

Sub HitSpike '============================================= HitSpike
  Sound.PlayMusic(TuneSpike)                'The man hit a spike routine
  Jump = "True"
  JumpCurentSpeed = -JumpPower / 2          'Give the man a half jump
  health = health - 1                       'Take away one health
  ShowHealthBar()
EndSub

Sub OnKeyDown '============================================ OnKeyDown
  key = GraphicsWindow.LastKey
  If key = "Left" Then                      'Left arrow was pressed
    bgInc = speed                           'Background increment to the right
    ManDir = 1                              'The man array for showing his left side
    ManIncIs = ManInc                       'The increment for the man walking animation
  ElseIf key = "Right"  Then                'Right arrow was pressed
    bgInc = -speed                          'Background increment to the left
    ManDir = 2                              'The man array for showing his right side
    ManIncIs = ManInc                       'The increment for the man walking animation
  ElseIf (key = "Up" or key = "Space") And Jump <> "True" Then 
    Jump = "True"                           'Turn on the Jump flag
    JumpCurentSpeed = -JumpPower            'Get the man momentum for the jump
  ElseIf key = "Escape" Then                'Escape was pressed to quit
    Program.End()
  EndIf
EndSub

Sub OnKeyUp '============================================== OnKeyUp
  key = GraphicsWindow.LastKey
  If key = "Right" Or key = "Left" Then
    bgInc = 0
    ManIncIs = ""
  EndIf
EndSub

Sub SetupVariables '======================================= SetupVariables
  gw = 800                                  'Window Width
  gh = 672                                  'Window Height
  lives = 3                                 'Players lives
  health = 7                                'Players health
  bgx = 0                                   'Backgound X position
  speed = 2                                 'Background X increment / speed
  bgy = 0                                   'Background Y position
  ManDir = 1                                'Man direction 1=left 2=right
  ManSlide  = 1                             'Man's slide number for walking animation
  ManX = gw/2-16                            'Man's position X, center on screen
  ManY = gh-96                              'Man's position Y
  ManInc = .1                               'Walking appearance speed
  JumpPower = 8                             'Jump power
  JumpInc = .2                              'Jump Gravity
  level = 1                                 'Start level
  Score = 0                                 'It's not a game without a score
  StarCount = 0                             'Stars collected
  delay = 9                                 'Change this for smooth frame rate
  TuneStar = "O6 C64"
  TuneSpike = "O3 C64"
  TuneDie = "R1 O2 G32"
  TuneGameOver = "O4 C8 C8 C8 R16 O3 G8"
  TuneHeart = "O7 C64 E64"
  TuneIntro = "O5 C8 C8 D8 E8 C8 E8 D4"
  Tune1Up = "O7 C64 G64"
EndSub

Sub LoadLevel '============================================ LoadPlayField
  pfy1 = 128                                'Playfield line 1 Y position
  pfy2 = 256                                'Playfield line 2 Y position
  pfy3 = 384                                'Playfield line 3 Y position
  pfy4 = 512                                'Playfield line 4 Y position
  pfy5 = 640                                'Playfield line 5 Y position, the ground
' The following line could be harmful and has been automatically commented.
'   '    pf1 = File.ReadLine(path + "levels/level" + level + ".txt",1)
' The following line could be harmful and has been automatically commented.
'   '    pf2 = File.ReadLine(path + "levels/level" + level + ".txt",2)
' The following line could be harmful and has been automatically commented.
'   '    pf3 = File.ReadLine(path + "levels/level" + level + ".txt",3)
' The following line could be harmful and has been automatically commented.
'   '    pf4 = File.ReadLine(path + "levels/level" + level + ".txt",4)
' The following line could be harmful and has been automatically commented.
'   '    pf5 = File.ReadLine(path + "levels/level" + level + ".txt",5)
' The following line could be harmful and has been automatically commented.
'   '    pfx = File.ReadLine(path + "levels/level" + level + ".txt",6)
' The following line could be harmful and has been automatically commented.
'   '    pfAuthor = File.ReadLine(path + "levels/level" + Level + ".txt",7)
  If level = 1 then
    pf1 = "26=34;29=53;70=31;74=61;"
    pf2 = "19=33;33=12;40=15;22=52;20=83;34=61;36=62;41=63;43=64;35=84;42=81;47=1;52=2;50=92;53=93;63=21;64=82;65=83;66=84;"
    pf3 = "12=32;19=13;40=14;15=55;13=82;41=65;43=66;22=88;42=85;63=4;65=51;"
    pf4 = "5=31;26=11;8=51;6=81;27=67;29=68;56=3;58=52;"
    pf5 = "1=41;6=42;11=43;26=46;31=41;36=42;41=43;56=46;61=41;66=42;76=44;81=45;2=101;16=47;27=91;19=92;84=102;80=119;"
    pfx = "14"
    pfAuthor = "Krueg"
  EndIf
  If level = 2 then
    pf1 = "41=14;83=4;85=54;126=22;131=23;136=21;141=24;195=1;248=23;287=23;302=34;84=91;303=84;121=25;197=53;289=62;305=63;346=33;347=86;349=63;"
    pf2 = "34=13;36=64;48=31;50=62;83=3;90=31;85=53;92=55;114=13;146=1;188=11;241=22;280=22;302=33;309=11;86=92;116=81;305=83;190=52;243=82;282=52;303=51;311=91;339=32;340=85;342=62;393=13;394=66;396=67;"
    pf3 = "27=12;29=63;55=32;57=63;83=2;97=32;85=52;99=56;121=12;140=2;181=21;234=21;257=31;259=71;273=21;302=32;317=12;84=93;260=91;261=92;303=82;141=61;142=62;143=63;183=51;236=81;275=61;305=61;319=92;332=31;333=84;335=61;353=1;361=2;355=71;363=72;369=3;371=73;386=12;387=64;389=65;"
    pf4 = "20=11;22=62;62=33;64=61;83=1;104=33;85=51;106=57;128=11;174=31;176=71;231=1;236=2;266=32;302=31;325=13;86=94;305=81;268=51;303=53;327=93;379=11;380=62;382=63;"
    pf5 = "0=41;5=42;10=43;15=44;20=45;1=101;17=61;71=41;76=42;73=62;78=64;113=41;118=42;123=43;115=71;148=41;158=42;168=43;205=42;210=43;224=45;200=44;209=101;248=42;253=43;258=44;292=41;297=42;336=42;341=43;346=44;351=45;356=46;364=41;366=109;21=82;77=81;127=82;202=81;207=82;217=83;212=84;338=82;340=83;226=71;204=54;215=41;214=51;245=61;250=62;255=63;260=64;358=101;243=46;331=41;332=94;335=96;374=61;390=45;395=44;398=119;403=102;372=42;400=43;"
    pfx = "13"
    pfAuthor = "Krueg"
  EndIf
  If level = 3 then
    pf1 = "0=1;5=5;10=9;20=11;25=15;30=19;40=21;45=25;50=29;60=31;65=35;70=39;1=51;2=52;3=53;6=61;7=62;8=63;21=81;22=82;23=83;26=91;27=92;28=93;41=71;42=72;43=73;"
    pf2 = "0=2;5=6;20=12;25=16;40=22;45=26;60=32;65=36;1=54;2=55;3=56;6=64;7=65;8=66;21=84;22=85;23=86;26=94;27=95;28=96;41=74;42=75;43=76;"
    pf3 = "0=3;5=7;20=13;25=17;40=23;45=27;60=33;65=37;1=57;2=58;3=59;6=67;7=68;8=69;21=87;22=88;23=89;26=97;27=98;28=99;41=77;42=78;43=79;"
    pf4 = "0=4;5=8;20=14;25=18;40=24;45=28;60=34;65=38;"
    pf5 = "0=41;5=42;10=43;15=44;20=45;25=47;30=41;35=42;40=48;45=49;50=46;55=47;65=43;70=44;75=45;80=41;85=42;79=119;87=102;1=101;7=109;84=109;"
    pfx = "7"
    pfAuthor = "Krueg"
  EndIf
  If level = 4 then
    pf1 = ""
    pf2 = "18=31;24=32;"
    pf3 = "13=11;29=12;"
    pf4 = "15=1;27=2;"
    pf5 = "1=41;6=42;11=43;16=44;26=46;31=41;36=42;41=43;42=119;"
    pfx = "13"
    pfAuthor = "Krueg"
  EndIf
  If level = 5 then
    pf1 = "46=31;138=32;49=61;186=22;"
    pf2 = "8=21;16=31;9=95;44=21;61=12;112=31;116=81;133=22;145=2;76=31;84=1;10=51;180=12;213=11;215=51;221=21;"
    pf3 = "8=11;10=81;23=12;42=11;54=2;55=81;56=91;68=22;106=21;128=12;151=11;174=1;206=1;226=32;"
    pf4 = "8=1;36=71;11=93;16=2;32=22;40=1;100=11;123=1;157=21;168=31;188=32;193=33;198=31;191=81;190=91;196=61;232=2;"
    pf5 = "1=41;11=43;16=44;21=45;26=46;2=101;6=42;7=91;8=82;31=41;36=42;41=43;46=44;51=45;56=46;66=42;71=48;76=44;91=41;96=42;101=43;106=44;111=45;116=46;60=51;61=47;81=49;82=82;83=92;85=93;84=83;95=109;126=42;118=91;119=82;120=61;121=41;163=41;168=42;173=43;178=44;183=45;188=46;193=41;198=42;203=43;208=44;165=71;-4=47;-9=46;236=41;237=81;239=82;238=91;240=92;241=42;246=43;251=44;252=119;248=101;"
    pfx = "4"
    pfAuthor = "Sarah"
  EndIf
  If level = 6 then
    pf1 = "41=14;83=4;85=54;126=22;131=23;136=21;141=24;195=1;248=23;287=23;302=34;84=91;303=84;121=25;197=53;289=62;305=63;346=33;347=86;349=63;"
    pf2 = "34=13;36=64;48=31;50=62;83=3;90=31;85=53;92=55;114=13;146=1;188=11;241=22;280=22;302=33;309=11;86=92;116=81;305=83;190=52;243=82;282=52;303=51;311=91;339=32;340=85;342=62;393=13;394=66;396=67;"
    pf3 = "27=12;29=63;55=32;57=63;83=2;97=32;85=52;99=56;121=12;140=2;181=21;234=21;257=31;259=71;273=21;302=32;317=12;84=93;260=91;261=92;303=82;141=61;142=62;143=63;183=51;236=81;275=61;305=61;319=92;332=31;333=84;335=61;353=1;361=2;355=71;363=72;369=3;371=73;386=12;387=64;389=65;"
    pf4 = "20=11;22=62;62=33;64=61;83=1;104=33;85=51;106=57;128=11;174=31;176=71;231=1;236=2;266=32;302=31;325=13;86=94;305=81;268=51;303=53;327=93;379=11;380=62;382=63;"
    pf5 = "0=41;5=42;10=43;15=44;20=45;1=101;17=61;71=41;76=42;73=62;78=64;113=41;118=42;123=43;115=71;148=41;158=42;168=43;205=42;210=43;224=45;200=44;209=101;248=42;253=43;258=44;292=41;297=42;336=42;341=43;346=44;351=45;356=46;364=41;366=109;6=81;21=82;77=81;127=82;202=81;207=82;217=83;212=84;339=81;338=82;340=83;226=71;204=54;215=41;214=51;245=61;250=62;255=63;260=64;358=101;243=46;331=41;332=94;334=95;335=96;374=61;390=45;395=44;398=119;403=102;372=42;400=43;"
    pfx = "13"
    pfAuthor = "Krueg"
  EndIf
  If level = 7 then
    pf1 = "0=1;5=5;10=9;20=11;25=15;30=19;40=21;45=25;50=29;60=31;65=35;70=39;1=51;2=52;3=53;6=61;7=62;8=63;21=81;22=82;23=83;26=91;27=92;28=93;41=71;42=72;43=73;"
    pf2 = "0=2;5=6;20=12;25=16;40=22;45=26;60=32;65=36;1=54;2=55;3=56;6=64;7=65;8=66;21=84;22=85;23=86;26=94;27=95;28=96;41=74;42=75;43=76;"
    pf3 = "0=3;5=7;20=13;25=17;40=23;45=27;60=33;65=37;1=57;2=58;3=59;6=67;7=68;8=69;21=87;22=88;23=89;26=97;27=98;28=99;41=77;42=78;43=79;"
    pf4 = "0=4;5=8;20=14;25=18;40=24;45=28;60=34;65=38;"
    pf5 = "0=41;5=42;10=43;15=44;20=45;25=47;30=41;35=42;40=48;45=49;50=46;55=47;65=43;70=44;75=45;80=41;85=42;79=119;87=102;1=101;7=109;84=109;"
    pfx = "7"
    pfAuthor = "Krueg"
  EndIf
  If level = 8 then
    pf1 = ""
    pf2 = ""
    pf3 = ""
    pf4 = ""
    pf5 = ""
    pfx = ""
    pfAuthor = "Krueg"
  EndIf
  
  If pf1 = "" And pf2 = "" And pf3 = "" And pf4 = "" And pf5 = "" Then
    GraphicsWindow.FontSize = 32
    GraphicsWindow.BrushColor = "Yellow"
    LevelIcon = Shapes.AddText("There are no more levels!" + Text.GetCharacter(13) + "You have beat the game")
    For i = gh To gh / 3 Step -2
      Shapes.Move(LevelIcon,gw*.25,i)
      Program.Delay(2)
    EndFor
    While ("True")
    EndWhile
  EndIf
  GraphicsWindow.FontSize = 96
  GraphicsWindow.BrushColor = "Yellow"
  LevelIcon = Shapes.AddText("LEVEL " + level)
  GraphicsWindow.FontSize = 32
  GraphicsWindow.BrushColor = "LightBlue"
  LevelAuthor = Shapes.AddText("Created by " + pfAuthor)
  For i = gh To gh / 3 Step -2
    Shapes.Move(LevelIcon,gw*.25,i)
    Shapes.Move(LevelAuthor,gw*.35,i+100)
    Program.Delay(2)
  EndFor
  Program.Delay(2000)
  For i = gh / 3 To gh
    Shapes.Move(LevelIcon,gw*.25,i)
    Shapes.Move(LevelAuthor,gw*.35,i+100)
    Program.Delay(2)
  EndFor
EndSub

Sub SetupWindow '========================================== SetupWindow
  If debug Then
    TextWindow.Left = gw + 20
    TextWindow.Top  = 10
    TextWindow.Title = "Soldier Dude Debug."
  EndIf
  GraphicsWindow.Hide()
  GraphicsWindow.Height = gh
  GraphicsWindow.Width  = gw
  GraphicsWindow.Top  = 5
  GraphicsWindow.Left = 5
  GraphicsWindow.Title = "RUN Little Soldier Dude, RUN! ---- By Krueg -- 2012"
  GraphicsWindow.BackgroundColor = "Black"
EndSub

Sub SetupImages '========================================== SetupImages
  'path = Program.Directory + "\"
  path = "http://imperialelectric.us/smallbasic/soldier/"
  background = Shapes.AddImage(ImageList.LoadImage(path + "background.png"))
  Shapes.Move(background,0,gh+32)
  roofline = Shapes.AddImage(ImageList.LoadImage(path + "roofline.png"))
  Shapes.Move(roofline,0,-32)
  bottom = Shapes.AddImage(ImageList.LoadImage(path + "bottom.png"))
  Shapes.Move(bottom,0,gh+32)
  icon[101] = Shapes.AddImage(ImageList.LoadImage(path + "signright.png"))
  Shapes.Move(icon[101],-100,pfy5-32)
  icon[102] = Shapes.AddImage(ImageList.LoadImage(path + "signleft.png"))
  Shapes.Move(icon[102],-100,pfy5-32)
  icon[109] = Shapes.AddImage(ImageList.LoadImage(path + "1up.png"))
  Shapes.Move(icon[109],-100,pfy5-32)
  icon[119] = Shapes.AddImage(ImageList.LoadImage(path + "arch.png"))
  Shapes.Move(icon[119],-100,pfy5-32)
  BrickBrownImage = ImageList.LoadImage(path + "5brickbrown.png")
  BrickBlueImage = ImageList.LoadImage(path + "5brickblue.png")
  BrickGreenImage = ImageList.LoadImage(path + "5brickgreen.png")
  BrickRedImage = ImageList.LoadImage(path + "5brickred.png")
  GroundImage = ImageList.LoadImage(path + "5ground.png")
  StarBlueImage = ImageList.LoadImage(path + "starblue.png")
  StarRedImage = ImageList.LoadImage(path + "starred.png")
  HeartImage = ImageList.LoadImage(path + "heart.png")
  SpikeGrenImage = ImageList.LoadImage(path + "spikegreen.png")
  SpikeRedImage = ImageList.LoadImage(path + "spikered.png")
  For i = 1 To 9
    brick[i] = Shapes.AddImage(BrickBrownImage)
    Shapes.Move(brick[i],-160,pfy1)
    brick[i+10] = Shapes.AddImage(BrickBlueImage)
    Shapes.Move(brick[i+10],-160,pfy2)
    brick[i+20] = Shapes.AddImage(BrickGreenImage)
    Shapes.Move(brick[i+20],-160,pfy3)
    brick[i+30] = Shapes.AddImage(BrickRedImage)
    Shapes.Move(brick[i+30],-160,pfy4)
    ground[i+40] = Shapes.AddImage(GroundImage)
    Shapes.Move(ground[i+40],-160,pfy5)
    star[i+50] = Shapes.AddImage(StarBlueImage)
    Shapes.Move(star[i+50],-160,pfy1-32)
    star[i+60] = Shapes.AddImage(StarRedImage)
    Shapes.Move(star[i+60],-160,pfy1-32)
    heart[i+70] = Shapes.AddImage(HeartImage)
    Shapes.Move(heart[i+70],-160,pfy2-32)
    spike[i+80] = Shapes.AddImage(SpikeGrenImage)
    Shapes.Move(spike[i+80],-160,pfy1-32)
    spike[i+90] = Shapes.AddImage(SpikeRedImage)
    Shapes.Move(spike[i+90],-160,pfy1-32)
  EndFor
  manl2 = ImageList.LoadImage(path + "man_l2.png")
  manr2 = ImageList.LoadImage(path + "man_r2.png")
  man[1][1] = Shapes.AddImage(ImageList.LoadImage(path + "man_l1.png"))
  man[1][2] = Shapes.AddImage(manl2)
  man[1][3] = Shapes.AddImage(ImageList.LoadImage(path + "man_l3.png"))
  man[1][4] = Shapes.AddImage(manl2)
  man[2][1] = Shapes.AddImage(ImageList.LoadImage(path + "man_r1.png"))
  man[2][2] = Shapes.AddImage(manr2)
  man[2][3] = Shapes.AddImage(ImageList.LoadImage(path + "man_r3.png"))
  man[2][4] = Shapes.AddImage(manr2)
  For i = 1 To 4
    Shapes.Move(man[1][i],ManX,ManY)
    Shapes.Zoom(man[1][i],2,2)
    Shapes.HideShape(man[1][i])
    Shapes.Move(man[2][i],ManX,ManY)
    Shapes.Zoom(man[2][i],2,2)
    Shapes.HideShape(man[2][i])
  EndFor
  For i = 0 To 7
    HealthBar[i] = Shapes.AddImage(ImageList.LoadImage(path + "healthbar" + i + ".png"))
    Shapes.Move(HealthBar[i],-160,32)
  EndFor
  For i = 0 To 5
    LifeIcon[i] = Shapes.AddImage(ImageList.LoadImage(path + "lives" + i + ".png"))
    Shapes.Move(LifeIcon[i],-160,32)
  EndFor
  GameOverImage = Shapes.AddImage(ImageList.LoadImage(path + "gameover.png"))
  Shapes.Move(GameOverImage,-600,pfy1)
EndSub

Sub ResetImages '========================================== ResetImages
  Shapes.Move(background,0,gh+32)
  Shapes.Move(roofline,0,-32)
  Shapes.Move(bottom,0,gh+32)
  Shapes.Move(icon[101],-100,pfy5-32)
  Shapes.Move(icon[102],-100,pfy5-32)
  Shapes.Move(icon[109],-100,pfy5-32)
  Shapes.Move(icon[119],-100,pfy5-32)
  For i = 0 To 7
    Shapes.Move(HealthBar[i],-160,32)
  EndFor
  For i = 0 To 5
    Shapes.Move(LifeIcon[i],-160,32)
  EndFor
  For i = 1 To 9
    Shapes.Move(brick[i],-160,pfy1)
    Shapes.Move(brick[i+10],-160,pfy2)
    Shapes.Move(brick[i+20],-160,pfy3)
    Shapes.Move(brick[i+30],-160,pfy4)
    Shapes.Move(ground[i+40],-165,pfy5)
    Shapes.Move(star[i+50],-160,pfy5)
    Shapes.Move(star[i+60],-160,pfy5)
    Shapes.Move(heart[i+70],-160,pfy5)
    Shapes.Move(spike[i+80],-160,pfy5)
    Shapes.Move(spike[i+90],-160,pfy5)
  EndFor
EndSub


Sub ShowIntro '============================================ ShowIntro
  GraphicsWindow.FontSize = 40
  'GraphicsWindow.DrawImage(path + "intro.png",0,0)
  introscreen = Shapes.AddImage(ImageList.LoadImage(path + "intro.png"))
  Shapes.SetOpacity(introscreen,0)
  GraphicsWindow.Show()
  For i = 0 To 100
    Shapes.SetOpacity(introscreen,i)
  EndFor
  Sound.PlayMusic(TuneIntro)
  GraphicsWindow.BrushColor = "DarkBlue"
  GraphicsWindow.FontSize = 12
  ready = Controls.AddButton("Play",gw*.75-100,gh*.65)
  quit = Controls.AddButton("Quit",gw*.75,gh*.65)
  Controls.SetSize(ready,90,30)
  Controls.SetSize(quit,90,30)
  Controls.ButtonClicked = onbuttonpress
  While play < 1
  EndWhile
  play = 0
  Controls.Remove(ready)
  Controls.Remove(quit)
  Mouse.HideCursor()
  For i = 0 To gh + 32
    Shapes.Move(introscreen,0,i)
    Program.Delay(2)
  EndFor
  Program.Delay(1000)
EndSub

Sub onbuttonpress '======================================== OnButtonPress
  If Controls.LastClickedButton = ready Then
    play = 1
  EndIf
  If Controls.LastClickedButton = quit Then
    Program.End()
  EndIf
EndSub

GameOver: '================================================ Game Over Routine
Program.Delay(1000)
For i = gh To gh / 3 Step -2
  Shapes.Move(GameOverImage,gw*.15,i)
  Program.Delay(2)
EndFor
Sound.PlayMusic(TuneGameOver)

While ("True")
EndWhile
Shapes.Move(GameOverImage,-600,pfy1)
'------------------------------------------------------------End game Over routine

Sub DebugData '============================================ Debug Window
  TextWindow.Clear()
  TextWindow.WriteLine("key = " + key)
  TextWindow.WriteLine("bgInc = " + bgInc)
  TextWindow.WriteLine("bgx = " + bgx)
  TextWindow.WriteLine("pfx = " + pfx)
  TextWindow.WriteLine("Jump = " + Jump)
  TextWindow.WriteLine("ManX = " + ManX)
  TextWindow.WriteLine("ManY = " + ManY)
  TextWindow.WriteLine("JumpCurentSpeed = " + JumpCurentSpeed)
  TextWindow.WriteLine("")
  TextWindow.WriteLine("pf1 = " + pf1)
  TextWindow.WriteLine("pf2 = " + pf2)
  TextWindow.WriteLine("pf3 = " + pf3)
  TextWindow.WriteLine("pf4 = " + pf4)
  TextWindow.WriteLine("pf5 = " + pf5)
EndSub

