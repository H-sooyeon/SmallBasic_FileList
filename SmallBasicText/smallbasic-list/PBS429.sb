ProgramRunning = "True"
MaxDecimalLength = 4 'max length of numbers after decimal point (4 and 3 yield best results)
MaxNumber = Math.SquareRoot(Math.Power(10,MaxDecimalLength))-1 'The max factor
While ProgramRunning
  TextWindow.Clear()
  TextWindow.WriteLine("Enter a decimal number to convert to lowest terms fraction:")
  Input = TextWindow.ReadNumber()
  WhereDecimalPoint = Text.GetIndexOf(Input,".")
  If WhereDecimalPoint = 0 Then
    'the decimal point is nonexistant so we add one
    Input = Input+".0"
    WhereDecimalPoint = Text.GetIndexOf(Input,".")
  EndIf
  'the part of it that's above one
  WholeNumber = Text.GetSubText(Input,1,WhereDecimalPoint-1)
  'the decimal part
  AfterDecimalPoint = Text.GetSubTextToEnd(Input,WhereDecimalPoint+1)
  If Text.GetLength(AfterDecimalPoint) > MaxDecimalLength Then
    'it exceeds the total length so we shorten it
    AfterDecimalPoint = Text.GetSubText(AfterDecimalPoint,1,MaxDecimalLength)
  ElseIf Text.GetLength(AfterDecimalPoint) < MaxDecimalLength Then
    'it is less than the length so we add 0's until it fits the length required
    While Text.GetLength(AfterDecimalPoint) < MaxDecimalLength
      AfterDecimalPoint = Text.Append(AfterDecimalPoint,"0")
    EndWhile
  EndIf
  Numerator = AfterDecimalPoint
  'the denominator is 10 to the power of the max length
  Denominator = Math.Power(10,MaxDecimalLength)
  For f = 1 To MaxNumber+1 'Do it to the max number of factors we know it can have
    CheckDivisibility()
    If DivisibleBy[1] <> "" Then 'This ensures no division by 0
      'Divide the numerator and the denominator by the same thing
      Numerator = Numerator/DivisibleBy[1]
      Denominator = Denominator/DivisibleBy[1]
    EndIf
  EndFor
  'sets the output to match Amir's output styling
  Output = WholeNumber+" AND "+Numerator+"/"+Denominator
  TextWindow.WriteLine(Output)
  'prompt whether or not to continue
  TextWindow.WriteLine("Again? (y/n)")
  Input = Text.ConvertToLowerCase(TextWindow.Read())
  If Input = "y" Or Input = "yes" Then
    ProgramRunning = "True"
  Else
    ProgramRunning = "False"
  EndIf
EndWhile
Program.End()

Sub CheckDivisibility
  'reset variables
  DivisibleBy = ""
  index = ""
  For i = 2 To (MaxNumber+1)/2
    If Math.Remainder(Numerator,i) = 0 And Math.Remainder(Denominator,i) = 0 Then
      'they're both divisible by that number
      'so we store that number in an array
      index = index+1
      DivisibleBy[index] = i
    EndIf
  EndFor
EndSub
