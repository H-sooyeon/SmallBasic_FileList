Init()

Main()



Sub Main

  GraphicsWindow.Title = "Uke madness"

  GraphicsWindow.ShowMessage("All tuned up and ready to go?","")

  GraphicsWindow.Show()

  While "True"



    ReCalc()  

    UpdateShapes()



    theChord = chords[chord]

    theLyric = lyrics[lyric]

    

    ' Show current

    Shapes.Move(theChord, 0, centerY)

    Shapes.Animate(theChord, centerX, centerY, delay / 4)



    Shapes.Move(theLyric, 0, centerY - 20)

    Shapes.Animate(theLyric, centerX, centerY - 20, delay / 4)

    

    Program.Delay(delay / 4)

    Sound.PlayClick()

    

    ' Next

    chord = chord + 1

    If chord > Array.GetItemCount(chords) Then

      chord = 1

    EndIf

    

    lyric = lyric + 1

    If lyric > Array.GetItemCount(lyrics) Then

      lyric = 1

    EndIf

    

    ' Preview next

    Shapes.Move(chords[chord], 0, centerY)

    Shapes.Move(lyrics[lyric], 5, centerY - fontSize)

    Program.Delay(delay / 4)

    

    Shapes.Animate(theChord, maxX, centerY, delay / 2)

    Shapes.Animate(theLyric, maxX, centerY - fontSize, delay / 2)

    

    Program.Delay(delay - delay / 2) ' already used half the delay above

    

  EndWhile

EndSub



Sub UpdateShapes

  For z = 1 to Array.GetItemCount(chords)

    Shapes.Move(chords[z],GraphicsWindow.Width,centerY)

  EndFor

  For z = 1 to Array.GetItemCount(lyrics)

    Shapes.Move(lyrics[z],GraphicsWindow.Width,centerY - fontSize)

  EndFor

EndSub    



Sub Init

  GraphicsWindow.Hide()

  InitVars()

  InitData()

  UpdateShapes()

EndSub



Sub InitData '  contrary to the name, it could use to be driven by actual data 

  

  If _Local = "True" Then

    img["C"] = ImageList.LoadImage(Program.Directory + "/c.png")

    img["F"] = ImageList.LoadImage(Program.Directory + "/f.png")

    img["G7"] = ImageList.LoadImage(Program.Directory + "/g7.png")

  Else

    img["C"] = ImageList.LoadImage("http://img696.imageshack.us/img696/7660/42811373.png")

    img["F"] = ImageList.LoadImage("http://img718.imageshack.us/img718/9028/45832583.png")

    img["G7"] = ImageList.LoadImage("http://img193.imageshack.us/img193/816/17800389.png")

  EndIf



  refImage = img["C"]



  ' C,C,F,C,C,G7,C,G7 - I wonder what else pandora would turn up with this progression in 3/4 time

  chords[1] = Shapes.AddImage(img["C"])

  chords[2] = Shapes.AddImage(img["C"])

  chords[3] = Shapes.AddImage(img["F"])

  chords[4] = Shapes.AddImage(img["C"])

  chords[5] = Shapes.AddImage(img["C"])

  chords[6] = Shapes.AddImage(img["G7"])

  chords[7] = Shapes.AddImage(img["C"])   

  chords[8] = Shapes.AddImage(img["G7"])

  

  GraphicsWindow.FontSize = fontSize

  

  lyrics[1] = Shapes.AddText("Little boxes on the") 

  lyrics[2] = Shapes.AddText("hillside, little") 

  lyrics[3] = Shapes.AddText("boxes made of")

  lyrics[4] = Shapes.AddText("ticky-tacky, little")

  lyrics[5] = Shapes.AddText("boxes on the") 

  lyrics[6] = Shapes.AddText("hillside, little") 

  lyrics[7] = Shapes.AddText("boxes all the") 

  lyrics[8] = Shapes.AddText("same. There's a")

  lyrics[9] = Shapes.AddText("green one, and a") 

  lyrics[10] = Shapes.AddText("pink one, and a") 

  lyrics[11] = Shapes.AddText("blue one, and a") 

  lyrics[12] = Shapes.AddText("yellow one, and they're")

  lyrics[13] = Shapes.AddText("all made out of") 

  lyrics[14] = Shapes.AddText("ticky-tacky, and they") 

  lyrics[15] = Shapes.AddText("all look just the") 

  lyrics[16] = Shapes.AddText("same.")  

EndSub

  

Sub InitVars

  SetLocal()

  fontSize = 20  

  delay = 1500

  chord = 1  

  lyric = 1

EndSub



Sub ReCalc

  centerX = GraphicsWindow.Width / 2 - ImageList.GetWidthOfImage(refImage) / 2

  centerY = GraphicsWindow.Height / 2 - ImageList.GetHeightOfImage(refImage) / 2

  imgX = ImageList.GetWidthOfImage(refImage)

  maxX = GraphicsWindow.Width - imgX

EndSub



Sub SetLocal

  If Text.GetIndexOf(Program.Directory, "http://") = 0 Then

    _Local = "True"

  Else    

    _Local = "False"

  EndIf    

EndSub  

  


