ss=44

Init()



z1=8

For dt=1 to z1

  For x=1 to z1

    gm[x][dt]=0 

    z=1 

    ldcall.Function5("DrawCube",x dt z 0 "teal")

    

  endfor

  LDFastShapes.Update ()

endfor

gm[4][4]=1

gm[4][5]=2

gm[5][4]=2

gm[5][5]=1

ccl[1]="Orange

ccl[2]="Brown

spl=1

chfrr()

bset()

sccd()

LDShapes.ShapeEvent =see

LDEvents.MouseWheel =rot



Sub sccd '-------------------------------------------scoreboad design

  GraphicsWindow.PenWidth =3

  GraphicsWindow.PenColor="teal

  For f=1 to 2

    pl[f]=Shapes.AddEllipse (40 25)

    Shapes.Move (pl[f],5,f*45+5)

    LDShapes.BrushColour (pl[f] ccl[f])

    scc[f]=shapes.AddText (2)

    Shapes.Move (scc[f],50,f*45+5)

    LDShapes.penColour (scc[f] "cyan")

  endfor

  GraphicsWindow.PenWidth =0

  LDShapes.PenColour (pl[spl] "yellow")

EndSub



Sub chkmv'------------------------------------------validate move

  valid="false

  If gm[px][py]>0 Then 

    'nil

  else

    LDCall.Function2 ("cdir" 0 1)

    LDCall.Function2 ("cdir" 0, -1)

    LDCall.Function2 ("cdir" 1 0)

    LDCall.Function2 ("cdir", -1 0)

    LDCall.Function2 ("cdir" 1 1)

    LDCall.Function2 ("cdir", -1, -1)

    LDCall.Function2 ("cdir" 1, -1)

    LDCall.Function2 ("cdir", -1 1)

  endif

EndSub 



Sub cdir'-----------------------------------------------check direction

  If cmod Then

    ipx=fx

    ipy=fy

  Else

    ipx=px

    ipy=py

  EndIf 

  

  dx=args[1]

  dy=args[2]

  cpx=ipx+dx

  cpy=ipy+dy

  

  If gm[cpx][cpy]=0 Then

    goto sxx'--------exit sub

  EndIf

  ww=0

  For f=1 To 7

    If gm[cpx][cpy]=3-spl Then 

      ww=ww+1

      cpx=cpx+dx

      cpy=cpy+dy

      

    Else 

      Goto xf'--------exit for

    EndIf

  EndFor

  

  xf:

  If ww>0 then

    if gm[cpx][cpy]=spl Then

      valid="true

      cpx=ipx

      cpy=ipy

      If cmod then

        ff[fx][fy]=2

      else

        For x=1 To ww

          cpx=cpx+dx

          cpy=cpy+dy

          

          gm[cpx][cpy]=spl

        endfor

      endif

    endif

  EndIf 

  sxx: 

EndSub 



Sub chfrr

  cmod="true

  For fx=1 To z1

    For fy=1 To z1

      ff[fx][fy]=0 

      If gm[fx][fy]=0 Then

        

        LDCall.Function2 ("cdir" 0 1)

        LDCall.Function2 ("cdir" 0, -1)

        LDCall.Function2 ("cdir" 1 0)

        LDCall.Function2 ("cdir", -1 0)

        LDCall.Function2 ("cdir" 1 1)

        LDCall.Function2 ("cdir", -1, -1)

        LDCall.Function2 ("cdir" 1, -1)

        LDCall.Function2 ("cdir", -1 1)

        

      EndIf

    EndFor

  EndFor

  cmod="false  

EndSub 





Sub see'--------------------------------------------shape events

  if LDShapes.LastEventType="MouseDown" Then

    ls=LDShapes.LastEventShape

    lt=text.GetSubTextToEnd(ls,8)/3

    py=Math.Remainder (lt-1,8)+1

    px=math.Floor ((lt-1)/8)+1 

    chkmv()

    If  valid Then

      GraphicsWindow.Title="Ok"

      gm[px][py]=spl

      

      bset()

      LDShapes.PenColour  (pl[spl] "teal")

      

      spl=3-spl

      LDShapes.PenColour  (pl[spl] "yellow")

      cntsc()

      chfrr()

    Else

      GraphicsWindow.Title="Wrong!!!"

    endif

    ' GraphicsWindow.Title= px+":"+py

  endif 

EndSub 



Sub cntsc '-------------------------------------score management

  cs1=0

  cs2=0 

  For ly=1 To z1

    For lx=1 To z1

      If gm[ly][lx]=1 Then

        cs1=cs1+1  

      elseIf gm[ly][lx]=2 Then

        cs2=cs2+1   

      EndIf

    EndFor 

  EndFor 

  Shapes.SetText (scc[1] cs1)

  Shapes.SetText (scc[2] cs2)

  

EndSub 



Sub rot '----------------------------------------wheel zoom

  ang=ang+ LDEvents.LastMouseWheelDelta/50 

  If ang<.75 Then

    ang=.75

  ElseIf ang>1.3 then

    ang=1.3

  EndIf 

  for w=1 to 8

    for wi=1 to 8

      For f=1 To 3

        ldfastShapes.zoom (po[w][wi][f],ang ang)

      endfor

    endfor

  endfor

  LDFastShapes.Update ()  

EndSub 



Sub bset'--------------------------------------update board

  ll=1

  For ly=1 To z1

    For lx=1 To z1

      

      If gm[ly][lx]>0 Then 

        lw=100

        si=(lx+(ly-1)*8)*3

        sn="Polygon"+si

        LDShapes.BrushColour (sn ccl[gm[ly][lx]])

     ' ElseIf ff[ly][lx]=2 then

        'lw=77 

      Else 

        lw=44

      EndIf

      

      ldfastShapes.SetOpacity (po[lx][ly][1] lw)

      ldfastShapes.SetOpacity (po[lx][ly][2] lw)

      ldfastShapes.SetOpacity (po[lx][ly][3] lw)

    EndFor 

  EndFor 

  

  LDFastShapes.Update ()   

EndSub 



'--------------------------=========cube draw section=============-------------------------------

Sub Conv3Dto2D

  cx = gw / 2 -   args[1]*ms1

  cx = cx + ms1 * args[2]

  cy = gh / 2 + ms6 * args[1]

  cy = cy + ms6 * args[2]

  cy = cy - ms3 * args[3]

EndSub



Sub DrawCube

  lz=args[3]

  xx=args[1]

  yy=args[2]

  Conv3Dto2D()

  x1 = math.Round (cx)

  y1 = math.Round (cy)

  sColor = args[5]

  For i=1 To 3 

    color[i] = LDColours.HSLtoRGB(ccc 1 .8/Math.Power(1.5 i))

  EndFor 

  GraphicsWindow.PenWidth =0

  i=1

  For a = 0 To 300 Step 60

    xxx[i] = math.Round (cx - mSin[a])

    yyy[i] = math.Round (cy - mCos[a])

    i=i+1

  endfor

  ppp=0

  For f=1 To 6

    ppp[f][1]=xxx[f]

    ppp[f][2]=yyy[f]

  endfor

  sargs=args

  LDCall.Function("Padd",1)

  

  ppp=0

  For f=2 To 4

    ppp[f-1][1]=xxx[f]

    ppp[f-1][2]=yyy[f]

  EndFor 

  ppp[4][1]=cx

  ppp[4][2]=cy

  

  LDCall.Function("Padd",2)

  

  ppp=0

  For f=1 To 2

    ppp[f][1]=xxx[f]

    ppp[f][2]=yyy[f]

  EndFor 

  ppp[3][1]=cx

  ppp[3][2]=cy

  ppp[4][1]=xxx[6]

  ppp[4][2]=yyy[6]

  

  LDCall.Function("Padd",3)

EndSub



Sub padd

  p2=args[1]

  sss=LDShapes.AddPolygon (ppp)

  

  po[xx][yy][p2]=   LDFastShapes.ShapeIndex (sss)

  If lz=1 And p2=3 Then

    ldShapes.SetShapeEvent (sss)

  endif

  If lz=2 Then

    sm[xx][yy][p2]=sss

  EndIf 

  LDShapes.BrushColour(sss,color[p2])

  if sargs[3]>1 then 

    ldfastShapes.SetOpacity (po[xx][yy][p2] 0)

  Else

    ldfastShapes.SetOpacity (po[xx][yy][p2] 100)

  endif 

  

EndSub 



Sub Init'---------------------------------------initialization

  args=0

  GraphicsWindow.Title = "IsoReversi

  GraphicsWindow.PenWidth=0

  bl=0

  rx=1

  LDUtilities.ShowNoShapeErrors ="false

  

  edge = Math.SquareRoot(2 / 3) * (ss-3)

  ms1=Math.SquareRoot(1 / 2)*ss

  ms6=Math.SquareRoot(1 / 6)*ss

  ms3=Math.SquareRoot(2 / 3)*ss

  

  c160=211

  ccc=c160

  gw = 1000

  gh = 800

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

  GraphicsWindow.BackgroundColor = "teal

  For a = 0 To 300 Step 60

    mSin[a]=edge * ldmath.Sin(a)

    mcos[a]=edge * ldmath.cos(a)

  EndFor 

EndSub


