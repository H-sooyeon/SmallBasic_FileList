Initialise()



While "True" 

  'UpdateGame

  If buttonClicked Then

    ProcessButtonClicked()

    buttonClicked = "False"

  EndIf

  

  'UpdateFrame() only for stuff that needs a frameRate

  FrameRate()

EndWhile



'=====================================================

'Change State -2nd Order (find best way to tell rest of program??) flag/flag off. How to hold state while game play??

'=====================================================

Sub ProcessButtonClicked

  lastButton = Controls.GetButtonCaption(Controls.LastClickedButton)    

  If lastButton = "FULL SCREEN" Then

    DoFullScreen()

    'flag Game State ???  if required

  ElseIf lastButton = "Restore" Then 

    DoRestoreScreen()

    'flag Game State ???

  ElseIf lastButton = "Show Task Bar" Then      

    DoShowTaskBar()

    'flag Game State ???     

  EndIf

EndSub



'=====================================================

'Button Response  - 3rd Order Subs

'=====================================================

Sub DoFullScreen

  GetWindowProperties()

  SetFullScreen()

  PositionWindowButtons()

  Controls.SetButtonCaption(winBtn[1], "Restore")

  Controls.ShowControl(winBtn[2]) 

EndSub



Sub DoRestoreScreen 

  RestoreWindowProperties()

  PositionWindowButtons()

  Controls.SetButtonCaption(winBtn[1], "FULL SCREEN")

  Controls.HideControl(winBtn[2])

EndSub



Sub DoShowTaskBar

  mssg = "Use the Windows key on your keyboard to access the task bar."

  mssg = mssg + LF + LF + "Ok to resume play"   

  GraphicsWindow.ShowMessage(mssg, "Game Paused")  

EndSub



'===============================================================

'Specific Action Subs

'===============================================================

Sub RestoreWindowProperties

  GraphicsWindow.Title = gwTitle

  GraphicsWindow.Left = gwLeft

  GraphicsWindow.Top = gwTop

  GraphicsWindow.Width = gwWidth

  GraphicsWindow.Height = gwHeight  

EndSub



Sub GetWindowProperties

  gwTitle = GraphicsWindow.Title

  gwLeft = GraphicsWindow.Left

  gwTop = GraphicsWindow.Top

  gwWidth = GraphicsWindow.Width

  gwHeight = GraphicsWindow.Height  

EndSub



Sub SetFullScreen  

  GraphicsWindow.Title = GraphicsWindow.Title + " FULL SCREEN"

  GraphicsWindow.Left = 0

  GraphicsWindow.Top = 0

  GraphicsWindow.CanResize = "False"

  GraphicsWindow.Width = Desktop.Width

  GraphicsWindow.Height = Desktop.Height  

EndSub



Sub PositionWindowButtons

  left = GraphicsWindow.Width - winBtnWidth - space

  Controls.Move(winBtn[1], left, space)

  left = left - winBtnWidth - space

  Controls.Move(winBtn[2], left, space)  

EndSub



'========================================================

'1st Order Subs

'========================================================

Sub FrameRate

  delay = 1000 / fps - (Clock.ElapsedMilliseconds - start)

  If delay > 0 Then

    Program.Delay(delay)

  EndIf

  start = Clock.ElapsedMilliseconds 

EndSub 



Sub Initialise

  LF = Text.GetCharacter(10)

  fps = 50

  space = 10

  winBtnWidth = 110

  winBtn[1] = Controls.AddButton("FULL SCREEN", left, space)

  winBtn[2] = Controls.AddButton("Show Task Bar", left, space)

  PositionWindowButtons()

  Controls.SetSize(winBtn[1], winBtnWidth, 25)

  Controls.SetSize(winBtn[2], winBtnWidth, 25)

  Controls.HideControl(winBtn[2])

  

  Controls.ButtonClicked = OnButtonClicked

EndSub



Sub OnButtonClicked

  buttonClicked = "True"

EndSub


