' SmallBasic Version 1.2

' Program: MaLEncode

' Changelog: 

' Author: Pappa Lapub

' Website: 

' ImportURL: http://smallbasic.com/program/?

' SBOnline: http://smallbasic-publicwebsite-dev.azurewebsites.net/Program/view.aspx?

' Extension: ---

' Comment: 

' 

' Variables: 

' ToDo: 

' ==============================================================================

enc = "MaLeFiSoHu"    ' "noralehibu"



str = "This is a primitive En-/Decoding with '"+ enc +"'."

'str = "Tufo fo m prfaftfvl Ln-/Dlcsdfng wftu 'AmElIfOsUh'."   '' Remove comment mark, to DEcode back



len = Text.GetLength(str)

enc = Text.ConvertToLowerCase(enc)

lenEnc = 2 * Math.Floor(Text.GetLength(enc) / 2)  ' only even #

For n = 1 To lenEnc

  If Array.ContainsValue(arrEnc, Text.GetSubText(enc, n,1)) Then

    TextWindow.WriteLine("Double '"+ Text.GetSubText(enc, n,1) +"' !")

    TextWindow.WriteLine("Use another cipher with only ONE character (even number) of each letter.")

    TextWindow.Read()

    Program.End()

  EndIf

  arrEnc[n] = Text.GetSubText(enc, n,1)

EndFor



For sc = 1 To len

  chr = Text.GetSubText(str, sc,1)

  If Array.ContainsValue(arrEnc, Text.ConvertToLowerCase(chr)) Then

    GetPosCase()

    If Math.Remainder(pos, 2) = 0 Then    ' if even, then previous # in the cipher

      cOut = arrEnc[pos - 1]

    Else                                  ' ... else next # in the cipher

      cOut = arrEnc[pos + 1]

    EndIf

    

    If bUpper Then                        ' use capitals like in the original

      cOut = Text.ConvertToUpperCase(cOut)

    EndIf

  Else

    cOut = chr

  EndIf

  out = Text.Append(out, cOut)

EndFor



TextWindow.WriteLine(str +"  -->")

TextWindow.WriteLine(out)

TextWindow.WriteLine("")





' ////////////////////  SUBs  ////////////////////

Sub GetPosCase

  pos = ""

  bUpper = ""

  For p = 1 To lenEnc

    If arrEnc[p] = Text.ConvertToLowerCase(chr) Then

      pos = p

      If chr = Text.ConvertToUpperCase(chr) Then

        bUpper = "True"

      EndIf

      p = lenEnc

    EndIf

  EndFor

EndSub




