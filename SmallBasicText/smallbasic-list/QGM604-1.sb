' Challenge of the month August 2013  Duck Shooting Game   by NaochanON      QGM604-0
' Interface Challenge  Write a fancy Game Opening screen for DuckShoot game   CRB694-1
'  Throw  Boomerang   FHX732-0
LRShapes_Init()
LRShapes_Add()         '  add  duck's shapes  
Back_Shapes_Init()
Back_Shapes_Add()      '  add  grass's  shapes 
BMRShapes_Init()
BMRShapes_Add()        '  add  boomerang's shapes
OpeningAnime()         '  opening animation 

GraphicsWindow.MouseDown=Onmousedown
GraphicsWindow.MouseMove=Onmousemove
GraphicsWindow.MouseUp=onmouseup
falling="False"
While ThrowBNMB<11
  Shapes.SetText(msg, " Now  you  threw "+(ThrowBNMB-1)+" boomerangs.   You  hit "+HitCount+"  ducks !!!!!")  
  NN=NN+1
  DuckFlying()
  Grass_Swing()
  If Throw="True" Then 
    Fly_boomerang()                            '   boomerang  is throwed
    hitcheck()
  endif
  Program.Delay(20)
endwhile
Ending()

Sub hitcheck
  FlyBMR_XY ="X="+ Shapes.GetLeft(BMRshp[ThrowBNMB][1]["obj"])+";Y="+Shapes.GetTop(BMRshp[ThrowBNMB][1]["obj"])    ' Flying boomerang position 
  FlyDuck_XY="X="+ Shapes.GetLeft(shp[LR[LRNMB]][M][1]["obj"]) +";Y="+ Shapes.Gettop(shp[LR[LRNMB]][M][1]["obj"]) ' Flying Duck position 
  HeadWidth=shp[LR[LRNMB]][M][1]["width"]*s1   '  Duck's  head size 
  If -30<FlyDuck_XY["Y"]  And  FlyDuck_XY["Y"]<450 Then 
    If Math.Abs(FlyDuck_XY["X"]-FlyBMR_XY["X"])<HeadWidth  and Math.Abs(FlyDuck_XY["Y"]-FlyBMR_XY["Y"])<HeadWidth Then '  if  Hit    
      HitCount=HitCount+1 
      Shapes.Move(BMRshp[ThrowBNMB][1]["obj"],Shapes.GetLeft(BMRshp[ThrowBNMB][1]["obj"]),Shapes.Gettop(BMRshp[ThrowBNMB][1]["obj"])-HeadWidth-3 )
      Shapes.Move(BMRshp[ThrowBNMB][2]["obj"],Shapes.GetLeft(BMRshp[ThrowBNMB][2]["obj"]),Shapes.Gettop(BMRshp[ThrowBNMB][2]["obj"])-HeadWidth-3 )
      Shapes.SetText(msg, " Hit  !!!!!")  
      Sound.PlayClickAndWait()
      Sound.PlayClickAndWait()
      falling="True"
      duck_falling()
    endif
  endif
EndSub

Sub duck_falling
  If falling="True" Then 
    for MM=1 To 200
      For i=1 to 9
        shapes.Move(shp[LR[LRNMB]][M][i]["obj"],Shapes.GetLeft(shp[LR[LRNMB]][M][i]["obj"]),Shapes.Gettop(shp[LR[LRNMB]][M][i]["obj"])+MM )
      EndFor
      Shapes.Move(BMRshp[ThrowBNMB][1]["obj"],Shapes.GetLeft(BMRshp[ThrowBNMB][1]["obj"]),Shapes.Gettop(BMRshp[ThrowBNMB][1]["obj"])+MM )
      Shapes.Move(BMRshp[ThrowBNMB][2]["obj"],Shapes.GetLeft(BMRshp[ThrowBNMB][2]["obj"]),Shapes.Gettop(BMRshp[ThrowBNMB][2]["obj"])+MM )
      Shapes.SetOpacity(BMRshp[ThrowBNMB][1]["obj"],100*(1-MM/200))
      Shapes.SetOpacity(BMRshp[ThrowBNMB][2]["obj"],100*(1-MM/200))
      Program.Delay(20)
    endfor  
    falling="False"
    FlyNMB=FlyNMB+1
    flyNMB_speed() 
    FlyBMR_NMB()
  EndIf
EndSub


Sub Fly_boomerang
  If  Shapes.GetTop(BMRshp[ThrowBNMB][1]["obj"])>-100  Then 
    k=k+dk                                 '  rotating angle 
    dZM=(1-K/ZM)                           '  zoom ratio
    dx=dx+dd["X"]                          '  sliding rate  X 
    dy=dy+dd["Y"]                          '  sliding rate  Y
    RK= Math.GetRadians(-k)                '  unclockwise   BMRX[j]
    '------------------------------------------------------------------------------
    Shapes.Zoom(BMRshp[ThrowBNMB][1]["obj"],dZM,dZM)  '  boomerang  size becomes smaller  gradually 
    Shapes.Move(BMRshp[ThrowBNMB][1]["obj"],BMRX[ThrowBNMB]+dZM*DR[1]*Math.Sin(-RK)-dZM*DW[1]+dx,BMRY[ThrowBNMB]+dZM*DR[1]*Math.cos(-RK)-dZM*DR[1]-dy)
    Shapes.Rotate(BMRshp[ThrowBNMB][1]["obj"],-k)
    '------------------------------------------------------------------------------
    RK2= Math.GetRadians(-(k-90))
    Shapes.Zoom(BMRshp[ThrowBNMB][2]["obj"],dZM,dZM)
    Shapes.Move(BMRshp[ThrowBNMB][2]["obj"],BMRX[ThrowBNMB]+dZM*DR[2]*Math.Sin(-RK2)-dZM*DW[2]+dx,BMRY[ThrowBNMB]+dZM*DR[2]*Math.cos(-RK2)-dZM*DR[2]-dy)
    Shapes.Rotate(BMRshp[ThrowBNMB][2]["obj"],-(k-90))
  Else 
    FlyBMR_NMB()
  endif
EndSub

Sub FlyBMR_NMB
  Throw="False"
  ThrowBNMB=ThrowBNMB+1                                '  next  boomerang 
  Shapes.SetOpacity(BMRshp[ThrowBNMB][1]["obj"],100)   '  next  boomerang  appears 
  Shapes.SetOpacity(BMRshp[ThrowBNMB][2]["obj"],100)
EndSub

Sub DuckFlying
  M=FlyNMB
  if  Shapes.Gettop(shp[LR[LRNMB]][M][1]["obj"])>-100 Then 
    delay=0
    FLYDY[M]= 5-Math.GetRandomNumber(10)                       '  Flapping rate 
    Shapes.Zoom(shp[LR[LRNMB]][M][4]["obj"],1,0.7-FlyDY[M]/7)  '  duck  Flapping  
    Shapes.Zoom(shp[LR[LRNMB]][M][6]["obj"],1,0.7-FlyDY[M]/7) 
    For i=1 to 9
      shapes.Move(shp[LR[LRNMB]][M][i]["obj"],Shapes.GetLeft(shp[LR[LRNMB]][M][i]["obj"])+DuckDX[M],Shapes.Gettop(shp[LR[LRNMB]][M][i]["obj"]) -DuckDY[M])
    EndFor
  Else 
    delay=delay+1
  EndIf
  If delay>= 30 Then                                           '  Next  Duck appears   if  delay>=30  
    FlyNMB=FlyNMB+1
    delay=0
    flyNMB_speed()
  EndIf 
EndSub

Sub flyNMB_speed
  LRNMB= Math.Remainder(FlyNMB,2)+1                            '   Duck flying direction  ....  1:Right  or   2:Left
  DuckDX[FlyNMB]=(Math.GetRandomNumber(10)-Math.Remainder(FlyNMB,2)*20)/3 '   flying speed  X 
  DuckDY[FlyNMB]=(16-Math.GetRandomNumber(6))/4                           '   flying speed  XY
endsub


Sub Ending
  Shapes.SetText(msg, " You hit "+HitCount+"  ducks !!!!!")
  Shapes.Zoom(msg,3,3)
  Shapes.Animate(msg,700,550,3000)
  Sound.PlayChime()
EndSub

Sub Onmouseup
  If catch<>"False" Then 
    Mx2= GraphicsWindow.MouseX
    My2= GraphicsWindow.MouseY
    BMRX[ThrowBNMB]=Mx2               '  released point  X
    BMRY[ThrowBNMB]=MY2               '  released point  Y
    MX1=MX2
    MY1=MY2 
  EndIf
  catch="false"
  dd="X="+(Mx1-MX)/10  +";Y="+ (MY-My1)/10   '   sliding  rate   
  Throw="True"
  k=0                                      '   angle 
  dx=0                                     '   sliding X
  dY=0                                     '   sliding Y 
  dk=20                                    '  rotating rate 
  ZM=3500                                  '  zoom rate 
  DR="1="+(BMRshp[ThrowBNMB][1]["height"]*s3/2)+";2="+(BMRshp[ThrowBNMB][2]["height"]*s3/2)        '  radius  length 
  DW="1="+(BMRshp[ThrowBNMB][1]["width"]*s3/2)+";2="+(BMRshp[ThrowBNMB][2]["width"]*s3/2)          '  width/2 
EndSub

Sub Onmousemove
  If catch="true" Then 
    Mx1= GraphicsWindow.MouseX
    My1= GraphicsWindow.MouseY
    Shapes.Move(BMRshp[ThrowBNMB][1]["obj"],Mx1,My1)                 '  moved by mouse
    Shapes.Move(BMRshp[ThrowBNMB][2]["obj"],Mx1-45*s3,My1-45*s3)
    Distance_r=Math.SquareRoot((Mx1-BMRX[ThrowBNMB])*(Mx1-BMRX[ThrowBNMB])+ (My1-BMRY[ThrowBNMB])*(My1-BMRY[ThrowBNMB])) 
    If Distance_r>150 Then              '  Distance_r=distance from initial position 
      BMRX[ThrowBNMB]=Mx1               '  released point  X
      BMRY[ThrowBNMB]=MY1               '  released point  Y
      catch="False"
      onmouseup()                       '  forced  throwing 
    endif 
  EndIf
EndSub

Sub Onmousedown
  Mx= GraphicsWindow.MouseX
  My= GraphicsWindow.MouseY
  If math.Abs(MX-BMRX[ThrowBNMB])<10 And math.Abs(MY-BMRY[ThrowBNMB])<30 Then '  if  boomerang is caught 
    catch="true"
  EndIf
EndSub

Sub OpeningAnime
  While LX>-1000
    NN=NN+1 
    Grass_Swing()
    For j=1 To 10
      DuckDX[j]=5+Math.GetRandomNumber(5)                    '  Flapping rate 
      DuckDY[j]= 5-Math.GetRandomNumber(10)
      Shapes.Zoom(shp["L"][j][4]["obj"],1,0.7-DuckDY[j]/7)   '  duck flaps
      Shapes.Zoom(shp["L"][j][6]["obj"],1,0.7-DuckDY[j]/7) 
      Shapes.Zoom(shp["R"][j][4]["obj"],1,0.7-DuckDY[j]/7)
      Shapes.Zoom(shp["R"][j][6]["obj"],1,0.7-DuckDY[j]/7) 
      For i=1 to 9
        shapes.Move(shp["L"][j][i]["obj"],Shapes.GetLeft(shp["L"][j][i]["obj"])-DuckDX[j],Shapes.Gettop(shp["L"][j][i]["obj"]) +DuckDY[j])
        shapes.Move(shp["R"][j][i]["obj"],Shapes.GetLeft(shp["R"][j][i]["obj"])+DuckDX[j],Shapes.Gettop(shp["R"][j][i]["obj"]) +DuckDY[j])
      EndFor
    endfor
    Program.Delay(20)   
    LX=Shapes.GetLeft( shp["L"][1][1]["obj"])                '  1st  Duck's  position  X
    Shapes.Move(msg,LX,shapes.GetTop(msg))
  EndWhile
  Shapes.Move(msg,100,shapes.GetTop(msg))
  Shapes.SetText(msg,"    Now start  Duck  shooting !!!!!!!!!!!!!!")
  InitPosition()
  Program.Delay(1500)   
  '-----------------------------------------------------------------------------------------------------------
  ThrowBNMB=1                                          '  1st boomerang 
  Shapes.SetOpacity(BMRshp[ThrowBNMB][1]["obj"],100)   '  1st boomerang  appears 
  Shapes.SetOpacity(BMRshp[ThrowBNMB][2]["obj"],100)
  '---------------------------------------------------------------------------------------------------------
  FlyNMB=1                                             '   starting  Duck   Number
  flyNMB_speed()
  '---------------------------------------------------------------------------------------------------------
  HitCount=0                                           '   hit count
endsub

Sub InitPosition
  For j=1 To 10
    Duckdy=50-Math.GetRandomNumber(50)
    For i=1 to 9
      Shapes.Move(shp["L"][j][i]["obj"], 700 + shp["L"][j][i]["x"]*s1+(j-1)*15, 400 + shp["L"][j][i]["y"]*s1+Duckdy)
      Shapes.Move(shp["R"][j][i]["obj"], 200 + shp["R"][j][i]["x"]*s1+(j-1)*15, 400 + shp["R"][j][i]["y"]*s1+Duckdy)
    EndFor
  EndFor 
endsub

Sub Grass_Swing
  If Math.Remainder(NN,8)=0 Then 
    Gda=5-Math.GetRandomNumber(8)
    For j=1 To GN
      For i=1 To 16 Step 2
        Shapes.Rotate(Bshp[j][i]["obj"], Gda)
        Shapes.Rotate(Bshp[j][i+1]["obj"], -1.2*Gda)
      EndFor
    EndFor
  endif
EndSub

Sub BMRShapes_Init
  BNMB=10                                    '  Boomerang number 
  CLR="1=#B1300E;2=#B13FFE;3=#316745;4=#DCCB18;5=#8D6449;6=#494A41;7=#D4ACAD;8=#82AE46;9=#00A497;10=#165E83" '  Color  of boomerang
  For j=1 To BNMB
    CLR[j]=GraphicsWindow.GetRandomColor()
    BMRshp[j][1] = "func=ell;x=0;y=0;width=20;height=60;angle=0;bc="+CLR[j]+";pc="+CLR[j]+";pw=2;"
    BMRshp[j][2] = "func=ell;x=-45;y=-45;width=20;height=90;angle=-90;bc="+CLR[j]+";pc="+CLR[j]+";pw=2;"
  EndFor
EndSub


Sub BMRShapes_Add
  BX0 = 450
  BY0 = 650 
  For j=BNMB To 1 Step -1
    s3=0.6                                       '  S3
    dx=dx+j*3
    dy=0 
    For i = 1 To 2
      GraphicsWindow.PenWidth = BMRshp[j][i]["pw"]*s3 
      GraphicsWindow.PenColor = BMRshp[j][i]["pc"]
      GraphicsWindow.BrushColor = BMRshp[j][i]["bc"]
      If BMRshp[j][i]["func"] = "ell" Then
        BMRshp[j][i]["obj"] = shapes.AddEllipse(BMRshp[j][i]["width"]*s3, BMRshp[j][i]["height"]*s3)
      EndIf
      Shapes.Move(BMRshp[j][i]["obj"], BX0 + BMRshp[j][i]["x"]*s3+dX, BY0 + BMRshp[j][i]["y"]*s3+dy)
      If BMRshp[j][i]["angle"] <> 0 Then
        Shapes.Rotate(BMRshp[j][i]["obj"], BMRshp[j][i]["angle"])
      EndIf
    EndFor
    Shapes.SetOpacity(BMRshp[j][1]["obj"],0)   '  boomerang  disappears 
    Shapes.SetOpacity(BMRshp[j][2]["obj"],0)
    BMRX[j]= Shapes.GetLeft(BMRshp[j][1]["obj"])+BMRshp[j][1]["width"]*s3/2      '   cross point  X
    BMRY[j]= Shapes.Gettop(BMRshp[j][1]["obj"])                                '   cross point  Y
  endfor
  dx=0
EndSub

Sub LRShapes_Init
  For j=1 To 10
    Shp["R"][j][1] = "func=ell;x=99;y=0;width=56;height=24;bc=#DFCA54;pc=#000000;pw=2;"              '    Head
    Shp["R"][j][2] = "func=ell;x=129;y=6;width=15;height=6;bc=#DF3954;pc=#000000;pw=2;"              '  Eye
    Shp["R"][j][3] = "func=ell;x=105;y=12;width=16;height=36;angle=45;bc=#00CA54;pc=#000000;pw=2;"    '   Neck
    Shp["R"][j][4] = "func=tri;x=50;y=5;x1=21;y1=0;x2=0;y2=57;x3=29;y3=57;bc=#DFCA54;pc=#000000;pw=2;"  'Wing
    Shp["R"][j][5] = "func=ell;x=1;y=38;width=108;height=31;bc=#DFCA54;pc=#000000;pw=2;"              '  Body
    Shp["R"][j][6] = "func=tri;x=37;y=0;x1=7;y1=0;x2=0;y2=57;x3=29;y3=57;bc=#DF8754;pc=#000000;pw=2;"  'Wing
    Shp["R"][j][7] = "func=ell;x=144;y=15;width=29;height=9;angle=13;bc=#DF8754;pc=#000000;pw=2;"      'Mouth
    Shp["R"][j][8] = "func=line;x=8;y=60;x1=0;y1=8;x2=49;y2=7;pc=#DF3954;pw=2;"
    Shp["R"][j][9] = "func=line;x=0;y=70;x1=9;y1=0;x2=0;y2=10;pc=#DF3954;pw=2;"
    Shp["L"][j][1] = "func=ell;x=29;y=17;width=61;height=38;bc=#DFCA54;pc=#000000;pw=2;"              '    Head
    Shp["L"][j][2] = "func=ell;x=46;y=23;width=18;height=9;angle=354;bc=#DF3954;pc=#000000;pw=2;"     '   Eye
    Shp["L"][j][3] = "func=ell;x=70;y=45;width=16;height=36;angle=313;bc=#00CA54;pc=#000000;pw=2;"    '   Neck 
    Shp["L"][j][4] = "func=tri;x=104;y=4;x1=10;y1=0;x2=0;y2=64;x3=45;y3=64;angle=0;bc=#DFCA54;pc=#000000;pw=2;" '  Wing 
    Shp["L"][j][5] = "func=ell;x=80;y=58;width=146;height=44;bc=#DFCA54;pc=#000000;pw=2;"             '  Body
    Shp["L"][j][6] = "func=tri;x=144;y=0;x1=40;y1=0;x2=0;y2=75;x3=46;y3=75;angle=0;bc=#DF8754;pc=#000000;pw=2;"  ' Wing 
    Shp["L"][j][7] = "func=ell;x=0;y=34;width=39;height=14;angle=354;bc=#DFCA54;pc=#000000;pw=2;"     '  Beak
    Shp["L"][j][8] = "func=line;x=157;y=95;x1=0;y1=0;x2=58;y2=4;pc=#DF3954;pw=4;"                     '   Leg  
    Shp["L"][j][9] = "func=line;x=213;y=99;x1=0;y1=0;x2=14;y2=9;pc=#DF3954;pw=4;"                     '  Leg
  EndFor 
EndSub

Sub Back_Shapes_Init
  GN=7
  X0 =-20 
  Y0 = 140 
  For j=1 To GN
    Bshp[j][1] = "func=tri;x=6;y=20;x1=6;y1=0;x2=0;y2=93;x3=13;y3=93;angle=1;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][2] = "func=rect;x=6;y=110;width=14;height=76;angle=4;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][3] = "func=tri;x=30;y=7;x1=6;y1=0;x2=0;y2=93;x3=13;y3=93;angle=1;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][4] = "func=rect;x=30;y=98;width=14;height=76;angle=1;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][5] = "func=tri;x=50;y=21;x1=5;y1=0;x2=0;y2=80;x3=11;y3=80;angle=359;bc=#AD9E00;pc=#AD9E00;pw=2;"
    Bshp[j][6] = "func=rect;x=49;y=97;width=13;height=78;angle=2;bc=#AD9E00;pc=#AD9E00;pw=2;"
    Bshp[j][7] = "func=tri;x=79;y=39;x1=5;y1=0;x2=0;y2=71;x3=10;y3=71;angle=358;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][8] = "func=rect;x=77;y=108;width=14;height=70;angle=2;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][9] = "func=tri;x=96;y=14;x1=6;y1=0;x2=0;y2=93;x3=13;y3=93;angle=6;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][10] = "func=rect;x=96;y=105;width=14;height=76;angle=3;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][11] = "func=tri;x=106;y=37;x1=6;y1=0;x2=0;y2=66;x3=13;y3=66;angle=1;bc=#759E00;pc=#759E00;pw=2;"
    Bshp[j][12] = "func=rect;x=106;y=104;width=14;height=70;angle=2;bc=#759E00;pc=#759E00;pw=2;"   '759
    Bshp[j][13] = "func=tri;x=123;y=45;x1=5;y1=0;x2=0;y2=71;x3=10;y3=71;angle=358;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][14] = "func=rect;x=122;y=111;width=13;height=67;angle=2;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][15] = "func=tri;x=174;y=54;x1=6;y1=0;x2=0;y2=57;x3=13;y3=57;angle=1;bc=#009E00;pc=#009E00;pw=2;"
    Bshp[j][16] = "func=rect;x=172;y=110;width=14;height=76;angle=3;bc=#009E00;pc=#009E00;pw=2;"
  endfor
  Bshp[1][17] ="func=rect;x=0;y=155;width=1200;height=600;bc=#DF8722;pc=#AD9E00;pw=2;"  '   land
EndSub


Sub LRShapes_Add
  GraphicsWindow.Hide()
  GraphicsWindow.Title=" Duck shooting game  ...   Using boomerang  !!!! "
  GraphicsWindow.BackgroundColor="Lightcyan" 
  GraphicsWindow.Left=20
  GraphicsWindow.top=20
  GraphicsWindow.Width=1200
  GraphicsWindow.Height=700
  '------------------------------------------------------------------------------------------
  XY0["X"] ="1=-2000;2=1500"
  XY0["Y"] ="1=300;2=300" 
  LR="1=R;2=L"
  For k=1 To 2
    For j=1 To 10
      Duckdy=50-Math.GetRandomNumber(100)
      s1=(3 + (30-Math.GetRandomNumber(20))/10 )/10                         '  S1        
      For i = 1 To 9
        GraphicsWindow.PenWidth = shp[LR[k]][j][i]["pw"]*s1 
        GraphicsWindow.PenColor = shp[LR[k]][j][i]["pc"]
        GraphicsWindow.BrushColor = shp[LR[k]][j][i]["bc"]
        If shp[LR[k]][j][i]["func"] = "ell" Then
          shp[LR[k]][j][i]["obj"] = shapes.AddEllipse(shp[LR[k]][j][i]["width"]*s1, shp[LR[k]][j][i]["height"]*s1)
        ElseIf shp[LR[k]][j][i]["func"] = "tri" Then
          shp[LR[k]][j][i]["obj"] = shapes.AddTriangle(shp[LR[k]][j][i]["x1"]*s1, shp[LR[k]][j][i]["y1"]*s1, shp[LR[k]][j][i]["x2"]*s1, shp[LR[k]][j][i]["y2"]*s1, shp[LR[k]][j][i]["x3"]*s1, shp[LR[k]][j][i]["y3"]*s1 )
        ElseIf shp[LR[k]][j][i]["func"] = "line" Then
          shp[LR[k]][j][i]["obj"] = shapes.AddLine(shp[LR[k]][j][i]["x1"]*s1, shp[LR[k]][j][i]["y1"]*s1, shp[LR[k]][j][i]["x2"]*s1, shp[LR[k]][j][i]["y2"]*s1)
        EndIf
        Shapes.Move(shp[LR[k]][j][i]["obj"], Xy0["X"][k] + shp[LR[k]][j][i]["x"]*s1+(j-1)*150, XY0["Y"][K] + shp[LR[k]][j][i]["y"]*s1+Duckdy)
        If shp[LR[k]][j][i]["angle"] <> 0 Then
          Shapes.Rotate(shp[LR[k]][j][i]["obj"], shp[LR[k]][j][i]["angle"])
        EndIf
      EndFor
    endfor
  endfor
  '------------------------------------------------------------------------------------------
  LX=Shapes.GetLeft( shp["L"][1][1]["obj"])
  RX=Shapes.GetLeft( shp["R"][1][1]["obj"])
EndSub

Sub Back_Shapes_Add
  '------------------------------------------------------------------------------------------
  For j=1 To GN
    s2=1.1+Math.GetRandomNumber(3)/10                          '  S2
    For i = 17 To 1 Step -1
      GraphicsWindow.PenWidth = Bshp[j][i]["pw"]*s2
      GraphicsWindow.PenColor = Bshp[j][i]["pc"]
      GraphicsWindow.BrushColor = Bshp[j][i]["bc"]
      If Bshp[j][i]["func"] = "rect" Then
        Bshp[j][i]["obj"] = shapes.AddRectangle(Bshp[j][i]["width"]*s2, Bshp[j][i]["height"]*s2)
      ElseIf Bshp[j][i]["func"] = "tri" Then
        Bshp[j][i]["obj"] = shapes.AddTriangle(Bshp[j][i]["x1"]*s2, Bshp[j][i]["y1"]*s2, Bshp[j][i]["x2"]*s2, Bshp[j][i]["y2"]*s2, Bshp[j][i]["x3"]*s2, Bshp[j][i]["y3"]*s2 )
      endif
      Shapes.Move(Bshp[j][i]["obj"], X0 + Bshp[j][i]["x"]*s2+(j-1)*170, Y0 + Bshp[j][i]["y"]*s2)
      If Bshp[j][i]["angle"] <> 0 Then
        Shapes.Rotate(Bshp[j][i]["obj"], Bshp[j][i]["angle"])
      EndIf
    EndFor
  endfor
  '------------------------------------------------------------------------------------------
  GraphicsWindow.BrushColor="White"
  Circle= Shapes.AddEllipse(600,600)
  Shapes.Move(Circle,300,450)
  '------------------------------------------------------------------------------------------
  GraphicsWindow.BrushColor="Red"
  GraphicsWindow.FontSize=20
  CRLF= Text.GetCharacter(13)+Text.GetCharacter(10)
  msg="Duck Shooting Game ........ You can throw 10 boomerangs to get Ducks ! "+CRLF+"Click  the boomerang and release it in the white circle."+CRLF+"If boomerang hits duck's head, you can get a duck"
  msg= Shapes.AddText(msg)
  Shapes.Move(msg,1200,50)
  GraphicsWindow.Show()
EndSub
