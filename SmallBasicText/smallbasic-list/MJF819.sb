' SmallBasic Version 1.2
' Program: BaseConverter
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/dc6ef888-5d4c-4806-a58b-51248d6c4ee4/convert-between-decimal-and-hexatrigesimal-numbers-base10-and-base36
' ImportURL: http://smallbasic.com/program/?
' Extension: 
' Comment: https://stackoverflow.com/questions/34574203/c-sharp-base-converter
'  https://stackoverflow.com/questions/923771/quickest-way-to-convert-a-base-10-number-to-any-base-in-net
' Variables: 
' ToDo: 
' ================================================================================
'' 1. ToBaseX:
val = 447       ' decimal
base = 36
upperCase = "True"      ' return letters in upperCase ABC..  ?

TextWindow.Write(val +" to base "+ base +" = ")
ConvertDecToBase()      ' (val, base) -> res
TextWindow.WriteLine(res)
TextWindow.WriteLine("")


'' 2. FromBaseX:
val = "CF"  ' "cf"  ' res
base = 36

TextWindow.Write(val +" from base "+ base +" = ")
ConvertBaseToDec()      ' (val, base) -> res
TextWindow.WriteLine(res)
TextWindow.WriteLine("")


' //////////  SUBs  \\\\\\\\\\
Sub ConvertDecToBase  ' (val, base) -> res
  chars = "0123456789abcdefghijklmnopqrstuvwxyz"
  If upperCase Then
    chars = Text.ConvertToUpperCase(chars)
  EndIf
  
  If val < base Then
    res = Text.GetSubText(chars, val+1,1)
  Else
    While val <> 0
      id = Math.Remainder(val, base)
      res = Text.Append(Text.GetSubText(chars, id+1,1), res)
      val = Math.Floor(val / base)
    EndWhile
  EndIf
EndSub


Sub ConvertBaseToDec  ' (val, base) -> res
  chars = "0123456789abcdefghijklmnopqrstuvwxyz"  ' val AND chars[]  ,  SAME  upper- / lowerCase
  val = Text.ConvertToLowerCase(val)
  res = 0
  
  len = Text.GetLength(val)
  For pow = len-1 To 0 Step -1
    ch = Text.GetSubText(val, len-pow,1)
    GetCharIndex()  ' (chars, ch) -> id [0,35]
    res = res + id * Math.Power(base, pow)
  EndFor
EndSub

Sub GetCharIndex  ' (chars, ch) -> id [0,35]
  id = 0
  While Text.GetSubText(chars, id+1,1) <> ch
    id = id + 1
  EndWhile
EndSub
    
