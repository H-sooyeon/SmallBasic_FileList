'===============================================
'STARTER PACMAN GAME
'===============================================

'===============================================
'INITIALISE
'===============================================

Initialise()

'Initialise Key events
GraphicsWindow.KeyDown = OnKeyDown
keyPressed = 0

'===============================================
'MAIN GAME LOOP
'===============================================

While ("True")
  If (keyPressed = 1) Then 'Process the key and reset the press
    processKey()
    keyPressed = 0
  EndIf
  Shapes.Animate(sprite,spriteX*50-25-20,spriteY*50-25-20,100) ' A short animate looks smoother
  Program.Delay(100)
EndWhile

'===============================================
'EVENT SUBROUTINES
'===============================================

'Key press event subroutines
'We need a new key press for each movement
Sub OnKeyDown
  lastKey = GraphicsWindow.LastKey
  keyPressed = 1
EndSub

'===============================================
'OTHER SUBROUTINES
'===============================================

Sub processKey
  'Check for walls
  cenX = spriteX*50-25 'Box centre
  cenY = spriteY*50-25
  leftPixel = GraphicsWindow.GetPixel(cenX-25,cenY)
  rightPixel = GraphicsWindow.GetPixel(cenX+25,cenY)
  upPixel = GraphicsWindow.GetPixel(cenX,cenY-25)
  downPixel = GraphicsWindow.GetPixel(cenX,cenY+25)
  
  'Move object - NO diagonal movement
  If (lastKey = "Left" And leftPixel = bgColor) Then
    spriteX = spriteX-1
  ElseIf (lastKey = "Right" And rightPixel = bgColor) Then
    spriteX = spriteX+1
  ElseIf (lastKey = "Up" And upPixel = bgColor) Then
    spriteY = spriteY-1
  ElseIf (lastKey = "Down" And downPixel = bgColor) Then
    spriteY = spriteY+1
  EndIf
EndSub

Sub Initialise
  'Create the Window
  gw = 400
  gh = 400
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.Show()
  'Draw the background (just bigger than the window) - we have to do this to detect the colour with Shapes.GetPixel
  bgColor = "#FFAAFF" 'Pink
  GraphicsWindow.BrushColor = bgColor
  GraphicsWindow.FillRectangle(-10,-10,gw+20,gh+20)
  
  'Draw Walls
  GraphicsWindow.PenWidth = 6
  GraphicsWindow.PenColor = "Black"
  'First a Full Mesh
  For i = 0 To 8
    GraphicsWindow.DrawLine(i*50,0,i*50,gh)
    GraphicsWindow.DrawLine(0,i*50,gw,i*50)
  EndFor
  'Now make some spaces - this is just a quick test - you will need a better way to create a grid
  GraphicsWindow.PenColor = bgColor
  For x = 1 To 50
    i = Math.GetRandomNumber(8)
    j = Math.GetRandomNumber(6)
    GraphicsWindow.DrawLine((i-1)*50+3,j*50,i*50-3,j*50)
    i = Math.GetRandomNumber(6)
    j = Math.GetRandomNumber(8)
    GraphicsWindow.DrawLine(i*50,(j-1)*50+3,i*50,j*50-3)
  EndFor
  
  'Add a sprite
  GraphicsWindow.BrushColor = "Blue"
  sprite = Shapes.AddEllipse(40,40)
  spriteX = 4 'Column
  spriteY = 6 'Row
  Shapes.Move(sprite,spriteX*50-25-20,spriteY*50-25-20) 'Note the 'spriteX*50-25' is box centre and 'spriteX*50-25-20' is the sprite left coordinate
EndSub
