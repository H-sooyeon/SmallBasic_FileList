LF = Text.GetCharacter(10)

numberOfShapesToDraw = 6
shapeType = "1=circle;2=square;3=triangle"

myShape[1] = "rectangle"  'pre selected

'RANDOMLY SELECT FROM shapeType
For i = 2 To numberOfShapesToDraw
  myShape[i] = shapeType[Math.GetRandomNumber(Array.GetItemCount(shapeType))]  
EndFor

TextWindow.WriteLine("Random Shapes: " + myShape + LF)

'RANDOMLY RE-ORDER ALL THESE SHAPES
For j = 1 To numberOfShapesToDraw
  rand = Math.GetRandomNumber(Array.GetItemCount(myShape))  
  
  'CREATE A randomOrder OF THE RANDOM SHAPES(myShape)
  randomOrder[j] = myShape[rand]
  
  'DELETE THIS ONE FROM myShape
  myShape[rand] = ""
  
  'GET myShape INTO A NEW ARRAY (getAll) - THIS EXCLUDES THE DELETED INDEX
  getAll = Array.GetAllIndices(myShape)
  
  'SWAP myShape WITH temp WHICH REINDEXES myShape WITHOUT THE DELETED VALUE
  For i = 1 To Array.GetItemCount(getAll)
    temp[i] = myShape[getAll[i]]    
  EndFor  
  myShape = temp  
  temp = ""  
EndFor

myShape = randomOrder  'NOW myShape IS RANDOMLY RE-ORDERED

TextWindow.WriteLine("Randomly Re-ordered Random Shapes: " + myShape + LF)

TextWindow.Pause()

'===========================================================
'ADD & POSITION ON THE GRAPICSWINDOW
'===========================================================
shapeWidth = 40

For i = 1 To numberOfShapesToDraw
  If myShape[i] = "rectangle" Then
    AddRectangle()
  ElseIf myShape[i] = "circle" Then
    AddCircle()
  ElseIf myShape[i] = "square" Then
    AddSquare()
  ElseIf myShape[i] = "triangle" Then
    AddTriangle()
  EndIf  
  
  If i = 1 Then
    X = 10 'space
  Else
    X = Shapes.GetLeft(shp[i-1]) + shapeWidth + 10
  EndIf
  Y = 15
  
  Shapes.Move(shp[i], X, Y)  
EndFor

Sub AddTriangle  
  shp[i] = Shapes.AddTriangle(0, 0, shapeWidth/2, shapeWidth, shapeWidth, 0)
EndSub

Sub AddSquare
  shp[i] = Shapes.AddRectangle(shapeWidth, shapeWidth)
EndSub

Sub AddCircle
  shp[i] = Shapes.AddEllipse(shapeWidth, shapeWidth)
EndSub

Sub AddRectangle  
  shp[i] = Shapes.AddRectangle(shapeWidth, shapeWidth * 0.8)
EndSub
