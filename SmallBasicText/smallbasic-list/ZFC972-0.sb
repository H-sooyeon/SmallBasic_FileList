' Score (ZFC972-0)

Form()

Controls.TextTyped = OnTextTyped

While "True"

  If redraw Then

    Calc()

    redraw = "False"

  Else

    Program.Delay(200)

  EndIf

EndWhile

Sub OnTextTyped

  textbox = Controls.LastTypedTextBox

  If Array.ContainsValue(score, textbox) Then

    redraw = "True"

  EndIf

EndSub

Sub Calc

  sum = 0

  For i = 1 To num

    s[i] = Controls.GetTextBoxText(score[i])

    sum = sum + s[i]

  EndFor

  Shapes.SetText(total, sum)

  If 0 < sum Then

    For i = 1 To num

      p = Math.Floor(Controls.GetTextBoxText(score[i]) / sum * 100)

      Shapes.SetText(percent[i], p)

    EndFor

  EndIf

  Sort()

  SetGrade()

EndSub

Sub Sort

  link = ""

  link[0] = 1

  For i = 2 To num

    j = 0

    While link[j] <> "" And s[i] < s[link[j]]

      j = link[j]

    EndWhile

    link[i] = link[j]

    link[j] = i

  EndFor

EndSub

Sub SetGrade

  j = 0

  g = 1

  While link[j] <> ""

    Shapes.SetText(grade[link[j]], Text.GetSubText("ABCDEFGHIJKLMNOPQRSTUVWXYZ", g, 1))

    If s[link[link[j]]] < s[link[j]] Then

      g = g + 1

    EndIf

    j = link[j]

  EndWhile

EndSub

Sub Form

  GraphicsWindow.BackgroundColor = "LightGray"

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.DrawText(10, 10, "Name")

  GraphicsWindow.DrawText(174, 10, "Score")

  GraphicsWindow.DrawText(238, 10, "Grade")

  GraphicsWindow.DrawText(302, 10, "%")

  num = 10

  For i = 1 To num

    name[i] = Controls.AddTextBox(10, 4 + i * 26)

    Controls.SetSize(name[i], 160, 22)

    score[i] = Controls.AddTextBox(174, 4 + i * 26)

    Controls.SetSize(score[i], 60, 22)

    grade[i] = Shapes.AddText("A")

    Shapes.Move(grade[i], 238, 8 + i * 26)

    percent[i] = Shapes.AddText(0)

    Shapes.Move(percent[i], 302, 8 + i * 26)

  EndFor

  GraphicsWIndow.DrawText(134, 8 + i * 26, "Total")

  total = Shapes.AddText(0)

  Shapes.Move(total, 174, 8 + i * 26)

EndSub


