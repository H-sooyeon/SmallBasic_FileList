'Window
GraphicsWindow.Hide()
GraphicsWindow.Width = 1000
GraphicsWindow.Height = 500
GraphicsWindow.Left = (Desktop.Width - 1000) / 2
GraphicsWindow.Top = (Desktop.Height - 590) / 2
GraphicsWindow.Title = "Dragon Game"
GraphicsWindow.CanResize = "False"
GraphicsWindow.FontSize = 15
GraphicsWindow.FontBold = "False"
GraphicsWindow.FontName = "Microsoft Sans Serif"
GraphicsWindow.BrushColor = "DodgerBlue"
GraphicsWindow.PenColor = "DarkSlateGray"
GraphicsWindow.BackgroundColor = GraphicsWindow.GetColorFromRGB(15, 15, 15)
GraphicsWindow.Show()
AddLoadingScreen()

'Variables
CurrentMenu = ""
CanPlay = ""
FillingBarSize = 0
PlayerLife = 396
EnemyLife = 396
CanClickSpace = "Yes"
CurrentFrame = 1

'Sounds
SndGameBack = "http://66.90.91.26/ost/dragon-age-origins/yewlscossy/12-darkspawn-in-the-wilds.mp3"
SndExplosion = "http://soundbible.com/grab.php?id=538&type=mp3"
SndVictory = "http://soundbible.com/grab.php?id=1823&type=mp3"
SndDefeat = "http://soundbible.com/grab.php?id=1830&type=mp3"
SndDragonAttacks = "http://soundbible.com/grab.php?id=1165&type=mp3"
SndPlayerHurts = "http://soundbible.com/grab.php?id=462&type=mp3"

'Images'Images
ImgDragonLogo = ImageList.LoadImage("http://www.sacredwiki.org/images/b/b1/Dragonmage_Logo.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgPlayer = ImageList.LoadImage("http://www.fireemblemwiki.org/w/images/a/aa/Bs_fe06_enemy_mage_anima.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgGameBackground = ImageList.LoadImage("http://th09.deviantart.net/fs71/PRE/f/2013/050/0/c/level_background_for_a_mobile_game__by_bvigec-d5vi8b0.jpg")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgEnemy = ImageList.LoadImage("http://cdn2.scratch.mit.edu/get_image/gallery/435997_170x100.png?v=1400465435.23")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgExplosion = ImageList.LoadImage("http://www.imarvintpa.com/Mapping/Overlays/Effects/Fire/Fireball/explosion_4.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgFinalExplosion = ImageList.LoadImage("http://img3.wikia.nocookie.net/__cb20130805183122/clubpenguin/images/d/d0/Explode.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgPlayerFB = ImageList.LoadImage("http://www.sunfireinvestmentproperties.com/wp-content/themes/default/images/fireball.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgEnemyFB = ImageList.LoadImage("https://support.ouat-e.com/media/luckypirate/images/help/tool_fireball.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgVs = ImageList.LoadImage("http://images3.wikia.nocookie.net/__cb20091129200245/pokeespectaculos/es/images/archive/6/64/20100330003313!Vs.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgCursor = ImageList.LoadImage("http://files.softicons.com/download/business-icons/onebit-2-icons-by-icojoy/png/48/onebit_27.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgDefeat = ImageList.LoadImage("http://i.imgur.com/u7vHLuX.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 41, 11)
FillingBarSize = FillingBarSize + 41
ImgVictory = ImageList.LoadImage("http://i.imgur.com/13jOyOI.png")
Controls.SetSize(LSFillingBar, FillingBarSize + 45, 11)

'Main
GraphicsWindow.Clear()
AddMainMenu()

'SUBROUTINES
'***************
'AddLoadingScreen
Sub AddLoadingScreen
  CurrentMenu = "LoadingScreenMenu"
  ImgBackLoading = ImageList.LoadImage("http://wallpaperesque.com/wp-content/uploads/plixpapers1502/nintendo_wallpaper_background_49056.jpg")
  GraphicsWindow.DrawResizedImage(ImgBackLoading, 0, 0, 1000, 500)
  LSBackReck = Shapes.AddRectangle(200, 40)
  Shapes.Move(LSBackReck, 400, 315)
  GraphicsWindow.BrushColor = "White"
  LSLabel = Shapes.AddText("Loading")
  Shapes.Move(LSLabel, 470, 326)
  GraphicsWindow.BrushColor = "Black"
  LSLBBack = Shapes.AddRectangle(500, 15)
  Shapes.Move(LSLBBack, 250, 400)
  GraphicsWindow.BrushColor = "DodgerBlue"
  GraphicsWindow.PenColor = "DodgerBlue"
  LSFillingBar = Shapes.AddRectangle(0, 11)
  Shapes.Move(LSFillingBar, 252, 402)
EndSub

'AddMainMenu
Sub AddMainMenu
  GraphicsWindow.PenColor = "DarkSlateGray"
  MMDragonLogo = Shapes.AddImage(ImgDragonLogo)
  Shapes.Zoom(MMDragonLogo, 0.7, 0.7)
  Shapes.Move(MMDragonLogo, 392, -25)
  CurrentMenu = "MainMenu"
  GraphicsWindow.DrawResizedImage(ImgBackLoading, 0, 0, 1000, 500)
  GraphicsWindow.BrushColor = "DodgerBlue"
  MMBackReck = Shapes.AddRectangle(200, 40)
  Shapes.Move(MMBackReck, 400, 300)
  MMBackReck2 = Shapes.AddRectangle(200, 40)
  Shapes.Move(MMBackReck2, 400, 360)
  MMBackReck3 = Shapes.AddRectangle(200, 40)
  Shapes.Move(MMBackReck3, 400, 420)
  GraphicsWindow.BrushColor = "White"
  MMLabel = Shapes.AddText("Play")
  Shapes.Move(MMLabel, 482, 311)
  MMLabel2 = Shapes.AddText("Tutorial")
  Shapes.Move(MMLabel2, 472, 371)
  MMLabel3 = Shapes.AddText("Exit")
  Shapes.Move(MMLabel3, 485, 431)
  Cursor = Shapes.AddImage(ImgCursor)
  Shapes.HideShape(Cursor)
EndSub

'AddGame
Sub AddGame
  Sound.Play(SndGameBack)
  CurrentMenu = "GameMenu"
  CanPlay = "No"
  GraphicsWindow.DrawResizedImage(ImgGameBackground, 0, 0, 1000, 500)
  Player = Shapes.AddImage(ImgPlayer)
  Shapes.Move(Player, 150, 350)
  Enemy = Shapes.AddImage(ImgEnemy)
  Shapes.Move(Enemy, 1100, -50)
  GraphicsWindow.BrushColor = "DodgerBlue"
  Balloon = Shapes.AddRectangle(150, 35)
  Shapes.Move(Balloon, 85, 300)
  GraphicsWindow.BrushColor = "White"
  CharacterText = Shapes.AddText("Oh no! A dragon!")
  Shapes.Move(CharacterText, 100, 310)
  GraphicsWindow.PenColor = "Green"
  GraphicsWindow.BrushColor = "Black"
  TopMenuBack = Shapes.AddRectangle(1000, 70)
  Shapes.SetOpacity(TopMenuBack, 50)
  PlayerLBBack = Shapes.AddRectangle(400, 20)
  Shapes.Move(PlayerLBBack, 50, 25)
  GraphicsWindow.PenColor = "Lime"
  GraphicsWindow.BrushColor = "Lime"
  PlayerLBFill = Shapes.AddRectangle(396, 16)
  Shapes.Move(PlayerLBFill, 52, 27)
  GraphicsWindow.PenColor = "Brown"
  GraphicsWindow.BrushColor = "Black"
  EnemyLBBack = Shapes.AddRectangle(400, 20)
  Shapes.Move(EnemyLBBack, 550, 25)
  GraphicsWindow.PenColor = "Red"
  GraphicsWindow.BrushColor = "Red"
  EnemyLBFill = Shapes.AddRectangle(396, 16)
  Shapes.Move(EnemyLBFill, 552, 27)
  VsLogo = Shapes.AddImage(ImgVs)
  Shapes.Move(VsLogo, 470, 10)
  PlayGameIntro()
EndSub

'PlayGameIntro
Sub PlayGameIntro
  For MoveX = 0 To 400 Step 0.009
    Shapes.Move(Enemy, 1100 - MoveX, (-150) + MoveX)
  EndFor
  Shapes.Move(Balloon, 720, 230)
  Shapes.Move(CharacterText, 738, 238)
  Shapes.SetText(CharacterText, "Prepare yourself!")
  For MoveX = 0 To 50 Step 0.003
    Shapes.Move(Player, 150 + MoveX, 350 + MoveX)
  EndFor
  CurrentMenu = "ClickToPlay"
  Controls.SetSize(Balloon, 170, 35)
  Shapes.Move(Balloon, 415, 75)
  Shapes.Move(CharacterText, 429, 83)
  Shapes.SetText(CharacterText, "Click anyware to play")
EndSub

'EnemyAttacks
Sub EnemyAttacks
  If CanPlay = "Yes" Then
    Shapes.Remove(PlayerFB)
    Shapes.Remove(EnemyFB)
    Shapes.Remove(Border)
    If PlayerLife <= 0 Or EnemyLife <= 0 Then
      CanPlay = "No"
      If PlayerLife > 0 Then
        Victory()
      ElseIf EnemyLife > 0 Then
        Defeat()
      EndIf
    Else
      Sound.Stop(SndDragonAttacks)
      Sound.Play(SndDragonAttacks)
      RandomBorderX = Math.GetRandomNumber(650)
      While RandomBorderX < 250
        RandomBorderX = Math.GetRandomNumber(650)
      EndWhile
      GraphicsWindow.PenColor = "DarkSlateGray"
      GraphicsWindow.BrushColor = "DarkSlateGray"
      Border = Shapes.AddRectangle(100, 100)
      Shapes.Move(Border, RandomBorderX, 345)
      Shapes.SetOpacity(Border, 50)
      EnemyFB = Shapes.AddImage(ImgEnemyFB)
      Shapes.HideShape(EnemyFB)
      Shapes.Move(EnemyFB, 700, 370)
      For ZoomIn = 1 To 1.2 Step 0.0001
        Shapes.Zoom(Enemy, ZoomIn, ZoomIn)
      EndFor
      Shapes.ShowShape(EnemyFB)
      For ZoomOut = 1.2 To 1 Step -0.0001
        Shapes.Zoom(Enemy, ZoomOut, ZoomOut)
      EndFor
      For MoveX = 700 To 200 Step - 0.05
        Shapes.Move(EnemyFB, MoveX, 370)
      EndFor
      If Shapes.GetLeft(EnemyFB) < 201 Then
        PlayerHurts()
        CanClickSpace = "Yes"
        EnemyAttacks()
      EndIf
    EndIf
  EndIf
EndSub

'PlayerAttacks
Sub PlayerAttacks
  If CanPlay = "Yes" Then
    Shapes.Remove(Border)
    Shapes.Remove(EnemyFB)
    Shapes.Remove(PlayerFB)
    If EnemyLife <= 0 Or PlayerLife <= 0 Then
      CanPlay = "No"
      If PlayerLife > 0 Then
        Victory()
      ElseIf EnemyLife > 0 Then
        Defeat()
      EndIf
    Else
      PlayerFB = Shapes.AddImage(ImgPlayerFB)
      Shapes.HideShape(PlayerFB)
      Shapes.Move(PlayerFB, 160, 300)
      For ZoomIn = 1 To 1.2 Step 0.0001
        Shapes.Zoom(Player, ZoomIn, ZoomIn)
      EndFor
      Shapes.ShowShape(PlayerFB)
      For ZoomOut = 1.2 To 1 Step -0.0001
        Shapes.Zoom(Player, ZoomOut, ZoomOut)
      EndFor
      For MoveX = 0 To 540 Step 0.03
        Shapes.Move(PlayerFB, 160 + MoveX, 300)
      EndFor
      If Shapes.GetLeft(PlayerFB) > 699 Then
        Shapes.Remove(PlayerFB)
        EnemyHurts()
        CanClickSpace = "Yes"
        EnemyAttacks()
      EndIf
    EndIf
  EndIf
EndSub

'PlayerHurts
Sub PlayerHurts
  If CanPlay = "Yes" Then
    Sound.Stop(SndPlayerHurts)
    Sound.Play(SndPlayerHurts)
    PlayerExplosion = Shapes.AddImage(ImgExplosion)
    Shapes.HideShape(PlayerExplosion)
    Shapes.Move(PlayerExplosion, 125, 260)
    Shapes.Zoom(PlayerExplosion, 0.5, 0.5)
    Shapes.ShowShape(PlayerExplosion)
    Controls.SetSize(PlayerLBFill, PlayerLife - 66, 16)
    PlayerLife = PlayerLife - 66
    For ZoomIn = 0.5 To 0.6 Step 0.00003
      Shapes.Zoom(PlayerExplosion, ZoomIn, ZoomIn)
    EndFor
    Shapes.Remove(PlayerExplosion)
  EndIf
EndSub

'EnemyHurts
Sub EnemyHurts
  If CanPlay = "Yes" Then
    Sound.Stop(SndExplosion)
    Sound.Play(SndExplosion)
    EnemyExplosion = Shapes.AddImage(ImgExplosion)
    Shapes.HideShape(EnemyExplosion)
    Shapes.Move(EnemyExplosion, 700, 250)
    Shapes.Zoom(EnemyExplosion, 0.5, 0.5)
    Shapes.ShowShape(EnemyExplosion)
    Controls.SetSize(EnemyLBFill, EnemyLife - 66, 16)
    EnemyLife = EnemyLife - 66
    For ZoomIn = 0.5 To 0.7 Step 0.00003
      Shapes.Zoom(EnemyExplosion, ZoomIn, ZoomIn)
    EndFor
    Shapes.Remove(EnemyExplosion)
  EndIf
EndSub

'Victory
Sub Victory
  CurrentMenu = "Victory"
  Shapes.Remove(EnemyExplosion)
  Shapes.Remove(PlayerExplosion)
  Sound.Stop(SndGameBack)
  Sound.Play(SndVictory)
  FinalEplosion = Shapes.AddImage(ImgFinalExplosion)
  Shapes.Move(FinalEplosion, 400, 100)
  For Fade = 100 To 0 Step -0.01
    Shapes.SetOpacity(FinalEplosion, Fade)
  EndFor
  VictoryLogo = Shapes.AddImage(ImgVictory)
  Shapes.Zoom(VictoryLogo, 0.5, 0.5)
  VLX = ImageList.GetWidthOfImage(ImgVictory)
  Shapes.Move(VictoryLogo, (1000 - VLX) / 2, -200)
  Shapes.SetOpacity(VictoryLogo, 0)
  GraphicsWindow.BrushColor = "Black"
  VSBtnContinue = Controls.AddButton("ok", 0, 0)
  Controls.HideControl(VSBtnContinue)
  Controls.Move(VSBtnContinue, 450, 350)
  Controls.SetSize(VSBtnContinue, 100, 30)
  For GoDown = Shapes.GetTop(Enemy) To 600 Step 0.008
    Shapes.Move(Enemy, Shapes.GetLeft(Enemy), GoDown)
  EndFor
  For Appear = 0 To 100 Step 0.007
    Shapes.SetOpacity(VictoryLogo, Appear)
  EndFor
  Controls.ShowControl(VSBtnContinue)
EndSub

'Defeat
Sub Defeat
  CurrentMenu = "Defeat"
  Sound.Stop(SndGameBack)
  Sound.Play(SndDefeat)
  DefeatLogo = Shapes.AddImage(ImgDefeat)
  DLX = ImageList.GetWidthOfImage(ImgDefeat)
  Shapes.Zoom(DefeatLogo, 0.5, 0.5)
  Shapes.Move(DefeatLogo, (1000 - DLX) / 2, -50)
  Shapes.SetOpacity(DefeatLogo, 0)
  GraphicsWindow.BrushColor = "Black"
  DSBtnContinue = Controls.AddButton("ok", 0, 0)
  Controls.HideControl(DSBtnContinue)
  Controls.Move(DSBtnContinue, 450, 350)
  Controls.SetSize(DSBtnContinue, 100, 30)
  For GoDown = Shapes.GetTop(Player) To 600 Step 0.008
    Shapes.Move(Player, Shapes.GetLeft(Player), GoDown)
  EndFor
  For Appear = 0 To 100 Step 0.007
    Shapes.SetOpacity(DefeatLogo, Appear)
  EndFor
  Controls.ShowControl(DSBtnContinue)
EndSub

'AddTutorial
Sub AddTutorial
  CurrentMenu = "Tutorial"
  GraphicsWindow.Clear()
  GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(70, 70, 70)
  GraphicsWindow.PenColor = "Gray"
  TMBackReck = Shapes.AddRectangle(600, 400)
  Shapes.Move(TMBackReck, 200, 50)
  GraphicsWindow.BrushColor = "Black"
  TMBtnPrevious = Controls.AddButton("Previous", 250, 410)
  Controls.SetSize(TMBtnPrevious, 100, 28)
  TMBtnNext = Controls.AddButton("Next", 355, 410)
  Controls.SetSize(TMBtnNext, 100, 28)
  TMBtnClose = Controls.AddButton("Close", 650, 410)
  Controls.SetSize(TMBtnClose, 100, 28)
  GraphicsWindow.BrushColor = "Black"
  TMBackReck2 = Shapes.AddRectangle(500, 300)
  Shapes.Move(TMBackReck2, 250, 100)
  GraphicsWindow.BrushColor = "Gainsboro"
  TMText = Shapes.AddText("")
  Shapes.Move(TMText, 270, 120)
  TMCurrentFrame = Shapes.AddText("")
  Shapes.Move(TMCurrentFrame, 490, 70)
  ChangeFrame()
EndSub

'ChangeFrame
Sub ChangeFrame
  If CurrentFrame = 1 Then
    Shapes.SetText(TMCurrentFrame, "1/4")
    Shapes.SetText(TMText, "This is you:")
    TMPlayer = Shapes.AddImage(ImgPlayer)
    Shapes.Move(TMPlayer, 485, 230)
    Controls.HideControl(TMBtnPrevious)
  ElseIf CurrentFrame = 2 Then
    Shapes.SetText(TMCurrentFrame, "2/4")
    Shapes.SetText(TMText, "This is the enemy:")
    TMEnemy = Shapes.AddImage(ImgEnemy)
    Shapes.Move(TMEnemy, 380, 140)
  ElseIf CurrentFrame = 3 Then
    Shapes.SetText(TMCurrentFrame, "3/4")
    Shapes.SetText(TMText, "This is the enemy fireball:")
    TMEnemyFB = Shapes.AddImage(ImgEnemyFB)
    Shapes.Move(TMEnemyFB, 475, 225)
  ElseIf CurrentFrame = 4 Then
    Shapes.SetText(TMCurrentFrame, "4/4")
    Shapes.SetText(TMText, "Press 'Spacebar' when the enemy fireball is over this rectangle:")
    TMRectangle = Shapes.AddRectangle(100, 100)
    Shapes.SetOpacity(TMRectangle, 10)
    Shapes.Move(TMRectangle, 450, 200)
    Controls.HideControl(TMBtnNext)
  EndIf
EndSub

'MouseUp
GraphicsWindow.MouseUp = MouseUp
Sub MouseUp
  x = GraphicsWindow.MouseX
  y = GraphicsWindow.MouseY
  
  If CurrentMenu = "MainMenu" Then
    If x > 400 And x < 600 And y > 300 And y < 340 Then
      Sound.PlayClick()
      GraphicsWindow.Clear()
      AddGame()
    ElseIf x > 400 And x < 600 And y > 360 And y < 400 Then
      Sound.PlayClick()
      AddTutorial()
    ElseIf x > 400 And x < 600 And y > 420 And y < 460 Then
      Sound.PlayClickAndWait()
      Program.End()
    EndIf
  ElseIf CurrentMenu = "ClickToPlay" Then
    Shapes.HideShape(Balloon)
    Shapes.HideShape(CharacterText)
    CurrentMenu = "GameMenu"
    CanPlay = "Yes"
    EnemyAttacks()
  EndIf
EndSub

'KeyDown
GraphicsWindow.KeyDown = KeyDown
Sub KeyDown
  If CurrentMenu = "GameMenu" Then
    If CanPlay = "Yes" Then
      If GraphicsWindow.LastKey = "Space" Then
        If CanClickSpace = "Yes" Then
          CanClickSpace = "No"
          EnemyFBLeft = Shapes.GetLeft(EnemyFB)
          BorderLeft = Shapes.GetLeft(Border)
          If EnemyFBLeft > BorderLeft - 20 And EnemyFBLeft < BorderLeft + 80 Then
            PlayerAttacks()
          Else
            PlayerHurts()
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf
EndSub

'ButtonClicked
Controls.ButtonClicked = ButtonClicked
Sub ButtonClicked
  If Controls.LastClickedButton = VSBtnContinue Or Controls.LastClickedButton = DSBtnContinue Then
    Sound.PlayClickAndWait()
    Program.End()
  ElseIf Controls.LastClickedButton = TMBtnNext Then
    Sound.PlayClick()
    CurrentFrame = CurrentFrame + 1
    GraphicsWindow.Clear()
    AddTutorial()
    ChangeFrame()
  ElseIf Controls.LastClickedButton = TMBtnPrevious Then
    Sound.PlayClick()
    CurrentFrame = CurrentFrame - 1
    GraphicsWindow.Clear()
    AddTutorial()
    ChangeFrame()
  ElseIf Controls.LastClickedButton = TMBtnClose Then
    Sound.PlayClick()
    GraphicsWindow.Clear()
    AddMainMenu()
  EndIf
EndSub
