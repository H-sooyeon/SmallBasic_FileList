args=0

x0=0

y0=0

a0=0

size=0

teta=0

penColor="white"

penWidth=2

instinct=""



axiom=""

iter=0

_stack= "Stack"



GraphicsWindow.Title="CyberTurtle" 



GraphicsWindow.Width= 1300

GraphicsWindow.Height=900

GraphicsWindow.Left= 5

GraphicsWindow.Top = 5

GraphicsWindow.BackgroundColor="teal"



height=GraphicsWindow.Height

width=GraphicsWindow.Width



CX= width/2

CY= height/2



x0= CX - 100

y0= CY + 80

a0= 0



ff="tree tree2 tree3 Bush Koch sc32 Peano Mozaic Snowflake triangles Rhombes PeanoGosper

fn=LDText.Split(ff " ")

ac=Array.GetItemCount(fn)



For rr=1 To ac

  LDCall.Function(fn[rr] "")

  createInstinct()

  start()

  execute()

  Program.Delay(3333)

  if rr<ac then

    GraphicsWindow.Clear()

    EndIf

EndFor



Sub Koch

  teta=60

  size=4

  iter= 1'4

  axiom="F++F++F"

  newF="F-F++F-F"

EndSub



Sub Peano

  a0= 45

  teta=90 

  size=10

  iter= 3

  axiom="F"

  newF="F-F+F+F+F-F-F-F+F"

EndSub



Sub sc32

  teta=90 

  size=4

  iter= 2

  axiom="F+F+F+F"

  newF="-F+F-F-F+F+FF-F+F+FF+F-F-FF+FF-FF+F+F-FF-F-F+FF-F-F+F+F-F+"

EndSub



Sub Mozaic

  a0= 0

  teta=90 

  size=10

  iter= 2

  x0= CX

  y0= CY

  axiom="F-F-F-F"

  newF="F-b+FF-F-FF-Fb-FF+b-FF+F+FF+Fb+FFF"

  newb= "bbbbbb"

EndSub



Sub triangles

  a0=30

  teta=120 

  size= 20

  iter= 6

  axiom="bX"

  newb = "b"

  newF="F"

  newX="--FXF++FXF++FXF--"

  x0= CX-30

  y0= CY+40

EndSub



Sub PeanoGosper 

  teta=60 

  size=10

  iter= 4

  axiom="FX"

  newF="F"  

  newX="X+YF++YF-FX--FXFX-YF+"

  newY="-FX+YFYF++YF+FX--FX-Y"  

EndSub



Sub Snowflake

  size=4

  iter= 2

  teta=60

  x0= CX

  y0= CY+100

  axiom="[F]+[F]+[F]+[F]+[F]+[F]"

  newF="F[++F][-FF]FF[+F][-F]FF"

EndSub



Sub Rhombes

  a0= 0

  teta=60 

  size=10

  iter= 6

  x0= CX

  y0= CY

  axiom="F"

  newF="-F+F+[+F+F]-"

EndSub



Sub Penrose 

  teta=36 

  size= 24

  iter= 4

  axiom="[Y]++[Y]++[Y]++[Y]++[Y]"

  newW="YF++ZF----XF[-YF----WF]++"  

  newX="+YF--ZF[---WF--XF]+"

  newY="-WF++XF[+++YF++ZF]-" 

  newZ="--YF++++WF[+ZF++++XF]--XF"  

  newF=""    

EndSub



Sub Bush

  size=8

  iter= 4

  teta=180/8

  x0= CX

  y0= CY+100

  axiom="F"

  newF="-F+F+[+F-F-]-[-F+F+F]"

EndSub



Sub Tree

  size=8

  iter= 4

  teta=180/6

  x0= CX

  y0= CY+100

  axiom="F"

  newF="F[-F]F[+F][F]"

EndSub



Sub Tree2

  size=4

  iter= 4

  teta=180/6

  x0= CX

  y0= CY+160

  axiom="F"

  newF="F[+F]F[-F]F"

EndSub



Sub Tree3

  size=6

  iter= 4

  teta=180/6

  x0= CX

  y0= CY+160

  axiom="F"

  newF="FF-[-F+F+F]+[+F-F-F]"

EndSub



Sub start

  GraphicsWindow.PenColor= penColor

  GraphicsWindow.PenWidth= penWidth 

  Tpen="true"

  t_X= x0

  t_Y= y0  

  t_Angle= a0  

  

EndSub



Sub createInstinct

  For i= 1 To iter

    instinct=""   

    For j= 1 To Text.GetLength(axiom)



      cmd= Text.GetSubText(axiom,j,1)



      If (cmd="F") Then

        instinct= instinct + newF

      ElseIf  (cmd="b") Then

        instinct= instinct + newb

      ElseIf  (cmd="W") Then

        instinct= instinct + newW          

      ElseIf  (cmd="X") Then

        instinct= instinct + newX  

      ElseIf  (cmd="Y") Then

        instinct= instinct + newY   

      ElseIf  (cmd="Z") Then

        instinct= instinct + newZ         

      Else

        instinct= instinct + cmd

      EndIf   



    EndFor

    axiom=instinct

  EndFor



EndSub



Sub execute

  

  For i= 1 To Text.GetLength(instinct)



    cmd= Text.GetSubText(instinct,i,1)

    If (cmd="F") Then

      Ldcall.function("tmov" size)

    EndIf

    If (cmd="b") Then

      Tpen="false"

      Ldcall.function("tmov" size)

      Tpen="True"

    EndIf    

    If (cmd="+") Then

      t_angle=t_angle+(teta)

    EndIf

    If (cmd="-") Then

      t_angle=t_angle+(-teta)

    EndIf  

    If (cmd="[") Then

      Stack.PushValue(_stack, t_Angle)

      Stack.PushValue(_stack, t_X)

      Stack.PushValue(_stack, t_Y)      

    EndIf   

    If (cmd="]") Then

      t_Y= Stack.PopValue(_stack) 

      t_X= Stack.PopValue(_stack)  

      t_Angle= Stack.PopValue(_stack)

    EndIf   

    

  EndFor

EndSub



Sub tmov

  ll=ldmath.Convert2Cartesian(t_x t_y args[1] t_Angle-90)

  If Tpen Then

    GraphicsWindow.DrawLine(t_x t_y ll[1] ll[2])

  EndIf

  t_x=ll[1]

  t_y=ll[2]

EndSub




