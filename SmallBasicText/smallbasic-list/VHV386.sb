' Backgammon Board 0.1
' Copyright (c) 2013 Nonki Takahashi.  All rights reserved.
'
' History
'  0.1 2013-08-04 Created.
'
gw = 640
gh = 480
fw = 60 ' frame width
fh = 30 ' frame height
mg = 10 ' margin
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.BrushColor = "#663300"
GraphicsWindow.FillRectangle(mg, mg, gw - mg * 2, gh - mg * 2)
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FillRectangle(mg + fw, mg + fh, (gw - mg * 2 - fw * 3) / 2, gh - (mg + fh) * 2)
GraphicsWindow.FillRectangle(gw / 2 + fw / 2, mg + fh, (gw - mg * 2 - fw * 3) / 2, gh - (mg + fh) * 2)
color = "0=BurlyWood;1=Maroon;"
x = mg + fw
y = mg + fh
dx = (gw - mg * 2 - fw * 3) / 12
dy = (gh - mg * 2 - fh * 3) / 2
y1 = y
y2 = y
y3 = y + dy
DrawPath()
x = gw / 2 + fw / 2
DrawPath()
color = "0=Maroon;1=BurlyWood;"
x = mg + fw
y = gh - mg - fh
dy = -(gh - mg * 2 - fh * 3) / 2
y1 = y
y2 = y
y3 = y + dy
DrawPath()
x = gw / 2 + fw / 2
DrawPath()
GraphicsWindow.PenWidth = 0
GraphicsWindow.BrushColor = "Red"
x = gw / 2 + fw / 2 + 5 * dx
y = mg + fh
dy = dx
o = 0
AddCheckers()
GraphicsWindow.BrushColor = "White"
y = gh - mg - fh - dx
dy = -dx
o = 6
AddCheckers()
Dice_Init()
x0 = gw / 2
y0 = gh / 2
r = size
a = Math.GetRandomNumber(360)
_a[1] = Math.GetRadians(a)
_a[2] = _a[1] - Math.Pi
For i = 1 To 2
  n = Math.GetRandomNumber(6)
  x = x0 + r * Math.Cos(_a[i]) - size / 2
  y = y0 + r * Math.Sin(_a[i]) - size / 2
  Dice_Add()
EndFor
Sub AddCheckers
  For i = 1 To 5
    checker[i + o] = Shapes.AddEllipse(dx, dx)
    Shapes.Move(checker[i + o], x, y + (i - 1) * dy)
  EndFor
EndSub
Sub DrawPath
  For i = 0 To 5
    mod = Math.Remainder(i, 2)
    GraphicsWindow.BrushColor = color[mod]
    x1 = x + i * dx
    x2 = x + (i + 1) * dx
    x3 = (x1 + x2) / 2
    GraphicsWindow.FillTriangle(x1, y1, x2, y2, x3, y3)
  EndFor
EndSub
Sub Dice_Init
  pattern[1] = "    .    "
  pattern[2] = ".       ."
  pattern[3] = "  . . .  "
  pattern[4] = ". .   . ."
  pattern[5] = ". . . . ."
  pattern[6] = ". .. .. ."
  size = 20
EndSub
Sub Dice_Add
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "White"
  dice[i][0] = Shapes.AddRectangle(size, size)
  Shapes.Move(dice[i][0], x, y)
  For j = 1 To 9
    k = 0
    If Text.GetSubText(pattern[n], j, 1) = "." Then
      k = k + 1
      If n = 1 Then
        GraphicsWindow.BrushColor = "Red"
        d = size / 3
        p = 0.35
      Else
        GraphicsWindow.BrushColor = "Black"
        d = size / 5
        p = 0.6
      EndIf
      x1 = x + (Math.Remainder(j - 1, 3) + p) * size / 4
      y1 = y + (Math.Floor((j - 1) / 3) + p) * size / 4
      dice[i][k] = Shapes.AddEllipse(d, d)
      Shapes.Move(dice[i][k], x1, y1)
    EndIf
  EndFor
EndSub
