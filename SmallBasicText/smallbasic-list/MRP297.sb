' SBace-Invaders-25 - A space invader game in under 25 lines by Davey~Wavey, Nov.2009, for Small Basic.
'Code clean up for the 50 Line Challenge - Matthew L. Parets (codingCat) Oct 2012

' Alien has increasing speed as it descends to increase difficulty, but you also get more score the faster they are!
' You are rated on your score and your hit accuracy. You have 3 lives. Use your mouse to move your rocket and left mousebutton to fire.

' initialise game settings
bx=0                              ' No bullet at start
by=0
lives=3                           'Starting number of lives
ammo=0                            'Tracking shoot/score ratio
score=0                           'Score starts at zero
ax=(GraphicsWindow.Width/2)       'Starting column for alien (middle)
ay=20                             'Starting row for alien (near the top)
adir=-1                           'Start alien moving left
aspeed=0.2                        'Start speed

'build shapes - Gun, Alien and Bullet
gun=Shapes.AddImage("http://www.tutorialized.com/upload/(2006.06.21-09:12:44)rocket50x50.jpg")
alien=shapes.AddImage("http://www.adiumxtras.com/images/thumbs/space_invaders_1_17601_6150_thumb.png")
bullet=Shapes.AddEllipse(5,10)   

'Set routine for windows to call when mouse button is pressed
GraphicsWindow.MouseDown=fire

While lives > 0
  
  ' update alien and bullet positions
  by=by-aspeed          'Bullet always moves up at current speed
  ax=ax + adir* aspeed  'Alien always moves across at current speed
  
  ' move the gun, alien and bullet
  Shapes.Move(gun, GraphicsWindow.MouseX-25, GraphicsWindow.Height-50)
  Shapes.move(alien, ax-25, ay-25)
  Shapes.Move(bullet, bx-3, by)
  
  ' bounce the alien at the screen edge (or has it hit player?)
  If (ax < 35) or (ax > GraphicsWindow.Width-35) Then
    ' alien has hit edge of screen, so bounce it
    ay=ay+10                    'Move alien down
    adir=adir * -1              'Reverse Direction
    aspeed=aspeed+0.01          'Increase the speed
  ElseIf (ay >= GraphicsWindow.Height-15) Then
    ' alien has reached the bottom - decrease life and reset alien.
    lives=lives-1
    ax=(GraphicsWindow.Width/2) 'Restart in the middle
    ay=20                       'Restart at the top
    adir=adir * -1              'Reverse Direction
    aspeed=aspeed+0.01          'Increase Speed
  ElseIf ( math.abs( math.Floor(bx-ax))  <25 ) and ( math.Abs( Math.Floor( by-ay ) ) < 10 ) Then
    ' bullet has collided with alien - increase score and reset alien
    score = score+Math.Floor((aspeed+1)*(aspeed+1))
    ax=(GraphicsWindow.Width/2) 'New alient in the middle
    ay=20                       'Start at the top
  EndIf
  
  ' show status - In the windows title bar
  GraphicsWindow.Title = "SBace-Invaders-25: SCORE=" + score + "   LIVES=" + lives
  
EndWhile

' Game Over!
GraphicsWindow.ShowMessage ("Well Done, you scored: " + score + " and your Skill = " + Math.Floor((score/ammo)*100) + "%", "<<< G-A-M-E   O-V-E-R >>>         ")

'Automatically called by windows when the mouse button is pressed
Sub fire  
  bx=(GraphicsWindow.mousex)    'Start the bullet at the current location of the mouse pointer
  by=(GraphicsWindow.Height-45) 'Start from bottom
  ammo=ammo+1                   'Track bullet count for shoot/score ratio
EndSub
