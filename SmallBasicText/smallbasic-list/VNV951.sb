'Circling Squares - Rotating sqares around a center, Farris Wheel style. -- Compliments of codingCat aka Matthew L. Parets - No rights resevered as long as no money is earned
blckCnt = 13            'Number of blocks to create
blckSiz = 50            'How big are the blocks
radious = 275           'Size of the circle
rotationSpeed = 1       'Angle change between steps in rotation 

frameLength = 45        'Number of milleseconds between animation frames

GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.BrushColor = "White"
GraphicsWindow.Width = 600
GraphicsWindow.Height = 600
GraphicsWindow.Show()

angleStep = 360 / blckCnt                       ' Amount of space (angle count) between shapes
centerx = GraphicsWindow.Width/2 - blckSiz / 2  'Find the center of the circle - offset will need to be nudged with different sized shapes
centery = GraphicsWindow.Height/2 - blckSiz / 2
CreateShapes()                                  'Create and place shapes

pos = 1                                         'Start with the first block
framestart = Clock.ElapsedMilliseconds          'Start the clock


'--- Main Program Loop -----------------------------------------------------------------------
While "True"
  'calcuate x/y based on angle around the circle
  angle[pos] = angle[pos] + rotationSpeed
  angle[pos] = Math.Remainder(angle[pos],360)
  radAngle = Math.GetRadians(angle[pos])
  x = Math.Cos(radAngle) * radious + centerx
  y = Math.Sin(radAngle) * radious + centery
  
  Shapes.Move(blck[pos],x,y)                   'Change the position
  
  pos = pos + 1                                ' Next shape
  If pos > blckCnt Then                        'Reached the end? start over
    frameDelay()                               'Wait a tick or two between moves
    pos = 1
  EndIf
EndWhile

Sub CreateShapes
  For i = 1 To blckCnt
    baseAngle = baseAngle + angleStep         'update the angle to match its position around the circle
    angle[i] = baseAngle                      'Store the shapes angle
    
    GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
    blck[i] = Shapes.AddRectangle(blckSiz,blckSiz)  'Create the  shape
    radAngle = Math.GetRadians(angle[i])
    x = Math.Cos(radAngle) * radious + centerx  
    y = Math.Sin(radAngle) * radious + centery
    Shapes.Move(blck[i],x,y)                  'Place the shape on the circle
  EndFor
EndSub

Sub frameDelay
  'Precise frame timing. More exact that program.delay
  While Clock.ElapsedMilliseconds - framestart < frameLength
  EndWhile
  framestart = Clock.ElapsedMilliseconds
EndSub
  
