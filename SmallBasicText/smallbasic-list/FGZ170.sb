'=============================================

'INITIALISATION

'=============================================



Initialise()



'=============================================

'MAIN PROGRAM

'=============================================



While ("True")

  'Do box changes with mouse click

  If (OMD = 1) Then

    GetTextBox()

    TextBoxFocus.SetFocus(TB[index])

    OMD = 0 'MouseDown handled

  EndIf

  'Text Entered

  If (OKD = 1 And key = "Return") Then

    GetTextBox()

    txt = Controls.GetTextBoxText(TB[index])

    GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor

    GraphicsWindow.FillRectangle(50,350,1000,30)

    GraphicsWindow.BrushColor = "Black"

    GraphicsWindow.DrawText(50,350,txt+" : entered in textbox "+index)

    index = index+1

    If (index > nBox) Then

      index = 1

    EndIf

    TextBoxFocus.SetFocus(TB[index])

    OKD = 0 'Key Return handled

  EndIf

  Program.Delay(20)

EndWhile



'=============================================

' SUBROUTINES

'=============================================



Sub Initialise

  nBox = 10

  For i = 1 To nBox

    TB[i] = Controls.AddTextBox(75,30*i)

  EndFor

  index = 1

  TextBoxFocus.SetFocus(TB[index])

  OMD = 0

  OKD = 0

  GraphicsWindow.MouseUp = OnMouseDown

  GraphicsWindow.KeyDown = OnKeyDown

EndSub



Sub GetTextBox ' Get the currently selected Textbox number

  newTB = TextBoxFocus.GetFocus()

  For i = 1 To nBox

    If (newTB = TB[i]) Then

      index = i

    EndIf

  EndFor

EndSub



'=============================================

'EVENT SUBROUTINES

'=============================================



Sub OnMouseDown

  OMD = 1

EndSub



Sub OnKeyDown

  OKD = 1

  key = GraphicsWindow.LastKey

EndSub


