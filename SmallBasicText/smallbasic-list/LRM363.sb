GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp

'Simplified dummy setup for images walking left and right
playerX[1] = 50 'Initial X (Left of player)
playerY[1] = GraphicsWindow.Height - 60 'Initial Y (Top of player)
playerX[2] = 350 'Initial X (Left of player)
playerY[2] = GraphicsWindow.Height - 60 'Initial Y (Top of player)

dir = Program.Directory+"\sidestroller\"
For i = 1 To 3
  walkL[i] = ImageList.LoadImage(dir+"walkL"+i+".png")
  walkR[i] = ImageList.LoadImage(dir+"walkR"+i+".png")
  For j = 1 To 2
    playerWalkLeft[j][i] = Shapes.AddImage(walkL[i])
    Shapes.HideShape(playerWalkLeft[j][i])  
    playerWalkRight[j][i] = Shapes.AddImage(walkR[i])
    Shapes.HideShape(playerWalkRight[j][i])
  EndFor
EndFor

'Show initial players
For i = 1 To 2
  playerFrame[i] = 1 'First frame
  player[i] = playerWalkRight[i][playerFrame[i]]
  Shapes.Move(player[i],playerX[i],playerY[i])
  Shapes.ShowShape(player[i])
EndFor

'Main Loop
While("True")
  start = Clock.ElapsedMilliseconds
  
  movePlayers()
  
  delay = 25 - (Clock.ElapsedMilliseconds-start) 'Max delay of 25ms is 40 fps
  If (delay > 0) Then
    Program.Delay(delay)
  EndIf
EndWhile

'Move players
'player1 is the current active shown image (there is only ever one not hidden at a time)
Sub movePlayers
  i = 1 'Player 1
  If (keyDown["Z"]) Then
    moveLeft()
  ElseIf (keyDown["X"]) Then
    moveRight()
  Else
    iFrame[i] = 0 'Reset frame for next movement
  EndIf
  
  i = 2 'Player 2
  If (keyDown["Left"]) Then
    moveLeft()
  ElseIf (keyDown["Right"]) Then
    moveRight()
  Else
    iFrame[i] = 0
  EndIf
EndSub

Sub moveLeft
  playerX[i] = playerX[i]-2 'Move player position left
  If (Math.Remainder(iFrame[i],8) = 0) Then 'Update player every 8 frames of movement
    Shapes.HideShape(player[i]) 'Hide current player image (will be last player image)
    playerFrame[i] = 1+Math.Remainder(playerFrame[i],3) 'Cycle player image in group (these for walking left)
    player[i] = playerWalkLeft[i][playerFrame[i]] 'The next player image in group is now the current player image
    Shapes.Move(player[i],playerX[i],playerY[i]) 'Move the image before we show it
    Shapes.ShowShape(player[i]) 'Show current player image
  Else
    Shapes.Move(player[i],playerX[i],playerY[i]) 'Just move player
  EndIf
  iFrame[i] = iFrame[i]+1
EndSub

Sub moveRight
  playerX[i] = playerX[i]+2 'Move player position right
  If (Math.Remainder(iFrame[i],8) = 0) Then 'Update player every 8 frames of movement
    Shapes.HideShape(player[i]) 'Hide current player image (will be last player image)
    playerFrame[i] = 1+Math.Remainder(playerFrame[i],3) 'Cycle player image in group (these for walking left)
    player[i] = playerWalkRight[i][playerFrame[i]] 'The next player image in group is now the current player image
    Shapes.Move(player[i],playerX[i],playerY[i]) 'Move the image before we show it
    Shapes.ShowShape(player[i]) 'Show current player image
  Else
    Shapes.Move(player[i],playerX[i],playerY[i]) 'Just move player
  EndIf
  iFrame[i] = iFrame[i]+1
EndSub

'Register events
Sub OnKeyDown
  key = GraphicsWindow.LastKey
  keyDown[key] = "True"
EndSub

Sub OnKeyUp
  key = GraphicsWindow.LastKey
  keyDown[key] = ""
EndSub
