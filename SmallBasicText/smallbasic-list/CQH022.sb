''Last One Loses Game, Submission for September Challenge of The Month

''Submission By DaCodeDude

''Images: Pixomar / FreeDigitalPhotos.net



''Create the game window

GraphicsWindow.Width = 650

GraphicsWindow.Title = "The Last One Loses"

GraphicsWindow.BackgroundColor = "Black"



''Load important game variables/data

matchNorm = "https://byfiles.storage.live.com/y1pf2eEsI2qMbTnoxSOsXGbYqfXK1A1YuTSBLw4qbHiSDXYV41bXdJLppy2ZOLfgEDBofvTCWNbnC4/matchNorm.JPG?psid=1"

matchGone  = "https://byfiles.storage.live.com/y1pf2eEsI2qMbRF8pfBbW3rGGt-h-8tL7xSCroq_YBM0gL7PzBsjWnJTdsY4OvRcg0Nf8ignf6oook/matchGone.JPG?psid=1"

nextMatch = 15



''Main Menu

GraphicsWindow.FontSize = 50

GraphicsWindow.BrushColor = "Red"

GraphicsWindow.DrawText(10, 10, "Last One Loses.")

GraphicsWindow.FontSize = 30

match1 = Controls.AddButton("Easy", 10, 130)

match2 = Controls.AddButton("Hard", 10, 200)

GraphicsWindow.DrawResizedImage(matchNorm, 130, 100, 60, 140)

Controls.ButtonClicked = CheckButton



Sub CheckButton

  ''Reads and buttons clicked within the game

  clicked = Controls.LastClickedButton

  If (clicked = match1) Then

    level = 1

    InitGame()

  ElseIf (clicked = match2) Then

    level = 2

    InitGame()

    firstmove = 1

    Hard()

  ElseIf (clicked = remove1) Then

    matches[nextMatch] = 0

    DrawMatches()

    nextMatch = nextMatch - 1

    record = 1

  ElseIf (clicked = remove2) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    DrawMatches()

    nextMatch = nextMatch - 2

    record = 2

  ElseIf (clicked = remove3) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    matches[nextMatch - 2] = 0

    DrawMatches()

    nextMatch = nextMatch - 3

    record = 3

  EndIf

  If (nextMatch = 1) Then

    YouWin()

  EndIf

  If (clicked <> match1) And (clicked <> match2) And (level = 1) Then

    Easy()

  ElseIf (clicked <> match1) And (clicked <> match2) And (level = 2) Then

    Hard()

  EndIf

EndSub



Sub InitGame

  ''Initialize the first screen (matches and array data) game for play

  GraphicsWindow.Clear()

  i = 1

  While (i < 16)

    matches[i] = 1

    i = i + 1

  EndWhile

  DrawMatches()

EndSub



Sub Easy

  ''Easy level AI

  Program.Delay(500)

  easyMove = Math.GetRandomNumber(3)

  If (easyMove = 1) Then

    matches[nextMatch] = 0

    DrawMatches()

    nextMatch = nextMatch - 1

  ElseIf (easyMove = 2) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    DrawMatches()

    nextMatch = nextMatch - 2

  ElseIf (easyMove = 3) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    matches[nextMatch - 2] = 0

    DrawMatches()

    nextMatch = nextMatch - 3

  EndIf

  If (nextMatch = 3) Then

    Controls.HideControl(remove3)

  ElseIf (nextMatch = 2) Then

    Controls.HideControl(remove2)

    Controls.HideControl(remove3)

  ElseIf (nextMatch = 1) Then

    YouLose()

  EndIf

EndSub



Sub Hard

  ''Hard level AI

  If (firstmove = 1) Then

    record = 2

    firstmove = 0

  Else

    record = 4 - record

  EndIf

  If (record = 1) Then

    matches[nextMatch] = 0

    DrawMatches()

    nextMatch = nextMatch - 1

  ElseIf (record = 2) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    DrawMatches()

    nextMatch = nextMatch - 2

  ElseIf (record = 3) Then

    matches[nextMatch] = 0

    matches[nextMatch - 1] = 0

    matches[nextMatch - 2] = 0

    DrawMatches()

    nextMatch = nextMatch - 3

  EndIf

  If (nextMatch = 3) Then

    Controls.HideControl(remove3)

  ElseIf (nextMatch = 2) Then

    Controls.HideControl(remove2)

    Controls.HideControl(remove3)

  ElseIf (nextMatch = 1) Then

    YouLose()

  EndIf

EndSub



Sub DrawMatches

  ''Creates game screen, ie: the matches and the buttons for playing

  GraphicsWindow.Clear()

  i = 1

  b = 1

  y = 1

  While (b < 4)

    c = 1

    x = 1

    While (c < 6)

      If (matches[i] = 0) Then

        GraphicsWindow.DrawResizedImage(matchGone, x, y, 60, 140)

      ElseIf (matches[i] = 1) Then

        GraphicsWindow.DrawResizedImage(matchNorm, x, y, 60, 140)

      EndIf

      x = x + 70

      c = c + 1

      i = i + 1

    EndWhile

    y = y + 150

    b = b + 1

  EndWhile

  GraphicsWindow.DrawBoundText(350, 40, 350, "How many matches would you like to remove?")

  remove1 = Controls.AddButton("1", 350, 150)

  remove2 = Controls.AddButton("2", 400, 150)

  remove3 = Controls.AddButton("3", 450, 150)

EndSub



Sub YouWin

  ''You Win

  GraphicsWindow.Clear()

  GraphicsWindow.DrawText(1,1,"You Win")

  Program.Delay(10000)

  Program.End()

EndSub



Sub YouLose

  ''You Lose

  GraphicsWindow.Clear()

  GraphicsWindow.DrawText(1,1,"You Lose")

  Program.Delay(10000)

  Program.End()

EndSub


