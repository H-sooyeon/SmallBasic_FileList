GraphicsWindow.Width = 600
GraphicsWindow.Height = 600
GraphicsWindow.BackgroundColor = "Black"

ang1 = 0
ang2 = 0

While ("True")
  If (ang1/20 = Math.Floor(ang1/20)) Then 'Change the centres after a while
    Cx1 = 150+Math.GetRandomNumber(300)
    Cy1 = 150+Math.GetRandomNumber(300)
    Cx2 = 150+Math.GetRandomNumber(300)
    Cy2 = 150+Math.GetRandomNumber(300)
    'Create a line shape of roughly the right length to help with the width scaling issue
    GraphicsWindow.PenColor = "Yellow"
    GraphicsWindow.PenWidth = 1
    Shapes.Remove(line)
    lineLen = Math.SquareRoot((Cx1-Cx2)*(Cx1-Cx2) + (Cy1-Cy2)*(Cy1-Cy2))
    line = Shapes.AddLine(0,0,lineLen,0) 'Default line pixels ling in X direction
    'End Blobs
    GraphicsWindow.BrushColor = "Red"
    GraphicsWindow.PenColor = "Red"
    Shapes.Remove(end1)
    Shapes.Remove(end2)
    end1 = Shapes.AddEllipse(20,20)
    end2 = Shapes.AddEllipse(20,20)
  EndIf
  ang1 = ang1+0.05
  ang2 = ang2-0.05
  x1 = Cx1+100*Math.Cos(ang1)
  y1 = Cy1+100*Math.Sin(ang1)
  x2 = Cx2+100*Math.Cos(ang2)
  y2 = Cy2+100*Math.Sin(ang2)
  Shapes.Move(end1,x1-10,y1-10)
  Shapes.Move(end2,x2-10,y2-10)
  moveLine()
  Program.Delay(20)
EndWhile

'Move, scale and rotate a line shape to run from x1,y1 to x2,y2
Sub moveLine
  'Line vector
  dx = x2-x1
  dy = y2-y1
  
  'Line angle
  If (dx = 0) Then
    angle = Math.Pi/2
  Else
    angle = Math.ArcTan(dy/dx)
  EndIf
  If (dx < 0) Then
    angle = angle+Math.Pi
  EndIf
  
  'Rotate
  Shapes.Rotate(line,angle*180/Math.Pi)
  
  'Scaling w.r.t original lineLen pixel line
  len = Math.SquareRoot(dx*dx + dy*dy)
  Shapes.Zoom(line,len/lineLen,len/lineLen)
  
  'Move to centre (note we need to then set top left w.r.t. original shape half width-height)
  cx = (x2+x1)/2
  cy = (y2+y1)/2
  Shapes.Move(line,cx-lineLen/2,cy)
EndSub
