' Prime Number
' Community Suggestion Challenge (CodingLikeCrazy) - May, 2012
' Copyright (C) 2012 Nonki Takahshi
max = 1000
space = "     "
While "True"
input:
  TextWindow.Write("How many primes to show? ")
  in = TextWindow.ReadNumber()
  If in < 1 Or max < in Then
    TextWindow.WriteLine("error: out of range 1-" + max)
    Goto input
  EndIf
  start = Clock.ElapsedMilliseconds
  out = 0
  p = 2
  If out < in Then
    WritePrime()
    out = out + 1
    p = p + 1
  EndIf
  While out < in
    IsPrime()
    While no
      p = p + 2
      IsPrime()
    EndWhile
    WritePrime()
    out = out + 1
    p = p + 2
  EndWhile
  TextWindow.WriteLine("")
  end = Clock.ElapsedMilliseconds
  TextWindow.WriteLine("(" + (end - start) + "ms)")
  TextWindow.WriteLine("")
EndWhile
' end of program

Sub IsPrime
  ' param p
  ' return no = "True" if p is not prime
  sqr = Math.Floor(Math.SquareRoot(p))
  d = 3
  While d <= sqr
    r = Math.Remainder(p, d)
    If r = 0 Then
      no = "True"
      Goto return
    EndIf
    d = d + 2
  EndWhile
  no = "False"
return:
EndSub
  
Sub WritePrime
  ' param p
  l = Text.GetLength(space) - Text.GetLength(p)
  If l < 0 Then
    l = 0
  EndIf
  TextWindow.Write(Text.GetSubText(space, 1, l))
  TextWindow.Write(p)
EndSub
