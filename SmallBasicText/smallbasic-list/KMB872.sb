' Flickr Sample Program

GraphicsWindow.Title = "Flickr Sample Program"

GraphicsWindow.Width = 640

GraphicsWindow.Height = 480

tag = "Star Wars"

While "True"

  param = "x=0;y=0;width=320;height=480;"

  param["url"] = Flickr.GetPictureOfMoment()

  DrawImage()

  param = "x=320;y=0;width=320;height=480;"

  param["url"] = Flickr.GetRandomPicture(tag)

  DrawImage()

  Program.Delay(5000)

EndWhile

Sub DrawImage

  ' param["x"] - the x co-ordinate of the point to draw the image at

  ' param["y"] - the y co-ordinate of the point of draw the image at

  ' param["width"] - the width to draw the image

  ' param["height"] - the height to draw the image

  ' param["url"] - URL of the image

  x = param["x"]

  y = param["y"]

  image = ImageList.LoadImage(param["url"])

  width = ImageList.GetWidthOfImage(image)

  height = ImageList.GetHeightOfImage(image)

  TextWindow.WriteLine(param["url"])

  TextWindow.WriteLine("width = " + width)

  TextWindow.WriteLine("height = " + height)

  If param["width"] < width Then

    height = height / width * param["width"]

    width = param["width"]

  EndIf

  If param["height"] < height Then

    width = width / height * param["height"]

    height = param["height"]

  EndIf

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.FillRectangle(x, y, param["width"], param["height"])

  GraphicsWindow.DrawResizedImage(image, x, y, width, height)

  caption = param["url"]

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.DrawText(x, y + height, caption)

EndSub


