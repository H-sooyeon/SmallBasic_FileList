' Controlless Buttons Example (v2.10)
' by Cookie_505 (2013/Oct)
' mod GoToLoop

' GZM429-1

' http://social.msdn.microsoft.com/Forums/en-US
'/ec0baec1-1470-4795-beae-531300dcc318/pragram-not-working-help
'-----------------------------------------------------------------------------------------------------------------------'
GW = 800
GH = 600
PicTheme = "Mountains"

GraphicsWindow.Title = "Controlless Buttons"
GraphicsWindow.Width = GW
GraphicsWindow.Height = GH
GraphicsWindow.CanResize = "False"

GraphicsWindow.PenColor = "Black"
GraphicsWindow.BackgroundColor = "MidnightBlue"

CreateButtons()

GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor
GraphicsWindow.MouseDown = CheckButton
'-----------------------------------------------------------------------------------------------------------------------'
Loop:
While btnClicked = ""
  Program.Delay(100)
EndWhile

If btnClicked = buttons[1]["t"] Then
  Sound.PlayBellRing()
  GraphicsWindow.FillRectangle(90,90  90,90)
  
ElseIf btnClicked = buttons[2]["t"] Then
  Sound.PlayClick()
  pic = Flickr.GetRandomPicture(PicTheme)
  GraphicsWindow.DrawResizedImage(pic   90,90  90,90)
  
ElseIf btnClicked = buttons[3]["t"] Then
  Sound.PlayChimeAndWait()
  Program.End()
  
EndIf

btnClicked = ""
Goto Loop
'-----------------------------------------------------------------------------------------------------------------------'
Sub CheckButton
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
  
  For i = Array.GetItemCount(buttons) To 1 Step -1
    btn = buttons[i]
    
    If mx > btn["x"] And mx < btn["x"]+btn["w"] And my > btn["y"] And my < btn["y"]+btn["h"] Then
      btnClicked = btn["t"]
      i = 0
    EndIf
  EndFor
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------'
Sub CreateButtons
  buttons[1] = "x=450;y=100;w=60;h=30;t=New"
  buttons[2] = "x=450;y=140;w=62;h=30;t=Load"
  buttons[3] = "x=432;y=180;w=100;h=30;t=Tutorial"
  
  GraphicsWindow.FontSize = "25"
  
  For i = Array.GetItemCount(buttons) To 1 Step -1
    btn = buttons[i]
    
    GraphicsWindow.BrushColor = "Navy"
    GraphicsWindow.FillRectangle(btn["x"], btn["y"], btn["w"], btn["h"])
    
    GraphicsWindow.BrushColor = "Purple"
    GraphicsWindow.DrawText(btn["x"], btn["y"], btn["t"])
  EndFor
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------'
