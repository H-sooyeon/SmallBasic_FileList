'Event Definition

Controls.ButtonClicked = OnButtonClicked

Controls.TextTyped = OnTextType

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp

GraphicsWindow.MouseDown = OnMouseDown

GraphicsWindow.MouseMove = OnMouseMove

GraphicsWindow.MouseUp = OnMouseUp

GraphicsWindow.TextInput = OnTextInput

Timer.Tick = OnTimerTick

Timer.Interval = 400



'Initialisation

Init() 



'Main Loop

While PE = F 

  start = Clock.ElapsedMilliseconds

  Eventreaction()

  delay = 10 - (Clock.ElapsedMilliseconds - start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile 

Program.End()



'Subroutines

Sub Eventreaction

  'By a status (True) from event start a subroutine

  if OBC = T Then 

    OBC = F

    ReactionButtonClicked()  'Controls.ButtonClicked = OnButtonClicked

  ElseIf OTTi = T Then

    OTTi = F

    ReactionTimerTicked()   'Timer.Tick = OnTimerTick

  ElseIf OTI = T Then

    OTI = F

    ReactionTextInput()   'GraphicsWindow.TextInput = OnTextInput

  ElseIf OTT = T Then

    OTT = F

    ReactionTextTyped()  'Controls.TextTyped = OnTextType

  ElseIf OKD = T Then

    OKD = F

    ReactionKeyDown()   'GraphicsWindow.KeyDown = OnKeyDown

  ElseIf OKU = T Then

    OKU = F

    ReactionKeyUp()   'GraphicsWindow.KeyUp = OnKeyUp

  ElseIf OMU = T Then

    OMU = F

    ReactionMouseUp()   'GraphicsWindow.MouseUp = OnMouseUp

  ElseIf OMD = T Then

    OMD = F 

    ReactionMouseDown()   'GraphicsWindow.MouseDown = OnMouseDown

  ElseIf OMM = T Then

    OMM = F

    ReactionMouseMove()   'GraphicsWindow.MouseMove = OnMouseMove

  EndIf 

EndSub 



Sub ReactionButtonClicked

  ''Controls.ButtonClicked = OnButtonClicked

  LCB = Controls.LastClickedButton 

  ShTe = Text.Append(LCB, " was clicked")

  Shapes.SetText(S[18],  ShTe)

  If LCB = Button[4] Then

    PE = T

  ElseIf LCB = Button[1] Then

    Controls.HideControl(Button[1])

    Controls.ShowControl(Button[2])

  ElseiF LCB = Button[2] Then

    Controls.HideControl(Button[2])

    Controls.ShowControl(Button[3])

  ElseIf LCB = Button[3] Then 

    Controls.HideControl(Button[3])

    Controls.ShowControl(Button[1])

  EndIf 

EndSub 



Sub ReactionTextTyped

  ''Controls.TextTyped = OnTextType

  MLTBText = Controls.GetTextBoxText(MLTB)

  ShTe = Text.Append("MLTB = ", MLTBText) 

  Shapes.SetText(S[18], ShTe)

EndSub 



Sub ReactionKeyDown

  ''GraphicsWindow.KeyDown = OnKeyDown

  LKDTC = GraphicsWindow.LastKey 

  Shapes.SetText(S[3], LKDTC)

EndSub 



Sub ReactionKeyUp

  ''GraphicsWindow.KeyUp = OnKeyUp

  LKUTC = GraphicsWindow.LastKey 

  Shapes.SetText(S[7], LKUTC)

EndSub 



Sub ReactionMouseDown

  'GraphicsWindow.MouseDown = OnMouseDown

  MD = T

  Shapes.SetText(S[11], "Down")

  Shapes.SetText(S[13], " ")  

EndSub 



Sub ReactionMouseUp

  'GraphicsWindow.MouseUp = OnMouseUp

  Shapes.SetText(S[11], " ")

  Shapes.SetText(S[13], "Up")

  MD = F 

EndSub 



Sub ReactionMouseMove

  ''GraphicsWindow.MouseUp = OnMouseUp

  mx = GraphicsWindow.MouseX 

  my = GraphicsWindow.MouseY 

  Shapes.SetText(S[15], mx+"/"+my)

  If MD = T Then 

    GraphicsWindow.DrawLine(omx, omy, mx, my)

    ShTe = Text.Append("Move ", omx)

    ShTe = Text.Append(omx, " : ")

    ShTe = Text.Append(ShTe, omy)

    ShTe = Text.Append(ShTe, " to ")

    ShTe = Text.Append(ShTe, mx)

    ShTe = Text.Append(ShTe, " : ")

    ShTe = Text.Append(ShTe, my)

    Shapes.SetText(S[18], ShTe)

  EndIf 

  omx = mx

  omy = my

EndSub 



Sub ReactionTextInput

  'GraphicsWindow.TextInput = OnTextInput

  LKDT = GraphicsWindow.LastText 

  Shapes.SetText(S[18], "Text Input: " + LKDT)

EndSub 



Sub ReactionTimerTicked

  'Timer.Tick = OnTimerTick

  TCount = Clock.Time 

  Shapes.SetText(S[19], TCount)

EndSub 



Sub Init

  GraphicsWindow.Show()

  gww = Desktop.Width - 20

  gwh = Desktop.Height - 80

  GraphicsWindow.Top = 0

  GraphicsWindow.Left = 5

  GraphicsWindow.Width = gww

  GraphicsWindow.Height = gwh 

  

  T = "True"

  F = "False"

  OBC = F

  OTT = F

  OKD = F

  OKU = F

  OMD = F

  OMM = F 

  OMU = F

  OTI = F

  OTT = F

  PE = F

  TCount = "00:00:00"

  

  'Button Defination

  BT[1] = "Button-1"

  BT[2] = "Button-2"

  BT[3] = "Button-3"

  BT[4] = "Program End"

  

  BX = 10

  BY = 10

  For i = 1 To 4

    Button[i] = Controls.AddButton(BT[i], BX, BY) 

    BX = BX + 150

  EndFor 

  Controls.HideControl(Button[2])

  Controls.HideControl(Button[3])

  'MultiLineTextBox Defination

  MLTBT = "Multi Line Text Box"

  S[1] = Shapes.AddText(MLTBT) 

  Shapes.Move(S[1], 10, 60)

  MLTB = Controls.AddMultiLineTextBox(10, 80)

  Controls.SetSize(MLTB, gww-20, 100)

  'Showing Key-Down

  S[2] = Shapes.AddText("Key Down Textcode")

  Shapes.Move(S[2], 10, 200)

  S[3] = Shapes.AddText(" ") 

  Shapes.Move(S[3], 10, 220)

  'Showing Key-Up

  S[6] = Shapes.AddText("Key Up Textcode")

  Shapes.Move(S[6], 310, 200)

  S[7] = Shapes.AddText(" ") 

  Shapes.Move(S[7], 310, 220)

  'Showing Mouse Down

  S[10] = Shapes.AddText("Mouse")

  Shapes.Move(S[10], 10, 250)

  S[11] = Shapes.AddText(" ") 

  Shapes.Move(S[11], 10, 270)

  'Showing Mouse Up

  S[12] = Shapes.AddText("Mouse")

  Shapes.Move(S[12], 160, 250)

  S[13] = Shapes.AddText("Up") 

  Shapes.Move(S[13], 160, 270)

  'Showing Mouse Coordinate

  mx = GraphicsWindow.MouseX

  my = GraphicsWindow.MouseY

  omx = mx

  omy = my

  S[14] = Shapes.AddText("Mouse X   Y")

  Shapes.Move(S[14], 310, 250)

  S[15] = Shapes.AddText(" ") 

  Shapes.Move(S[15], 310, 270)  

  'TextBox Defination

  TBT = "Text Box"

  S[16] = Shapes.AddText(TBT) 

  Shapes.Move(S[16], 10, 300)

  TB = Controls.AddTextBox(10, 330)

  'ShowingText

  S[17] = Shapes.AddText("Show Text:") 

  Shapes.Move(S[17], 10, 360)

  S[18] = Shapes.AddText(" ") 

  Shapes.Move(S[18], 10, 380)

  Controls.SetSize(S[18], gww - 20, 200) 

  'Showing Timer

  S[19] = Shapes.AddText("0") 

  Shapes.Move(S[19], gww-100, 15)

EndSub 'Init



'Subroutines Events

Sub OnButtonClicked

  'Controls.ButtonClicked = OnButtonClicked

  OBC = T

EndSub 



Sub OnTextType

  'Controls.TextTyped = OnTextType

  OTT = T

EndSub 



Sub OnKeyDown

  'GraphicsWindow.KeyDown = OnKeyDown

  OKD = T

EndSub 



Sub OnKeyUp

  'GraphicsWindow.KeyUp = OnKeyUp

  OKU = T

EndSub 



Sub OnMouseDown

  'GraphicsWindow.MouseDown = OnMouseDown

  OMD = T

EndSub 



Sub OnMouseMove

  'GraphicsWindow.MouseMove = OnMouseMove

  OMM = T 

EndSub 



Sub OnMouseUp

  'GraphicsWindow.MouseUp = OnMouseUp

  OMU = T

EndSub 



Sub OnTextInput

  'GraphicsWindow.TextInput = OnTextInput

  OTI = T

EndSub 



Sub OnTimerTick

  'Timer.Tick = OnTimerTick

  OTTi = T

EndSub 


