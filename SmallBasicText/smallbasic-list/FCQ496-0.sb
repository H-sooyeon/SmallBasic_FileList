' SmallBasic Version 1.2

' Program: MouseClicksAt

' Changelog: 

' Author: Pappa Lapub

' Website: https://social.msdn.microsoft.com/Forums/en-US/775229e2-3fa5-46f3-a7ba-17e7ba1ac6fc/a-progamme-to-auto-click-every-after-40seconds

' ImportURL: http://smallbasic.com/program/?FCQ496-0

' Extension: LitDev

' Comment: Clicks everywhere on screen, not limited to GW only

'  

' Variables: 

' ToDo: 

' ================================================================================

intv = 10 ' auto click every ? seconds

dbg = ""  ' "True"



'GraphicsWindow.Left = 0

'GraphicsWindow.Top = 0

gw = GraphicsWindow.Width

gh = GraphicsWindow.Height

GraphicsWindow.Title = "Please WAIT ..."

GraphicsWindow.FontBold = ""

GraphicsWindow.FontSize = 20

GraphicsWindow.BrushColor = "Red"

shp = Shapes.AddText("")



CSExt()

CSComp()



' Place mouse into GW center

Mouse.MouseX = GraphicsWindow.Left + gw/2 + 8   ' gwX: 312

Mouse.MouseY = GraphicsWindow.Top + gh/2 + 30  ' gwY: 221



'x = Mouse.MouseX - 1

'y = Mouse.MouseY - 1

x = GraphicsWindow.Left + gw/2 + 7  ' 624/2 + 7 = 319

y = GraphicsWindow.Top + gh/2 + 29  ' 442/2 + 29 = 250



GraphicsWindow.BrushColor = "Black"

GraphicsWindow.FontBold = "True"

GraphicsWindow.FontName = "Courier New"

btn = Controls.AddButton("Kick"+ lf +" me", gw/2-50, gh/2-50)

LDShapes.BrushShape(btn, LDShapes.BrushGradient("1=Orange;2=#101010;","R"))

Controls.SetSize(btn, 100,100)

LDShapes.SetProperty(btn, "Focusable","False")



Timer.Interval = intv * 1000

Timer.Tick = OnTimerTick

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.MouseDown = OnMouseDown        ' I.)

'LDEvents.MouseDoubleClick = OnDoubleMouseDown ' II.)

Controls.ButtonClicked = OnButtonClick





' //////////  SUBs  \\\\\\\\\\

Sub CSExt

  lf = Text.GetCharacter(10)

  qu = Text.GetCharacter(34)

  

  cs = "using System;"+ lf

  cs = cs +"using System.Runtime.InteropServices;"+ lf

  cs = cs +"using System.Threading;"+ lf

  cs = cs +"using Microsoft.SmallBasic.Library;"+ lf

  cs = cs +"[SmallBasicType]"+ lf

  cs = cs +"public static class SBMouse {"+ lf

  cs = cs +"  private static double fx = 65535 / (Desktop.Width - 1);"+ lf

  cs = cs +"  private static double fy = 65535 / (Desktop.Height - 1);"+ lf

  cs = cs +"  // https://msdn.microsoft.com/en-us/library/windows/desktop/ms646260.aspx	"+ lf

  cs = cs +"  // http://www.pinvoke.net/default.aspx/user32/mouse_event.html"+ lf

  cs = cs +"  [DllImport("+ qu +"user32.dll"+ qu +", SetLastError=true)]"+ lf

  cs = cs +"  private static extern void mouse_event(uint dwFlags, int dx, int dy, uint dwData, int dwExtraInfo);"+ lf

  cs = cs +"  /// <summary>Clicks the left mouse button (Down+Up).</summary>"+ lf

  cs = cs +"  public static void LeftClick() {"+ lf

  cs = cs +"    SBMouse.mouse_event(6, 0,0, 0, 0); }"+ lf         ' 2 | 4 = 6

  cs = cs +"  /// <summary>Clicks the left mouse button at the specified position (Down+Up).</summary>"+ lf

  cs = cs +"  /// <param name="+ qu +"x"+ qu +">The x-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  /// <param name="+ qu +"y"+ qu +">The y-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  public static void LeftClickAt(Primitive x, Primitive y) {"+ lf

  cs = cs +"    int px = (int)((int)x * fx);"+ lf

  cs = cs +"    int py = (int)((int)y * fy);"+ lf

  cs = cs +"    if (px >= 0 & py >= 0)"+ lf

  cs = cs +"      SBMouse.mouse_event(32775, px,py, 0, 0); }"+ lf   ' 2 | 4 = 6   | 32768 =  32774 (at x,y) | 1 (move) = 32775

  

  cs = cs +"  /// <summary>Clicks the middle mouse button (Down+Up).</summary>"+ lf

  cs = cs +"  public static void MiddleClick() {"+ lf

  cs = cs +"    SBMouse.mouse_event(96, 0,0, 0, 0); }"+ lf          ' 32 | 64 = 96  | 32768 = 32864 (at x,y)

  cs = cs +"  /// <summary>Clicks the middle mouse button at the specified position (Down+Up).</summary>"+ lf

  cs = cs +"  /// <param name="+ qu +"x"+ qu +">The x-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  /// <param name="+ qu +"y"+ qu +">The y-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  public static void MiddleClickAt(Primitive x, Primitive y) {"+ lf

  cs = cs +"    int px = (int)((int)x * fx);"+ lf

  cs = cs +"    int py = (int)((int)y * fy);"+ lf

  cs = cs +"    if (px >= 0 & py >= 0)"+ lf

  cs = cs +"      SBMouse.mouse_event(32865, px,py, 0, 0); }"+ lf   ' 32 | 64  = 96  | 32768 =  32864 (at x,y) | 1 (move) = 32865

  

  cs = cs +"  /// <summary>Clicks the right mouse button (Down+Up).</summary>"+ lf

  cs = cs +"  public static void RightClick() {"+ lf

  cs = cs +"    SBMouse.mouse_event(24, 0,0, 0, 0); }"+ lf          ' 8 | 16 = 24  | 32768 = 32792 (at x,y)

  cs = cs +"  /// <summary>Clicks the right mouse button at the specified position (Down+Up).</summary>"+ lf

  cs = cs +"  /// <param name="+ qu +"x"+ qu +">The x-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  /// <param name="+ qu +"y"+ qu +">The y-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  public static void RightClickAt(Primitive x, Primitive y) {"+ lf

  cs = cs +"    int px = (int)((int)x * fx);"+ lf

  cs = cs +"    int py = (int)((int)y * fy);"+ lf

  cs = cs +"    if (px >= 0 & py >= 0)"+ lf

  cs = cs +"      SBMouse.mouse_event(32793, px,py, 0, 0); }"+ lf   ' 8 | 16  = 24  | 32768 =  32792 (at x,y) | 1 (move) = 32793

  

  cs = cs +"  /// <summary>Doubleclicks the left mouse button (100 ms delay).</summary>"+ lf

  cs = cs +"  public static void DoubleClick() {"+ lf

  cs = cs +"    SBMouse.mouse_event(6, 0,0, 0, 0);"+ lf             ' 2 | 4 = 6

  cs = cs +"    Thread.Sleep(100);"+ lf

  cs = cs +"    SBMouse.mouse_event(6, 0,0, 0, 0); }"+ lf           ' 2 | 4 = 6

  cs = cs +"  /// <summary>Doubleclicks the left mouse button at the specified position (100 ms delay).</summary>"+ lf

  cs = cs +"  /// <param name="+ qu +"x"+ qu +">The x-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  /// <param name="+ qu +"y"+ qu +">The y-screen coord. to click (Default: 0).</param>"+ lf

  cs = cs +"  public static void DoubleClickAt(Primitive x, Primitive y) {"+ lf

  cs = cs +"    int px = (int)((int)x * fx);"+ lf

  cs = cs +"    int py = (int)((int)y * fy);"+ lf

  cs = cs +"    if (px >= 0 & py >= 0) {"+ lf

  cs = cs +"      SBMouse.mouse_event(32775, px,py, 0, 0);"+ lf         ' 2 | 4 = 6  | 32768 =  32774 (at x,y) | 1 (move) = 32775

  cs = cs +"      Thread.Sleep(100);"+ lf

  cs = cs +"      SBMouse.mouse_event(32775, px,py, 0, 0); } } }"+ lf   ' 2 | 4 = 6   | 32768 =  32774 (at x,y) | 1 (move) = 32775

  

  If dbg Then

    arrCS = LDText.Split(cs, lf)

    For n = 1 To Array.GetItemCount(arrCS)

      l = Text.GetSubTextToEnd(Text.Append("0",n), Text.GetLength(n)) ' 2 digits,  prefix with 0 for n<10 ("00" for 3 digits etc.)

      TextWindow.WriteLine(l +": "+ arrCS[n])

    EndFor

  EndIf

' The following line could be harmful and has been automatically commented.

'   'File.WriteContents(Program.Directory +"\MouseClicks-SBMouse.cs", cs)

EndSub



Sub CSComp

  resComp = LDInline.IncludeCS(cs, "","")

  If dbg Then

    TextWindow.WriteLine(lf +"Compiling: "+ resComp +lf)

    If resComp <> "SUCCESS" Then

      TextWindow.Pause()

      Program.End()

    EndIf

  EndIf

  GraphicsWindow.Title = "Ready, L-Click fires every "+ intv +" sec   [Esc .. Exit]"

EndSub





' //////////  EVENTs  \\\\\\\\\\

Sub OnTimerTick

  arg[1] = x  ' 319

  arg[2] = y  ' 250

  LDInline.Call("SBMouse.LeftClickAt", arg)

  'LDInline.Call("SBMouse.LeftClick", "")      ' I.)

  'LDInline.Call("SBMouse.DoubleClick", "")   ' II.)

EndSub



Sub OnMouseDown       ' Single click I.)

  If Mouse.IsLeftButtonDown Then

    mx = GraphicsWindow.MouseX

    my = GraphicsWindow.MouseY

    'Sound.PlayClick()

    GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()

    GraphicsWindow.FillEllipse(mx-2,my-2, 5,5)

    Shapes.SetText(shp, "L-Click  "+ mx +","+ my)

    LDShapes.AnimateOpacity(shp, 8000, 0.5)

  EndIf

EndSub



Sub OnDoubleMouseDown   ' Testing Doubleclick II.)

  If Mouse.IsLeftButtonDown Then

    mx = GraphicsWindow.MouseX

    my = GraphicsWindow.MouseY

    GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()

    GraphicsWindow.FillEllipse(mx-5,my-5, 10,10)

    Shapes.SetText(shp, "L-DoubleClick  "+ mx +","+ my)

    LDShapes.AnimateOpacity(shp, 8000, 0.5)

  EndIf

EndSub



Sub OnKeyDown

  If GraphicsWindow.LastKey = "Escape" Then

    Program.End()

  EndIf

EndSub



Sub OnButtonClick

  dtX = Mouse.MouseX

  dtY = Mouse.MouseY

  Shapes.SetText(shp, "ButtonClick  "+ dtX +","+ dtY)  ' 319,250

  LDShapes.AnimateOpacity(shp, 8000, 0.5)

  

  ' move mouse away from button

  Program.Delay(1500)

  Mouse.MouseX = GraphicsWindow.Left + 8 + gw/2 + 100*(2*Math.GetRandomNumber(2) - 3)

  Mouse.MouseY = GraphicsWindow.Top + 30 + gh/2 + 100*(2*Math.GetRandomNumber(2) - 3)

EndSub


