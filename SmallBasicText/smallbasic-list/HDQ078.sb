' initialize graphics window
GraphicsWindow.Hide()
GraphicsWindow.Title = "RGB Color Sliders"
GraphicsWindow.Width = 430
GraphicsWindow.Height = 420
GraphicsWindow.CanResize = "False"
GraphicsWindow.BackgroundColor = GraphicsWindow.GetColorFromRGB(245,245,245)

' initialize event handlers
GraphicsWindow.MouseDown = onMouseDown
GraphicsWindow.MouseMove = onMouseMove

' define some constants
R=1
G=2
B=3
color[R] = "Red"
color[G] = "Green"
color[B] = "Blue"

' initialize color sliders
For i = R To B
  GraphicsWindow.DrawLine(50*i, 50, 50*i, 305)
  GraphicsWindow.BrushColor = color[i]
  sliderShape[i] = Shapes.AddEllipse(50, 10)
  Shapes.SetOpacity(sliderShape[i], 70)
  Shapes.Move(sliderShape[i], 50*i-25, 45)
  sliderValue[i] = 255
  sliderText[i] = Shapes.AddText(sliderValue[i])
  Shapes.Move(sliderText[i], 50*i-10, 25)
EndFor

' initialize color view window
GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(sliderValue[R],sliderValue[G],sliderValue[B])
GraphicsWindow.DrawRectangle(200,30,200,280)
GraphicsWindow.FillRectangle(200,30,200,280)

' make the tool useful by adding an option to copy the color to the clipboard
GraphicsWindow.BrushColor = "DimGray"
GraphicsWindow.DrawBoundText(35,330,360,"To use the color, click the box below, press Control+A, then Control+C to copy the text into your clipboard. Finally, press Control+V to paste the code into your program.")
colorTextBox = Controls.AddTextBox(35,380)
Controls.SetSize(colorTextBox,360,22)
Controls.SetTextBoxText(colorTextBox, "GraphicsWindow.GetColorFromRGB(" + sliderValue[R] + "," + sliderValue[G] + "," + sliderValue[B]+")")

GraphicsWindow.Show()

'== Event Handlers =='
Sub onMouseDown
  onMouseMove()
EndSub
'================'
Sub onMouseMove
  If Mouse.IsLeftButtonDown Then
    For i = R To B
      If (GraphicsWindow.MouseX > (50*i-25)) And (GraphicsWindow.MouseX < (50*i+25)) Then
        If (GraphicsWindow.MouseY >= 50) And (GraphicsWindow.MouseY <= 305) Then
          Shapes.Move(sliderShape[i], 50*i-25, GraphicsWindow.MouseY-5)
          sliderValue[i] = 305-GraphicsWindow.MouseY
          Shapes.SetText(sliderText[i],sliderValue[i])
          GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(sliderValue[R],sliderValue[G],sliderValue[B])
          GraphicsWindow.FillRectangle(200,30,200,280)
          Controls.SetTextBoxText(colorTextBox, "GraphicsWindow.GetColorFromRGB(" + sliderValue[R] + "," + sliderValue[G] + "," + sliderValue[B]+")")
        EndIf
      EndIf
    EndFor
  EndIf
EndSub
'================'
