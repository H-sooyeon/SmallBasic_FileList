'Analogue Clock Created by moorem34, modified to use Shapes by Stendec, 1/3/09



'Later shapes are on top.

'Create hour hand

GraphicsWindow.PenWidth = 3

GraphicsWindow.PenColor = "Blue"

hourhand = Shapes.AddLine(40,40,80,80)

'Move hour hand into position

Shapes.Move(hourhand,120,120)

'Create minute hand

GraphicsWindow.PenWidth = 2

GraphicsWindow.PenColor = "Red"

minutehand = Shapes.AddLine(51,51,102,102)

'Move minute hand into position

Shapes.Move(minutehand,109,109)

'Create second hand

GraphicsWindow.PenWidth = 1

GraphicsWindow.PenColor = "White"

secondhand = Shapes.AddLine(56,56,112,112)

'Move second hand into position

Shapes.Move(secondhand,104,104)

'Fill in centre

GraphicsWindow.BrushColor = "White"

centre = Shapes.AddEllipse(15,15)

Shapes.Move(centre, 153,153)



init()

drawclockface()





'Run clock until key "q" is hit and endprogram is set to "yes"

While (EndProgram <> "yes")

  sec = Clock.Second

  min = Clock.Minute

  'system clock is military time -adjust hours for 12-hour format

  If Clock.Hour >= 0 And Clock.Hour <=12 Then

    hr = Clock.Hour

  Else

    hr = Clock.hour-12

  EndIf

   

  'only go to subroutines when a new second is present

  If oldsec <> sec Then

    seconds()

    minutes()

    hours()

  EndIf

  

  oldsec = sec



  If GraphicsWindow.LastKey = "Q" Then

    EndProgram = "yes"

  EndIf



EndWhile

Program.End()



Sub init

  GraphicsWindow.BackgroundColor="DarkBlue"

  GraphicsWindow.Width=320

  GraphicsWindow.Height=320

  GraphicsWindow.Title = "Press 'q' to quit"

endsub





Sub drawclockface



  'Draw the numerals on the face of the clock



  GraphicsWindow.DrawText(152,60,12)

  GraphicsWindow.DrawText(204,76,1)

  GraphicsWindow.DrawText(235,105,2)

  GraphicsWindow.DrawText(248,151,3)

  GraphicsWindow.DrawText(232,197,4)

  GraphicsWindow.DrawText(203,229,5)

  GraphicsWindow.DrawText(155,245,6)

  GraphicsWindow.DrawText(112,229,7)

  GraphicsWindow.DrawText(79,199,8)

  GraphicsWindow.DrawText(64,153,9)

  GraphicsWindow.DrawText(72,105,10)

  GraphicsWindow.DrawText(106,74,11)

endsub





Sub seconds

  Shapes.Rotate(secondhand, sec * 6 - 135)

  Sound.PlayClickAndWait()

Endsub



Sub minutes

  Shapes.Rotate(minutehand, (min * 6) + (sec / 10) - 135)

Endsub  



Sub hours 

  Shapes.Rotate(hourhand, (hr * 30) + (min / 2) + (sec / 120) - 135)

EndSub








