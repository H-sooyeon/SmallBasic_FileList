' Happy Numbers below 1000
' Copyright (c) 2012 Nonki Takahashi.  All rights reserved.
'
' History:
'  0.1 2012/12/12 Created.
'
For n = 1 To 1000
  IsHappy()
  If happy Then
    TextWindow.Write(" " + n)
  EndIf
EndFor
TextWindow.WriteLine("")

Sub IsHappy
  ' param n - number to check
  ' return happy - "True" if n is happy number
  Stack.PushValue("local", n)
  set = ""
  i = 0
  While (n <> 1) And (Array.ContainsValue(set, n) = "False")
    i = i + 1
    set[i] = n
    len = Text.GetLength(n)
    _n = 0
    For d = 1 To len
      _n = _n + Math.Power(Text.GetSubText(n, d, 1), 2)
    EndFor
    n = _n
  EndWhile
  If n = 1 Then
    happy = "True"
  Else
    happy = "False"
  EndIf
  n = Stack.PopValue("local")
EndSub
