'Get main image
imgURL = "http://litdev.co.uk/forum/parrot.png" 'Image is 100*100 pixels
img = ImageList.LoadImage(imgURL)
width = ImageList.GetWidthOfImage(img)
height = ImageList.GetHeightOfImage(img)

'Setup GW
rows = 6
gw = rows*width
gh = rows*height
GraphicsWindow.Title = "Image Colour Hue Conversion"
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh

'For hex to decimal conversion
For i = 0 To 9
  dec[i] = i
EndFor
dec["A"] = 10
dec["B"] = 11
dec["C"] = 12
dec["D"] = 13
dec["E"] = 14
dec["F"] = 15

GraphicsWindow.DrawImage(img,0,0)

For i = 0 To width-1
  For j = 0 To height-1
    pixel = GraphicsWindow.GetPixel(i,j) 'Pixel colour
    GetRGB() 'Convert to R,G,B components
    RGB2HSL() 'Convert to HSL
    For jj = 0 To rows-1
      For ii = 0 To rows-1
        If (ii+jj > 0) Then 'Don't overwrite original
          H = H+360/rows/rows 'Modify the Hue (colour shift)
          HSL2RGB() 'Convert to RGB
          col = GraphicsWindow.GetColorFromRGB(R,G,B) 'Set color
          GraphicsWindow.SetPixel(width*ii+i,height*jj+j,col) 'Display modified colour pixel
        EndIf
      EndFor
    EndFor
  EndFor
EndFor

Sub GetRGB 'Converts colour e.g. "#AB12FF" to R,G,B components
  R = 16*dec[Text.GetSubText(pixel,2,1)]+dec[Text.GetSubText(pixel,3,1)]
  G = 16*dec[Text.GetSubText(pixel,4,1)]+dec[Text.GetSubText(pixel,5,1)]
  B = 16*dec[Text.GetSubText(pixel,6,1)]+dec[Text.GetSubText(pixel,7,1)]
EndSub

Sub HSL2RGB 'Converts HSL to RGB
  While (H < 0)
    H = H+360
  EndWhile
  While (H >= 360)
    H = H-360
  EndWhile
  
  S = Math.Max(0,Math.Min(1,S))
  L = Math.Max(0,Math.Min(1,L))
  
  C = (1-Math.Abs(2*L-1))*S
  H2 = H/60
  X = C*(1-Math.Abs(Math.Remainder(H2,2)-1))
  If (H2 >= 0 And H2 < 1) Then
    R = C
    G = X
    B = 0
  ElseIf (H2 >= 1 And H2 < 2) Then
    R = X
    G = C
    B = 0
  ElseIf (H2 >= 2 And H2 < 3) Then
    R = 0
    G = C
    B = X
  ElseIf (H2 >= 3 And H2 < 4) Then
    R = 0
    G = X
    B = C
  ElseIf (H2 >= 4 And H2 < 5) Then
    R = X
    G = 0
    B = C
  ElseIf (H2 >= 5 And H2 < 6) Then
    R = C
    G = 0
    B = X
  Else
    R = 0
    G = 0
    B = 0
  EndIf
  M = L-C/2
  R = 255*(R+M)
  G = 255*(G+M)
  B = 255*(B+M)
EndSub

Sub RGB2HSL 'Converts RGB to HSL
  R = Math.Max(0,Math.Min(255,R))
  G = Math.Max(0,Math.Min(255,G))
  B = Math.Max(0,Math.Min(255,B))
  
  Max = Math.Max(R,Math.Max(G,B))
  Min = Math.Min(R,Math.Min(G,B))
  C = Max-Min
  
  If (C = 0) Then
    H = 0
  ElseIf (Max = R) Then
    H = Math.Remainder((G-B)/C,6)
  ElseIf (Max = G) Then
    H = (B-R)/C + 2
  ElseIf (Max = B) Then
    H = (R-G)/C + 4
  EndIf
  
  H = H*60
  While (H < 0)
    H = H+360
  EndWhile
  While (H >= 360)
    H = H-360
  EndWhile
  H = Math.Remainder(H,360)
  
  L = (Max+Min)/2/255
  If (Max+Min = 0 Or L = 1) Then
    S = 0
  Else
    S = C/(1-Math.Abs(2*L-1))/255
  EndIf
EndSub
