' SmallBasic Version 1.2
' Program: LudwigVan2 (CwO)
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/fdd47786-c5c6-4d47-aa76-457d96502493/ludwig-van-beethoven-moonlight-sonata-in-small-basic
' ImportURL: http://smallbasic.com/program/?GMC157-0
' Extension: LitDev
' Comment: ESC ... Exit;  MouseWheel  ... Zoom In/Out
' 
' Variables: 
' ToDo: 
' ================================================================================
'pic = "https://41.media.tumblr.com/tumblr_lu7ub1Cpr11qav3uso1_1280.png"
pic = Program.Directory +"\img\LudwigVan.png"

tbh = 60        '' Taskbar Height
h = 600         '' init Height for image = gh

'----------------------------------------------------------------------------------
maxR = 200
maxG = 150
maxB = 100

fact = 1
dZoom = 1.05
maxGH = Desktop.Height-22 - tbh + 20          '' +20 ... tolerance beyond taskbarH

imgOrig = ImageList.LoadImage(pic)
imgW = ImageList.GetWidthOfImage(imgOrig)
imgH = ImageList.GetHeightOfImage(imgOrig)
w = Math.Round(imgW * h / imgH)
' The following line could be harmful and has been automatically commented.
' title = LDFile.GetFile(pic) +"."+ LDFile.GetExtension(pic) +"  [Orig: "+ imgW +"x"+ imgH +"] "+ w +"x"+ h +"  x"

img = LDImage.Copy(imgOrig)
LDImage.Resize(img, w,h)

img2 = LDImage.Copy(img)
LDImage.Add(img2, 0,-90, -255)

ProcessImagePxl()
LDImage.EffectEdge(img)

GraphicsWindow.Top = 0
GraphicsWindow.Width = w
GraphicsWindow.Height = h
GraphicsWindow.Title = title + fact
LDGraphicsWindow.TopMost = "True"

LDGraphicsWindow.BackgroundImage(img)
shp = Shapes.AddImage(img2)
LDShapes.AnimateOpacity(shp, 12000, 0)

GraphicsWindow.KeyDown = OnKeyDown
LDEvents.MouseWheel = OnMouseWheel


' //////////  SUB  \\\\\\\\\\
Sub ProcessImagePxl
  LDImage.OpenWorkingImage(img)
  For x = 1 To w
    For y = 1 To h
      argb = LDImage.GetWorkingImagePixelARGB(img, x,y)
      cR = argb["R"]
      cG = argb["G"]
      cB = argb["B"]
      
      If cR < maxR Then
        cR = 255 * cR/maxR
      EndIf
      
      If cG < maxG Then
        cG = 255 * cG/maxG
      EndIf
      
      If cB < maxB Then
        cB = 255 * cB/maxB
      EndIf
      
      LDImage.SetWorkingImagePixel(img, x,y, GraphicsWindow.GetColorFromRGB(cR, cG, cB))
    EndFor
  EndFor
  LDImage.CloseWorkingImage(img)
EndSub

' //////////  EVENTs  \\\\\\\\\\
Sub OnKeyDown
  If GraphicsWindow.LastKey = "Escape" Then
    Program.End()
  EndIf
EndSub

Sub OnMouseWheel
  wDir = LDEvents.LastMouseWheelDelta
  If GraphicsWindow.Height * dZoom <= maxGH Or wDir < 0 Then
    fact = fact * Math.Power(dZoom, wDir)
    GraphicsWindow.Width = w * fact
    GraphicsWindow.Height = h * fact
    LDShapes.SetSize(shp, w * fact ,h * fact)
    GraphicsWindow.Title = title + Math.Round(100*fact)/100
  EndIf
EndSub
  
