GraphicsWindow.Width = 400
GraphicsWindow.Height = 100
GraphicsWindow.Title = "Random Password Generator"
Box = Controls.AddTextBox(5,5)
Controls.SetSize(Box,GraphicsWindow.Width - 10,25)
GraphicsWindow.DrawText(5,40,"Choose Password Lenght & Difficulty")
Len = 5
Diffc = 1
Diff[1] = "Weak"
Diff[2] = "Modrate"
Diff[3] = "Strong"
Diff[4] = "Strongest"
chars = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890~!@#$%^&*()_+|,./;'<>?:[]{}-=\"
LenTxt = Shapes.AddText(Len)
Shapes.Move(LenTxt,45,65)
BtnDwn = Controls.AddButton("<",5,60)
BtnUp = Controls.AddButton(">",70,60)
DiffTxt = Shapes.AddText(Diff[1])
Shapes.Move(DiffTxt,160,65)
BtnDiffDwn = Controls.AddButton("<<",120,60)
BtnDiffUp = Controls.AddButton(">>",250,60)
BtnGen = Controls.AddButton("Generate",300,60)
Controls.ButtonClicked = Click
Sub Click
  LastButton = Controls.GetButtonCaption(Controls.LastClickedButton)
  If LastButton = "<" Then
    Len = Len - 1
    If Len < 1 Then
      Len = 1
    EndIf
    Shapes.SetText(LenTxt,Len)
  EndIf
  If LastButton = ">" Then
    Len = Len + 1
    If Len > 50 Then
      Len = 50
    EndIf
    Shapes.SetText(LenTxt,Len)
  EndIf
  If LastButton = "<<" Then
    Diffc = Diffc - 1
    If Diffc < 1 Then
      Diffc = 1
    EndIf
    Shapes.SetText(DiffTxt,Diff[Diffc])
  EndIf
  If LastButton = ">>" Then
    Diffc = Diffc + 1
    If Diffc > 4 Then
      Diffc = 4
    EndIf
    Shapes.SetText(DiffTxt,Diff[Diffc])
  EndIf
  If LastButton = "Generate" Then
    randKey()
  EndIf
EndSub
Sub randKey
  If Diffc = 1 Then
    level = 26
  ElseIf Diffc = 2 Then
    level = 52
  Elseif Diffc = 3 Then
    level = 62
  Else
    level = 93
  EndIf
  lenght = Len
  keyw = ""
  While Text.GetLength(keyw) < lenght
    randigit = Math.GetRandomNumber(level)
    kchar = Text.GetSubText(chars,randigit,1)
    keyw = keyw + kchar
  EndWhile
  Controls.SetTextBoxText(Box,keyw)
EndSub
