Amount = 20
Diameter = 50
Radius = Diameter/2

SCREEN_W = 512
SCREEN_H = 512
GraphicsWindow.Width = SCREEN_W
GraphicsWindow.Height = SCREEN_H

GraphicsWindow.PenWidth = 0

For i = 1 to Amount
  GraphicsWindow.BrushColor = GraphicsWindow.GetRandomColor()
  Ball[i] = Shapes.AddEllipse(Diameter*1,Diameter*1)
  
  Ball_x[i] = Math.GetRandomNumber(SCREEN_W)
  Ball_y[i] = Math.GetRandomNumber(SCREEN_H)
  Ball_vx[i] = 0
  Ball_vy[i] = 0
endfor

While GraphicsWindow.LastKey <> "Escape"
  
  MX = GraphicsWindow.MouseX
  MY = GraphicsWindow.MouseY
  
  Ball_x[1] = MX
  Ball_y[1] = MY
  
  Check_Collision()
  Check_Boundaries()
  
  For i = 1 To Amount
    Ball_vy[i] = Ball_vy[i] + 0.5
    
    Ball_vx[i] = Ball_vx[i] * 0.9
    Ball_vy[i] = Ball_vy[i] * 0.9
    
    Ball_x[i] = Ball_x[i] + Ball_vx[i]
    Ball_y[i] = Ball_y[i] + Ball_vy[i]
    
    'Shapes.Remove(Ball[i])
    'GraphicsWindow.BrushColor = GraphicsWindow.GetColorFromRGB(Ball_vx[i]*10,0,0)
    'Ball[i] = Shapes.AddEllipse(Diameter,Diameter)
    Shapes.Move(Ball[i],Ball_x[i]-Radius,Ball_y[i]-Radius)
    
  EndFor
  
  Program.Delay(15)
EndWhile


Sub Check_Collision
  For i = 1 To Amount
    For j = i+1 to Amount
      
      Distance_X = Math.Abs(Ball_x[i] - Ball_x[j])
      Distance_Y = Math.Abs(Ball_y[i] - Ball_y[j])
      Distance_XY = Math.SquareRoot(Distance_X*Distance_X+Distance_Y*Distance_Y)
      
      If Distance_XY < Diameter Then
        Scale_X = ((1/Diameter) * (Diameter - Distance_X))*0.1
        Scale_Y = ((1/Diameter) * (Diameter - Distance_Y))*0.1
        
        Ball_vx[i] = Ball_vx[i] + (Ball_x[i] - Ball_x[j]) * Scale_X
        Ball_vy[i] = Ball_vy[i] + (Ball_y[i] - Ball_y[j]) * Scale_Y
        
        Ball_vx[j] = Ball_vx[j] + (Ball_x[j] - Ball_x[i]) * Scale_X
        Ball_vy[j] = Ball_vy[j] + (Ball_y[j] - Ball_y[i]) * Scale_Y
      EndIf
      
    EndFor
  endfor
EndSub

Sub Check_Boundaries
  For i = 1 To Amount
    
    If Ball_x[i]-Radius < 0 Then
      Ball_x[i] = Radius
      Ball_vx[i] = -Ball_vx[i]*0.9
    ElseIf Ball_x[i]+Radius > SCREEN_W Then
      Ball_x[i] = SCREEN_W - Radius
      Ball_vx[i] = -Ball_vx[i]*0.9
    EndIf
    
    If Ball_y[i]-Radius < 0 Then
      Ball_y[i] = Radius
      Ball_vy[i] = -Ball_vy[i]*0.9
    ElseIf Ball_y[i]+Radius > SCREEN_H Then
      Ball_y[i] = SCREEN_H - Radius
      Ball_vy[i] = -Ball_vy[i]*0.9
    EndIf
  
  EndFor
EndSub

Program.End()
