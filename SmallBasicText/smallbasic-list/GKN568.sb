'  Palindrome checking program 

Palindrome_Sample()
Show_Sample()
Input_your_sample()

Check_Palindrome()
Ok_or_Not()

Sub Show_Sample
  GraphicsWindow.BackgroundColor="Lightcyan" 
  GraphicsWindow.left=0
  GraphicsWindow.top=0
  GraphicsWindow.Height=700
  GraphicsWindow.Width=1200
  GraphicsWindow.Show()
  GraphicsWindow.FontSize=18
  For i = 1 To 15
    GraphicsWindow.DrawText(10,5+35*(i-1),i)
    GraphicsWindow.DrawText(70,5+35*(i-1),sample[i])
  endfor  
  
  textbox[1] = Controls.AddTextBox(10,550)
  Controls.SetSize(textbox[1] ,800,30)
  GraphicsWindow.BrushColor="Green"
  GraphicsWindow.FillRectangle(820,550,200,30)
  GraphicsWindow.BrushColor="Yellow"
  GraphicsWindow.DrawText(822,552,"  Check Start! ")
  
  i=17
  GraphicsWindow.BrushColor="Red"
  GraphicsWindow.FontSize=30
  GraphicsWindow.DrawText(70,5+40*(i-1),sample[i])
  GraphicsWindow.DrawText(500,5+40*3,"Please refer these samples!")
  GraphicsWindow.FontSize=18
Endsub  

Sub Input_your_sample
  Loop:
  OnX=mouse.IsLeftButtonDown
  GMX=GraphicsWindow.MouseX
  GMY=GraphicsWindow.MouseY
  If Onx="true" Then
    If (820<GMX and GMX<1020 And 550<GMY And GMY<580) Then
      Goto EndLoop
    Else 
      Goto Loop
    EndIf
  Endif    
  Goto Loop 
  
  Endloop:  
    
  Your_Line = Controls.GetTextBoxText(textbox[1]) 
  
  Program.Delay(500)
EndSub  

Sub Check_Palindrome
  GraphicsWindow.Clear()
  Review()
  Program.Delay(1000)
  reject()
  Program.Delay(1000)
  Reverse_Check()
  Program.Delay(1000)
  merged()
EndSub

Sub Review    ' with  small word
  GraphicsWindow.FontName="Coorie new"
  GraphicsWindow.BrushColor="Red"
  GraphicsWindow.DrawText(10,25,"Original")
  GraphicsWindow.DrawText(10,45,"small word")
  
  Get_Len=Text.GetLength(Your_Line)
  For i=1 To Get_Len
    sMword=text.GetSubText(Your_Line,i,1)
    
    MwordNo=text.getCharacterCode(sMword)
    If 64<MwordNo And MwordNo<91 Then    '  Larg word   65 --  90  
      smword=Text.ConvertToLowerCase(sMword) ' To  Small  
    EndIf
    
    b[i]=sMword            ' Original  data with small word
    a[i]=shapes.AddText(sMword)   ' small word shape
    Shapes.Animate(a[i], 150+15*(i-1),25,500)
  EndFor
EndSub

Sub reject  'split  
  GraphicsWindow.DrawText(10,75,"Rejected")
  For i=1 To Get_Len
    M1=b[i]     '   1 word 
    M2=Text.GetCharacterCode(M1)    '   Char check 
    If M2<97 Or 122<M2  then   '  Reject   '  ,  .  :  ?  !  -     etc...    
      bRejecword[i]=M1                    '    rejected data
      Rejecword[i]=shapes.AddText(M1)     '   rejected shape
      Shapes.Animate(Rejecword[i],150+15*(i-1),75,500)
      M1=""   
    EndIf
    Rejec_Sample=rejec_Sample+M1     '    Rejected residue
  Endfor
  
  GraphicsWindow.BrushColor="Navy"
  GraphicsWindow.DrawText(10,125,"Residue")
  Get_Len2=Text.GetLength(rejec_Sample) 
  For i=1 To Get_Len2
    Mword2=text.GetSubText(rejec_Sample,i,1)
    b2[i]=Mword2                 '    Residue data
    a2[i]=shapes.AddText(Mword2) ' Residue   shape
    Shapes.Animate(a2[i],150+15*(i-1),125,500)  'animate
  EndFor
  
EndSub

Sub Reverse_Check 
  GraphicsWindow.DrawText(10,200,"Reversed")
  For i = Get_Len2 To 1 Step -1
    Shapes.Animate(a2[i],150+15*(Get_Len2-i),200,1000)  '  Reberse  data 
    b3[Get_Len2-i+1]=b2[i]     '  Reverse  data 
    a3[Get_Len2-i+1]=a2[i]     '  Reverse  shape
    Program.Delay(500)  
  Endfor
EndSub  


Sub merged
  GraphicsWindow.DrawText(10,300,"Merged") 
  For i=1 To Get_Len
    If bRejecword[i]="" Then 
      Shapes.Animate(a3[i-n],150+15*(i-1),300,1000)
      b4[i]=b3[i-n]
      Program.Delay(500) 
    Else
      Shapes.Animate(rejecword[i],150+15*(i-1),300,1000)
      b4[i]=bRejecword[i]
      n=n+1
    EndIf
  EndFor  
EndSub  

Sub Ok_or_Not
  GraphicsWindow.DrawText(10,320,"O or X")
  For i= 1 To Get_Len
    If b[i]=b4[i] Then 
      GraphicsWindow.DrawText(150+15*(i-1),320,"O")
      nn=nn+1
    Else
      GraphicsWindow.DrawText(150+15*(i-1),320,"X")
    Endif   
  Endfor 
  
  GraphicsWindow.FontSize=30
  If  nn= Get_Len Then 
    Cong=shapes.AddText("Congratulation! Palindrome!")
    Shapes.Animate(Cong,350,400,500) 
  Else
    Unf= shapes.AddText("Boo....  not Palindrome!") 
    Shapes.Animate(Unf,350,400,500) 
  Endif  
EndSub

Sub Palindrome_Sample
  '    These samples are listed on  [ http://www.fun-with-words.com/palin_example.html] .    
  '    From Sample [1]  to   Sample [15] 
  
  Sample[1]="Don't nod"
  Sample[2]="Dogma: I am God"
  Sample[3]="Never odd or even"
  Sample[4]="Too bad ? I hid a boot"
  Sample[5]="Rats live on no evil star"
  Sample[6]="No trace; not one carton"
  Sample[7]="Was it Eliot's toilet I saw?"
  Sample[8]="Murder for a jar of red rum"
  Sample[9]="May a moody baby doom a yam?"
  Sample[10]="Go hang a salami; I'm a lasagna hog!"
  Sample[11]="Satan, oscillate my metallic sonatas!"
  Sample[12]="A Toyota! Race fast... safe car: a Toyota"
  Sample[13]="Straw? No, too stupid a fad; I put soot on warts"
  Sample[14]="Are we not drawn onward, we few, drawn onward to new era?"
  Sample[15]="Doc Note: I dissent. A fast never prevents a fatness. I diet on cod"
  '  Sample[16]  is not  palindrome.  
  Sample[16]="Sonata konata donata anata okata"
  
  Sample[17]="Please input your palindrome in the box and click check button! "  '  Attention
EndSub

