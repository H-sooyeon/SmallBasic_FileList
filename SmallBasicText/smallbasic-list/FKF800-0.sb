GraphicsWindow.Title = "Orange Trees with Fruits
gw = 1400
gh = 500
GraphicsWindow.Left =5
GraphicsWindow.Top =5
args=0
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
' sky
GraphicsWindow.BackgroundColor = "SkyBlue"
' ground
GraphicsWindow.BrushColor = "#221100"
GraphicsWindow.FillRectangle(0, gh - 40, gw, 40)

Sub mtree
  bc = "#003300
  n = 220
  DrawLeaves()
  bc = "DarkGreen
  n = 160
  DrawLeaves()
  DrawTree()
  bc = "Green
  n = 130
  DrawLeaves()
  n = 72
  DrawFruits()
EndSub 

For rr=1 to 5
  mtree ()
  If rr<5 then
    sh=LDShapes.GetAllShapes ()
    For x=1 to Array.GetItemCount (sh)
      Shapes.Move (sh[x] Shapes.GetLeft(sh[x])+300 Shapes.GetTop  (sh[x]))
      
    endfor
  endif
endfor 

GraphicsWindow.MouseDown =mdd
GraphicsWindow.MouseMove=mdd
s1=1
LDEvents.MouseWheel =mww

Sub mww
  s1=s1+ LDEvents.LastMouseWheelDelta/10 
  ldGraphicsWindow.Reposition (s1 s1 px-900 py-250 0)
EndSub 

Sub mdd
  If Mouse.IsLeftButtonDown then
    px=LDGraphicsWindow.MouseXScale*GraphicsWindow.MouseX+LDGraphicsWindow.MouseXOffset  
    py=LDGraphicsWindow.MouseyScale*GraphicsWindow.Mousey+LDGraphicsWindow.MouseyOffset 
    ldGraphicsWindow.Reposition (s1 s1 px-600 py-350 0)
  endif
EndSub 

Sub DrawBranch
  If 0 < level Then
    ldcall.Function("t_Move",len)
    t_Angle =t_Angle -a
    level = level - 1
    len = len * 0.8
    pw = pw * 0.8
    GraphicsWindow.PenWidth = pw
    DrawBranch()
    t_Angle =t_Angle + 2 * a
    DrawBranch()
    t_Angle =t_Angle-a
    pw = pw / 0.8
    GraphicsWindow.PenWidth = pw
    len = len / 0.8
    level = level + 1
    t_Angle =t_Angle +180
    ldcall.Function("t_Move",len)
    t_Angle =t_Angle +180
  EndIf
EndSub

Sub DrawFruits
  GraphicsWindow.BrushColor = "Orange"
  GraphicsWindow.PenWidth = 0
  xo = gw * 0.5
  yo = gh * 0.36
  lx = 220
  ly = 120
  For i = 1 To n
    a = Math.GetRandomNumber(360)
    _a = Math.GetRadians(a)
    rx = Math.GetRandomNumber(lx)
    ry = Math.GetRandomNumber(ly)
    x = xo + rx * Math.Cos(_a)
    y = yo + ry * Math.Sin(_a)
    fruit = Shapes.AddEllipse(10, 10)
    Shapes.Move(fruit, x - 5, y - 5)
  EndFor
EndSub

Sub DrawLeaves
  GraphicsWindow.BrushColor = bc
  GraphicsWindow.PenWidth = 0
  xo = gw * 0.5
  yo = gh * 0.36
  lx = 220
  ly = 120
  For i = 1 To n
    a = Math.GetRandomNumber(360)
    _a = Math.GetRadians(a)
    rx = Math.GetRandomNumber(lx)
    ry = Math.GetRandomNumber(ly)
    x = xo + rx * Math.Cos(_a)
    y = yo + ry * Math.Sin(_a)
    leaf = Shapes.AddEllipse(20, 10)
    Shapes.Move(leaf, x - 10, y - 5)
    Shapes.Rotate(leaf, a)
  EndFor
EndSub

Sub DrawTree
  pc = "#442200
  pw = 20
  GraphicsWindow.PenColor = pc
  GraphicsWindow.PenWidth = pw
  level = 5
  len = 100
  a = 30
  t_x= gw / 2
  t_y = gh - 40
  
  DrawBranch()
  
EndSub

sub t_Move
  ds=args[1]
  mm=LDMath.Convert2Cartesian  (t_x,t_y,ds, t_Angle-90 )
  ox=t_x
  oy=t_y
  
  t_x= (mm[1])
  t_y= (mm[2])
  Shapes.AddLine (ox oy t_x t_y)
EndSub
  
