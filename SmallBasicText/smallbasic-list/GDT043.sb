'======================================================
' Program: AceyDucey                                                                                 Version 1.2
' Author: Midlandman, (after Bill Palmby, Illinois - page 2 of David Ahl's book)
' Purpose:  A simulation of the card betting game                    Date: 17.02.2013
' --------------------------------------------------------------------------------------------
' Actions: 1) Set up the deck of cards  and  display the instructions
'                 2) For each round:
'                 3)    show the two initial cards
'                 4)    invite a bet (validated to be greater than zero and not greater than the pot)
'                 5)    show the all-important third card
'                 6)    if the player has won increase his pot, otherwise decrease it
'                 7)    if there is any money left in the pot, play another round
'                 8) Show a final message if the player quits
' ============== START OF PROGRAM =================
init()
Instructions()
Prepare()
While (again="Y")
  ShowNextRound()
  TakeBet()
  If (pot = 0) Then
    AskPlayAgain()
  EndIf
Endwhile
' ----------------------------------------------------------------------------------
Sub ShowNextRound
  A=Math.GetRandomNumber(12)+1
  B=Math.GetRandomNumber(14-A)+A  ' This ensures that the second card is not lower than the first
  TextWindow.WriteLine("")
  TextWindow.ForegroundColor="Yellow"
  TextWindow.WriteLine("You now have " + pot + " dollars.")
  TextWindow.ForegroundColor="White"
  TextWindow.WriteLine("Here are your next two cards: " + card[A] + " ... " + card[B])
EndSub

Sub TakeBet
  GetInput="True"
  While (GetInput)
    TextWindow.Write("What is your bet? ")
    bet=TextWindow.ReadNumber()
    If (bet > pot) Then
      TextWindow.WriteLine("Sorry, my friend but you bet too much.")
      TextWindow.Write("You have only ")
      TextWindow.ForegroundColor = "Yellow"
      TextWindow.Write(pot)
      TextWindow.ForegroundColor = "White"
      TextWindow.WriteLine(" dollars to bet.")
    ElseIf (bet = 0) Then
      GetInput="False"
      TextWindow.ForegroundColor = "Magenta"
      TextWindow.WriteLine("Chicken!")
      TextWindow.ForegroundColor = "White"
    elseif (bet < 0) then
      TextWindow.WriteLine("You cannot ask the dealer for a loan!")
    Else
      GetInput="False"
      ThirdCard()   
    EndIf
  EndWhile
EndSub

Sub ThirdCard
  C = Math.GetRandomNumber(13)+1
  TextWindow.Write("And the third card is ... ... ... " + card[C])
  TextWindow.CursorLeft=40  ' place the result in a fixed place to the right
  TextWindow.Write("<--   ")
  If (C > A And C < B) Then
    TextWindow.ForegroundColor="Green"
    TextWindow.WriteLine("You Win!")
    pot=pot+bet
  Else
    TextWindow.ForegroundColor="Red"
    TextWindow.WriteLine("You Lose")
    pot=pot-bet
  EndIf
  TextWindow.ForegroundColor="White"
EndSub

Sub AskPlayAgain
  If (pot = 0) Then
    TextWindow.WriteLine("")
    TextWindow.WriteLine("Sorry, my friend but you blew your wad!")
  EndIf
  again=""
  While (again <> "Y" And again <> "N")
    TextWindow.Write("Try again? (Yes/No) ")
    again=text.ConvertToUpperCase(Text.GetSubText(TextWindow.Read(),1,1))
  EndWhile
  If (again="N") Then
    TextWindow.WriteLine("OK Hope you had fun.")
    TextWindow.WriteLine("")
  Else
    TextWindow.WriteLine("")
    Prepare()
  EndIf
EndSub

Sub Prepare
  pot=100
  again="Y"
EndSub

Sub init  ' note that card[1] is empty, because "Ace" is high.
  card[2]  = "Two"
  card[3]  = "Three"
  card[4]  = "Four"
  card[5]  = "Five"
  card[6]  = "Six"
  card[7]  = "Seven"
  card[8]  = "Eight"
  card[9]  = "Nine"
  card[10] = "Ten"
  card[11] = "Jack"
  card[12] = "Queen"
  card[13] = "King"
  card[14] = "Ace"
EndSub

Sub Instructions
  TextWindow.ForegroundColor = "Cyan"
  TextWindow.WriteLine("Acey-Ducey is played in the following manner:")
  TextWindow.WriteLine("The dealer, (computer), deals two cards face up")
  TextWindow.WriteLine("Then the game pauses before a third card is dealt.")
  TextWindow.WriteLine("You have an option: to bet or not to bet depending")
  TextWindow.WriteLine("on whether or not you feel that card will have a")
  TextWindow.WriteLine("value between the other two.")
  TextWindow.WriteLine("If you do not want to bet, input a 0.")
  TextWindow.WriteLine("")
  TextWindow.ForegroundColor = "White"
EndSub
' ================ END OF PROGRAM =================
