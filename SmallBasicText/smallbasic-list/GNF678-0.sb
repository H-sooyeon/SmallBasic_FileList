' Moon

' Version 0.2

' Copyright Â© 2019 Nonki Takahashi.  The MIT License.

' 2019-01-05 9:03:11 PM Shapes generated by Shapes 2.3b.

' Program ID GNF678-0



GraphicsWindow.Title = "Moon"

SB_Workaround()

gw = 598

gh = 428

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

size = 260

GraphicsWindow.BackgroundColor = "Black"

' initialize shapes

Shapes_Init()

' add shapes

scale = 0.6

angle = 0

iMin = 1

iMax = Array.GetItemCount(shape)

Shapes_Add()

DrawStars()

mask = "left=2;right=3;dark=4;light=5;h1=17;h2=18;h3=19;h4=20;"

While "True"

  Program.Delay(500)

  angle = angle - 12

  If angle < 0 Then

    angle = angle + 360

  EndIf

  RotateMoon()

EndWhile



Sub DrawStars

  ox = shX + 139 * scale

  oy = shY + 130 * scale

  r = 130 * scale

  For i = 1 To 500

    x = Math.GetRandomNumber(gw)

    y = Math.GetRandomNumber(gh)

    s = Math.GetRandomNumber(4)

    l = Text.GetSubText("ABCDEF", Math.GetRandomNumber(6), 1)

    GraphicsWindow.PenWidth = 0

    c = "#" + l + l + l + l + l + l

    GraphicsWindow.PenColor = c

    GraphicsWindow.BrushColor = c

    dx = x - ox

    dy = y - oy

    If r + s / 2 < Math.SquareRoot(dx * dx + dy * dy) Then

      star = Shapes.AddEllipse(s, s)

      Shapes.Move(star, x - s / 2, y - s / 2)

    EndIf

  EndFor

EndSub



Sub RotateMoon

  i = mask["left"]

  shp = shape[i]

  If (0 < angle) And (angle <= 180) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  i = mask["right"]

  shp = shape[i]

  If (180 <= angle) And (angle < 360) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  _angle = Math.GetRadians(angle)

  i = mask["light"]

  shp = shape[i]

  k = Math.Abs(Math.Cos(_angle))

  If (0 < angle And angle < 90) Or (270 < angle And angle < 360) Then

    If k < 0.1 Then

      Shapes.HideShape(shp["obj"])

    Else

      Shapes.ShowShape(shp["obj"])

      Shapes.Zoom(shp["obj"], k, 1)

    EndIf

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  i = mask["dark"]

  shp = shape[i]

  k = Math.Abs(Math.Cos(_angle))

  If 90 < angle And angle < 270 Then

    If k < 0.1 Then

      Shapes.HideShape(shp["obj"])

    Else

      Shapes.ShowShape(shp["obj"])

      Shapes.Zoom(shp["obj"], k, 1)

    EndIf

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  ' hills

  i = mask["h1"]

  shp = shape[i]

  If (228 <= angle) Or (angle <= 36) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  i = mask["h2"]

  shp = shape[i]

  If (240 <= angle) Or (angle <= 48) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  i = mask["h3"]

  shp = shape[i]

  If (264 <= angle) Or (angle <= 72) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

  i = mask["h4"]

  shp = shape[i]

  If (288 <= angle) Or (angle <= 96) Then

    Shapes.ShowShape(shp["obj"])

  Else

    Shapes.HideShape(shp["obj"])

  EndIf

EndSub



Sub Shapes_Init

  ' Shapes | Initialize shapes data

  ' return shX, shY - current position of shapes

  ' return shape - array of shapes

  shX = 61  ' x offset

  shY = 70  ' y offset

  shape = ""

  shape[1] = "func=ell;x=9;y=0;width=260;height=260;bc=#FFFFCC;pw=0;"

  shape[2] = "func=rect;x=9;y=0;width=130;height=260;bc=#000000;pw=0;"

  shape[3] = "func=rect;x=139;y=0;width=130;height=260;bc=#000000;pw=0;"

  shape[4] = "func=ell;x=9;y=0;width=260;height=260;bc=#000000;pw=0;"

  shape[5] = "func=ell;x=9;y=0;width=260;height=260;bc=#FFFFCC;pw=0;"

  shape[6] = "func=ell;x=216.32;y=58.47;width=35;height=21;angle=57;bc=#08000000;pw=0;"

  shape[7] = "func=ell;x=220;y=102;width=38;height=26;angle=70;bc=#08000000;pw=0;"

  shape[8] = "func=ell;x=179;y=80;width=46;height=42;angle=58;bc=#08000000;pw=0;"

  shape[9] = "func=ell;x=205.59;y=134.03;width=21;height=16;angle=57;bc=#08000000;pw=0;"

  shape[10] = "func=ell;x=142;y=46;width=53;height=39;angle=385;bc=#08000000;pw=0;"

  shape[11] = "func=ell;x=56;y=37;width=89;height=75;angle=327;bc=#08000000;pw=0;"

  shape[12] = "func=ell;x=9.38;y=124.31;width=82;height=49;angle=57;bc=#08000000;pw=0;"

  shape[13] = "func=ell;x=84.37;y=135.98;width=66;height=48;angle=71;bc=#08000000;pw=0;"

  shape[14] = "func=ell;x=73.43;y=173.2;width=34;height=26;angle=392;bc=#08000000;pw=0;"

  shape[15] = "func=ell;x=131;y=90;width=41;height=51;angle=46;bc=#08000000;pw=0;"

  shape[16] = "func=ell;x=0;y=77;width=77;height=43;angle=287;bc=#08000000;pw=0;"

  shape[17] = "func=ell;x=37.49;y=107.24;width=12;height=10;angle=60;bc=#FFFFCC;pw=0;"

  shape[18] = "func=ell;x=55.79;y=138.08;width=17;height=11;angle=65;bc=#FFFFCC;pw=0;"

  shape[19] = "func=ell;x=90;y=135;width=43;height=13;angle=58;bc=#FFFFCC;pw=0;"

  shape[20] = "func=ell;x=155;y=109;width=22;height=21;angle=357;bc=#FFFFCC;pw=0;"

EndSub



Sub Math_CartesianToPolar

  ' Math | convert cartesian coodinate to polar coordinate

  ' param x, y - cartesian coordinate

  ' return r, a - polar coordinate

  r = Math.SquareRoot(x * x + y * y)

  If x = 0 And y > 0 Then

    a = 90 ' [degree]

  ElseIf x = 0 And y < 0 Then

    a = -90

  ElseIf x = 0 Then

    a = 0

  Else

    a = Math.ArcTan(y / x) * 180 / Math.Pi

  EndIf

  If x < 0 Then

    a = a + 180

  ElseIf x > 0 And y < 0 Then

    a = a + 360

  EndIf

EndSub



Sub SB_RotateWorkaround

  ' Small Basic | Rotate workaround for Silverlight

  ' param shp - current shape

  ' param x, y - original coordinate

  ' param alpha - angle [radian]

  ' returns x, y - workaround coordinate

  If shp["func"] = "tri" Then

    x1 = -Math.Floor(shp["x3"] / 2)

    y1 = -Math.Floor(shp["y3"] / 2)

  ElseIf shp["func"] = "line" Then

    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)

    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)

  EndIf

  ox = x - x1

  oy = y - y1

  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox

  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy

EndSub



Sub SB_Workaround

  ' Small Basic | Workaround for Silverlight

  ' returns silverlight - "True" if in remote

  color = GraphicsWindow.GetPixel(0, 0)

  If Text.GetLength(color) > 7 Then

    silverlight = "True"

    msWait = 300

  Else

    silverlight = "False"

  EndIf

EndSub



Sub Shapes_Add

  ' Shapes | add shapes as shapes data

  ' param iMin, iMax - shape indices to add

  ' param shape - array of shapes

  ' param scale - 1 if same scale

  ' return shWidth, shHeight - total size of shapes

  ' return shAngle - current angle of shapes

  Stack.PushValue("local", i)

  Stack.PushValue("local", x)

  Stack.PushValue("local", y)

  Shapes_CalcWidthAndHeight()

  s = scale

  For i = iMin To iMax

    shp = shape[i]

    GraphicsWindow.PenWidth = shp["pw"] * s

    If shp["pw"] > 0 Then

      GraphicsWindow.PenColor = shp["pc"]

    EndIf

    If Text.IsSubText("rect|ell|tri|text", shp["func"]) Then

      GraphicsWindow.BrushColor = shp["bc"]

    EndIf

    If shp["func"] = "rect" Then

      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)

    ElseIf shp["func"] = "ell" Then

      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)

    ElseIf shp["func"] = "tri" Then

      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)

    ElseIf shp["func"] = "line" Then

      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)

    ElseIf shp["func"] = "text" Then

      If silverlight Then

        fs = Math.Floor(shp["fs"] * 0.9)

      Else

        fs = shp["fs"]

      EndIf

      GraphicsWindow.FontSize = fs * s

      GraphicsWindow.FontName = shp["fn"]

      shp["obj"] = Shapes.AddText(shp["text"])

    EndIf

    x = shp["x"]

    y = shp["y"]

    shp["rx"] = x

    shp["ry"] = y

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      alpha = Math.GetRadians(shp["angle"])

      SB_RotateWorkaround()

      shp["wx"] = x

      shp["wy"] = y

    EndIf

    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)

    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then

      Shapes.Rotate(shp["obj"], shp["angle"])

    EndIf

    shape[i] = shp

  EndFor

  shAngle = 0

  y = Stack.PopValue("local")

  x = Stack.PopValue("local")

  i = Stack.PopValue("local")

EndSub



Sub Shapes_CalcRotatePos

  ' Shapes | Calculate position for rotated shape

  ' param["x"], param["y"] - position of a shape

  ' param["width"], param["height"] - size of a shape

  ' param ["cx"], param["cy"] - center of rotation

  ' param ["angle"] - rotate angle

  ' return x, y - rotated position of a shape

  _cx = param["x"] + param["width"] / 2

  _cy = param["y"] + param["height"] / 2

  x = _cx - param["cx"]

  y = _cy - param["cy"]

  Math_CartesianToPolar()

  a = a + param["angle"]

  x = r * Math.Cos(a * Math.Pi / 180)

  y = r * Math.Sin(a * Math.Pi / 180)

  _cx = x + param["cx"]

  _cy = y + param["cy"]

  x = _cx - param["width"] / 2

  y = _cy - param["height"] / 2

EndSub



Sub Shapes_CalcWidthAndHeight

  ' Shapes | Calculate total width and height of shapes

  ' param iMin, iMax - shape indices to add

  ' return shWidth, shHeight - total size of shapes

  For i = iMin To iMax

    shp = shape[i]

    If shp["func"] = "tri" Or shp["func"] = "line" Then

      xmin = shp["x1"]

      xmax = shp["x1"]

      ymin = shp["y1"]

      ymax = shp["y1"]

      If shp["x2"] < xmin Then

        xmin = shp["x2"]

      EndIf

      If xmax < shp["x2"] Then

        xmax = shp["x2"]

      EndIf

      If shp["y2"] < ymin Then

        ymin = shp["y2"]

      EndIf

      If ymax < shp["y2"] Then

        ymax = shp["y2"]

      EndIf

      If shp["func"] = "tri" Then

        If shp["x3"] < xmin Then

          xmin = shp["x3"]

        EndIf

        If xmax < shp["x3"] Then

          xmax = shp["x3"]

        EndIf

        If shp["y3"] < ymin Then

          ymin = shp["y3"]

        EndIf

        If ymax < shp["y3"] Then

          ymax = shp["y3"]

        EndIf

      EndIf

      shp["width"] = xmax - xmin

      shp["height"] = ymax - ymin

    EndIf

    If i = 1 Then

      shWidth = shp["x"] + shp["width"]

      shHeight = shp["y"] + shp["height"]

    Else

      If shWidth < shp["x"] + shp["width"] Then

        shWidth = shp["x"] + shp["width"]

      EndIf

      If shHeight < shp["y"] + shp["height"] Then

        shHeight = shp["y"] + shp["height"]

      EndIf

    EndIf

    shape[i] = shp

  EndFor

EndSub



Sub Shapes_Move

  ' Shapes | Move shapes

  ' param iMin, iMax - shape indices to add

  ' param shape - array of shapes

  ' param scale - to zoom

  ' param x, y - position to move

  ' return shX, shY - new position of shapes

  Stack.PushValue("local", i)

  s = scale

  shX = x

  shY = y

  For i = iMin To iMax

    shp = shape[i]

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      _x = shp["wx"]

      _y = shp["wy"]

    Else

      _x = shp["rx"]

      _y = shp["ry"]

    EndIf

    Shapes.Move(shp["obj"], shX + _x * s, shY + _y * s)

  EndFor

  i = Stack.PopValue("local")

EndSub



Sub Shapes_Remove

  ' Shapes | Remove shapes

  ' param iMin, iMax - shapes indices to remove

  ' param shape - array of shapes

  Stack.PushValue("local", i)

  For i = iMin To iMax

    shp = shape[i]

    Shapes.Remove(shp["obj"])

  EndFor

  i = Stack.PopValue("local")

EndSub



Sub Shapes_Rotate

  ' Shapes | Rotate shapes

  ' param iMin, iMax - shapes indices to rotate

  ' param shape - array of shapes

  ' param cx, cy - rotation center

  ' param scale - to zoom

  ' param angle - to rotate

  Stack.PushValue("local", i)

  Stack.PushValue("local", x)

  Stack.PushValue("local", y)

  s = scale

  param["angle"] = angle

  If cx <> "" Then

    param["cx"] = cx

  Else

    cx = ""   ' to avoid syntax error

    param["cx"] = shWidth / 2

  EndIf

  If cy <> "" Then

    param["cy"] = cy

  Else

    cy = ""   ' to avoid syntax error

    param["cy"] = shHeight / 2

  EndIf

  For i = iMin To iMax

    shp = shape[i]

    param["x"] = shp["x"]

    param["y"] = shp["y"]

    param["width"] = shp["width"]

    param["height"] = shp["height"]

    Shapes_CalcRotatePos()

    shp["rx"] = x

    shp["ry"] = y

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      alpha = Math.GetRadians(angle + shp["angle"])

      SB_RotateWorkAround()

      shp["wx"] = x

      shp["wy"] = y

    EndIf

    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)

    Shapes.Rotate(shp["obj"], angle + shp["angle"])

    shape[i] = shp

  EndFor

  y = Stack.PopValue("local")

  x = Stack.PopValue("local")

  i = Stack.PopValue("local")

EndSub


