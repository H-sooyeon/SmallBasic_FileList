GraphicsWindow.BackgroundColor = "LightBlue"

'Main infinite loop just reading and outputting text input
y = 50
While ("True")
  GetInput()
  GraphicsWindow.DrawText(20,y,input)
  If (input*1 = input) Then
    GraphicsWindow.DrawText(100,y,"This is a number")
  Else
    GraphicsWindow.DrawText(100,y,"This is text")    
  EndIf
  y = y+20
EndWhile

'Get Input from GraphicsWindow
Sub GetInput
  keymode = 1 ' Flag to show key event we are doing text input
  input = "" 'String of data input
  finished = 0 ' Flag when Return is pressed (we return then)
  case = 0 'Case set by Shift 0 is lower, 1 is upper
  
  'Draw input box
  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor
  GraphicsWindow.FillRectangle(10,10,69,20)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(10,12,"Input here")
  GraphicsWindow.PenColor = "Black"
  GraphicsWindow.DrawRectangle(80,10,200,20)
  'Update text in display box - blank here
  TextBox()
  'Start key up and down events
  GraphicsWindow.KeyDown = OnKeyDown
  GraphicsWindow.KeyUp = OnKeyUp
  
  While (finished = 0) ' Return not pressed yet
    If (keypressed = 1) Then ' A key has been pressed
      keypressed = 0 ' Set this flag back to wait for the next key press
      'Check they key and handle Return, Space, Backspace and numbers as well as letters
      If (key = "Return") Then
        finished = 1
      ElseIf (key = "Back") Then
        input = Text.GetSubText(input,1,Text.GetLength(input)-1)
      ElseIf (key = "Space") Then
        input = Text.Append(input," ")
      ElseIf (key = "OemComma") Then
        input = Text.Append(input,",")
      ElseIf (key = "OemPeriod") Then
        input = Text.Append(input,".")
      ElseIf (key = "OemMinus") Then
        input = Text.Append(input,"-")
      ElseIf (Text.GetLength(key) = 1) Then
        If (case = 0) Then
          input = Text.Append(input,Text.ConvertToLowerCase(key))
        Else
          input = Text.Append(input,Text.ConvertToUpperCase(key))
        EndIf
      ElseIf (Text.GetLength(key) = 2 And Text.GetSubText(key,1,1) = "D") Then
        input = Text.Append(input,Text.GetSubText(key,2,1))
      Else
'        input = Text.Append(input,key) 'For debugging other keys
      EndIf
      TextBox() ' Update text box display
    EndIf
  EndWhile
  keymode = 0 'Turn off event key input mode
EndSub

'Update the text box
Sub TextBox
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.FillRectangle(81,11,198,18)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(82,12,input)
EndSub

'Set upper case if Shift pressed - also return key pressed and flag that a key was pressed
Sub OnKeyDown
  If (keymode = 1) Then
    key = GraphicsWindow.LastKey
    If (Text.IsSubText(key,"Shift") = "True") Then
      case = 1
    EndIf
    keypressed = 1
  EndIf
EndSub

'Only used to show Shift was return to lower case
Sub OnKeyUp
  If (keymode = 1) Then
    key = GraphicsWindow.LastKey
    If (Text.IsSubText(key,"Shift") = "True") Then
      case = 0
    EndIf
  EndIf
EndSub

