'Written by Thaelmann-Pioniere

init()

writename()

drawCarbonnatom()

For i = 1 To 3

  Turtle.Angle=-45-90*(i-1)

  Turtle.PenDown()

  drawC_Hbond()

  drawHydrogenatom()

  Turtle.Turn(180)

  Turtle.PenUp()

  drawC_Obond()

EndFor

Turtle.Angle=45

For i = 1 To 16

  Turtle.PenDown()

  drawC_Cbond()

  drawCarbonnatom()

  drawC_Hbond()

  drawHydrogenatom()

  Turtle.Turn(180)

  Turtle.PenUp()

  drawC_Hbond()

  If Math.Remainder(i,2)=1 Then

    Turtle.TurnRight()

  Else

    Turtle.TurnLeft()

  EndIf

  Turtle.PenDown()

  drawC_Hbond()

  drawHydrogenatom()

  Turtle.Turn(180)

  Turtle.PenUp()

  drawC_Hbond()

EndFor

Turtle.PenDown()

drawC_Cbond()

drawCarbonnatom()

Turtle.Angle=0

drawC_Odoublebond()

GraphicsWindow.BrushColor="Red"

GraphicsWindow.FillEllipse(x-r,y-r,d,d)

Turtle.PenDown()

Turtle.Angle=120

drawC_Obond()

drawOxygenatom()

Turtle.PenUp()

Turtle.Angle=60

drawH_Obond()

drawHydrogenatom()

Turtle.Turn(180)

Turtle.PenDown()

drawH_Obond()

Turtle.Hide()



Sub init

  r=10

  d=r*2

  GraphicsWindow.Title="Molecular Challenge-Stearic acid(CH3(CH2)16COOH)"

  GraphicsWindow.Height=500

  GraphicsWindow.Width=1000

  GraphicsWindow.BackgroundColor="#dabc72"

  GraphicsWindow.PenWidth=5

  Turtle.Speed=10

  Turtle.Angle=-45

  Turtle.PenUp()

  Turtle.MoveTo(100,200)

EndSub

Sub writename

  GraphicsWindow.FontBold="false"

  GraphicsWindow.FontName="Times New Roman"

  GraphicsWindow.BrushColor="White"

  GraphicsWindow.FontSize=48

  GraphicsWindow.DrawText(500,20,"CH")

  GraphicsWindow.FontSize=24

  GraphicsWindow.DrawText(570,50,"3")

  GraphicsWindow.FontSize=48

  GraphicsWindow.DrawText(590,20,"(CH")

  GraphicsWindow.FontSize=24

  GraphicsWindow.DrawText(680,50,"2")

  GraphicsWindow.FontSize=48

  GraphicsWindow.DrawText(690,20,")")

  GraphicsWindow.FontSize=24

  GraphicsWindow.DrawText(710,50,"16")

  GraphicsWindow.FontSize=48

  GraphicsWindow.DrawText(740,20,"COOH")

EndSub

Sub drawHydrogenatom

  GraphicsWindow.BrushColor="White"

  GraphicsWindow.FillEllipse(Turtle.X-r,Turtle.Y-r,d,d)

EndSub

Sub drawCarbonnatom

  GraphicsWindow.BrushColor="Black"

  GraphicsWindow.FillEllipse(Turtle.X-r,Turtle.Y-r,d,d)

EndSub

Sub drawOxygenatom

  GraphicsWindow.BrushColor="Red"

  GraphicsWindow.FillEllipse(Turtle.X-r,Turtle.Y-r,d,d)

EndSub



Sub drawC_Obond

  GraphicsWindow.PenColor="Black"

  Turtle.Move(r+20)

  GraphicsWindow.PenColor="Red"

  Turtle.Move(r+20)

EndSub

Sub drawC_Hbond

  GraphicsWindow.PenColor="Black"

  Turtle.Move(r+20)

  GraphicsWindow.PenColor="White"

  Turtle.Move(r+20)

EndSub

Sub drawH_Obond

  GraphicsWindow.PenColor="White"

  Turtle.Move(r+20)

  GraphicsWindow.PenColor="Red"

  Turtle.Move(r+20)

EndSub

Sub drawC_Cbond

  GraphicsWindow.PenColor="Black"

  Turtle.Move(r+20+r)

EndSub 

Sub drawC_Odoublebond

  Turtle.PenUp()

  Turtle.TurnLeft()

  Turtle.Move(4)

  Turtle.TurnRight()

  Turtle.PenDown()

  GraphicsWindow.PenColor="Black"

  Turtle.Move(r+20)

  GraphicsWindow.PenColor="Red"

  Turtle.Move(20+r)

  Turtle.TurnRight()

  Turtle.PenUp()

  Turtle.Move(4)

  x=Turtle.X

  y=Turtle.Y

  Turtle.Move(4)

  Turtle.TurnRight()

  Turtle.PenDown()

  GraphicsWindow.PenColor="Red"

  Turtle.Move(r+20)

  GraphicsWindow.PenColor="Black"

  Turtle.Move(20+r)

  Turtle.PenUp()

  Turtle.TurnRight()

  Turtle.Move(4)

EndSub


