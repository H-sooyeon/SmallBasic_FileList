name= "Bob"

colour= "cyan"

GraphicsWindow.Show()

GraphicsWindow.Height= 650

GraphicsWindow.Width= 1000

GraphicsWindow.Left= 5

GraphicsWindow.Top= 5

GraphicsWindow.BackgroundColor= "black"

GraphicsWindow.BrushColor= colour

GraphicsWindow.PenColor= "white"

GraphicsWindow.KeyDown= MoveCycle  

level=1

init()



While "true"

  moving()

  Program.Delay(50)

  WinGame()

  If crashed="true" Then 

    Shapes.Remove(player)

    Program.Delay(500)

    init() 

  EndIf

endwhile



Sub init

  GraphicsWindow.Clear()

  delXY="X=1;Y=0"                    ' X=1 to avoid X=0 crashed

  player= Shapes.AddEllipse(15,40)

  Shapes.Rotate(player,90)           '  move right direction

  crashed="false"

  AddObstacles()

  t0=clock.ElapsedMilliseconds 

endsub



Sub AddObstacles

  GraphicsWindow.BrushColor="Yellow"

  winx= Math.GetRandomNumber(980)

  winy= Math.GetRandomNumber(630)

  egg= Shapes.AddEllipse(20,30)

  Shapes.Move(egg,winx,winy)

  For i= 1 To level

    retry:

    GraphicsWindow.BrushColor=GraphicsWindow.GetRandomColor()

    locationx= Math.GetRandomNumber(900)

    locationy= Math.GetRandomNumber(550)

    recsize= 30+Math.GetRandomNumber(70)

    If (locationx<40 and locationy<15) Or (math.Abs(locationx-winx)<recsize And math.Abs(locationy-winy)<recsize) then 

      Goto retry

    EndIf

    GraphicsWindow.FillRectangle (locationx, locationy,recsize ,recsize)

  EndFor

EndSub



Sub moving

  X = shapes.GetLeft(player)+delXY["X"]

  Y = shapes.GetTop(player)+delXY["Y"]

  If x<0 then              

    x=-1

  elseif x>1001 then 

    X=1001

  elseif Y<0 then 

    Y=-1 

  elseif y>650 then 

    Y=651

  endif                     

  Shapes.Move(player,x,y)

  GraphicsWindow.Title= "X=  "+X+  " : Y=  "+Y+ "Ellapsed time= "+Math.Floor((Clock.ElapsedMilliseconds-t0)/100)/10+" Sec"

  If  (GraphicsWindow.GetPixel(x+40/2+delXY["X"],y+40/2+delXY["Y"]) <> "#000000") Then

    GraphicsWindow.ShowMessage("1  You have crashed, and are banished from Kyragetron!", name+", YOU ARE BANISHED!")

    crashed="true"

  ElseIf (x>1000 or x<0) then

    GraphicsWindow.ShowMessage("2  You have crashed, and are banished from Kyragetron!", name+", YOU ARE BANISHED!")

    crashed="true"

  ElseIf (y>650 or y<0) then

    GraphicsWindow.ShowMessage("3  You have crashed, and are banished from Kyragetron!", name+", YOU ARE BANISHED!")

    crashed="true"

  EndIf

  GraphicsWindow.BrushColor= colour

  GraphicsWindow.FillRectangle(x +15/4-1, y + 40/3+2, 12, 12)

EndSub



Sub MoveCycle

  key= GraphicsWindow.LastKey

  If (key= "Right") Then    

    delXY="X=12;Y=0;rot=1"

  ElseIf (key= "Left") Then    

    delXY="X=-12;Y=0;rot=1"

  ElseIf (key= "Down") Then 

    delXY="X=0;Y=12;rot=0"

  ElseIf (key= "Up") Then  

    delXY="X=0;Y=-12;rot=0"

  EndIf

  Shapes.Rotate(player,delxy["rot"]*90)

EndSub



Sub WinGame

  If (math.Abs(x-winx)<20 and math.Abs(y-winy)<30) Then

    GraphicsWindow.ShowMessage("Congrats! You have completed Level "+level+", and live to see another day. Good luck on the next level!", "Good Job")

    level= level +1

    init()

  EndIf

EndSub


