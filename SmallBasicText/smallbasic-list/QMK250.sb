'  Digital  &  Analog  stop watch   by NaochanON

init()

analogview()

Controls.ButtonClicked=Secondhand





Sub Secondhand

  nm= Controls.GetButtonCaption(Controls.LastClickedButton)

  If nm="Start" Then 

    TF="True"

    Controls.SetButtonCaption(start,"Pause")

    t0=clock.ElapsedMilliseconds

    st=0

  Elseif nm="Pause" then 

    st=t1                 '   sum up time

    TF="False"

    Controls.SetButtonCaption(start,"Resume")

  Elseif nm="Resume" then 

    TF="True"

    t0=clock.ElapsedMilliseconds

    Controls.SetButtonCaption(start,"Pause")

  Elseif nm="Stop" then 

    TF="False"

    j=0.00

    Shapes.SetText(dtime,j)

    Shapes.Move(sSH,X0,Y0)

    Shapes.Rotate(sSH,0)

    Controls.SetButtonCaption(start,"Start")

  EndIf

 

  While TF="true"

    t1=Clock.ElapsedMilliseconds-t0+st

    J=Math.Round(t1/10)/100

    x3=GW/2+R*math.Cos(Math.GetRadians(j*6-90))/2-1.5

    Y3=GH/2+R*math.sin(Math.GetRadians(j*6-90))/2-GH/4+20

    Shapes.Move(sSH,x3,y3)

    Shapes.Rotate(sSH,6*j)

    Shapes.SetText(dtime,j)

 EndWhile

EndSub





Sub init

  GW=900

  GH=700

  GraphicsWindow.Width=GW

  GraphicsWindow.Height=GH

  GraphicsWindow.Top =  10

  GraphicsWindow.Left = 10

  GraphicsWindow.Title = "Stop Watch"

  GraphicsWindow.BackgroundColor = "Lightcyan"

  Start=controls.AddButton("Start",GW-100,20)

  Controls.SetSize(start,80,35)

  Stop=controls.AddButton("Stop",GW-100,60)

  Controls.SetSize(stop,80,35)

  R=(GH-80)/2

  sSH=Shapes.AddTriangle(-3,R,0,0,3,R)   ' Second hand

  X0=GW/2-1.5

  Y0=40

  Shapes.Move(sSH,X0,Y0)

  GraphicsWindow.FontSize=20

  dtime=shapes.AddText("00.00")            ' digital second

  Shapes.Move(dtime,GW-200,25) 

  GraphicsWindow.FontSize=16

EndSub



Sub AnalogView

 GraphicsWindow.penColor= "red"

 GraphicsWindow.BrushColor= "red"

 cntr=Shapes.AddEllipse(14,14)

 Shapes.Move(cntr,(GW-14)/2,(GH-14)/2)

 

  For i = 1 To 60

    If Math.Remainder(i,5)=0 Then 

      GraphicsWindow.BrushColor= "red"

      GraphicsWindow.PenColor = "Red"

      PW=2

    Else 

      GraphicsWindow.BrushColor= "Navy"

      GraphicsWindow.PenColor = "Navy"

      PW=1

    EndIf

    mark[i]=Shapes.AddRectangle(5*PW,5)

    NMB[i]=Shapes.Addtext(i)

    x=GW/2-5+R*math.Cos(Math.GetRadians(6*i-90))

    y=GH/2+R*math.Sin(Math.GetRadians(6*i-90))

    Shapes.Move(mark[i],x,y)

    Shapes.Rotate(mark[i],6*i-90)

    x2=GW/2+(R+20)*math.Cos(Math.GetRadians(6*i-90))

    y2=GH/2+(R+20)*math.Sin(Math.GetRadians(6*i-90))

    Shapes.Move(NMB[i],x2-10,y2-5)

  EndFor  

EndSub

  


