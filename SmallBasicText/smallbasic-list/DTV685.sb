' SmallBasic Version 1.0
' Funktion: Relais-Steuerung IOWarrior
' Autor: Pappa Lapub
' Herkunft: http://social.msdn.microsoft.com/Forums/en-US/bc86767d-323c-4920-8118-16436fb3af02/zeiteingabe
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev
' Kommentar: angenommener Basiswert von 255 für 8bit-Steuerung (=256=2^8 mögliche Steuer-Kombinationen)
'  
' Variablen: Wert ... Steuerwert-Ausgabe für IOWarrior-Relais ??
'  
' --------------------------------------------------------------------------------
Wert = 255    ' Grundwert, wenn alle CheckBoxen aus bzw. keinerlei Ansteuerung
GraphicsWindow.BrushColor = "Black"
TB = Controls.AddTextBox(20,10)
Controls.SetSize(TB,40,22)
Controls.SetTextBoxText(TB,Wert)
GraphicsWindow.DrawText(70,13,"= Steuerwert (dez.)")

Info = ""
Tab = Text.GetCharacter(9)
For N = 1 To 8
  Info = Tab+Tab + N+". Bit = 2^"+(N-1)+" = "+ Math.Power(2,N-1)  ' Info auskommentieren
  CBox[N] = LDControls.AddCheckBox("Relais " + N + Info)
  Controls.Move(CBox[N],20,20*N+40)
EndFor

LDControls.CheckBoxChanged = CBChange

' ##### SUBs #####
Sub CBChange
  CBNam = LDControls.LastCheckBox
  
  For N = 1 To 8
    If CBox[N] = CBNam Then   ' richtige Checkbox Control1-8 gefunden, die sich geändert hat
      If LDControls.LastCheckBoxState = "True" Then
        Wert = Wert - Math.Power(2,N-1)   ' XOR
      Else
        Wert = Wert + Math.Power(2,N-1)   ' OR
      EndIf
      Controls.SetTextBoxText(TB,Wert)
    EndIf
  EndFor
  
EndSub
  
