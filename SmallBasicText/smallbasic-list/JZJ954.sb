' Pattern Login Demo
' Version 0.1a
' Copyright Â© 2016 Nonki Takahashi.  All rights reserved.
' Challenge 2016-05
GraphicsWindow.Title = "Pattern Login Demo"
Init()
Form()
Demo()
ClearDemo()
Sub ClearDemo
  GraphicsWindow.BrushColor = "#111111"
  GraphicsWindow.FillRectangle(0, 150 - r, gw, gh)
  For i = 1 To 9
    Shapes.HideShape(ring[i])
  EndFor
EndSub
Sub Demo
  p = 1
  len = Text.GetLength(demoPtn)
  GraphicsWindow.PenWidth = pr
  GraphicsWindow.PenColor = "#80FFFFFF"
  buttonClicked = "False"
  While Not[buttonClicked]
    i = Text.GetSubText(demoPtn, p, 1)
    Shapes.ShowShape(ring[i])
    If 1 < p Then
      x1 = point[iLast]["x"]
      y1 = point[iLast]["y"]
      x2 = point[i]["x"]
      y2 = point[i]["y"]
      GraphicsWindow.DrawLine(x1, y1, x2, y2)
    EndIf
    iLast = i
    p = p + 1
    Program.Delay(500)
    If len < p Then
      p = 1
      ClearDemo()
    EndIf
  EndWhile
EndSub
Sub Form
  gw = 300
  gh = 400
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.BackgroundColor = "#111111"
  GraphicsWindow.PenColor = "Black"
  GraphicsWindow.PenWidth = 1
  x1 = 10
  x2 = gw - 10
  pr = 4
  r = 27
  y = 150 - 1.7 * r
  GraphicsWindow.DrawLine(x1, y, x2, y)
  For row = 1 To 3
    y = 150 + (row - 1) * (3 * r)
    For col = 1 To 3
      x = (gw - (6 * r)) / 2 + (col - 1) * (3 * r)
      i = (row - 1) * 3 + col
      point[i]["x"] = x
      point[i]["y"] = y
      GraphicsWindow.PenColor = "White"
      GraphicsWindow.PenWidth = 1
      GraphicsWindow.BrushColor = "Gray"
      p = Shapes.AddEllipse(2 * pr, 2 * pr)
      Shapes.Move(p, x - pr, y - pr)
      GraphicsWindow.PenColor = "Lime"
      GraphicsWindow.PenWidth = 4
      GraphicsWindow.BrushColor = "#00000000"
      ring[i] = Shapes.AddEllipse(2 * r, 2 * r)
      Shapes.Move(ring[i], x - r, y - r)
      Shapes.HideShape(ring[i])
    EndFor
  EndFor
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.FontName = "Segoe UI"
  GraphicsWindow.FontBold = "False"
  txtMsg = Shapes.AddText(msg[1])
  Shapes.Move(txtMsg, 10, 10)
  GraphicsWindow.FontBold = "True"
  GraphicsWindow.BrushColor = "#333333"
  If Text.StartsWith(b1[1], "Gray:") Then
    gray1 = "True"
    caption = Text.GetSubTextToEnd(b1[1], 6)
  Else
    gray1 = "False"
    caption = b1[1]
  EndIf
  btn1 = Controls.AddButton(caption, 10, gh - 40)
  Controls.SetSize(btn1, (gw - 30) / 2, 26)
  GraphicsWindow.PenWidth = 0
  mask1 = Shapes.AddRectangle((gw - 30) / 2, 26)
  Shapes.SetOpacity(mask1, 50)
  Shapes.Move(mask1, 10, gh - 40)
  If Not[gray1] Then
    Shapes.HideShape(mask1)
  EndIf
  If Text.StartsWith(b2[1], "Gray:") Then
    gray2 = "True"
    caption = Text.GetSubTextToEnd(b2[1], 6)
  Else
    gray2 = "False"
    caption = b2[1]
  EndIf
  GraphicsWindow.BrushColor = "Black"
  btn2 = Controls.AddButton(caption, gw / 2 + 5, gh - 40)
  Controls.SetSize(btn2, (gw - 30) / 2, 26)
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "White"
  mask2 = Shapes.AddRectangle((gw - 30) / 2, 26)
  Shapes.SetOpacity(mask2, 50)
  Shapes.Move(mask2, gw / 2 + 5, gh - 40)
  If Not[gray2] Then
    Shapes.HideShape(mask2)
  EndIf
EndSub
Sub Init
  CRLF = Text.GetCharacter(13) + Text.GetCharacter(10)
  Not = "False=True;True=False;"
  msg[1] = "Connect at least four dots.  Tap Next when you" + CRLF
  msg[1] = msg[1] + "are ready to draw your own pattern."
  b1[1] = "Gray:Cancel"
  b2[1] = "Next"
  msg[2] = "Draw an unlock pattern."
  b1[2] = "Cancel"
  b2[2] = "Gray:Next"
  msg[3] = "Release finger when finished."
  b1[3] = "Gray:Cancel"
  b2[3] = "Gray:Next"
  msg[4] = "Connect at least 4 dots.  Try again:"
  b1[4] = "Gray:Retry"
  b2[4] = "Gray:Next"
  msg[5] = "Pattern recorded."
  b1[5] = "Retry"
  b2[5] = "Next"
  msg[6] = "Confirm saved pattern."
  b1[6] = "Cancel"
  b2[6] = "Gray:Confirm"
  msg[7] = "Sorry, try again."
  b1[7] = "Cancel"
  b2[7] = "Gray:Confirm"
  msg[8] = "Your new unlock pattern:"
  b1[8] = "Cancel"
  b2[8] = "Confirm"
  demoPtn = "215369"
  Controls.ButtonClicked = OnButtonClicked
EndSub
Sub OnButtonClicked
  buttonClicked = "True"
  caption = Controls.GetButtonCaption(Controls.LastClickedButton)
EndSub
  
