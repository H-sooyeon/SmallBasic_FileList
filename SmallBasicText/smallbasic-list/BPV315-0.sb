GraphicsWindow.Height = 600

GraphicsWindow.Width = 600

TextWindow.Left = GraphicsWindow.Left + 615

TextWindow.Top = GraphicsWindow.Top

Mouse.HideCursor()



Steam_Skin=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pSDvsXajbdg0-y3xRxTA_W7gio52NarIE4aiSvo3pk3QhqsV72UwBk7HWeBd5i6PlZ2W5CtYZzegxsrkfsbLAxA/smoke1.png")

'Steam_Skin = ImageList.LoadImage(Program.Directory + "\smoke1.png")

Steam_Air_Resistance = -8



GraphicsWindow.BrushColor = "Black"

Emitter = Shapes.AddTriangle(0,0,70,0,35,70)



While "True"

  TimeStart = Clock.ElapsedMilliseconds

  

  Mouse_X=GraphicsWindow.mouseX

  Mouse_Y=GraphicsWindow.mouseY

  Shapes.Move(Emitter,Mouse_X-35,Mouse_Y-35)

  

  If Mouse.IsRightButtonDown Then

    ang = ang+1

    Shapes.Rotate(Emitter,ang)

  EndIF

  If Mouse.IsLeftButtonDown Then

    Steam_EmitDelay =  Steam_EmitDelay + 1

    If Steam_EmitDelay = 2 Then

      SteamEmitter()

      Steam_EmitDelay = 0

    endif

  endif

  

  SteamCode()

  

  TextWindow.Clear()

  TextWindow.WriteLine("Steam Sprites: " + (Steam_New-Steam_Old))

  TextWindow.WriteLine("Steam_New: " + Steam_New)

  TextWindow.WriteLine("Steam_Old: " + Steam_Old)

  

  TimeEnd = Clock.ElapsedMilliseconds

  PassedTime=TimeEnd-TimeStart

  PerfectDelay = 30-PassedTime

  If PerfectDelay > 0 Then

    Program.Delay(PerfectDelay)

  endif

endwhile



Sub SteamEmitter

  Steam_New = Steam_New + 1

  

  Steam[Steam_New] = Shapes.AddImage(Steam_Skin)

  Shapes.Move(Steam[Steam_New],-1000,-1000)

  

  vel = 10+Math.GetRandomNumber(20)

  angle = Math.Pi*(ang/180-0.5)

  

  Steam_X[Steam_New] = Mouse_X+35*Math.Cos(angle)

  Steam_Y[Steam_New] = Mouse_Y+35*Math.Sin(angle)

  

  Steam_WX[Steam_New] = Math.Cos(angle) * vel

  Steam_WY[Steam_New] = Math.Sin(angle) * vel

  Steam_Opacity[Steam_New] = 100

  Steam_Angle[Steam_New] = Math.GetRandomNumber(359)

  Steam_Zoom[Steam_New] = Math.GetRandomNumber(5) * 0.1 + 0.8

endsub



Sub SteamCode

  For s = Steam_Old+1 To Steam_New

    'if Steam_WY[s] < Steam_Air_Resistance Then

    Steam_WX[s] = Steam_WX[s] * 0.95

    Steam_WY[s] = Steam_WY[s] * 0.95

    'endIf

    Steam_X[s] = Steam_X[s] + Steam_WX[s]

    Steam_Y[s] = Steam_Y[s] + Steam_WY[s]

    

    'Steam_Angle[s] = Steam_Angle[s] + Steam_WX[s] * 1

    Steam_Zoom[s] = Steam_Zoom[s] + Math.GetRandomNumber(10)*0.02

    Steam_Opacity[s] = Steam_Opacity[s] - 2

    

    Shapes.SetOpacity(Steam[s],Steam_Opacity[s])

    If Steam_Opacity[s] <= 0 Then

      Shapes.Remove(Steam[s])

      Steam[s] = ""

      Steam_X[s] = ""

      Steam_Y[s] = ""

      Steam_WX[s] = ""

      Steam_WY[s] = ""

      Steam_Opacity[s] = ""

      Steam_Angle[s] = ""

      Steam_Zoom[s] = ""

      Steam_Old = Steam_Old + 1

    endif

    

    Shapes.Zoom(Steam[s],Steam_Zoom[s],Steam_Zoom[s])

    Shapes.Rotate(Steam[s],Steam_Angle[s])

    Shapes.Move(Steam[s],Steam_X[s]-35,Steam_Y[s]-35)

  endfor

endsub






