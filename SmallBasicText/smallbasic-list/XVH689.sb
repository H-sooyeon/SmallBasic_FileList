'  challenge of the month  August  2012   - Soroban -    by  NaochanON  
'   if you calculate on degital calculator,   beads move on Soroban      
degitalbuttons()
soroban()
Controls.ButtonClicked = OnButtonClicked


Sub OnButtonClicked
  GraphicsWindow.BrushColor = "Blue"
  nm=controls.GetButtonCaption(controls.LastClickedButton)
  If nm="End"  Then 
    Program.End()
  elseIf nm="Del" Then 
    add_num=Text.GetSubText(add_num,1,Text.GetLength(add_num)-1)  'delete 1 word
    settxt() 
  elseIf nm="AC"  Then 
    NN=0
    add_num=""
    settxt()                            '   Clear
    Init_beads()  
  elseif  nm="="  then 
    RightNMB=""
    param()
    If RightNMB<>"" then
      calc()
      add_num=add_num+text.GetCharacter(10)+"= "+ans
      settxt()
      add_num=ans
      Init_beads()                    '  beads  initial position
      set_soroban()                   '  set      Answer 
    EndIf
  Else  
    If Text.IsSubText(List,nm) Then 
      set_soroban()                   '  set      LeftNMB
    endif
    add_num=text.Append(add_num,nm)
    settxt() 
  endif
EndSub

Sub param
  If add_num<>"" Then 
    NN=0
    midt="False"
    While midt="False"
      NN=NN+1
      midT=text.IsSubText(List,text.GetSubText(add_num,NN,1)) '  search  * /  +  -   
    EndWhile 
    midW= text.GetSubText(add_num,NN,1)            '---------  * /  +  -   
    LeftNMB=text.GetSubText(add_num,1,NN-1)        '  1st number 
    RightNMB=text.GetSubTextToEnd(add_num,NN+1)    '  2nd number
  EndIf 
EndSub

Sub calc
  ans=""
  If midW="+" Then 
    ans=LeftNMB+RightNMB
  elseIf  midW="-" Then 
    ans=LeftNMB-RightNMB
  elseIf  midW="*" Then 
    ans=LeftNMB*RightNMB
  elseIf  midW="รท" Then 
    ans=LeftNMB/RightNMB
  endif
  ans=math.Round(ans*math.Power(10,7))/math.Power(10,7)  '  KKKK . LLLLLLL    7digits
endsub

Sub settxt
  Shapes.SetText(numbers,add_num)
  Shapes.Move(numbers, 30, 5)
EndSub

Sub set_soroban
  number=""
  dcP=text.GetIndexOf(add_num,".")    '     decimal  point
  If 0<dCP Then 
    UdNMB=Text.GetLength(add_num)-dcP    '    KKKK.LLL     in this case   UdNMB=3 
    If UdNMB>7 Then 
      UdNMB=7
    EndIf
    number=text.Append(Text.GetSubText(add_num,1,dcP-1),Text.GetSubText(add_num,dcP+1,UdNMB))'   KKKKLLL
  Else 
    UdNMB=0
    number=add_num
  EndIf
  NL=Text.GetLength(number)                    '   Number  without  decimal point
  For I=1 To NL
    NMB=text.GetSubText(number,i,1)
    If 4<NMB Then
      Shapes.Animate(Beads["U"][8-UdNMB+NL-i],396.5+770-35*(8-UdNMB+NL-i),53+35,1000) '  Upper bead
      Shapes.SetOpacity(Beads["U"][8-UdNMB+NL-i],100)  
      Sound.PlayClickAndWait()
    endif 
    For j=1 To Math.Remainder(NMB,5)      
      Shapes.Animate(Beads["L"][j][8-UdNMB+NL-i],396.5+770-35*(8-UdNMB+NL-i) ,170+35*(j-1)-35 ,1000) 'Lower beads
      Shapes.SetOpacity(Beads["L"][j][8-UdNMB+NL-i],100)  
    EndFor   
    Sound.PlayClickAndWait()
  EndFor
EndSub

Sub Degitalbuttons
  List="*รท+-"
  GraphicsWindow.Hide()
  GraphicsWindow.Width = 1200
  GraphicsWindow.Height = 360
  GraphicsWindow.Title = "Calculator"
  GraphicsWindow.Show()
  GraphicsWindow.BackgroundColor = "Darkcyan"
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.FillRectangle(10, 5, 350, 60)
  GraphicsWindow.FontSize = 30
  GraphicsWindow.FontName = "Coorie new"
  GraphicsWindow.BrushColor = "Blue"
  d[1]="7 8 9 รท Del"
  d[2]="4 5 6 * AC"
  d[3]="1 2 3 - ="
  d[4]="0 00 . + End" 
  For i=1 To 4
    data=d[i]+" "
    For j=1 To 5
      btn=text.GetSubText(data,1,Text.GetIndexOf(data," ")-1)
      button[i][j] = Controls.AddButton(btn,10+ 70*(j-1), 70*i)
      Controls.SetSize(button[i][j],70,70)
      data= Text.GetSubTextToEnd(data,Text.GetIndexOf(data," ")+1)      
    EndFor
  EndFor
  GraphicsWindow.FontSize = 20
  numbers=Shapes.AddText("  ")
  Shapes.Move(numbers,10,5)
EndSub

Sub soroban
  GraphicsWindow.BrushColor="saddlebrown"
  base=Shapes.AddRectangle(800,310)
  Shapes.Move(base,390,15)
  GraphicsWindow.BrushColor="khaki"
  base2=Shapes.AddRectangle(760,265)
  Shapes.Move(base2,410,40)
  '----------------    pole  ----------------------------
  GraphicsWindow.BrushColor="maroon"
  For i=1 To 21
    pole[i]=Shapes.AddRectangle(5,265)
    Shapes.Move(pole[i],405+35*i,40)
  endfor
  '----------------    separater  ----------------------------
  GraphicsWindow.BrushColor="Black"
  base3=Shapes.AddRectangle(760,15)
  Shapes.Move(base3,410,115)
  '----------------    Mark  ----------------------------
  GraphicsWindow.BrushColor="White"
  For j=1 To 7
    Mark[j]=Shapes.AddEllipse(10,10)
    Shapes.Move(Mark[j],473+35*3*(j-1),118)
  EndFor
  '----------------    Beads  ----------------------------
  GraphicsWindow.BrushColor="DarkGreen"
  GraphicsWindow.penColor="Darkgreen"
  '-------   Upper  -----------
  For i=1 To 21 
    Beads["U"][i]=Shapes.AddRectangle(22,22)
    Shapes.Move( Beads["U"][i],396.5+770-35*i,53)
    Shapes.Rotate( Beads["U"][i],-45)
    Shapes.SetOpacity(Beads["U"][i],30)  
  EndFor
  '-------   Lower  -----------
  For i=1 To 21 
    For j=1 To 4
      Beads["L"][j][i]=Shapes.AddRectangle(22,22)
      Shapes.Move( Beads["L"][j][i],396.5+770-35*i,170+35*(j-1))
      Shapes.Rotate( Beads["L"][j][i],-45)
      Shapes.SetOpacity(Beads["L"][j][i],30)  
    EndFor
  EndFor
  '-------   decimal  point    -----------
  GraphicsWindow.BrushColor="Yellow"
  GraphicsWindow.PenColor="Yellow"
  Mark[7]=Shapes.AddEllipse(15,15)
  Shapes.Move(Mark[7],470+35*3*(5-1),308)
  smark=Shapes.AddText("Decimal point")
  Shapes.Move(sMark,425+35*4*(4-1),325)
EndSub

Sub Init_beads
  '-------   Upper  -----------
  For i=1 To 21 
    Shapes.Move( Beads["U"][i],396.5+770-35*i,53)
    Shapes.SetOpacity(Beads["U"][i],30)  
  EndFor
  '-------   Lower  -----------
  For i=1 To 21 
    For j=1 To 4
      Shapes.Animate( Beads["L"][j][i],396.5+770-35*i,170+35*(j-1),500)
      Shapes.SetOpacity(Beads["L"][j][i],30)  
    EndFor
  EndFor
endsub 
