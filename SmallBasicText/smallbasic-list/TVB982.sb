'Nearest stars to our sun rendered in 3D

'distance out to approx 16 light-years

'Zeven Provincien May 2010

'

'Star data taken from a version of the Gliese star catalog

'that I found at this address: 

'http://cc.joensuu.fi/~tniemi/starmaps/TN_Gliese3simplified.dat

'

'3D rendering code adapted from code originally published

'by rdohmen and modified by litdev and Oskariok

'on the Small Basic forum, 'Rotating Cube' thread

'

'Use a,s,d,f keys to rotate

'Use x&c keys to zoom

'Use arrow keys to slide up/down, left/right

'Use mouse-click to set view in rotation

'Hover over a star to show its name and distance from our sun in light-years



GraphicsWindow.BackgroundColor = "Black"



Star["X"]="1=      0;2=   2.93;3=   3.12;4=   4.98;5=   -1.9;"

Star["X"]=Star["X"]+"6=   -3.4;7=   -2.1;8=  -5.75;9=   9.22;10=  -3.38;"

Star["X"]=Star["X"]+"11=  -6.86;12=   0.01;13=   4.12;14=  -4.79;15=   6.91;"

Star["X"]=Star["X"]+"16=   1.51;17=   0.13;18=  -3.23;19=  -9.24;20=   4.67;"

Star["X"]=Star["X"]+"21=  -9.54;22=  -2.05;23= -10.25;24=      9;25=  -3.42;"

Star["X"]=Star["X"]+"26=  -3.29;27=  12.18;28= -11.24;29=  -2.63;30=   1.45;"

Star["X"]=Star["X"]+"31=  -4.01;32=  -9.41;33=  -8.47;34=   4.58;35=   3.44;"

Star["X"]=Star["X"]+"36=  -1.74;37=  14.09;38=   6.53;39=  10.28;40=  -9.09;"

Star["Y"]="1=      0;2=  -3.04;3=  -3.04;4=   2.98;5=   -3.9;"

Star["Y"]=Star["Y"]+"6=   -0.3;7=   0.17;8=  -6.21;9=   1.84;10=   9.29;"

Star["Y"]=Star["Y"]+"11=  -1.95;12=  -5.48;13=   4.42;14=   9.55;15=  -3.04;"

Star["Y"]=Star["Y"]+"16=  11.13;17=  10.39;18=   0.39;19=  -6.17;20=   0.42;"

Star["Y"]=Star["Y"]+"21=  -2.92;22=    1.2;23=  -6.48;24=   0.62;25=  -9.61;"

Star["Y"]=Star["Y"]+"26=  12.52;27=   0.72;28=  -7.28;29=  -8.02;30=  -4.24;"

Star["Y"]=Star["Y"]+"31=   6.45;32=  -7.95;33=   5.37;34= -13.89;35=     -1;"

Star["Y"]=Star["Y"]+"36= -10.94;37=   -4.3;38= -13.39;39=  -1.96;40=   2.29;"

Star["Z"]="1=      0;2=  -0.14;3=  -0.05;4=   1.45;5=   6.48;"

Star["Z"]=Star["Z"]+"6=   7.46;7=   -8.3;8=  -1.32;9=   -1.7;10=  -3.01;"

Star["Z"]=Star["Z"]+"11=  -7.93;12=   9.34;13=  -9.29;14=  -3.56;15=  -8.39;"

Star["Z"]=Star["Z"]+"16=  -1.14;17=   4.67;18= -10.93;19=   2.57;20= -10.46;"

Star["Z"]=Star["Z"]+"21=   6.34;22= -11.96;23=   2.22;24=   -8.8;25=  -7.43;"

Star["Z"]=Star["Z"]+"26=      0;27=   5.35;28=  -1.45;29= -11.16;30=  13.31;"

Star["Z"]=Star["Z"]+"31=  -11.9;32=   7.74;33= -10.56;34=  -0.51;35= -14.25;"

Star["Z"]=Star["Z"]+"36=   9.74;37=  -1.75;38=  -0.74;39= -10.97;40=  12.08;"

Star["Class"]="1=G5;2=M5;3=G5;4=M5;5=M5;6=M5;7=M5;8=A5;9=M5;10=M5;"

Star["Class"]=Star["Class"]+"11=K5;12=M5;13=M5;14=M5;15=K5;16=K5;17=M5;"

Star["Class"]=Star["Class"]+"18=G5;19=F4;20=M5;21=M5;22=M5;23=M5;24=M5;25=M5;"

Star["Class"]=Star["Class"]+"26=M5;27=M5;28=M5;29=M5;30=M5;31=DZ;32=K5;"

Star["Class"]=Star["Class"]+"33=M5;34=M5;35=M5;36=M5;37=M5;38=DQ;39=M5;40=K5;"

Star["Name"]="1=Sol;2=Proxima Centauri;3=Alpha Centauri;4=Barnard's Star;"

Star["Name"]=Star["Name"]+"5=Wolf 359;6=LHS 37;7=UV Ceti;8=Sirius;9=Ross 154;"

Star["Name"]=Star["Name"]+"10=Ross 248;11=Epsilon Eridani;12=Ross 128;"

Star["Name"]=Star["Name"]+"13=LHS 68;14=GX Andromedae;15=Epsilon Indi;16=61 Cygni;"

Star["Name"]=Star["Name"]+"17=LHS 58;18=Tau Ceti;19=LHS 233;20=LHS 70;"

Star["Name"]=Star["Name"]+"21=LHS 248;22=L 725-32;23=LHS 33;24=LHS 66;25=Kapteyn's Star;"

Star["Name"]=Star["Name"]+"26=DO Cephei;27=LHS 419;28=577 Monocerotis;29=LFT 295;30=FL Virginis;"

Star["Name"]=Star["Name"]+"31=Van Maanen's Star;32=LHS 6;33=LHS 11;34=LHS 288;35=LHS 1;"

Star["Name"]=Star["Name"]+"36=LHS 292;37=LHS 449;38=LHS 43;39=L 354-89;40=LHS 280;"



i = 0

'While (Line[i] <> "EOF") And (i < 40)

While i < 40

  i = i + 1

' The following line could be harmful and has been automatically commented.

'   'Line[i] = File.ReadLine("C:\users\Craig\Documents\SmallBasic\gliese_catalog_3.txt", i)

  

  'TextWindow.WriteLine(Line[i])

  

  'Star["Gliese"][i] = Text.GetSubText(Line[i],1,8)

  'Star["X"][i] = Text.GetSubText(Line[i],18,7)

  'Star["Y"][i] = Text.GetSubText(Line[i],26,7)

  'Star["Z"][i] = Text.GetSubText(Line[i],34,7)

  'Star["dist"][i] = Text.GetSubText(Line[i],43,6)

  'Star["Class"][i] = Text.GetSubText(Line[i],57,2)

  'Star["Name"][i] = Text.GetSubText(Line[i],73,20)

  

  If Text.StartsWith(Star["Class"][i], "O") Then

    GraphicsWindow.BrushColor = "Blue"

  ElseIf Text.StartsWith(Star["Class"][i], "B") Then

    GraphicsWindow.BrushColor = "CornflowerBlue"

  ElseIf Text.StartsWith(Star["Class"][i], "A") Then

    GraphicsWindow.BrushColor = "White"

  ElseIf Text.StartsWith(Star["Class"][i], "F") Then

    GraphicsWindow.BrushColor = "LightYellow"

  ElseIf Text.StartsWith(Star["Class"][i], "G") Then

    GraphicsWindow.BrushColor = "Yellow"

  ElseIf Text.StartsWith(Star["Class"][i], "K") Then

    GraphicsWindow.BrushColor = "Orange"

  Else

    GraphicsWindow.BrushColor = "Red" 'M class star

  EndIf

  

  Star["Shape"][i] = Shapes.AddEllipse(7,7)

  distsq = Star["X"][i]*Star["X"][i]+Star["Y"][i]*Star["Y"][i]+Star["Z"][i]*Star["Z"][i]

  Star["dist"][i] = Math.SquareRoot(distsq)

  Star["dist"][i] = Math.Round(Star["dist"][i]*100)/100

  

EndWhile

'TextWindow.WriteLine(Star["Gliese"])

' The following line could be harmful and has been automatically commented.

' 'File.WriteContents("C:\users\Craig\Documents\SmallBasic\test.txt",Star["Class"])

StarNo = i



'add coordinate system as last three points

Star["Gliese"][StarNo+1] = ""

Star["X"][StarNo+1] = 25

Star["Y"][StarNo+1] = 0

Star["Z"][StarNo+1] = 0

Star["dist"][StarNo+1] = 25

Star["Class"][StarNo+1] = ""

Star["Name"][StarNo+1] = "Galactic X coordinate"

  

Star["Gliese"][StarNo+2] = ""

Star["X"][StarNo+2] = 0

Star["Y"][StarNo+2] = 25

Star["Z"][StarNo+2] = 0

Star["dist"][StarNo+2] = 25

Star["Class"][StarNo+2] = ""

Star["Name"][StarNo+2] = "Galactic Y coordinate"

  

Star["Gliese"][StarNo+3] = ""

Star["X"][StarNo+3] = 0

Star["Y"][StarNo+3] = 0

Star["Z"][StarNo+3] = 25

Star["dist"][StarNo+3] = 25

Star["Class"][StarNo+3] = ""

Star["Name"][StarNo+3] = "Galactic Z coordinate"



GraphicsWindow.BrushColor = "Yellow"



IDStatus = "No"

M = 0.5

R = Math.GetRadians(0.5)

S = 0.5

CubeX = 500

CubeY = 400

CubeSize = 20

Info = "False"

'Event handling

GraphicsWindow.KeyDown=OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp

'GraphicsWindow.MouseMove = OnMouseMove

GraphicsWindow.MouseDown = OnMouseDown

Timer.Tick=OnTimer

Timer.Interval=20

CreateProgram()

While Run = "True"

  If MovingDown = "True" Then

    CubeY = CubeY + M

    NeedToDraw = "True"

  EndIf

  If MovingUp = "True" Then

    CubeY = CubeY - M

    NeedToDraw = "True"

  EndIf

  If MovingRight = "True" Then

    CubeX = CubeX + M

    NeedToDraw = "True"

  EndIf

  If MovingLeft = "True" Then

    CubeX = CubeX - M

    NeedToDraw = "True"

  EndIf

  If RotatingDown = "True" Then

    angleV=angleV-R

    NeedToDraw = "True"

  EndIf

  If RotatingUp = "True" Then

    angleV=angleV+R

    NeedToDraw = "True"

  EndIf

  If RotatingRight = "True" Then

    angleH=angleH-R

    NeedToDraw = "True"

  EndIf

  If RotatingLeft = "True" Then

    angleH=angleH+R

    NeedToDraw = "True"

  EndIf

  If ZoomingPlus = "True" Then

    CubeSize=CubeSize+S

    NeedToDraw = "True"

  EndIf

  If ZoomingMinus = "True" Then

    CubeSize=CubeSize-S

    NeedToDraw = "True"

    If CubeSize < 1 Then

      CubeSize = 1

    EndIf

  EndIf  

  If NeedToDraw = "True" Then

    Draw()

  EndIf

EndWhile



Sub CreateProgram

 

  angleH=30*math.Pi/180

  angleV=30*math.Pi/180

  NeedToDraw = "False"

  Run = "True"

  GraphicsWindow.Show()

  GraphicsWindow.Title = "Our sun and its neighbours"

  GraphicsWindow.Width = 1000

  GraphicsWindow.Height = 800

  GraphicsWindow.Left = Desktop.Width/2-GraphicsWindow.Width/2

  GraphicsWindow.Top = Desktop.Height/2-GraphicsWindow.Height/2

  If Info = "True" Then

    TextWindow.Show()

    TextWindow.Title = "3D info"

    TextWindow.Left = Desktop.Width/2-330

    TextWindow.Top = Desktop.Height/2+330

  EndIf 

  RotatingLeft = "False"

  RotatingRight = "False"

  RotatingUp = "False"

  RotatingDown = "False"

  MovingLeft = "False"

  MovingRight = "False"

  MovingUp = "False"

  MovingDown = "False"

  ZoomingPlus = "False"

  ZoomingMinus = "False"

  Draw()

EndSub



Sub OnKeyDown

  K = GraphicsWindow.LastKey

  If K = "A" Then 

    RotatingLeft = "True"

  EndIF

  If K = "S" Then 

    RotatingRight = "True"

  EndIF

  If K = "W" Then

    RotatingUp = "True"

  EndIf

  If K = "Z" Then 

    RotatingDown = "True"

  EndIf

  If K = "Up" Then

    MovingUp = "True"

  EndIf

  If K = "Down" Then

    MovingDown = "True"

  EndIf

  If K = "Right" Then

    MovingRight = "True"

  EndIf

  If K = "Left" Then

    MovingLeft = "True"

  EndIf

  If K = "X" Then

    ZoomingPlus = "True"

  EndIf

  If K = "C" Then

    ZoomingMinus = "True"

  EndIf

EndSub



Sub OnKeyUp

  K = GraphicsWindow.LastKey

  If K = "A" Then 

    RotatingLeft = "False"

  EndIF

  If K = "S" Then 

    RotatingRight = "False"

  EndIF

  If K = "W" Then

    RotatingUp = "False"

  EndIf

  If K = "Z" Then 

    RotatingDown = "False"

  EndIf

  If K = "Up" Then

    MovingUp = "False"

  EndIf

  If K = "Down" Then

    MovingDown = "False"

  EndIf

  If K = "Right" Then

    MovingRight = "False"

  EndIf

  If K = "Left" Then

    MovingLeft = "False"

  EndIf

  If K = "X" Then

    ZoomingPlus = "False"

  EndIf

  If K = "C" Then

    ZoomingMinus = "False"

  EndIf

EndSub



Sub calcPoints

  For i=1 To StarNo+3

    ' draai in horizontale richting  

    'x' = x*cos q - y*sin q

    Star["x1"][i]=Star["X"][i]*math.cos(angleH)-Star["Y"][i]*math.sin(angleH)

    'y' = x*sin q + y*cos q

    Star["y1"][i]=Star["X"][i]*math.sin(angleH)+Star["Y"][i]*math.cos(angleH)

    'z' = z 

    Star["z1"][i]=Star["Z"][i]

    ' draai in verticale richting

    'y' = x*cos q - y*sin q

    Star["y2"][i]=Star["y1"][i]*math.cos(angleV)-Star["z1"][i]*math.sin(angleV)

    'z' = x*sin q + y*cos q

    Star["z2"][i]=Star["y1"][i]*math.sin(angleV)+Star["z1"][i]*math.cos(angleV)

    'x' = x

    Star["x2"][i]=Star["x1"][i]

    'Calculate X And Y

    Star["x2"][i] = Star["x2"][i]*CubeSize

    Star["y2"][i] = Star["y2"][i]*CubeSize

    Star["x2"][i] = Star["x2"][i]+CubeX

    Star["y2"][i] = Star["y2"][i]+CubeY

  endfor

endsub



Sub Draw

  'Draw Info

  If Info = "True" Then

    ShowInfo()

  EndIf

  'erase old coordinate lines

  GraphicsWindow.PenColor = "Black"

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.PenWidth = 4

  For i = (StarNo+1) to (StarNo+3)

    GraphicsWindow.DrawLine(Star["x2"][1],Star["y2"][1],Star["x2"][i],Star["y2"][i])

    GraphicsWindow.FillRectangle(Star["x2"][i]+10,Star["y2"][i], 140, 20)

  EndFor

  

  'Calculate Points

  calcPoints()

  'Delete Old Cube

  'GraphicsWindow.Clear()

  

  For i = 1 to StarNo

  

  'GraphicsWindow.DrawEllipse(Star["x2"][i],Star["y2"][i],5,5)

  'GraphicsWindow.SetPixel(Star["x2"][i],Star["y2"][i],"black")

  Shapes.Move(Star["Shape"][i],Star["x2"][i],Star["y2"][i])

  EndFor

  

  ' Redraw The Coordinate system

  

  GraphicsWindow.PenColor = "Gray"

  GraphicsWindow.BrushColor = "Gray"

  GraphicsWindow.PenWidth = 1

  For i = (StarNo+1) to (StarNo+3)

    GraphicsWindow.DrawLine(Star["x2"][1],Star["y2"][1],Star["x2"][i],Star["y2"][i])

    GraphicsWindow.DrawText(Star["x2"][i]+10,Star["y2"][i], Star["Name"][i])

   

  EndFor

  

  'Set NeedToDrawFalse

  NeedToDraw = "False"

EndSub



Sub ShowInfo

  TextWindow.Clear()

  TextWindow.WriteLine("3D info")

  TextWindow.WriteLine("CubeX = "+CubeX)

  TextWindow.WriteLine("CubeY = "+CubeY)

  TextWindow.WriteLine("Cube Size = "+CubeSize)

  TextWindow.WriteLine("Cube Horizontal Angle = "+angleH)

  TextWindow.WriteLine("Cube Vertical Angle = "+angleV)

EndSub



Sub OnTimer

  If IDStatus = "No" then

    For loop = 1 To StarNo

      If Math.Abs(GraphicsWindow.MouseX-Star["x2"][loop]) < 10 And Math.Abs(GraphicsWindow.MouseY-Star["y2"][loop]) < 10 Then

        StarIdent = loop

        IDStatus = "Yes"

        StarX = Star["x2"][loop]

        StarY = Star["y2"][loop]

        Stardist = Star["dist"][loop]

        IDStar()

      Endif

    EndFor

  Elseif IDStatus = "Yes" then

      If Math.Abs(GraphicsWindow.MouseX-Star["x2"][StarIdent]) > 10 Or Math.Abs(GraphicsWindow.MouseY-Star["y2"][StarIdent]) > 10 Then

        IDStatus = "No"

        GraphicsWindow.BrushColor = "Black"

        GraphicsWindow.FillRectangle(StarX+10, StarY, 120, 50)

        StarIdent = 0

      Endif

  Endif

EndSub

  

Sub IDStar

  GraphicsWindow.BrushColor = "Yellow"

  'GraphicsWindow.DrawText(Star["x2"][StarIdent]+10,Star["y2"][StarIdent],Star["Name"][StarIdent])

  GraphicsWindow.DrawText(StarX+10, StarY, Star["Name"][StarIdent])

   GraphicsWindow.DrawText(StarX+10,StarY+20, Stardist + " LY")

EndSub



Sub OnMouseDown

  If RotatingLeft = "False" Then

    RotatingLeft = "True"

  Else

    RotatingLeft = "False"

  EndIf

EndSub

  


