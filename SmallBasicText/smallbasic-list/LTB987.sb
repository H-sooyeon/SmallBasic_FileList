'======================================================

'PASSWORD LOGIN CODE WITH MODERATELY SECURE ENCRYPTION

'======================================================



'======================================================

'INITIALISE

'======================================================



usersFile = Program.Directory+"\passwords.txt"

readUsers()



'======================================================

'MAIN CODE

'======================================================



While ("True")

  'Read user name and check it is at least 4 characters

  TextWindow.WriteLine("Enter your name")

  name = TextWindow.Read()

  If (Text.GetLength(name) < 4) Then

    TextWindow.WriteLine("Name must have at least 4 characters")

  Else

    'Check if this user name exists and get its index in the list of users

    userNumber = 0

    indices = Array.GetAllIndices(users)

    For i = 1 To Array.GetItemCount(indices)

      If (users[indices[i]]["name"] = name) Then

        userNumber = indices[i]

      EndIf

    EndFor

    If (userNumber > 0) Then

      'Existing user - get password and check it

      TextWindow.WriteLine("Enter your password")

      password = TextWindow.Read()

      encrypt()

      If (encrypted = users[userNumber]["pwd"]) Then

        TextWindow.WriteLine("Logged In")

      Else

        TextWindow.WriteLine("Incorrect Password")

      EndIf

    Else

      'New user - get a password - check it is at least 6 characters, encrypt, add it to the current list and save it

      TextWindow.WriteLine("Enter password for new user")

      password = TextWindow.Read()

      If (Text.GetLength(password) < 6) Then

        TextWindow.WriteLine("Password must have at least 6 characters")

      Else

        encrypt()

        AddUser()

        writeUsers()

        TextWindow.WriteLine("New user added")

      EndIf

    EndIf

  EndIf

  TextWindow.WriteLine("")

EndWhile



'======================================================

'SUBROUTINES

'======================================================



Sub encrypt

  'Convert string to a number (a big number)

  len = Text.GetLength(password)

  var1 = 1

  For i = 1 To len

    val = Text.GetCharacterCode(Text.GetSubText(password,i,1))

    var1 = var1*val

  EndFor

  'TextWindow.WriteLine(var1)

  'Limit the size of the number to 9 digits because we will cube it and there are limits to the number of digits of numbers in SmallBasic

  While (var1 > 1000000000)

    var1 = Math.Floor(var1/2)

  EndWhile

  'TextWindow.WriteLine(var1)

  'Make the number very big (cube it)

  var2 = var1*var1*var1

  'Take every second digit as the encrypted value - very hard to fill the blanks to create a number that is a perfect cube

  'TextWindow.WriteLine(var2)

  len = Text.GetLength(var2)

  encrypted = ""

  For i = 1 To len Step 2

    val = Text.GetSubText(var2,i,1)

    encrypted = Text.Append(encrypted,val)

  EndFor

EndSub



Sub readUsers

' The following line could be harmful and has been automatically commented.

'   users = File.ReadContents(usersFile)

EndSub



Sub writeUsers

' The following line could be harmful and has been automatically commented.

'   File.WriteContents(usersFile,users)

EndSub



Sub AddUser

  'Find first free index number

  count = 1

  While (Array.ContainsIndex(users,count))

    count = count+1

  EndWhile

  users[count]["name"] = name

  users[count]["pwd"] = encrypted

EndSub


