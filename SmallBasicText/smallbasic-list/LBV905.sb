' Kinect Piezo Player

' Version 0.1

' Copyright Â© 2016 Nonki Takahashi.  The MIT License.



GraphicsWindow.Title = "Kinect Piezo Player"

Init()

tracking = "True"

While tracking

  Loop()

EndWhile



Sub Init

  LF = Text.GetCharacter(10)

  WQ = Text.GetCharacter(34)

  gw = 300

  gh = 600

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

  GraphicsWindow.BackgroundColor = "Black"

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.FontName = "Trebuchet MS"

  GraphicsWindow.FontSize = 18

  txt1 = Shapes.AddText("")

  Shapes.Move(txt1, 10, 10)

  txt2 = Shapes.AddText("")

  Shapes.Move(txt2, 10, 30)

  txt3 = Shapes.AddText("")

  Shapes.Move(txt3, 10, 50)

  ell = Shapes.AddEllipse(20, 20)

  Shapes.Move(ell, gw / 2 - 10, gh / 2 - 10)

  Shapes.SetOpacity(ell, 20)

  status = LDCommPort.OpenPort("COM3",9600)

  LDCommPort.SetEncoding("Ascii")

  KinectBodyList.BodiesChanged = OnBodiesChanged

  KinectBodyList.StartTracking()

EndSub



Sub Loop

  If bodiesChanged Then

    count = KinectBodyList.Count

    For index = 1 To count

      isTracked = KinectBodyList.IsTracked(index)

      If isTracked Then

        state = KinectBodyList.GetHandState(index, "right")

        pos = KinectBodyList.GetJointPosition(index, "HandRight")

        freq = Math.Round(pos["Y"] * 500 + 440)

        Shapes.SetText(txt1, "state = " + state)

        Shapes.SetText(txt2, "pos[Y] = " + (Math.Round(pos["Y"] * 1000) / 1000))

        Shapes.SetText(txt3, "freq = " + freq)

        Shapes.Move(ell, gw / 2 - 10, gh / 2 - (freq - 440))

        If state = "Open" Then

          LDCommPort.TXString(freq + LF)

          Shapes.SetOpacity(ell, 100)

        ElseIf state = "Closed" Then

          LDCommPort.TXString(0 + LF)

          Shapes.SetOpacity(ell, 20)

        EndIf

        Program.Delay(200)

      EndIf

    EndFor

  EndIf

EndSub



Sub OnBodiesChanged

  bodiesChanged = "True"

EndSub


