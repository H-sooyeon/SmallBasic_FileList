GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp
GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.CanResize="False"

GW=600
GraphicsWindow.Width=GW 'Set up the graphicswindow
GraphicsWindow.Height=GW
GraphicsWindow.Title="Dudeson's Example Project Compilation"

GraphicsWindow.BrushColor="Black"
GraphicsWindow.FillRectangle(0,0,GW,GW)

GraphicsWindow.BrushColor="Orange"
GraphicsWindow.FontSize=50
GraphicsWindow.DrawBoundText(50,250,550,"Loading Imagefiles, please wait...")
Button_Skin[1]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pLXVeGqmpLy2nBnZU3wXp3bhwUBFXoeUaFozaQpMqTi4cX31mDQiVl_aA8evjBOnBcOqi8svZjG023gJc1qKUnA/GravityballSmall.png")
Button_Skin[2]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pSDvsXajbdg0EqL5RwyuZq3BtHkM3feN2T3fLRWuegacWe7blUkhYDuMIKW6p8dQqXOBd7cVsUvBiq29Kl5CrYg/SmokeSmall.png")
Button_Skin[3]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pKqNJpJiIn8A_a-JvaiNLnN7VVdvoXrAJ_TctyCTbVm03sH4455h2Mt9gn7hZofWfA3bVB-l18spf3TVvye4-Ug/SwarmSmall.png")
Button_Skin[4]=ImageList.LoadImage("http://tfxqwg.bay.livefilestore.com/y1pW2EyYpWC7Ce7uho4XC565fhWkaGBCHZgRg4O6P_nOGbHjs99tuSM5uvIKisCmZ0U7XsFp8AfXPefMTxOxO3HAE8QvNv4jl8l/3DSmall.png")
Button_Skin[5]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1ptnNKA-j-m9QY9PAYm8-fI2c-laoG2esDCBdfTiRI2YCwoNAyz80JWWl20QwSJI_zC3m3nUaMBeYFmtgMiJpMOQ/BlurSmall.png")
Button_Skin[6]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pKqNJpJiIn8BWAf7kvQrR8zM4yWoIdQNLaRnFS90I142k_kxxa4KXBEmfWGMzUCZHeWRsJy3LwPHglgnyRani5A/SteamSmall.png")
Button_Skin[7]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pF_E9rK3xaMTmgkdfNS8d_1dO9ldbWHmML0MLPk-cgKdbxUKAk95VNC29c63jzB5_3coHtCvKX4_7rIbeKdr1TA/ShadowsSmall.png")
Button_Skin[8]=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pLXVeGqmpLy0ycLud1PIZHjmNo6OPkRhn2wPlLOmijCF_ozTuU6yVxt1KN2Dwcil-LiTTlE2Wt8dSEpsKMWe0_A/CollisionSmall.png")
Button_Skin[9]=ImageList.LoadImage("http://tfxqwg.bay.livefilestore.com/y1pc1fxBodrVYIec0lZTiuNLPQKk-JCiijy7eSp17Ofa__ambIDnSamDhzR8o1GYKwdYabrkqLsOoodEO8fniXSN1mF5L09ne6u/AboutSmall.png")
Smoke_Skin=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pSDvsXajbdg0-y3xRxTA_W7gio52NarIE4aiSvo3pk3QhqsV72UwBk7HWeBd5i6PlZ2W5CtYZzegxsrkfsbLAxA/smoke1.png")
InfoText_Skin=ImageList.LoadImage("http://public.bay.livefilestore.com/y1ptvitTdRJlFfe5a4bjgj7958rgl3VD9b5MbJusYAk1v7R6-fBBBxktY6MqDe6-XKDmv-gAnxiYci6yo3WXfsNSQ/infotext.png")
Scrollbar_Skin=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pOlSut6DIZYsMLsI9mjPlXagSGqdWlux0b0CnxHN0HAOZ7EV5kzPzospU-A7Z8s2GcKONgwngBICaiUBTa5Z_0g/scrollbar.png")
Light=ImageList.LoadImage("http://public.bay.livefilestore.com/y1p7paUa2FNt_h56QCZrwCVnWWK401mkP0NsFvOypJKf4JSOUYywabBP59mrlEFP38gVZ3eX_rElIw5NllDqMouPg/light.png")
Raster=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pOlSut6DIZYtHUozrMPYp9Uv3cTN8MfCdkys_yb6oPXfyLTZHBkZOQMoZb0G7Q2rOMkzdZ-_vreKW1sYh2z8NUw/raster2.jpg")

Project="Menu"
Button_Size=200
Button_Left[0]=-200

Start:
If Project="Menu" Then
  For i = 1 to 9
    Button[i]=Shapes.AddImage(Button_Skin[i])
    Button_Left[i]=Button_Left[i-1]+Button_Size
    Button_Top[i]=Button_Top[i-1]
    If Button_Left[i] >= GW Then
      Button_Top[i]=Button_Top[i]+200
      Button_Left[i]=0
    endif
    Button_Right[i]=Button_Left[i]+Button_Size
    Button_Bottom[i]=Button_Top[i]+Button_Size
    Shapes.Move(Button[i],Button_Left[i]+GW+i*200,Button_Top[i])
    Shapes.Animate(Button[i],Button_Left[i],Button_Top[i],1000)
  endfor
endif
While Project="Menu"
  If Mouse.IsLeftButtonDown Then
    For i = 1 To 9
      If GraphicsWindow.MouseY > Button_Top[i] And GraphicsWindow.MouseY < Button_Bottom[i] Then
        If GraphicsWindow.MouseX > Button_Left[i] And GraphicsWindow.MouseX < Button_Right[i] Then
          If i=1 then
            Project="Gravityball"
          elseif i=2 then
            Project="Smoke"
          elseif i=3 then
            Project="Swarm"
          elseif i=4 then
            Project="3D"
          elseif i=5 then
            Project="Blur"
          elseif i=6 then
            Project="Steam"
          elseif i=7 then
            Project="Shadows"
          elseif i=8 then
            Project="Collision"
          elseif i=9 then
            Project="About"
          endif
        endif
      endif
    endfor
  endif
endwhile

Sub HideMenu
  For i = 1 to 9
    Button[i]=Shapes.AddImage(Button_Skin[i])
    Button_Left[i]=Button_Left[i-1]+Button_Size
    Button_Top[i]=Button_Top[i-1]
    If Button_Left[i] >= GW Then
      Button_Top[i]=Button_Top[i]+200
      Button_Left[i]=0
    endif
    Button_Right[i]=Button_Left[i]+Button_Size
    Button_Bottom[i]=Button_Top[i]+Button_Size
    Shapes.Move(Button[i],Button_Left[i],Button_Top[i])
    Shapes.Animate(Button[i],Button_Left[i],Button_Top[i]+GW+i*200,1000)
  endfor
  While Shapes.GetTop(Button[1])<780
  endwhile
endsub

If Project="Swarm" then
  GraphicsWindow.Clear()
  GraphicsWindow.PenWidth=5
  GraphicsWindow.PenColor="GreenYellow"
  GraphicsWindow.BrushColor="Transparent"
  Emitter=Shapes.AddEllipse(50,50)
  EmitterX = 275
  EmitterY = 275
  Shapes.Move(Emitter,EmitterX,EmitterY)
  Dragging = "False" 'set emitter Dragging to off
  Ground=550
  Swarm_Speed=0.8
  Swarm_MaxSpeed=20
  Swarm_Amount=20
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(50,50,50)
  GraphicsWindow.FillRectangle(0,0,GW,GW)
  GraphicsWindow.PenWidth=0
  For i = 1 to Swarm_Amount
    GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(Math.GetRandomNumber(200),Math.GetRandomNumber(200),Math.GetRandomNumber(200))
    Swarm[i]=shapes.AddEllipse(10,10)
    SX[i]=Math.GetRandomNumber(150)+225
    SY[i]=Math.GetRandomNumber(150)+225
    Shapes.Move(Swarm[i],SX[i],SY[i])
  endfor
  HideMenu()
endif
While Project="Swarm" 'Repeat the whole time:
  TimeStart = Clock.ElapsedMilliseconds
  MouseControlSwarm()
  SwarmMovement()
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 10-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
endwhile
Sub MouseControlSwarm
  If Mouse.IsLeftButtonDown Then 'Drag and drop the Emitter
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If Dragging = "False" and MX >= EmitterX And MX <= EmitterX+50 And MY >= EmitterY And MY <= EmitterY+50 then
      Dragging = "True"
    Elseif Dragging = "True" Then
      EmitterX = MX-25
      EmitterY = MY-25
      Shapes.Move(Emitter,EmitterX,EmitterY)
    EndIf
  Else
    Dragging = "False"
  endif
  If Mouse.IsRightButtonDown and Released = "True" Then
    For i = 1 To Swarm_Amount
      SX[i]=Math.GetRandomNumber(300)+150
      SY[i]=Math.GetRandomNumber(300)+150
    endfor
    Released = "False"
  ElseIf Mouse.IsRightButtonDown = "False" Then
    Released = "True"
  endif
endsub
Sub SwarmMovement 'Move the Swarm sprites around by their corresponding WX and WY
  For i = 1 To Swarm_Amount
    If SY[i]<EmitterY+25 and SWY[i] < Swarm_MaxSpeed Then
      SWY[i]=SWY[i]+Swarm_Speed
    elseIf SY[i] > EmitterY+25 and SWY[i] > Swarm_MaxSpeed*-1 THen
      SWY[i]=SWY[i]-Swarm_Speed
    endif
    If SX[i] < EmitterX+25 and SWX[i] < Swarm_MaxSpeed THen
      SWX[i]=SWX[i]+Swarm_Speed
    elseIf SX[i] > EmitterX+25 and SWX[i] > Swarm_MaxSpeed*-1 THen
      SWX[i]=SWX[i]-Swarm_Speed
    endif
    SX[i]=SX[i]+SWX[i]
    SY[i]=SY[i]+SWY[i]
    Shapes.Move(Swarm[i],SX[i]+5,SY[i]+5)
  endfor
endsub

If Project="Gravityball" then
  GraphicsWindow.Clear()
  GraphicsWindow.PenWidth=2
  GraphicsWindow.BrushColor="LightGray"
  GraphicsWindow.FillRectangle(0,0,600,600)
  GraphicsWindow.BrushColor="#44000000"
  GraphicsWindow.FillRectangle(57,57,500,500)
  GraphicsWindow.BrushColor="White"
  GraphicsWindow.PenColor="Black"
  GraphicsWindow.FillRectangle(50,50,500,500)
  GraphicsWindow.DrawRectangle(50,50,500,500)
  GraphicsWindow.BrushColor="LightBlue"
  GraphicsWindow.PenWidth=0
  Box=Shapes.AddEllipse(50,50)
  GraphicsWindow.BrushColor="YellowGreen"
  Arrow=shapes.AddTriangle(0,0,100,0,50,30)
  Shapes.Move(Arrow,GW/2-50,GW/2+50)
  GraphicsWindow.BrushColor="LightGray"
  GraphicsWindow.FillTriangle(GW/2-50,GW/2+50,GW/2+50,GW/2+50,GW/2,GW/2+80)
  GraphicsWindow.FillTriangle(GW/2-50,GW/2-50,GW/2+50,GW/2-50,GW/2,GW/2-80)
  GraphicsWindow.FillTriangle(GW/2-50,GW/2-50,GW/2-50,GW/2+50,GW/2-80,GW/2)
  GraphicsWindow.FillTriangle(GW/2+50,GW/2+50,GW/2+50,GW/2-50,GW/2+80,GW/2)
  BoxX=GW/2-25
  BoxY=GW/2-25
  Shapes.Move(Box,BoxX,BoxY)
  Gravity="Down"
  HideMenu()
endif
While Project="Gravityball"
  BoxMovement()
  If Gravity="Up" Then
    UpGravity()
  elseif Gravity="Down" Then
    DownGravity()
  elseif Gravity="Left" Then
    LeftGravity()
  elseif Gravity="Right" Then
    RightGravity()
  endif
  Boundaries()
  Program.Delay(1)
endwhile
Sub BoxMovement
  BoxX = BoxX+BoxWX
  BoxY = BoxY+BoxWY
  Shapes.Move(Box,BoxX,BoxY)
endsub
Sub UpGravity
  If BoxY > 50 and BoxWY>-2 Then
    BoxWY=BoxWY-0.002
  endif
endsub
Sub DownGravity
  If BoxY < 500 and BoxWY<2 Then
    BoxWY=BoxWY+0.002
  endif
endsub
Sub RightGravity
  If BoxX < 500 and BoxWX<2 Then
    BoxWX=BoxWX+0.002
  endif
endsub
Sub LeftGravity
  If BoxX > 50 and BoxWX>-2 Then
    BoxWX=BoxWX-0.002
  endif
endsub
Sub Boundaries
  If BoxX<=50 then
    BoxWX = BoxWX * 0.998
    BoxWY = BoxWY * 0.998
    If BoxWX<0 then
      BoxWX = BoxWX*-0.7
    endif
  elseif BoxX >= 500 Then
    BoxWX = BoxWX * 0.998
    BoxWY = BoxWY * 0.998
    If BoxWX>0 then
      BoxWX = BoxWX*-0.7
    endif
  endif
  If BoxY<=50 then
    BoxWX = BoxWX * 0.998
    BoxWY = BoxWY * 0.998
    If BoxWY<0 then
      BoxWY = BoxWY*-0.7
    endif
  elseif BoxY >= 500 Then
    BoxWX = BoxWX * 0.998
    BoxWY = BoxWY * 0.998
    If BoxWY>0 then
      BoxWY = BoxWY*-0.7
    endif
  endif
endsub
Sub OnKeyDown
  Key = GraphicsWindow.LastKey
  If (Key = "Left") Then
    Left = 1
    Gravity="Left"
    Shapes.Rotate(Arrow,90)
    Shapes.Move(Arrow,GW/2-115,GW/2-15)
  ElseIf (Key = "Right") Then
    Right = 1
    Gravity="Right"
    Shapes.Rotate(Arrow,270)
    Shapes.Move(Arrow,GW/2+15,GW/2-15)
  ElseIf (Key = "Up") Then
    Up = 1
    Gravity="Up"
    Shapes.Rotate(Arrow,180)
    Shapes.Move(Arrow,GW/2-50,GW/2-80)
  ElseIf (Key = "Down") Then
    Down = 1
    Gravity="Down"
    Shapes.Rotate(Arrow,0)
    Shapes.Move(Arrow,GW/2-50,GW/2+50)
  ElseIf (Key = "Escape") Then
    Project="Menu"
  endif
EndSub
Sub OnKeyUp
  Key = GraphicsWindow.LastKey
  If (Key = "Left") Then
    left = 0
  ElseIf (Key = "Right") Then
    right = 0
  ElseIf (Key = "Up") Then
    Up = 0
  ElseIf (Key = "Down") Then
    Down = 0
  endif
EndSub


If Project="Blur" Then
  GraphicsWindow.Clear()
  GraphicsWindow.PenWidth=0
  GraphicsWindow.BrushColor="Blue"
  Box2=Shapes.AddRectangle(100,100)
  BoxX2=GW/2-50
  BoxY2=GW/2-50
  Shapes.Move(Box2,BoxX2,BoxY2)
  HideMenu()
endif
While Project="Blur"
  TimeStart = Clock.ElapsedMilliseconds
  If FadeDelay=5 Then
    White()
    FadeDelay=0
  else
    FadeDelay=FadeDelay+1
  endif
  'Program.Delay(5)
  If x2<BoxX2-1 Or x2>BoxX2+1 or y2<BoxY2-1 Or y2>BoxY2+1 then 'checks if the box has moved more than 5 pixels.
    GraphicsWindow.BrushColor="#0000FF" 'All the rectangles here are for anti aliasing, because otherwise it would look pretty pixelated.
    GraphicsWindow.FillRectangle(BoxX2+5,BoxY2+5,90,90)
    GraphicsWindow.BrushColor="#220000FF"
    GraphicsWindow.FillRectangle(BoxX2+4,BoxY2+4,92,92)
    GraphicsWindow.FillRectangle(BoxX2+3,BoxY2+3,94,94)
    GraphicsWindow.FillRectangle(BoxX2+2,BoxY2+2,96,96)
    GraphicsWindow.FillRectangle(BoxX2+1,BoxY2+1,98,98)
    x2=BoxX2' set the old x position to the new one so it can be compared again.
    y2=BoxY2' set the old y position to the new one so it can be compared again.
  endif
  MouseControlBlur()
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 5-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
endwhile
Sub MouseControlBlur ' used for dragging around the box. i wont further explain it, because its not important atm since i want just to fix the lag.
  If Mouse.IsLeftButtonDown = "True" Then 'Drag and drop the Emitter
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If dragging2 = 0 Then
      If MX >= BoxX2 And MX <= BoxX2+100 And MY >= BoxY2 And MY <= BoxY2+100 then
        dragging2 = 1
      endif
    EndIf
    If dragging2 = 1 Then
      BoxX2 = MX-50
      BoxY2 = MY-50
      Shapes.Move(Box2,BoxX2,BoxY2)
    EndIf
  Else
    dragging2 = 0
  endif
endsub
Sub White
  GraphicsWindow.BrushColor="#50FFFFFF" 'The white transparent color to fade out the blue rectangles.
  GraphicsWindow.FillRectangle(0,0,GW,GW) 'Fade out the rectangles a bit.
endsub

If Project="Smoke" Then
  GraphicsWindow.Clear()
  GraphicsWindow.DrawImage(Raster,0,0)
  GraphicsWindow.PenWidth=0
  Ground=GW-100
  Meteorite_Amount=2
  Particle_Amount=7
  Smoke_Amount=10
  Wind=-0.1
  Smoke_Emit_X=275
  Smoke_Emit_Y=GW-100
  GraphicsWindow.BrushColor="Gray"
  For p=1 to Particle_Amount
    Particle_Size[p]=Math.GetRandomNumber(20)+20
    Particle[p]=Shapes.AddRectangle(Particle_Size[p],Particle_Size[p])
    Particle_Y[p]=Ground+100
    Shapes.Move(Particle[p],Particle_X[p],Particle_Y[p])
  endfor
  For k = 1 to Smoke_Amount
    Smoke[k] = Shapes.AddImage(Smoke_Skin)
    Smoke_WY[k] = 1
    Smoke_Y[k] = 1000
    Shapes.Move(Smoke[k],Smoke_X[k],Smoke_Y[k])
  endfor
  HideMenu()
endif
While Project="Smoke"
  TimeStart = Clock.ElapsedMilliseconds
  SmokeCode()
  Particle_Code()
  If Mouse.IsLeftButtonDown and Released = "True" Then
    SmokeEmitter()
    Particle_Emitter()
    Released = "False"
  ElseIf Mouse.IsLeftButtonDown = "False" Then
    Released = "True"
  endif
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 20-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
endwhile
Sub SmokeEmitter
  For v=1 to Smoke_Amount
    Smoke_X[v]=Smoke_Emit_X
    Smoke_Y[v]=Smoke_Emit_Y
    Smoke_WX[v] = (1.5*(Math.GetRandomNumber(30)-16))/2
    Smoke_WY[v] = (3*(Math.GetRandomNumber(35)-40))/1.8
    Smoke_Zoom[v]=0.1*(Math.GetRandomNumber(10)+20)
    Smoke_Opacity[v]=100
  endfor
endsub
Sub Particle_Emitter
  For p=1 to Particle_Amount
    'if Particle_Y[p] >= Ground Then
    Particle_WX[p]=0.5*(Math.GetRandomNumber(31)-16)
    Particle_WY[p]=Math.GetRandomNumber(20)-40
    Particle_X[p]=Smoke_Emit_X
    Particle_Y[p]=Smoke_Emit_Y-50
    'endif
  endfor
endsub
Sub Particle_Code
  For p=1 To Particle_Amount
    If Particle_WY[p]<-0.5 Or Particle_WY[p]>0.5 or Particle_Y[p] < Ground Then
      Particle_X[p]=Particle_X[p]+Particle_WX[p]
      Particle_Y[p]=Particle_Y[p]+Particle_WY[p]
      IF Particle_Y[p] >= Ground Then
        If Particle_WY[p]>0 then
          Particle_WY[p] = Particle_WY[p]*-0.5
        endif
        Particle_WX[p] = Particle_WX[p] * 0.9
      else
        if Particle_WY[p] < 50 Then
          Particle_WY[p]=Particle_WY[p]+2
        endIf
      endif
      Particle_WX[p]=Particle_WX[p] * 0.99
      Particle_Angle[p]=Particle_Angle[p]+Particle_WX[p]
      Shapes.Rotate(Particle[p],Particle_Angle[p])
      Shapes.Move(Particle[p],Particle_X[p],Particle_Y[p])
    endif
  endfor
endsub
Sub SmokeCode
  For k=1 To Smoke_Amount
    If Smoke_Opacity[k]>0 Then
      if Smoke_WY[k] < -5 Then
        Smoke_WY[k]=Smoke_WY[k]+4
      endIf
      Smoke_WX[k]=Smoke_WX[k]*0.92+Wind
      Smoke_X[k]=Smoke_X[k]+Smoke_WX[k]
      Smoke_Y[k]=Smoke_Y[k]+Smoke_WY[k]
      Smoke_Angle[k]=Smoke_Angle[k]+Smoke_WX[k]/2
      Smoke_Zoom[k]=Smoke_Zoom[k]+0.05
      Smoke_Opacity[k]=Smoke_Opacity[k]-1
      Shapes.SetOpacity(Smoke[k],Smoke_Opacity[k])
      Shapes.Zoom(Smoke[k],Smoke_Zoom[k],Smoke_Zoom[k])
      Shapes.Rotate(Smoke[k],Smoke_Angle[k])
      Shapes.Move(Smoke[k],Smoke_X[k]-35,Smoke_Y[k]-35)
    endif
  endfor
endsub

If Project="3D" Then
  GraphicsWindow.Clear()
  RBS=70
  RTS=100
  GraphicsWindow.PenWidth=0
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(220,220,220)
  RectangleTop=Shapes.AddRectangle(RTS,RTS)
  GraphicsWindow.PenWidth=0
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(100,100,100)
  MX=GraphicsWindow.MouseX
  MY=GraphicsWindow.MouseY
  Shapes.Move(RectangleTop,MX-50,MY-50)
  RTY=MY-50
  RTX=MX-50
  RBY=(MY+400)*0.4
  RBX=(MX+400)*0.4
  Clear()
  AddTriangles()
  HideMenu()
endif
While Project="3D"
  MX=GraphicsWindow.MouseX
  MY=GraphicsWindow.MouseY
  RTY=MY-50
  RTX=MX-50
  RBY=(MY+400)*0.4
  RBX=(MX+400)*0.4
  Shapes.Move(RectangleTop,RTX,RTY)
  If OldMX<>MX Or OldMY<>MY then
    Clear()
    AddTriangles()
  endif
  OldMX=MX
  OldMY=MY
endwhile
Sub Clear
  GraphicsWindow.BrushColor="LightGray"
  GraphicsWindow.FillRectangle(0,0,GW,GW)
endsub
Sub AddTriangles
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(120,120,120)'SHADOWS
  GraphicsWindow.FillTriangle(RBX,RBY+RBS,RBX-200,RBY-200,RBX-200,RBY-300)
  GraphicsWindow.FillTriangle(RBX+RBS,RBY,RBX-200,RBY-300,RBX-100,RBY-300)
  GraphicsWindow.BrushColor="DimGray"
  GraphicsWindow.FillTriangle(RBX,RBY+RBS,RBX+RBS,RBY,RBX-200,RBY-300)
  If RTX>RBX Then
    GraphicsWindow.BrushColor="Gray" 'WALL LEFT
    GraphicsWindow.FillTriangle(RTX,RTY+RTS,RBX,RBY,RBX,RBY+RBS)
    GraphicsWindow.FillTriangle(RTX,RTY,RTX,RTY+RTS,RBX,RBY)
  ElseIf RTX+RTS<RBX+RBS Then
    GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(190,190,190) 'WALL RIGHT
    GraphicsWindow.FillTriangle(RTX+RTS,RTY,RTX+RTS,RTY+RTS,RBX+RBS,RBY+RBS)
    GraphicsWindow.FillTriangle(RTX+RTS,RTY,RBX+RBS,RBY,RBX+RBS,RBY+RBS)
  endif
  If RTY+RTS<RBY+RBS Then
    GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(150,150,150) 'WALL DOWN
    GraphicsWindow.FillTriangle(RTX,RTY+RTS,RTX+RTS,RTY+RTS,RBX,RBY+RBS)
    GraphicsWindow.FillTriangle(RBX,RBY+RBS,RBX+RBS,RBY+RBS,RTX+RTS,RTY+RTS)
  ElseIf RTY>RBY Then
    GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(160,160,160) 'WALL UP
    GraphicsWindow.FillTriangle(RTX,RTY,RTX+RTS,RTY,RBX,RBY)
    GraphicsWindow.FillTriangle(RTX+RTS,RTY,RBX,RBY,RBX+RBS,RBY)
  endif
endsub

If Project="Steam" Then
  GraphicsWindow.Clear()
  GraphicsWindow.DrawImage(Raster,0,0)
  Ground=GW
  Steam_Amount=20
  Steam_Air_Resistance=5.5
  Steam_Emit_X=GW/2-35
  Steam_Emit_Y=GW-50
  For k = 1 to Steam_Amount
    Steam[k] = shapes.addimage(Smoke_Skin)
    Steam_Y[k] = 590+Math.GetRandomNumber(30)
    Steam_X[k] = 1000
    Shapes.Move(Steam[k],Steam_X[k],Steam_Y[k])
  endfor
  HideMenu()
endif
While Project="Steam"
  TimeStart = Clock.ElapsedMilliseconds
  Steam_Emit_X=GraphicsWindow.mouseX-35
  Steam_Emit_Y=GraphicsWindow.mouseY-35
  SteamCode()
  If Mouse.IsLeftButtonDown Then
    IF c>2 THEN
      SteamEmitter()
      c=1
    ELSE
      c=c+1
    ENDIF
  endif
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 10-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
endwhile
Sub SteamEmitter
  IF v>Steam_Amount THEN
    v=1
  ELSE
    v=v+1
  ENDIF
  Steam_X[v]=Steam_Emit_X
  Steam_Y[v]=Steam_Emit_Y
  Steam_WX[v] = 0.1*(Math.GetRandomNumber(60)-31)
  Steam_WY[v] = 0.09*(Math.GetRandomNumber(20)-150)
  counter[v]=Math.GetRandomNumber(10)*0.1
  Steam_Opacity[v] = Math.GetRandomNumber(10)+90
  Steam_Zoom[v]=1
  Shapes.Zoom(Steam[k],Steam_Zoom[k],Steam_Zoom[k])
endsub
Sub SteamCode
  For k=1 To Steam_Amount
    IF Steam_Y[k] < Ground Then
      if Steam_WY[k] < 2 Then
        Steam_WY[k]=Steam_WY[k]+0.4
      endIf
      Steam_WX[k]=Steam_WX[k]*0.98
      Steam_X[k]=Steam_X[k]+Steam_WX[k]
      Steam_Y[k]=Steam_Y[k]+Steam_WY[k]-Steam_Air_Resistance
      counter[k]=counter[k]+0.01
      Steam_Angle[k]=Steam_Angle[k]+Steam_WX[k]*3
      Steam_Zoom[k]=3.5+(Steam_WY[k]/6)-counter[k]
      Steam_Opacity[k]=Steam_Opacity[k]-1.5
      Shapes.SetOpacity(Steam[k],Steam_Opacity[k])
      Shapes.Zoom(Steam[k],Steam_Zoom[k],Steam_Zoom[k])
      Shapes.Rotate(Steam[k],Steam_Angle[k])
      Shapes.Move(Steam[k],Steam_X[k],Steam_Y[k])
    ENDIF
  endfor
endsub

If Project="About" Then
  GraphicsWindow.Clear()
  InfoText=Shapes.AddImage(InfoText_Skin)
  Scrollbar_height=GW/(imagelist.GetHeightOfImage(InfoText_Skin)/GW)
  Scrollproportion=(imagelist.GetHeightOfImage(InfoText_Skin)/GW)
  Scrollbar=shapes.AddImage(Scrollbar_Skin)
  Shapes.Move(Scrollbar,GW-20,0)
  ScrollbarX=GW-20
  ScrollbarY=0
  HideMenu()
endif
While Project="About"
  MouseControlAbout()
  Shapes.Move(InfoText,0,ScrollbarY*-Scrollproportion)
endwhile
Sub MouseControlAbout
  If Mouse.IsLeftButtonDown Then 'Drag and drop the Emitter
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If Dragging = "False" Then
      If MX >= ScrollbarX And MX <= ScrollbarX+20 And MY >= ScrollbarY And MY <= ScrollbarY+Scrollbar_height then
        Dragging = "True"
      endif
    Elseif Dragging = "True" Then
      ScrollbarY = MY-Scrollbar_height/2' would be awesome if you could move the scrollbar where you grabbed it, instead of the middle... Help plz?
      If ScrollbarY<0 Then
        ScrollbarY=0
      ElseIf ScrollbarY+Scrollbar_height>GW Then
        ScrollbarY=GW-Scrollbar_height
      endif
      Shapes.Move(Scrollbar,ScrollbarX,ScrollbarY)
    EndIf
  Else
    Dragging = "False"
  endif
endsub

If Project="Shadows" Then
  GraphicsWindow.Clear()
  gw = GW
  gh = GW
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  'Setup box and sun
  Sx = 100
  Sy = 100
  SxLast = 0
  SyLast = 0
  Shadow_Amount=5
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "DarkGray"
  For i = 1 to Shadow_Amount
    Brad[i] = Math.GetRandomNumber(40)+10
    box[i] = Shapes.AddRectangle(2*Brad[i],2*Brad[i])
    Bx[i] = Math.GetRandomNumber(GW-200)+150
    By[i] = Math.GetRandomNumber(GW-200)+150
    BxLast[i] = 0
    ByLast[i] = 0
    Shapes.Move(box[i],Bx[i]-Brad[i],By[i]-Brad[i])
  endfor
  UpdateShadow()
  HideMenu()
  GraphicsWindow.BrushColor="Gray"
  GraphicsWindow.FillRectangle(0,0,GW,GW)  
endif
While Project="Shadows"
  TimeStart = Clock.ElapsedMilliseconds
  UpdateShadow()
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 20-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
EndWhile

Sub UpdateShadow
  Change = 0
  For i = 1 to Shadow_Amount
    If (Sx <> SxLast Or Sy <> SyLast Or Bx[i] <> BxLast[i] Or By[i] <> ByLast[i]) Then
      Change = 1
    Endif
  EndFor
  If (Change = 1) Then
    GraphicsWindow.BrushColor = "Gray"
    GraphicsWindow.FillRectangle(0,0,gw,gh)
    GraphicsWindow.DrawImage(light,SX-300,SY-300)
    For i = 1 to Shadow_Amount
      SxLast = Sx
      SyLast = Sy
      BxLast[i] = Bx[i]
      ByLast[i] = By[i]
      'Vector from sun to box centre
      Vx[i] = Bx[i]-Sx
      Vy[i] = By[i]-Sy
      'Locate sun in possible segments
      'Set the box corner coordinates to use and the face to draw shadow to
      If (Math.Abs(Vx[i]) < Brad[i] And Math.Abs(Vy[i]) < Brad[i]) Then
        'Sun inside box
        Face[i] = "None"
      ElseIf (Math.Abs(Vx[i]) < Brad[i] And Vy[i] > 0) Then
        'Sun above box
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]-Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]-Brad[i]
        Face[i] = "Bottom"
      ElseIf (Math.Abs(Vx[i]) < Brad[i] And Vy[i] < 0) Then
        'Sun below box
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]+Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]+Brad[i]
        Face[i] = "Top"
      ElseIf (Math.Abs(Vy[i]) < Brad[i] And Vx[i] > 0) Then
        'Sun left of box
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]-Brad[i]
        C2x[i] = Bx[i]-Brad[i]
        C2y[i] = By[i]+Brad[i]
        Face[i] = "Right"
      ElseIf (Math.Abs(Vy[i]) < Brad[i] And Vx[i] < 0) Then
        'Sun right of box
        C1x[i] = Bx[i]+Brad[i]
        C1y[i] = By[i]-Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]+Brad[i]
        Face[i] = "Left"
      ElseIf (Vx[i] < 0 And Vy[i] < 0) Then
        'Sun bottom right
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]+Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]-Brad[i]
        Face[i] = "Top"
      ElseIf (Vx[i] > 0 And Vy[i] < 0) Then
        'Sun bottom left
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]-Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]+Brad[i]
        Face[i] = "Top"
      ElseIf (Vx[i] < 0 And Vy[i] > 0) Then
        'Sun top right
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]-Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]+Brad[i]
        Face[i] = "Bottom"
      ElseIf (Vx[i] > 0 And Vy[i] > 0) Then
        'Sun top left
        C1x[i] = Bx[i]-Brad[i]
        C1y[i] = By[i]+Brad[i]
        C2x[i] = Bx[i]+Brad[i]
        C2y[i] = By[i]-Brad[i]
        Face[i] = "Bottom"
      EndIf
      'Set line equations
      A1[i] = (Sy-C1y[i])/(Sx-C1x[i])
      B1[i] = Sy-A1[i]*Sx
      A2[i] = (Sy-C2y[i])/(Sx-C2x[i])
      B2[i] = Sy-A2[i]*Sx
      'Intersections with boundary
      If (Face[i] = "Top") Then
        D1y[i] = 0
        D1x[i] = (D1y[i]-B1[i])/A1[i]
        D2y[i] = 0
        D2x[i] = (D2y[i]-B2[i])/A2[i]
      ElseIf (Face[i] = "Bottom") Then
        D1y[i] = gh
        D1x[i] = (D1y[i]-B1[i])/A1[i]
        D2y[i] = gh
        D2x[i] = (D2y[i]-B2[i])/A2[i]
      ElseIf (Face[i] = "Left") Then
        D1x[i] = 0
        D1y[i] = A1[i]*D1x[i]+B1[i]
        D2x[i] = 0
        D2y[i] = A2[i]*D2x[i]+B2[i]
      ElseIf (Face[i] = "Right") Then
        D1x[i] = gw
        D1y[i] = A1[i]*D1x[i]+B1[i]
        D2x[i] = gw
        D2y[i] = A2[i]*D2x[i]+B2[i]
      ElseIf (Face[i] = "None") Then
        D1x[i] = Bx[i]
        D1y[i] = By[i]
        D2x[i] = Bx[i]
        D2y[i] = By[i]
      EndIf
      GraphicsWindow.BrushColor = "Black"
      GraphicsWindow.FillTriangle(C1x[i],C1y[i],C2x[i],C2y[i],D1x[i],D1y[i])
      GraphicsWindow.FillTriangle(C2x[i],C2y[i],D1x[i],D1y[i],D2x[i],D2y[i])
    endfor
  endif
EndSub
'Event control
Sub OnMouseMove
  If Project="Shadows" Then
    'The small added terms prevent Sx=Bx etc
    Sx = GraphicsWindow.MouseX+0.001
    Sy = GraphicsWindow.MouseY+0.001
  endif
EndSub

If Project="Collision" Then
  GraphicsWindow.Clear()
  GraphicsWindow.BrushColor="Purple"
  Player=Shapes.AddRectangle(20,20)
  PlayerSpeed = 1
  PlayerWX = 0
  PlayerWY = 0
  PlayerX = 150
  PlayerY = 150
  Shapes.Move(Player,PlayerX,PlayerY)
  Up=0
  Down=0
  Left=0
  Right=0
  GraphicsWindow.BrushColor="LightCyan"
  GraphicsWindow.FillRectangle(0,0,GW,GW)
  GraphicsWindow.PenWidth=0
  GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(Math.GetRandomNumber(200),Math.GetRandomNumber(200),Math.GetRandomNumber(200))
  For i = 1 to 5
    BlockHeight[i]=Math.GetRandomNumber(100)+10
    BlockWidth[i]=Math.GetRandomNumber(100)+10
    Block[i]=Shapes.AddRectangle(BlockWidth[i],BlockHeight[i])
    Shapes.Move(Block[i],Math.GetRandomNumber(200)+200,Math.GetRandomNumber(200)+200)
    BlockX[i] = shapes.GetLeft(Block[i])
    BlockY[i] = shapes.GetTop(Block[i])
  endfor
  HideMenu()
endif
While Project="Collision"
  TimeStart = Clock.ElapsedMilliseconds
  Collision()
  PlayerX = PlayerX+PlayerWX
  PlayerY = PlayerY+PlayerWY
  Shapes.Move(Player,PlayerX,PlayerY)
  If Right = 1 Then 
    PlayerWX = PlayerSpeed
  endif
  if Left = 1 then
    PlayerWX = PlayerSpeed*-1
  endif
  If Down = 1 Then 
    PlayerWY = PlayerSpeed
  elseif Up = 1 then
    PlayerWY = PlayerSpeed*-1
  endif
  If Down=0 And Up=0 Then
    PlayerWY=0
  endif
  If Right=0 And Left=0 Then
    PlayerWX=0
  endif
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime=TimeEnd-TimeStart
  PerfectDelay = 8-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
Endwhile
Sub Collision
  For i = 1 To 5
    if PlayerY+20 >= BlockY[i] and PlayerY <= BlockY[i]+BlockHeight[i] then
      If PlayerX+20 >= BlockX[i] and PlayerX+20 <= BlockX[i] then
        playerX = playerX-1
      elseif PlayerX <= BlockX[i]+BlockWidth[i] and PlayerX >= BlockX[i] then
        playerX = playerX+1
      endif
    Elseif PlayerX+20 >= BlockX[i] and PlayerX <= BlockX[i]+BlockWidth[i] then
      If PlayerY+22 >= BlockY[i] and PlayerY+20 <= BlockY[i] then
        playerY = playerY-1
      elseif PlayerY-2 <= BlockY[i]+BlockHeight[i] and PlayerY >= BlockY[i]+BlockHeight[i]-1 then
        playerY = playerY+1
      endif
    endif
  endfor
  If PlayerY<0 Then
    playerY = 0
  ElseIf PlayerY>GW-20 then
    playerY = GW-20
  endif
  If PlayerX<0 Then
    playerX = 0
  ElseIf PlayerX > GW-20 then
    playerX = GW-20
  endif
endsub

Goto Start

