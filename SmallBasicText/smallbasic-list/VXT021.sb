' SmallBasic Version 1.2
' Program: AutoComplete from Array
' Changelog: 16.6
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/089278d8-ca55-42fb-8c9c-f04be2554045/showing-results-as-they-are-showing-up-similar-to-suggested-searches-when-you-type-into-a-search
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev
' Comment: 
'  
' Variables: 
' ToDo: 
' ================================================================================
arr = "0= ;1=abcd;2=Hello World;3=small basic;4=1234;5=bla Bla;"  ' "0=..;"  is for empty ComboBox
arrLow = Text.ConvertToLowerCase(arr)
nArr = Array.GetItemCount(arr)

GraphicsWindow.Title = "AutoComplete TextBox [choose by 'Return']"
GraphicsWindow.BackgroundColor = "Gainsboro"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontBold = ""
tb = Controls.AddTextBox(10,10)

GraphicsWindow.BrushColor = "Red"
AddShape()        '' I.  Choose  AddShape()  OR  AddCBox()
'AddCBox()         '' I.  Choose  AddShape()  OR  AddCBox()


Controls.TextTyped = OnTextTyped_Shape  ''II.  Choose  %_Shape OR %_CBox
LDTextWindow.KeyDown = OnKeyDown_Shape  ''II.  Choose  %_Shape OR %_CBox


' //////////  EVENTs  \\\\\\\\\\
Sub OnTextTyped_Shape
  txt = Text.ConvertToLowerCase(Controls.GetTextBoxText(tb))
  len = Text.GetLength(txt)
  If len < 1 Then
    shpTxt = ""
  Else
    For n = 1 To nArr
      If Text.StartsWith(arrLow[n], txt) Then
        shpTxt = arr[n]
        n = nArr      '' exit For loop on 1st match
      Else
        shpTxt = ""
      EndIf
    EndFor
  EndIf
  Shapes.SetText(shp, shpTxt)
EndSub

Sub OnTextTyped_CBox
  txt = Text.ConvertToLowerCase(Controls.GetTextBoxText(tb))
  len = Text.GetLength(txt)
  If len < 1 Then
    LDControls.ComboBoxSelect(cb, 0)
  Else
    For n = 1 To nArr
      If Text.StartsWith(arrLow[n], txt) Then
        LDControls.ComboBoxSelect(cb, n)
        n = nArr      '' exit For loop on 1st match
      Else
        LDControls.ComboBoxSelect(cb, 0)
      EndIf
    EndFor
  EndIf
EndSub

' ------------------------------------------------------------------------------------------

Sub OnKeyDown_Shape
  If LDTextWindow.LastKey = "Return" And shpTxt <> "" Then
    selection = shpTxt
    'GraphicsWindow.ShowMessage(selection +" selected.", "Selection")
    GraphicsWindow.Title = "Selection: "+ selection
  EndIf
EndSub

Sub OnKeyDown_CBox
  If LDTextWindow.LastKey = "Return" And LDControls.ComboBoxGetSelected(cb) > 0 Then
    selection = arr[LDControls.ComboBoxGetSelected(cb)]
    'GraphicsWindow.ShowMessage(selection +" selected.", "Selection")
    GraphicsWindow.Title = "Selection: "+ selection
  EndIf
EndSub


' //////////  SUBs  \\\\\\\\\\
Sub AddShape
  shp = Shapes.AddText("")
  LDShapes.BrushColour(shp, "LightBlue")
  Shapes.Move(shp, 10,32)
EndSub

Sub AddCBox
  cb = LDControls.AddComboBox(arr, 160,100)
  Controls.Move(cb, 10,32)
EndSub
  
