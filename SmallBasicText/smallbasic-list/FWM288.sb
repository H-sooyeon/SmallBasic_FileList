'連珠 2015/03/13
sInitData()
sInitBoard()
sDispBoard()
gn0=0 'ノード数
gn1=0 '最後のノード
gt0=1 'number(Turn)
gp0=GEBLACK
gButton=1 'ボタンを有効にする。

Controls.ButtonClicked=eClick
'GraphicsWindow.mousemove=eMouse 'タッチパネルで操作する。
GraphicsWindow.mousedown=eMouse 'マウスで操作する。
eMouse()

Sub sInitData 'データを初期設定する。
  GFILENODE="C:\Users\明\Documents\SmallBasic\連珠.text"
  GLN=15 '盤の線の数
  GWD=50 '盤の枡の大きさ
  GMLEN=50 'メッセージ文字数
  GEWALL=0 '壁
  GEBLACK=1 '黒
  GEWHITE=2 '白
  GESIGNA=3 '記号
  GESIGNB=4 '記号(黒)
  GESIGNW=5 '記号(白)
  GEOUT=6 '場の外
  GEFIELD=7 '場
  GE3=8 '三
  GE4=9 '四
  GE33=10 '三三
  GE43=11 '四三
  GE44=12 '黒:四四(両端場),白四四(両端場も含む。)
  GE5=13 '五
  GE6=14 '禁手(黒:長連,四四,三三)
  GEBW=3 'GEBLACK+GEWHITE
  GNF=1 'Forward
  GNB=2 'Back
  GNR=3 'Right
  GNL=4 'Left
  GNZ=5 'z
  GNT=6 'Turn(Number)
  GNP=7 'Player
  GNE=8 'e
  GNS=9 'Sign
  GROLE[1]="黒"
  GROLE[2]="白"
  GROLE[3]="空"
  GROLE[4]="場"
  GROLE[5]="三"
  GROLE[6]="四"
  GROLE[7]="三三"
  GROLE[8]="四三"
  GROLE[9]="四四"
  GROLE[10]="五"
  GROLE[11]="禁手"
  GDIR[1]=1 '石の方向
  GDIR[2]=20
  GDIR[3]=19
  GDIR[4]=18
  GDIR[5]=-1
  GDIR[6]=-20
  GDIR[7]=-19
  GDIR[8]=-18
  GCOL[1]="black" '黒石
  GCOL[2]="white" '白石
  GCOL[3]="skyblue" '次の手
  GCOL[4]="chocolate" '盤
  GDIG[1]="一"
  GDIG[2]="二"
  GDIG[3]="三"
  GDIG[4]="四"
  GDIG[5]="五"
  GDIG[6]="六"
  GDIG[7]="七"
  GDIG[8]="八"
  GDIG[9]="九"
  GDIG[10]="十"
  
  'gbd[361] 盤
  'gnd[9][] ノード
EndSub

Sub sInitBoard '盤のデータを初期設定する。 出力:gbd[]
  For z2=1 To (GLN+4)*(GLN+4)
    gbd[z2]=GEWALL
  EndFor
  For j2=1 To GLN
    For i2=1 To GLN
      gbd[(GLN+4)*(j2+1)+i2+2]=GEOUT
    EndFor
  EndFor
  gbd[181]=GEFIELD '中央
EndSub

Sub sFieldB44 '黒の44 入力:ge0 出力:ge0
  If ge0=0 or ge0=GE3 Then
    ge0=GE44
  Else
    ge0=GE6
  EndIf
EndSub

Sub sFieldB4 '黒の4 入力:ge0 出力:ge0
  If ge0=0 Then
    ge0=GE4
  ElseIf ge0=GE3 then
    ge0=GE43
  Else
    ge0=GE6
  EndIf
EndSub

Sub sFieldW4 '白の4 入力:ge0 出力:ge0
  If ge0=0 Then
    ge0=GE4
  ElseIf ge0=GE3 or ge0=GE33 then
    ge0=GE43
  Else
    ge0=GE44
  EndIf
EndSub

Sub sFieldB3 '黒の3 入力:ge0 出力:ge0
  If ge0=0 Then
    ge0=GE3
  ElseIf ge0=GE4 then
    ge0=GE43
  ElseIf ge0=GE44 then
    ge0=GE44
  Else
    ge0=GE6
  EndIf
EndSub

Sub sFieldW3 '白の3 入力:ge0 出力:ge0
  If ge0=0 Then
    ge0=GE3
  ElseIf ge0=GE3 or ge0=GE33 then
    ge0=GE33
  ElseIf ge0=GE4 or ge0=GE43 then
    ge0=GE43
  Else
    ge0=GE44
  EndIf
EndSub

Sub sFieldB8 '黒の場 入力:gz0 出力:ge0
  ge0=0
  For d2=1 To 4
    m0=1
    z1=gz0+GDIR[d2]
    While gbd[z1]=GEBLACK
      m0=m0+1
      z1=z1+GDIR[d2]
    EndWhile
    z2=gz0-GDIR[d2]
    While gbd[z2]=GEBLACK
      m0=m0+1
      z2=z2-GDIR[d2]
    EndWhile
    If gbd[z1]>GEWHITE Then
      m1=0
      z3=z1+GDIR[d2]
      While gbd[z3]=GEBLACK
        m1=m1+1
        z3=z3+GDIR[d2]
      EndWhile
    Else
      m1=-255
    EndIf
    If gbd[z2]>GEWHITE Then
      m2=0
      z4=z2-GDIR[d2]
      While gbd[z4]=GEBLACK
        m2=m2+1
        z4=z4-GDIR[d2]
      EndWhile
    Else
      m2=-255
    EndIf
    If m0=1 Then
      If m1=2 And m2=0 Then
        sFieldB3()
      ElseIf m1=0 And m2=2 then
        sFieldB3()
      ElseIf m1=3 And m2=3 then
        ge0=GE6
      ElseIf m1=3 or m2=3 then
        sFieldB4()
      EndIf
    ElseIf m0=2 then
      If m1=1 And m2=0 Then
        sFieldB3()
      ElseIf m1=0 And m2=1 then
        sFieldB3()
      ElseIf m1=2 And m2=2 then
        ge0=GE6
      ElseIf m1=2 or m2=2 then
        sFieldB4()
        EndIf
    ElseIf m0=3 then
      If m1=0 And m2=0 Then
        sFieldB3()
      ElseIf m1=1 And m2=1 then
        ge0=GE6
      ElseIf m1=1 or m2=1 then
        sFieldB4()
      EndIf
    ElseIf m0=4 then
      If m1=0 And m2=0 Then
        sFieldB44()
      ElseIf m1=0 or m2=0 then
        sFieldB4()
      EndIf
    ElseIf m0=5 then
      ge0=GE5
      Goto End_sFieldB8
    Else
      ge0=GE6
    EndIf
  EndFor
  End_sFieldB8:
EndSub

Sub sFieldW8 '白の場 入力:gz0 出力:ge0
  ge0=0
  For d2=1 To 4
    m0=1
    z1=gz0+GDIR[d2]
    while gbd[z1]=GEWHITE
      m0=m0+1
      z1=z1+GDIR[d2]
    EndWhile
    z2=gz0-GDIR[d2]
    while gbd[z2]=GEWHITE
      m0=m0+1
      z2=z2-GDIR[d2]
    EndWhile
    If gbd[z1]>GEWHITE Then
      m1=0
      z3=z1+GDIR[d2]
      while gbd[z3]=GEWHITE
        m1=m1+1
        z3=z3+GDIR[d2]
      EndWhile
    Else
      m1=-255
    EndIf
    If gbd[z2]>GEWHITE Then
      m2=0
      z4=z2-GDIR[d2]
      while gbd[z4]=GEWHITE
        m2=m2+1
        z4=z4-GDIR[d2]
      EndWhile
    Else
      m2=-255
    EndIf
    If m0=1 Then
      If (m1=2 And m2=0) Or (m1=0 And m2=2) Then
        sFieldW3()
      ElseIf m1>=3 and m2>=3 then
        ge0=GE44
      ElseIf m1>=3 or m2>=3 then
        sFieldW4()
      EndIf
    ElseIf m0=2 then
      If (m1=1 And m2=0) Or (m1=0 And m2=1) Then
        sFieldW3()
      ElseIf m1>=2 and m2>=2 then
        ge0=GE44
      ElseIf m1>=2 or m2>=2 then
        sFieldW4()
      EndIf
    ElseIf m0=3 then
      If m1=0 And m2=0 Then
        sFieldW3()
      ElseIf m1>=1 and m2>=1 then
        ge0=GE44
      ElseIf m1>=1 or m2>=1 then
        sFieldW4()
      EndIf
    ElseIf m0=4 then
      If m1=0 And m2=0 Then
        ge0=GE44
      ElseIf m1=0 or m2=0 then
        sFieldW4()
      EndIf
    Else
      ge0=GE5
      Goto End_sFieldW8
    EndIf
  EndFor
  End_sFieldW8:
EndSub

Sub sSetField '場を設定する。 入力:gi0,gj0 出力:gbd[]
  For j2=gj0-2 To gj0+2
    For i2=gi0-2 To gi0+2
      z2=(GLN+4)*(j2+1)+i2+2
      If gbd[z2]=GEOUT Then
        gbd[z2]=GEFIELD
      EndIf
    EndFor
  EndFor
EndSub

Sub sDebugNode 'ノードを表示する。
  
  gtext0=""
  For n9=1 To gn0
    gtext0=gtext0+"("
    'For k2=1 To 8
    For k2=1 To 4
      gtext0=gtext0+gnd[k2][n9]+","
    EndFor
    gtext0=gtext0+gnd[9][n9]+")"
    sMessage()
  EndFor
EndSub

Sub sAddNode 'ノードを追加する。 入力:gnd[][],gn0,gnb6,gnl6,gz0,gt0,gp0 出力:gnd[][],gn0
  gn0=gn0+1
  gnd[GNF][gn0]=0 'Forward
  gnd[GNB][gn0]=gnb6 'Back
  gnd[GNR][gn0]=0 'Right
  gnd[GNL][gn0]=gnl6 'Left
  gnd[GNZ][gn0]=gz0 'z
  gnd[GNT][gn0]=gt0 'number(Turn)
  gnd[GNP][gn0]=gp0 'Player
  gnd[GNE][gn0]=0 'value
  if gbd[gz0]=GESIGNA then
    ns6=gsign8
  Else
    ns6=""
  EndIf
  gnd[GNS][gn0]=ns6 'sign
  If gnb6<>0 Then
    gnd[GNF][gnb6]=gn0
  EndIf
  If gnl6<>0 Then
    gnd[GNR][gnl6]=gn0
  EndIf
EndSub

Sub sTraceNode 'ノードを追跡する。 入力:gnd[][],gn0,gn1,gz0,gt0,gp0 出力:gnd[][],gn0,gnb6
  If gn1=0 Then '局面が新しい。
    If gt0>0 then
      If gn0=0 Then '初手(ノードが無い。)
        gt0=1
        gp0=GEBLACK
        gnb6=0
        gnl6=0
        sAddNode()
        gnb6=1
        Goto End_sTraceNode
      ElseIf gnd[GNZ][1]=gz0 then
        gnb6=1
        Goto End_sTraceNode
      EndIf
    Else
      If gn0=0 Then '初手(ノードが無い。) 
        gnb6=0
        gnl6=0
        sAddNode()
        gnb6=1
        Goto End_sTraceNode
      ElseIf gnd[GNZ][1]=gz0 then
        gnb6=1
        Goto End_sTraceNode
      EndIf
    EndIf
  endif
  nf6=gnd[GNF][gn1]
  If nf6=0 Then '次の手の候補が無い。 forward
    gnb6=gn1
    gnl6=0
    sAddNode()
    gnb6=gn0
    Goto End_sTraceNode
  endif
  
  If gnd[GNT][nf6]=0 Then '次が記号の場合、最後の記号の後に追加する。
    While nf6<>0
      nb6=nf6
      nf6=gnd[GNF][nf6]
    EndWhile
    gnb6=nb6
    gnl6=0
    sAddNode()
    gnb6=gn0
    Goto End_sTraceNode
  EndIf
  
  If gnd[GNZ][nf6]=gz0 Then
    gnb6=nf6
    Goto End_sTraceNode
  endif
  nr6=nf6
  nf6=gnd[GNR][nf6]
  While nf6<>0
    If gnd[GNZ][nf6]=gz0 then
      gnb6=nf6
      Goto End_sTraceNode
    EndIf
    nr6=nf6
    nf6=gnd[GNR][nf6]
  EndWhile
  gnb6=0 '次の手の候補を追加する。right
  gnl6=nr6
  sAddNode()
  gnb6=gn0
  
  End_sTraceNode:
EndSub

Sub sMessage '盤の下にメッセージを表示する。 入力:gtext0 出力:画面
  l2=Text.GetLength(gtext0)
  if l2>GMLEN Then
    gtext0=Text.GetSubText(gtext0,l2-GMLEN+1,GMLEN)
  EndIf
  Shapes.SetText(oMessage1,gtext0)
  'Sound.PlayBellRing()
EndSub

Sub sDispStone '石(次の手)を表示する。(Z) 入力:gz8,gc8 出力:画面
  If gxyz=0 Then
    i2=gi0
    j2=gj0
  else
    i2=math.Remainder(gz8-1,GLN+4)-1
    j2=math.Ceiling((gz8-1)/(GLN+4))-2
  endif
  GraphicsWindow.brushcolor=gcol0
  oStone[i2][j2]=Shapes.AddEllipse(GWD,GWD)
  Shapes.HideShape(oStone[i2][j2])
  Shapes.Move(oStone[i2][j2],GWD*(i2-1/2),GWD*(j2-1/2))
  Shapes.ShowShape(oStone[i2][j2])
EndSub

Sub sDispNum '番号を表示する。 入力:gi0,gj0,gp0,gt0 出力:画面
  If gt0<10 Then
    iDif2=1/4
  ElseIf gt0<100 then
    iDif2=1/3
  Else
    iDif2=1/2
  EndIf
  GraphicsWindow.brushcolor=GCOL[GEBW-gp0]
  oNum[gi0][gj0]=shapes.AddText("")
  Shapes.SetText(oNum[gi0][gj0],gt0)
  Shapes.Move(oNum[gi0][gj0],GWD*(gi0-iDif2),GWD*(gj0-1/3))
EndSub

Sub sDispSignA '記号(A)を表示する。(Z) 入力:gz8,gsign8 出力:画面
  If gxyz=0 Then
    i2=gi0
    j2=gj0
  else
    i2=math.Remainder(gz8-1,GLN+4)-1
    j2=math.Ceiling((gz8-1)/(GLN+4))-2
  endif
  if Text.GetLength(gsign8)>2 Then
    sign2=Text.GetSubText(gsign8,1,2)
    gtext0=gsign8
    sMessage()
  Else
    sign2=gsign8
  endif
  GraphicsWindow.brushcolor=GCOL[1]
  oNum[i2][j2]=shapes.AddText("")
  Shapes.SetText(oNum[i2][j2],sign2)
  Shapes.Move(oNum[i2][j2],GWD*(i2-1/3),GWD*(j2-1/3))
EndSub

Sub sDispNext '次の手を表示する。入力:gnd[],gn1 出力:画面
  gButton=1 'ボタンを有効にする。
  If gt0=0 Then
    Goto End_sDispNext
  EndIf
  If gn1=0 Then
    Goto End_sDispNext
  EndIf
  nfr2=gnd[GNF][gn1]
  If nfr2=0 Then
    Goto End_sDispNext
  endif
  
  While nfr2<>0
    gz8=gnd[GNZ][nfr2]
    p8=gnd[GNP][nfr2]
    if p8=GEBLACK Or p8=GEWHITE then
      gButton=0 'ボタンを無効にする。
      gxyz=1
      gcol0=GCOL[3] '次の手の色を設定する。
      sDispStone()
      
      nfr2=gnd[GNR][nfr2]
      While nfr2<>0
        gz8=gnd[GNZ][nfr2]
        gxyz=1
        gcol0=GCOL[3] '次の手の色を設定する。
        sDispStone()
        nfr2=gnd[GNR][nfr2]
      EndWhile
      Goto End_sDispNext
    ElseIf p8=GESIGNA then
      gt0=0
      gsign8=gnd[GNS][nfr2]
      gbd[gnd[GNZ][nfr2]]=p8
      gxyz=1
      sDispSignA()
    elseif p8=GESIGNB or p8=GESIGNW then
      gt0=0
      gbd[gnd[GNZ][nfr2]]=p8
      gxyz=1
      gcol0=GCOL[p8-GESIGNA]
      sDispStone()
    endif
    nfr2=gnd[GNF][nfr2]
  endwhile
  
  End_sDispNext:
EndSub

Sub sEraseNext '次の手を消去する。入力:gnd[],gn1 出力:画面
  'If gt0=0 Then '石が置かれていない。
    'Goto End_sEraseNext
  'EndIf
  If gn1=0 Then '石が置かれていない。
    Goto End_sEraseNext
  EndIf
  nfr2=gnd[GNF][gn1]
  If nfr2=0 Then
    Goto End_sEraseNext
  endif
  
  While nfr2<>0
    z2=gnd[GNZ][nfr2]
    i2=math.Remainder(z2-1,GLN+4)-1
    j2=math.Ceiling((z2-1)/(GLN+4))-2
    Shapes.remove(oStone[i2][j2])
    Shapes.remove(oNum[i2][j2])
    p2=gnd[GNP][nfr2]
    If p2=GEBLACK Or p2=GEWHITE Then
      Goto Right_sEraseNext
    Else
      nfr2=gnd[GNF][nfr2]
    EndIf
  EndWhile
  
  Right_sEraseNext:
  nfr2=gnd[GNR][nfr2]
  While nfr2<>0
    z2=gnd[GNZ][nfr2]
    i2=math.Remainder(z2-1,GLN+4)-1
    j2=math.Ceiling((z2-1)/(GLN+4))-2
    Shapes.remove(oStone[i2][j2])
    Shapes.remove(oNum[i2][j2])
    nfr2=gnd[GNR][nfr2]
  EndWhile
  
  End_sEraseNext:
EndSub

Sub sDispPut '石を置く。
  gxyz=0
  gcol0=GCOL[gp0]
  sDispStone()
  sDispNum()
  If gp0=GEBLACK Then
    sFieldB8()
  Else
    sFieldW8()
  EndIf
  'gtext0=gtext0+GROLE[gp0]+":"+GROLE[ge0]
  'sMessage()
  'If ge0=GE5 Then
  '  gtext0=gtext0+GROLE[gp0]+"の勝ち"
  '  sMessage()
  'ElseIf ge0=GE6 then
  '  gtext0=gtext0+"黒の禁手です。白の勝ち"
  '  sMessage()
  'EndIf
EndSub

Sub sWriteNode 'ノードを書き込む。
  Controls.SetButtonCaption(bWrite,"↓書込み")
  If gn0>0 then
    For n2=1 To gn0
      buf2=""
      For k2=1 to 8
        buf2=buf2+gnd[k2][n2]+","
      EndFor
      buf2=buf2+gnd[9][n2]
' The following line could be harmful and has been automatically commented.
'       File.WriteLine(GFILENODE,n2,buf2)
    EndFor
  EndIf
  Controls.SetButtonCaption(bWrite,"書込み")
EndSub

Sub sSplitLine '行を分割する。
  if Text.GetLength(gLine9)>0 Then
    i2=Text.GetIndexOf(gLine9,",")
    j2=1
    While i2>0
      gnd[j2][gn9]=Text.GetSubText(gLine9,1,i2-1)
      gtext0=gtext0+gnd[j2][gn9]+","
      sMessage()
      gLine9=Text.GetSubTextToEnd(gLine9,i2+1)
      j2=j2+1
      i2=Text.GetIndexOf(gLine9,",")
    Endwhile
    gnd[j2][gn9]=gLine9
    gtext0=gtext0+gnd[j2][gn9]+","
    sMessage()
  EndIf
EndSub

Sub sReadNode 'ノードを読み込む。
  Controls.SetButtonCaption(bRead,"↓読込み")
  gn9=1
  Loop_sRead:
' The following line could be harmful and has been automatically commented.
'     gLine9=File.ReadLine(GFILENODE,gn9)
    If gLine9="" then
      goto End_sRead
    EndIf
    sSplitLine()
    gn9=gn9+1
  Goto Loop_sRead
  End_sRead:
  gn0=gn9-1
  gn1=0
  gt0=1
  gp0=GEBLACK
  Controls.SetButtonCaption(bRead,"読込み")
EndSub

Sub sAlternate 'ボタン名を戻す。
  if gp0=GESIGNB then
    Controls.SetButtonCaption(bBlack,"黒")
  elseif gp0=GESIGNW then
    Controls.SetButtonCaption(bWhite,"白")
  elseif gp0=GESIGNA then
    Controls.SetButtonCaption(bSign,"記号")
  endif
EndSub

Sub sInitGame
  Controls.SetButtonCaption(bInit,"↓初期化")
  sInitBoard()
  sDispBoard()
  gn1=0
  gt0=1
  gp0=GEBLACK
  Controls.SetButtonCaption(bInit,"初期化")
EndSub

Sub eMouseSign
  If gn0=0 Then
    Goto End_eMouseSign '初手に記号を置けない。
  EndIf
  If gbd[gz0]<>GEFIELD and gbd[gz0]<>GEOUT Then
    sDelSign() '記号(Aまたは石)を消去する。
    Goto End_eMouseSign
  EndIf
  If gp0=GESIGNA Then
    gsign8=Controls.GetTextBoxText(bContent)
    If gsign8="" Then
      Goto End_eMouseSign
    EndIf
    gbd[gz0]=gp0
    gxyz=0
    sDispSignA() '記号(A)を置く。
  ElseIf gp0=GESIGNB or gp0=GESIGNW then
    gbd[gz0]=gp0
    gxyz=0
    gcol0=GCOL[gp0-GESIGNA]
    sDispStone() '記号(石)を置く。
  EndIf
  sTraceNode()
  gn1=gnb6
  sDebugNode()
  
  End_eMouseSign:
EndSub

Sub sClearNode 'ノードを消去する。
  z2=gnd[GNZ][gn3]
  If z2=0 Then 'チェックする。
    Goto End_sClearNode
  endif
  p2=gbd[z2]
  If p2<>GEBLACK And p2<>GEWHITE Then
    Goto End_sClearNode
  endif
  gbd[z2]=GEFIELD '盤の石のデータを消去する。
  i2=math.Remainder(z2-1,GLN+4)-1 '盤の石を消去する。
  j2=math.Ceiling((z2-1)/(GLN+4))-2
  Shapes.remove(oNum[i2][j2])
  Shapes.remove(oStone[i2][j2])
  For k2=1 To 8 'ノードのデータを消去する。
    gnd[k2][gn3]=0
  endfor
  gnd[9][gn3]=""
  If gn3=gn0 Then 'ノード数(gn0)を削減する。
    gn0=gn0-1
    While gnd[GNZ][gn0]=0
    gn0=gn0-1
    if gn0<1 Then
      Goto End_sClearNode
    EndIf
  EndWhile
  EndIf
  
  End_sClearNode:
  'sInitGame()
  'sDebugNode()
EndSub

Sub sDelStone '石を消去する。
  n2=gn1　'最後に表示された石のノード(gn1)
  If n2=0 Then
    Goto End_sDelStone
  EndIf
  z2=gnd[GNZ][n2] 'クリックした石のノード(n2)を求める。Back
  while z2<>gz0
    n2=gnd[GNB][n2]
    If n2=0 Then
      Goto End_sDelStone
    EndIf
    z2=gnd[GNZ][n2]
  EndWhile
  n3=n2 '次のノードが無い最後のノード(n3)を求める。Forward
  
  F_sDelStone:
  nf2=gnd[GNF][n3]
  While nf2<>0
    n3=nf2
    nf2=gnd[GNF][nf2]
  EndWhile
  nb2=gnd[GNB][n3]
  nr2=gnd[GNR][n3]
  nl2=gnd[GNL][n3]
  If nb2=0 then
    sInitGame()
    Goto End_sDelStone
  EndIf
  If nl2=0 then
    gnd[GNF][nb2]=nr2
    If nr2=0 Then
      n4=nb2 '消去する予定のノード
    else
      gnd[GNB][nr2]=nb2
      gnd[GNL][nr2]=0
      n4=nr2 '消去する予定のノード
    EndIf
  else
    'gnd[GNR][nl2]=nr2
    If nr2=0 Then
      gnd[GNR][nl2]=nr2
      n4=nl2 '消去する予定のノード
      
      gtext0="test"
      sMessage()
      Goto End_sDelStone
      gn3=n3
      sClearNode() 'n3ノードを消去する。
      n3=n4
      Goto End_sDelStone
    
    else
      gnd[GNR][nl2]=nr2
      gnd[GNL][nr2]=nl2
      n4=nr2 '消去する予定のノード
    EndIf
    'n4=nl2 '消去する予定のノード
    'n4=nr2 '消去する予定のノード
  EndIf
  gn3=n3
  sClearNode() 'n3ノードを消去する。
  If n3<>n2 Then
    n3=n4
    Goto F_sDelStone
  EndIf
    
  End_sDelStone:
endsub

Sub sDelSign '記号(Aまたは石)を消去する。
  If gn1>0 then
    nb2=gnd[GNB][gn1]
    If nb2=0 Then
      nb2=1
    endif
  Else
    nb2=1
  EndIf
  nf2=nb2
  While nf2<>0
    If gnd[GNZ][nf2]=gz0 Then
      goto Del_sDelSign
    endif
    nb2=nf2
    nf2=gnd[GNF][nf2]
  EndWhile
  Goto End_sDelSign
  
  Del_sDelSign:
  nf3=gnd[GNF][nf2]
  gnd[GNF][nb2]=nf3
  If nf3<>0 then
    gnd[GNB][nf3]=nb2
  endif
  gnd[GNZ][nf2]=0
  gnd[GNB][nf2]=0
  gnd[GNF][nf2]=0
  gnd[GNR][nf2]=0
  gnd[GNL][nf2]=0
  gnd[GNP][nf2]=0
  gnd[GNS][nf2]=""
  If gi0<>0 Then
    i2=gi0
    j2=gj0
  else
    i2=math.Remainder(gz0-1,GLN+4)-1
    j2=math.Ceiling((gz0-1)/(GLN+4))-2
  endif
  p2=gbd[gz0]
  If p2=GESIGNA Then
    Shapes.remove(oNum[i2][j2]) '記号(A)を消去する。
  elseIf p2=GESIGNB Or p2=GESIGNW Then
    Shapes.remove(oStone[i2][j2]) '記号(石)を消去する。
  endif
  gbd[gz0]=GEFIELD
  
  End_sDelSign:
endsub

Sub sDispBoard '盤を表示する。 出力:画面
  GraphicsWindow.Clear()
  GraphicsWindow.Title = "連珠"
  GraphicsWindow.Height = GWD*(GLN+2)
  GraphicsWindow.Width = GWD*(GLN+1)
  GraphicsWindow.FontSize=GWD/2
  GraphicsWindow.backgroundcolor=GCOL[4]
  GraphicsWindow.brushcolor=GCOL[1]
  For j2=1 to GLN '線を引く。
    GraphicsWindow.DrawLine(GWD,GWD*j2,GWD*GLN,GWD*j2)
  endfor
  For i2=1 to GLN
    GraphicsWindow.DrawLine(GWD*i2,GWD,GWD*i2,GWD*GLN)
  endfor
  For j2=4 To 12 Step 4 '星を描く。
    For i2=4 To 12 Step 4
      GraphicsWindow.FillEllipse(GWD*(i2-1/8),GWD*(j2-1/8),GWD/4,GWD/4)
    EndFor
  EndFor
  'For i2=1 To 9 '横の座標を書く。
  '  GraphicsWindow.DrawBoundText(GWD*(i2-1/8),0,GWD,i2)
  'endfor
  'For i2=10 To GLN
  '  GraphicsWindow.DrawBoundText(GWD*(i2-1/4),0,GWD,i2)
  'endfor
  'For j2=1 To 9 '縦の座標を書く。
  '  GraphicsWindow.DrawBoundText(GWD/3,GWD*(j2-1/4),GWD,j2)
  'endfor
  'For j2=10 To GLN
  '  GraphicsWindow.DrawBoundText(0,GWD*(j2-1/4),GWD,j2)
  'endfor
  For i2=GLN To 11 Step -1'横の座標を書く。
    GraphicsWindow.DrawBoundText(GWD*(GLN+1-i2-1/8),0,GWD,GDIG[10])
  endfor
  For i2=GLN To 11 Step -1
    GraphicsWindow.DrawBoundText(GWD*(GLN+1-i2-1/8),GWD/2,GWD,GDIG[i2-10])
  endfor
  For i2=10 To 1 Step -1
    GraphicsWindow.DrawBoundText(GWD*(GLN+1-i2-1/8),GWD/2,GWD,GDIG[i2])
  endfor
  For j2=1 To 9 '縦の座標を書く。
    GraphicsWindow.DrawBoundText(GWD*(GLN+1/3),GWD*(j2-1/4),GWD,j2)
  endfor
  For j2=10 To GLN
    GraphicsWindow.DrawBoundText(GWD*GLN,GWD*(j2-1/4),GWD,j2)
  endfor
  oMessage1=shapes.AddText("") 'メッセージを表示する。
  shapes.SetText(oMessage1,"")
  Shapes.Move(oMessage1,GWD,GWD*GLN)
  bEnd=Controls.AddButton("終了",GWD/2,GWD*(GLN+1)) 'ボタンを表示する。
  bWrite=Controls.AddButton("書込み",GWD*2,GWD*(GLN+1))
  bRead=Controls.AddButton("読込み",GWD*4,GWD*(GLN+1))
  bInit=Controls.AddButton("初期化",GWD*6,GWD*(GLN+1))
  bBlack=Controls.AddButton("黒",GWD*8,GWD*(GLN+1))
  bWhite=Controls.AddButton("白",GWD*9,GWD*(GLN+1))
  bContent=Controls.AddTextBox(GWD*10,GWD*(GLN+1))
  Controls.SetSize(bContent,GWD,GWD*3/4)
  Controls.SetTextBoxText(bContent,"A")
  bSign=Controls.AddButton("記号",GWD*23/2,GWD*(GLN+1))
  bDel=Controls.AddButton("削除",GWD*13,GWD*(GLN+1))
EndSub

Sub eClick 'ボタンをクリックした時、処理する。
  iLast=Controls.LastClickedButton
  if iLast=bEnd then
    Program.End()
  elseif iLast=bWrite then
    sWriteNode()
  elseif iLast=bRead then
    sInitGame()
    sReadNode()
  elseIf iLast=bInit Then
    sInitGame()
  elseif iLast=bBlack then
    If gButton=1 then
      gt0=0
      sAlternate()
      gp0=GESIGNB
      Controls.SetButtonCaption(bBlack,"↓黒")
    endif
  elseif iLast=bWhite then
    If gButton=1 then
      gt0=0
      sAlternate()
      gp0=GESIGNW
      Controls.SetButtonCaption(bWhite,"↓白")
    endif
  elseif iLast=bSign then
    If gButton=1 then
      gt0=0
      sAlternate()
      gp0=GESIGNA
      Controls.SetButtonCaption(bSign,"↓記号")
    endif
  elseif iLast=bDel then
    If gButton=1 then
      
      Controls.SetButtonCaption(bSign,"↓削除")
    endif
  EndIf
EndSub

Sub eMouse 'マウス
  gi0=math.ceiling((GraphicsWindow.MouseX+GWD/2)/GWD)-1
  gj0=math.ceiling((GraphicsWindow.MouseY+GWD/2)/GWD)-1
  If gi0<1 or gi0>GLN or gj0<1 or gj0>GLN Then
    Goto End_eMouse
  EndIf
  gz0=(GLN+4)*(gj0+1)+gi0+2
  If gt0>0 Then
    p2=gbd[gz0]
    If p2=GEBLACK Or p2=GEWHITE Then
      sDelStone()
      Goto End_eMouse
    endif
    '次の手を場のみに制限する。
    'If p2<>GEFIELD Then
    '次の手を場と場の外に制限する。
    If p2<>GEFIELD and p2<>GEOUT Then
      Goto End_eMouse
    EndIf
    gbd[gz0]=gp0 '石を置く。
    sSetField() '場を設定する。
    sEraseNext()
    sDispPut()
    sTraceNode()
    gn1=gnb6
    sDebugNode()
    sDispNext()
    If gt0>0 Then
      gt0=gt0+1
      gp0=GEBW-gp0
    Else
      gp0=GESIGNA
      Controls.SetButtonCaption(bSign,"↓記号")
    Endif
  Else
    eMouseSign()
  EndIf
  
  End_eMouse:
EndSub
