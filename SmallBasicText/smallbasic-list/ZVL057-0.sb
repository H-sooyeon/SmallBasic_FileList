' Football 0.2a
' Copyright (c) 2013 Nonki Takahashi.  All rights reserved.
'
' History:
'  0.2a 2013/03/05 Enabled score and paddle. (ZVL057-0)
'  0.1a 2013/03/05 Graphics created. (ZVL057)
'
GraphicsWindow.Title = "Football 0.2a"
GraphicsWindow.BackgroundColor = "DarkBlue"
Num_Init()
VRAM_Init()
CenterLine_Init()
Score_Init()
Timer.Interval = 1000
Timer.Tick = OnTick
GraphicsWindow.KeyDown = OnKeyDown
Sub OnTick
  hour = Clock.Hour
  minute = Clock.Minute
  second = Clock.Second
  s = hour
  player = 1
  Score_Set()
  s = minute
  player = 2
  Score_Set()
  Ball_Set()
EndSub
Sub Paddle_Down
  ' param player - 1 or 2
  If paddleRow[player] < lowRow Then
    iUp = (paddleRow[player] - 1) * 40 + paddleCol[player][1]
    iLow = (paddleRow[player] + 4) * 40 + paddleCol[player][1]
    Shapes.SetOpacity(vram[iUp]["obj"], 0)
    vram[iUp]["on"] = "False"
    Shapes.SetOpacity(vram[iLow]["obj"], 100)
    vram[iLow]["on"] = "True"
    iUp = (paddleRow[player] - 1) * 40 + paddleCol[player][2]
    iLow = (paddleRow[player] + 4) * 40 + paddleCol[player][2]
    Shapes.SetOpacity(vram[iUp]["obj"], 0)
    vram[iUp]["on"] = "False"
    Shapes.SetOpacity(vram[iLow]["obj"], 100)
    vram[iLow]["on"] = "True"
    paddleRow[player] = paddleRow[player] + 1
  EndIf
EndSub
Sub Paddle_Up
  ' param player - 1 or 2
  If upRow < paddleRow[player] Then
    iUp = (paddleRow[player] - 2) * 40 + paddleCol[player][1]
    iLow = (paddleRow[player] + 3) * 40 + paddleCol[player][1]
    Shapes.SetOpacity(vram[iLow]["obj"], 0)
    vram[iLow]["on"] = "False"
    Shapes.SetOpacity(vram[iUp]["obj"], 100)
    vram[iUp]["on"] = "True"
    iUp = (paddleRow[player] - 2) * 40 + paddleCol[player][2]
    iLow = (paddleRow[player] + 3) * 40 + paddleCol[player][2]
    Shapes.SetOpacity(vram[iLow]["obj"], 0)
    vram[iLow]["on"] = "False"
    Shapes.SetOpacity(vram[iUp]["obj"], 100)
    vram[iUp]["on"] = "True"
    paddleRow[player] = paddleRow[player] - 1
  EndIf
EndSub
Sub OnKeyDown
  key = GraphicsWindow.LastKey
  If key = "S" Then     ' player 1 paddle up
    player = 1
    Paddle_Up()
  ElseIf key = "D" Then ' player 1 paddle down
    player = 1
    Paddle_Down()
  ElseIf key = "K" Then ' player 2 paddle up
    player = 2
    Paddle_Up()
  ElseIf key = "L" Then ' player 2 paddle down
    player = 2
    Paddle_Down()
  EndIf
EndSub
Sub Ball_Set
  ' param second - for clock
  If iBall > 0 Then
    obj = vram[iBall]["obj"]
    Shapes.SetOpacity(obj, 0)
  EndIf
  If 0 <= second And second < 15 Then
    col = centerCol + (paddleCol[2][2] - 1 - centerCol) * second / 15
    row = topRow + 1 + (centerRow - topRow - 1) * second / 15
  ElseIf 15 <= second And second < 30 Then
    col = paddleCol[2][2] - 1 - (paddleCol[2][2] - 1 - centerCol) * (second - 15) / 15
    row = centerRow + (bottomRow - 1 - centerRow) * (second - 15) / 15
  ElseIf 30 <= second And second < 45 Then
    col = centerCol - (centerCol - paddleCol[1][1] - 1) * (second - 30) / 15
    row = bottomRow - 1 - (bottomRow - 1 - centerRow) * (second - 30) / 15
  ElseIf 45 <= second And second < 60 Then
    col = paddleCol[1][1] + 1 + (centerCol - paddleCol[1][1] - 1) * (second - 45) / 15
    row = centerRow - (centerRow - topRow - 1) * (second - 45) / 15
  EndIf
  iBall = (Math.Round(row) - 1) * 40 + Math.Round(col)
  obj = vram[iBall]["obj"]
  Shapes.SetOpacity(obj, 100)
EndSub
Sub Num_Init
  num[0][1] = "***"
  num[0][2] = "* *"
  num[0][3] = "* *"
  num[0][4] = "* *"
  num[0][5] = "***"
  num[1][1] = "  *"
  num[1][2] = "  *"
  num[1][3] = "  *"
  num[1][4] = "  *"
  num[1][5] = "  *"
  num[2][1] = "***"
  num[2][2] = "  *"
  num[2][3] = "***"
  num[2][4] = "*  "
  num[2][5] = "***"
  num[3][1] = "***"
  num[3][2] = "  *"
  num[3][3] = "***"
  num[3][4] = "  *"
  num[3][5] = "***"
  num[4][1] = "* *"
  num[4][2] = "* *"
  num[4][3] = "***"
  num[4][4] = "  *"
  num[4][5] = "  *"
  num[5][1] = "***"
  num[5][2] = "*  "
  num[5][3] = "***"
  num[5][4] = "  *"
  num[5][5] = "***"
  num[6][1] = "***"
  num[6][2] = "*  "
  num[6][3] = "***"
  num[6][4] = "* *"
  num[6][5] = "***"
  num[7][1] = "***"
  num[7][2] = "  *"
  num[7][3] = "  *"
  num[7][4] = "  *"
  num[7][5] = "  *"
  num[8][1] = "***"
  num[8][2] = "* *"
  num[8][3] = "***"
  num[8][4] = "* *"
  num[8][5] = "***"
  num[9][1] = "***"
  num[9][2] = "* *"
  num[9][3] = "***"
  num[9][4] = "  *"
  num[9][5] = "***"
  num[" "][1] = "   "
  num[" "][2] = "   "
  num[" "][3] = "   "
  num[" "][4] = "   "
  num[" "][5] = "   "
EndSub
Sub Score_Init
  For player = 1 To 2
    score[player]["score"] = 0
    s = score[player]["score"]
    len = Text.GetLength(s)
    If len = 1 Then
      s = " " + s
    EndIf
    For o = 2 To 1 Step -1
      d = Text.GetSubText(s, 3 - o, 1)
      For i = 1 To 15
        j = (o - 1) * 15 + i
        col = Math.Remainder(i - 1, 3) + 1
        row = Math.Floor((i - 1) / 3) + 1
        x = x0 + (sizeX + 1) * (10 + 12 * (player - 1) + 4 * (2 - o) + col - 1)
        y = y0 + (sizeY + 2) * (2 + row - 1)
        obj = Shapes.AddRectangle(sizeX, sizeY)
        score[player]["obj" + j] = obj
        Shapes.Move(obj, x, y)
        If Text.GetSubText(num[d][row], col, 1) = " " Then
          Shapes.SetOpacity(obj, 0)
        EndIf
      EndFor
    EndFor
  EndFor
EndSub
Sub CenterLine_Init
  col = centerCol
  For row = 3 To 21 Step 2
    y = y0 + (row - 1) * (sizeY + 2)
    i = (row - 1) * 40 + col
    x = x0 + (col - 1) * (sizeX + 1)
    obj = Shapes.AddRectangle(sizeX, sizeY)
    Shapes.Move(obj, x, y)
  EndFor
EndSub
Sub Score_Set
  ' param player - 1 or 2
  ' param s - score
  score[player]["score"] = s
  len = Text.GetLength(s)
  If len = 1 Then
    s = " " + s
  EndIf
  For o = 2 To 1 Step -1
    d = Text.GetSubText(s, 3 - o, 1)
    For i = 1 To 15
      j = (o - 1) * 15 + i
      col = Math.Remainder(i - 1, 3) + 1
      row = Math.Floor((i - 1) / 3) + 1
      obj = score[player]["obj" + j]
      If Text.GetSubText(num[d][row], col, 1) = " " Then
        Shapes.SetOpacity(obj, 0)
      Else
        Shapes.SetOpacity(obj, 100)
      EndIf
    EndFor
  EndFor
EndSub
Sub VRAM_Init
  init[0]  = "----+----1----+----2----+----3----+----4"
  init[1]  = " *************************************  "
  init[2]  = " *                                   *  "
  init[3]  = " *                                   *  "
  init[4]  = " *                                   *  "
  init[5]  = " *                                   *  "
  init[6]  = " *                                   *  "
  init[7]  = "                                        "
  init[8]  = "                                        "
  init[9]  = "                                        "
  init[10] = "           *                       *    "
  init[11] = "   *       *               *       *    "
  init[12] = "   *       *               *       *    "
  init[13] = "   *       *               *       *    "
  init[14] = "   *       *               *       *    "
  init[15] = "   *                       *            "
  init[16] = "                                        "
  init[17] = "                                        "
  init[18] = " *                                   *  "
  init[19] = " *                                   *  "
  init[20] = " *                                   *  "
  init[21] = " *                                   *  "
  init[22] = " *                                   *  "
  init[23] = " *************************************  "
  init[24] = "                                        "
  centerCol = 20
  centerRow = 12
  goalCol[1] = 2  ' left end
  goalCol[2] = 38 ' right end
  topRow = 1      ' top end
  bottomRow = 23  ' bottom end
  goalUpRow = 7   ' upper row of goal
  goalLowRow = 17 ' lower row of goal
  upRow = 6       ' upper end for paddle
  lowRow = 14     ' lower end for paddle
  paddleCol[1][1] = 4
  paddleCol[1][2] = 28
  paddleRow[1] = 11
  paddleCol[2][1] = 12
  paddleCol[2][2] = 36
  paddleRow[2] = 10
  sizeX = 12
  sizeY = 15
  x0 = (GraphicsWindow.Width - (sizeX + 1) * 40) / 2
  y0 = (GraphicsWindow.Height - (sizeY + 2) * 24) / 2
  GraphicsWindow.PenWidth = 2
  GraphicsWindow.PenColor = "#7F7FC5"
  GraphicsWindow.BrushColor = "#F2F2F9"
  For row = 1 To 24
    y = y0 + (row - 1) * (sizeY + 2)
    For col = 1 To 40
      i = (row - 1) * 40 + col
      x = x0 + (col - 1) * (sizeX + 1)
      obj = Shapes.AddRectangle(sizeX, sizeY)
      vram[i]["obj"] = obj
      If Text.GetSubText(init[row], col, 1) = " " Then
        vram[i]["on"] = "False"
        Shapes.SetOpacity(obj, 0)
      Else
        vram[i]["on"] = "True"
      EndIf
      Shapes.Move(obj, x, y)
    EndFor
  EndFor
EndSub
      
