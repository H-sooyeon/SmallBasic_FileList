Initialise()

While 0=0
  'UPDATE KEYBOARD INPUT
  If Text.IsSubText(tempWord, letter) Then
    Sound.Stop(host + "Click.mp3")
    Sound.Play(host + "Click.mp3")  
    For i = 1 To Text.GetLength(tempWord)
      If letter = Text.GetSubText(tempWord, i, 1) Then
        result = Text.GetSubText(result, 1, i *2 -2) + letter + Text.GetSubTextToEnd(result, i *2)
        tempWord = Text.GetSubText(tempWord, 1, i -1) + "#" + Text.GetSubTextToEnd(tempWord, i + 1)
      EndIf
    EndFor
  ElseIf Text.IsSubText(used + result, letter) Then
    Sound.Stop(host + "Error.mp3")
    Sound.Play(host + "Error.mp3")
    GraphicsWindow.ShowMessage("Try again", "Already USED!")    
    Sound.Stop(host + "Ok.mp3")
    Sound.Play(host + "Ok.mp3") 
  ElseIf letter <> "" Then
    Sound.Stop(host + "Click.mp3")
    Sound.Play(host + "Click.mp3")    
    tries = tries - 1
    used = used + letter
    GraphicsWindow.DrawImage(hangman[6-tries], 0, 0)
  EndIf
  letter = "" 
  
  Shapes.SetText(displayProgress, tries + LF + used + LF + LF + result)
  
  'UPDATE SCORE
  If Text.IsSubText("#################################", tempWord) Then
    won = won + 1
    Sound.Stop(host + "Cheering.mp3")
    Sound.Play(host + "Cheering.mp3")
    Shapes.SetText(displayScore, Text.Append(Text.Append(won, "/" + wordCount + "  "), Math.Round(won / wordCount * 100) + "%"))    
    GraphicsWindow.ShowMessage("Next word", "You WIN!")
    Sound.Stop(host + "Ok.mp3")
    Sound.Play(host + "Ok.mp3") 
    LoadWord()
  ElseIf tries = 0 Then
    Sound.Stop(host + "Pain.mp3")
    Sound.Play(host + "Pain.mp3")
    Shapes.SetText(displayScore, Text.Append(Text.Append(won, "/" + wordCount + "  "), Math.Round(won / wordCount * 100) + "%"))    
    GraphicsWindow.ShowMessage("Solution: " + Text.ConvertToUpperCase(word[wordCount]) + LF + "Next word", "You LOOSE!")
    Sound.Stop(host + "Ok.mp3")
    Sound.Play(host + "Ok.mp3")  
    LoadWord()
  EndIf
  
  Program.Delay(62)
EndWhile

Sub LoadWord  
  wordCount = wordCount + 1
  tempWord = Text.ConvertToUpperCase(word[wordCount])
  result = Text.GetSubText("_ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ ", 1, Text.GetLength(tempWord) *2)
  tries = 6
  used = ""
  GraphicsWindow.DrawImage(hangman[0], 0, 0)
EndSub

Sub Initialise  
  LF = Text.GetCharacter(10)
  GraphicsWindow.CanResize = "False"
  GraphicsWindow.Width =  508
  GraphicsWindow.Height = 264  
  GraphicsWindow.Left = (Desktop.Width - GraphicsWindow.Width) / 2
  GraphicsWindow.Top = (Desktop.Height - GraphicsWindow.Height) / 2
  GraphicsWindow.Title = "Hangman" 
  
  GraphicsWindow.DrawText(100, GraphicsWindow.Height /2, "Downloading images and data! Please wait...")  
  host = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Hangman/assets/"   ''host = Program.Directory + "\images\"  
  
  Sound.Stop(host + "Click.mp3")  'PRIMING SOUND FX (not validated) 
  Sound.Stop(host + "Error.mp3")
  Sound.Stop(host + "Ok.mp3")  
  Sound.Stop(host + "Pain.mp3")  
  Sound.Stop(host + "Cheering.mp3")
  
  'D/LOADS & HANDLING VALIDATION
' The following line could be harmful and has been automatically commented.
'   temp = File.ReadContents(Network.DownloadFile(host + "words.txt"))  ''temp = File.ReadContents(host + "words.txt")    
  For i = 0 To 6    
    hangman[i] = ImageList.LoadImage(Text.Append(host, "hangman[" + i + "].png"))
    If ImageList.GetWidthOfImage(hangman[i]) = 0 Or temp = "" Then      
      GraphicsWindow.ShowMessage("A network error has occured!" + LF + "Close and try again.", "NETWORK ERROR!")
      Program.End()
    EndIf
  EndFor 
  
  'RANDONISE WORD LIST
  While temp <> ""
    i = i + 1
    tempWords[i] = Text.GetSubText(temp, 1, Text.GetIndexOf(temp, Text.GetCharacter(13)) -1)
    temp = Text.GetSubTextToEnd(temp, Text.GetIndexOf(temp, Text.GetCharacter(13)) + 2)  
  EndWhile  
  For j = i To 1 Step -1
    getAll = Array.GetAllIndices(tempWords)
    i = getAll[Math.GetRandomNumber(j)]  
    word[j] = tempWords[i]
    tempWords[i] = ""  
  EndFor  
  
  'ADD VIRTUAL KEYBOARD
  For i = 1 To 3
    For j = 1 To 11 - i      
      btn[Array.GetItemCount(btn)+1] = Controls.AddButton(Text.GetSubText("QWERTYUIOPASDFGHJKLZXCVBNM'", Array.GetItemCount(btn)+1, 1), 10+ (i-1) * (32/2) + 32 * (j-1), 152 + (i-1) * 32)
      Controls.SetSize(btn[Array.GetItemCount(btn)], 27, 27)      
    EndFor
  EndFor 
  
  'ADD  PROGRESS & SCORE DISPLAYS
  GraphicsWindow.FontSize = 18
  GraphicsWindow.FontName = "Comic Sans MS"
  GraphicsWindow.BrushColor = "Black"
  displayProgress = Shapes.AddText("")
  GraphicsWindow.FontSize = 14
  displayScore = Shapes.AddText("")
  Shapes.Move(displayProgress, 60, 5)
  Shapes.Move(displayScore, 390, 237)  
  
  'INITIALISE PROGRESS & SCORE DISPLAYS
  won = 0  
  Shapes.SetText(displayScore, Text.Append(Text.Append(0, "/" + 0 + "  "), Math.Round(0 / 0 * 100) + "%"))
  LoadWord()  
  
  Controls.ButtonClicked = OnButtonClicked  
EndSub

Sub OnButtonClicked 
  letter = Controls.GetButtonCaption(Controls.LastClickedButton)  
EndSub
