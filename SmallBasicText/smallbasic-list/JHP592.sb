'________________________________________________________'
' Grade Average Example (v1.0)
' by GoToLoop (2013/Jul)

' http://social.msdn.microsoft.com/Forums/en-US
'/b78d5626-5827-4277-8cbc-c996ec97b91c/intro-to-programming-lab
'________________________________________________________'

'________________________________________________________'
' Text Screen Init:
TextWindow.Title = "Grade Average"
TextWindow.Left = (Desktop.Width  - 80*8)   / 2
TextWindow.Top = (Desktop.Height - 26*12) / 2

' Special ASCII codes:
LF   =  Text.GetCharacter(10)   ' Line-Feed (jumps a line)
QUO = Text.GetCharacter(34)  ' Double Quotation Mark
ARW = Text.GetCharacter(26)  ' Right Arrow
BEL = Text.GetCharacter(7)      ' Makes internal PC-Speaker buzz
SPC = " "                                  ' SPaCe
'________________________________________________________'
While isFinished <> "True"
  createStudents()
EndWhile

calculateAverages()
displayAverages()
'________________________________________________________'
Sub createStudents
  
  TextWindow.ForegroundColor = "Red"
  
  TextWindow.WriteLine("Enter student's name or type in END:")
  name = TextWindow.Read()
  TextWindow.WriteLine("")
  
  If Text.ConvertToUpperCase(name) = "END" Then
    isFinished = "True"   'flags it's done creating students
  ElseIf name <> "" Then
    askGrades()
  Else
    TextWindow.WriteLine("Empty name input! Try again!!!" + BEL + LF)
  EndIf
  
EndSub
'________________________________________________________'
Sub askGrades
  
  TextWindow.ForegroundColor = "Yellow"
  
  TextWindow.WriteLine("Now, enter each grade for " + QUO + name + QUO + ".")
  TextWindow.WriteLine("Type in a negative value to end it." + LF)
  
  entry  = 0
  grade = 0
  
  While grade >= 0
    entry = entry + 1   ' next entry's number
    TextWindow.WriteLine("Enter grade #" + entry + ":")
    grade = TextWindow.ReadNumber() * 1
    students[name][entry] = grade
  EndWhile
  
  students[name][entry] = ""    'erases lastest negative entry
  TextWindow.WriteLine("")
  
EndSub
'________________________________________________________'
Sub calculateAverages
  
  indices = Array.GetAllIndices(students)
  count   = Array.GetItemCount(indices)
  
  For i = 1 To count
    current = students[ indices[i] ]
    entry = 1
    sum = 0
    
    While current[entry] <> ""
      sum = sum + current[entry]
      entry = entry + 1
    EndWhile
    
    students[ indices[i] ] ["Average"] = sum / (entry-1)
  EndFor
  
EndSub
'________________________________________________________'
Sub displayAverages
  
  indices = Array.GetAllIndices(students)
  count   = Array.GetItemCount(indices)
  
  TextWindow.ForegroundColor = "Cyan"
  
  For i = 1 To count
    TextWindow.Write(QUO + indices[i] + QUO + SPC + ARW + SPC)
    TextWindow.WriteLine(students[ indices[i] ] ["Average"])
  EndFor
  
  TextWindow.WriteLine("")
  
EndSub
'________________________________________________________'
