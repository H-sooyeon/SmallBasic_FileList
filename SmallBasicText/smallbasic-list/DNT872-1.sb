' Pisces 0.3

' ?†?Š

' March

' Copyright Â© 2016 Nonki Takahashi.  The MIT License.

' Update 2016-03-13

' Program ID DNT872-1

'

' Reference:

'  http://en.wikipedia.org/wiki/List_of_stars_in_Pisces

'

GraphicsWindow.Title = "Pisces 0.3"

wrap = "True"

gw = 598

gh = 428

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.BackgroundColor = "Black"

' initialize shapes

Shapes_Init()

' add shapes

scale = 1

angle = 0

iMin = 1

iMax = 21

Shapes_Add()

GraphicsWindow.FontSize = 100

GraphicsWindow.BrushColor = "Gray"

symbol = Shapes.AddText("?™“")

Shapes.Move(symbol, gw - 120, 10)

InitStars()

num = Array.GetItemCount(star)

index = Array.GetAllIndices(star)

offsetX = 0

offsetY = 0

GetMinMax()

offsetX = -xMin + 50

offsetY = -yMin + 90

DrawGrids()

GraphicsWindow.PenWidth = 1

GraphicsWindow.PenColor = "DarkGray"

num = Array.GetItemCount(edge)

index = Array.GetAllIndices(edge)

For i = 1 To num

  e = edge[index[i]]

  m = Text.GetIndexOf(e, "-")

  ra = star[Text.GetSubText(e, 1, m - 1)]["RA"]

  dec = star[Text.GetSubText(e, 1, m - 1)]["Dec"]

  RADec2XY()

  x1 = x

  y1 = y

  ra = star[Text.GetSubTextToEnd(e, m + 1)]["RA"]

  dec = star[Text.GetSubTextToEnd(e, m + 1)]["Dec"]

  RADec2XY()

  x2 = x

  y2 = y

  Shapes.AddLine(x1, y1, x2, y2)

EndFor

num = Array.GetItemCount(star)

index = Array.GetAllIndices(star)

GraphicsWindow.BrushColor = "White"

For i = 1 To num

  ra = star[index[i]]["RA"]

  dec = star[index[i]]["Dec"]

  RADec2XY()

  mag = star[index[i]]["Mag"]

  d = 20 / mag

  ell = Shapes.AddEllipse(d, d)

  Shapes.Move(ell, x - d / 2, y - d / 2) 

EndFor

GraphicsWindow.BrushColor = "White"

GraphicsWindow.FontSize = 30

GraphicsWindow.DrawText(40, 30, "March")

GraphicsWindow.FontSize = 40

year = 2016

GraphicsWindow.DrawText(40, 60, year)

Cal_Init()

sBuf = year + ",3"

Command_GetArgs()

iYear = sArg[1]

if iYear = "" Then

  Goto lEnd

Endif

If Math.Remainder(iYear, 4) = 0 And Math.Remainder(iYear, 100) > 0 Or Math.Remainder(iYear, 400) = 0 Then

  iDoM[2] = iDoM[2] + 1

Endif

iNoL = Math.Floor((iYear - 1) / 4) - Math.Floor((iYear - 1) / 100) + Math.Floor((iYear - 1) / 400)  ' number of leap year

iWoY = Math.Remainder((iYear + iNoL), 7)  ' week of year

iMonth = sArg[2]

If iMonth = "" Then

  iM0 = 1

  iM1 = 12

Else

  iM0 = iMonth

  iM1 = iMonth

Endif

iDoY = 0  ' days of year

iNoM = 1  ' number of month

For iM = iM0 To iM1

  While iNoM < iM

    iDoY = iDoY + iDoM[iNoM]

    iNoM = iNoM + 1

  EndWhile

  Cal_DrawMonth()

EndFor  

lEnd:

' Blink start

While "True"

  Program.Delay(2900)

  Shapes.HideShape(shape[4]["obj"])

  Shapes.HideShape(shape[10]["obj"])

  Program.Delay(100)

  Shapes.ShowShape(shape[4]["obj"])

  Shapes.ShowShape(shape[10]["obj"])

EndWhile

' end of main

Sub Cal_Init

  ' Calendar | Initialize days of month

  CRLF = Text.GetCharacter(13) + Text.GetCharacter(10)

  iDoM = "1=31;2=28;3=31;4=30;5=31;6=30;7=31;8=31;9=30;10=31;11=30;12=31;"

EndSub

Sub Cal_DrawMonth

  ' Calendar | Print month

  ' param iM - month

  ' param iDoY - days of year

  ' param iWoY - week of year

  GraphicsWindow.FontSize = 16

  GraphicsWindow.FontName = "Consolas"

  iW = Math.Remainder((iDoY + iWoY), 7)

  line = "SUN MON TUE WED THU FRI SAT" + CRLF

  iWoM = 0

  While iWoM < iW

    line = line + "    "

    iWoM = iWoM + 1

  EndWhile

  For iD = 1 To iDoM[iM]

    If iD < 10 Then

      line = line + "  " + iD + " "

    Else

      line = line + " " + iD + " "

    EndIf

    If Math.Remainder(iWoM, 7) = 6 Then

      line = line + CRLF

    EndIf

    iWoM = iWoM + 1

  EndFor

  If Math.Remainder(iWoM, 7) > 0 Then

    line = line + CRLF

  EndIf

  cal = Shapes.AddText(line)

  Shapes.Move(cal, 340, 160)

EndSub

Sub Command_GetArgs

  ' Command line | Get arguments

  ' param sBuf - input buffer

  ' return sArg[] - arguments

  ' return iN - number of arguments

  iP = 1  ' buffer pointer

  iN = 1  ' number of args

  iC = Text.GetIndexOf(sBuf, ",")   ' index of comma

  While iC > iP

    sArg[iN] = Text.GetSubText(sBuf, iP, iC - iP)

    iP = iC + 1

    iN = iN + 1

    iC = Text.GetIndexOf(sBuf, ",")

  EndWhile

  iE = Text.GetLength(sBuf) + 1     ' end of buffer

  sArg[iN] = Text.GetSubText(sBuf, iP, iE - iP)  

EndSub

Sub GetMinMax

  For i = 1 To num

    ra = star[index[i]]["RA"]

    dec = star[index[i]]["Dec"]

    RADec2XY()

    ra1 = Text.GetSubText(ra, 1, 2)

    If wrap And 12 < ra1 Then

      ra1 = ra1 - 24

    EndIf

    If i = 1 Then

      raMin = ra1

      decMin = Text.GetSubText(dec, 1, 3)

      raMax = ra1

      decMax = Text.GetSubText(dec, 1, 3)

      xMin = x

      yMin = y

      xMax = x

      yMax = y

    Else

      raMin = Math.Min(ra1, raMin)

      decMin = Math.Min(Text.GetSubText(dec, 1, 3), decMin)

      raMax = Math.Max(ra1, raMax)

      decMax = Math.Max(Text.GetSubText(dec, 1, 3), decMax)

      xMin = Math.Min(x, xMin)

      yMin = Math.Min(y, yMin)

      xMax = Math.Max(x, xMax)

      yMax = Math.Max(y, yMax)

    EndIf

  EndFor

  decMin = Math.Floor(decMin / 10) * 10

  raMax = raMax + 1

  decMax = decMax + 1

EndSub

Sub InitStars

  ' Initialize stars in Pisces

  ' index: Flamsteed designation

  ' RA (Right ascension), Dec (Declination), Mag (Apparent magnitude)

  star[99] = "name=Î· Psc;ra=01 31 28.99;dec=+15 20 45.0;mag=3.62;"

  star[6] = "name=Î³ Psc;ra=23 17 09.49;dec=+03 16 56.1;mag=3.70;"

  star[28] = "name=?? Psc;ra=23 59 18.60;dec=+06 51 48.9;mag=4.03;"

  star[17] = "name=Î¹ Psc;ra=23 39 56.82;dec=+05 37 38.5;mag=4.13;"

  star[110] = "name=Î¿ Psc;ra=01 45 23.59;dec=+09 09 27.5;mag=4.26;"

  star[71] = "name=Îµ Psc;ra=01 02 56.66;dec=+07 53 24.3;mag=4.27;"

  star[10] = "name=Î¸ Psc;ra=23 27 58.17;dec=+06 22 44.8;mag=4.27;"

  star[113] = "name=Î± Psc A;ra=02 02 02.80;dec=+02 45 49.5;mag=4.33;"

  star[30] = "name=30 Psc;ra=00 01 57.59;dec=?ˆ’06 00 50.3;mag=4.37;"

  star[63] = "name=Î´ Psc;ra=00 48 40.90;dec=+07 35 06.7;mag=4.44;"

  star[106] = "name=Î½ Psc;ra=01 41 25.91;dec=+05 29 15.4;mag=4.45;"

  star[4] = "name=Î² Psc;ra=23 03 52.61;dec=+03 49 12.3;mag=4.48;"

  star[18] = "name=Î» Psc;ra=23 42 02.88;dec=+01 46 49.5;mag=4.49;"

  star[83] = "name=?? Psc;ra=01 11 39.59;dec=+30 05 23.0;mag=4.51;"

  star[33] = "name=33 Psc;ra=00 05 20.15;dec=?ˆ’05 42 28.2;mag=4.61;"

  star[111] = "name=Î¾ Psc;ra=01 53 33.34;dec=+03 11 14.9;mag=4.61;"

  star[84] = "name=?? Psc;ra=01 11 27.19;dec=+21 02 04.8;mag=4.66;"

  star[85] = "name=?? Psc;ra=01 13 44.94;dec=+24 35 01.6;mag=4.67;"

  star[90] = "name=?? Psc;ra=01 19 27.98;dec=+27 15 50.7;mag=4.74;"

  star[98] = "name=Î¼ Psc;ra=01 30 10.94;dec=+06 08 38.2;mag=4.84;"

  star[27] = "name=27 Psc;ra=23 58 40.41;dec=?ˆ’03 33 20.9;mag=4.88;"

  star[8] = "name=Îº Psc;ra=23 26 55.91;dec=+01 15 21.0;mag=4.95;"

  star[19] = "name=19 Psc;ra=23 46 23.54;dec=+03 29 12.7;mag=4.95;"

  star[47] = "name=47 Psc;ra=00 28 02.84;dec=+17 53 35.1;mag=5.01;"

  star[7] = "name=7 Psc;ra=23 20 20.54;dec=+05 22 53.2;mag=5.05;"

  star[64] = "name=64 Psc;ra=00 48 58.71;dec=+16 56 28.1;mag=5.07;"

  star[29] = "name=29 Psc;ra=00 01 49.44;dec=?ˆ’03 01 38.9;mag=5.13;"

  star[89] = "name=89 Psc;ra=01 17 47.98;dec=+03 36 52.3;mag=5.13;"

  star[82] = "name=82 Psc;ra=01 11 06.77;dec=+31 25 29.2;mag=5.15;"

  star[86] = "name=Î¶ Psc A;ra=01 13 43.80;dec=+07 34 31.8;mag=5.21;"

  star[113] = "name=Î± Psc B;ra=02 02 02.80;dec=+02 45 49.0;mag=5.23;"

  star[91] = "name=91 Psc;ra=01 21 07.35;dec=+28 44 18.2;mag=5.23;"

  star[107] = "name=107 Psc;ra=01 42 29.95;dec=+20 16 12.5;mag=5.24;"

  star[74] = "name=?ˆÂ? Psc;ra=01 05 40.93;dec=+21 28 23.6;mag=5.33;"

  star[93] = "name=?? Psc;ra=01 26 15.28;dec=+19 10 20.4;mag=5.35;"

  star[55] = "name=55 Psc;ra=00 39 55.55;dec=+21 26 18.8;mag=5.36;"

  star[57] = "name=57 Psc;ra=00 46 32.98;dec=+15 28 32.2;mag=5.36;"

  star[41] = "name=41 Psc;ra=00 20 35.86;dec=+08 11 24.9;mag=5.38;"

  star[52] = "name=52 Psc;ra=00 32 35.40;dec=+20 17 40.0;mag=5.38;"

  star[5] = "name=5 Psc;ra=23 08 40.84;dec=+02 07 39.4;mag=5.42;"

  star[2] = "name=2 Psc;ra=22 59 27.40;dec=+00 57 47.1;mag=5.43;"

  star[68] = "name=68 Psc;ra=00 57 50.15;dec=+28 59 32.0;mag=5.44;"

  star[20] = "name=20 Psc;ra=23 47 56.49;dec=?ˆ’02 45 41.8;mag=5.49;"

  star[69] = "name=?? Psc;ra=01 02 49.09;dec=+31 48 15.6;mag=5.50;"

  star[94] = "name=94 Psc;ra=01 26 41.65;dec=+19 14 26.0;mag=5.50;"

  star[58] = "name=58 Psc;ra=00 47 01.43;dec=+11 58 26.2;mag=5.51;"

  star[80] = "name=80 Psc;ra=01 08 22.34;dec=+05 39 00.8;mag=5.51;"

  star[34] = "name=34 Psc;ra=00 10 02.18;dec=+11 08 44.9;mag=5.54;"

  star[102] = "name=? Psc;ra=01 37 05.96;dec=+12 08 29.4;mag=5.54;"

  star[74] = "name=?ˆÂ? Psc;ra=01 05 41.68;dec=+21 27 55.7;mag=5.55;"

  star[79] = "name=?ˆÂ? Psc;ra=01 07 57.11;dec=+20 44 21.6;mag=5.56;"

  star[81] = "name=?ˆÂ? Psc;ra=01 09 49.20;dec=+19 39 30.2;mag=5.57;"

  star[22] = "name=22 Psc;ra=23 51 57.83;dec=+02 55 49.5;mag=5.59;"

  star[72] = "name=72 Psc;ra=01 05 05.35;dec=+14 56 45.6;mag=5.64;"

  star[16] = "name=16 Psc;ra=23 36 23.35;dec=+02 06 07.5;mag=5.68;"

  star[51] = "name=51 Psc;ra=00 32 23.75;dec=+06 57 19.6;mag=5.69;"

  star[32] = "name=32 Psc;ra=00 02 29.76;dec=+08 29 08.1;mag=5.70;"

  star[44] = "name=44 Psc;ra=00 25 24.22;dec=+01 56 23.0;mag=5.77;"

  star[21] = "name=21 Psc;ra=23 49 27.48;dec=+01 04 34.3;mag=5.77;"

  star[66] = "name=66 Psc;ra=00 54 35.22;dec=+19 11 18.4;mag=5.80;"

  star[54] = "name=54 Psc;ra=00 39 22.09;dec=+21 15 04.9;mag=5.88;"

  star[53] = "name=53 Psc;ra=00 36 47.31;dec=+15 13 54.3;mag=5.89;"

  star[112] = "name=112 Psc;ra=02 00 09.02;dec=+03 05 51.5;mag=5.89;"

  star[14] = "name=14 Psc;ra=23 34 08.95;dec=?ˆ’01 14 51.1;mag=5.91;"

  star[62] = "name=62 Psc;ra=00 48 17.34;dec=+07 17 59.7;mag=5.92;"

  star[24] = "name=24 Psc;ra=23 52 55.52;dec=?ˆ’03 09 19.4;mag=5.93;"

  star[87] = "name=87 Psc;ra=01 14 07.65;dec=+16 08 00.8;mag=5.97;"

  star[60] = "name=60 Psc;ra=00 47 23.62;dec=+06 44 27.5;mag=5.98;"

  star[105] = "name=105 Psc;ra=01 39 40.77;dec=+16 24 21.2;mag=5.98;"

  star[97] = "name=97 Psc;ra=01 29 52.83;dec=+18 21 20.4;mag=6.01;"

  star[35] = "name=35 Psc;ra=00 14 58.78;dec=+08 49 15.7;mag=6.02;"

  star[73] = "name=73 Psc;ra=01 04 52.62;dec=+05 39 22.7;mag=6.03;"

  star[88] = "name=88 Psc;ra=01 14 42.40;dec=+06 59 42.7;mag=6.04;"

  star[48] = "name=48 Psc;ra=00 28 12.69;dec=+16 26 42.4;mag=6.05;"

  star[67] = "name=67 Psc;ra=00 55 58.52;dec=+27 12 33.7;mag=6.08;"

  star[59] = "name=59 Psc;ra=00 47 13.56;dec=+19 34 43.3;mag=6.11;"

  star[1] = "name=1 Psc;ra=22 54 59.47;dec=+01 03 53.6;mag=6.11;"

  star[36] = "name=36 Psc;ra=00 16 34.06;dec=+08 14 24.7;mag=6.12;"

  star[75] = "name=75 Psc;ra=01 06 33.62;dec=+12 57 21.5;mag=6.14;"

  star[3] = "name=3 Psc;ra=23 00 37.88;dec=+00 11 09.0;mag=6.22;"

  star[26] = "name=26 Psc;ra=23 55 07.78;dec=+07 04 15.7;mag=6.22;"

  star[78] = "name=78 Psc;ra=01 08 01.20;dec=+32 00 43.9;mag=6.23;"

  star[101] = "name=101 Psc;ra=01 35 46.44;dec=+14 39 41.2;mag=6.23;"

  star[42] = "name=42 Psc;ra=00 22 25.45;dec=+13 28 56.8;mag=6.25;"

  star[9] = "name=9 Psc;ra=23 27 14.77;dec=+01 07 21.7;mag=6.26;"

  star[109] = "name=109 Psc;ra=01 44 55.85;dec=+20 05 00.3;mag=6.27;"

  star[76] = "name=76 Psc;ra=01 06 11.20;dec=+32 10 53.6;mag=6.28;"

  star[25] = "name=25 Psc;ra=23 53 04.75;dec=+02 05 26.3;mag=6.29;"

  star[31] = "name=31 Psc;ra=00 02 24.17;dec=+08 57 24.6;mag=6.33;"

  star[77] = "name=77 Psc A;ra=01 05 49.22;dec=+04 54 31.2;mag=6.35;"

  star[13] = "name=13 Psc;ra=23 31 57.56;dec=?ˆ’01 05 09.3;mag=6.39;"

  star[46] = "name=46 Psc;ra=00 27 58.48;dec=+19 30 50.7;mag=6.40;"

  star[86] = "name=Î¶ Psc B;ra=01 13 45.17;dec=+07 34 42.2;mag=6.44;"

  star[15] = "name=15 Psc;ra=23 35 28.61;dec=+01 18 47.5;mag=6.47;"

  star[43] = "name=43 Psc;ra=00 24 38.15;dec=+14 18 55.9;mag=6.48;"

  star[61] = "name=61 Psc;ra=00 47 54.73;dec=+20 55 31.1;mag=6.51;"

  star[40] = "name=40 Psc;ra=00 19 56.28;dec=+16 15 03.2;mag=6.60;"

  star[38] = "name=38 Psc;ra=00 17 24.50;dec=+08 52 34.8;mag=6.66;"

  star[104] = "name=104 Psc;ra=01 39 15.39;dec=+14 17 08.3;mag=6.74;"

  star[45] = "name=45 Psc;ra=00 25 41.89;dec=+07 41 28.4;mag=6.77;"

  star[65] = "name=65 Psc;ra=00 49 53.11;dec=+27 42 37.1;mag=7.0;"

  star[95] = "name=95 Psc;ra=01 27 39.81;dec=+05 21 11.2;mag=7.01;"

  star[65] = "name=65 Psc;ra=00 49 53.20;dec=+27 42 37.0;mag=7.1;"

  star[77] = "name=77 Psc B;ra=01 05 51.42;dec=+04 54 35.0;mag=7.26;"

  star[100] = "name=100 Psc;ra=01 34 51.61;dec=+12 33 31.2;mag=7.28;"

  edge = "1=69-83;2=83-90;3=90-85;4=85-74;5=74-84;6=84-99;7=99-110;"

  edge = edge + "8=110-113;9=113-106;10=106-71;11=71-63;12=63-28;"

  edge = edge + "13=28-10;14=10-6;15=6-8;16=8-18;"

EndSub

Sub DrawGrids

  GraphicsWindow.PenWidth = 1

  GraphicsWindow.PenColor = "DimGray"

  GraphicsWindow.BrushColor = "DimGray"

  GraphicsWindow.FontSize = 12

  y1 = 0

  y2 = GraphicsWindow.Height

  For ra = raMin To raMax

    RADec2XY()

    Shapes.AddLine(x, y1, x, y2)

    raTxt = ra

    If wrap And raTxt < 0 Then

      raTxt = raTxt + 24

    EndIf

    txt = Shapes.AddText(raTxt + "h")

    Shapes.Move(txt, x, 5) 

  EndFor

  x1 = 0

  x2 = GraphicsWindow.Width

  For dec = decMin To decMax + 10 Step 10

    If dec > 0 Then

      dec = Text.Append("+", Math.Abs(dec))

    EndIf

    RADec2XY()

    Shapes.AddLine(x1, y, x2, y)

    txt = Shapes.AddText(dec)

    Shapes.Move(txt, x1, y) 

  EndFor

EndSub

Sub RADec2XY

  ' param ra - right ascension

  ' param dec - declination

  ' return x, y

  x = Text.GetSubText(ra, 1, 2)

  If wrap And 12 < x Then

    x = x - 24

  EndIf

  x = x + Text.GetSubText(ra, 4, 2) / 60

  x = x + Text.GetSubText(ra, 7, 5) / 3600

  x = offsetX - (x - raMin) * 3600 / 24

  y = Text.GetSubText(dec, 2, 2)

  y = y + Text.GetSubText(dec, 5, 2) / 60

  y = y + Text.GetSubText(dec, 8, 5) / 3600

  y = y * Text.Append(Text.GetSubText(dec, 1, 1), "1")

  y = offsetY - y * 3600 / 360

EndSub

Sub Shapes_Init

  ' Shapes | Initialize shapes data

  ' return shX, shY - current position of shapes

  ' return shape - array of shapes

  shX = 41 ' x offset

  shY = 110 ' y offset

  shape = ""

  shape[1] = "func=ell;x=76;y=10;width=63;height=119;angle=23;bc=#2F559D;pw=0;"

  shape[2] = "func=tri;x=70;y=94;x1=17;y1=0;x2=0;y2=60;x3=35;y3=60;angle=25;bc=#2F559D;pw=0;"

  shape[3] = "func=tri;x=108;y=0;x1=14;y1=0;x2=0;y2=36;x3=28;y3=36;angle=199;bc=#000000;pw=0;"

  shape[4] = "func=ell;x=118;y=34;width=20;height=21;bc=#000000;pw=0;"

  shape[5] = "func=ell;x=74;y=40;width=12;height=32;angle=58;bc=#2F559D;pw=0;"

  shape[6] = "func=ell;x=121;y=72;width=15;height=40;angle=17;bc=#2F559D;pw=0;"

  shape[7] = "func=ell;x=342;y=195;width=63;height=119;angle=106;bc=#2F559D;pw=0;"

  shape[8] = "func=tri;x=304;y=210;x1=17;y1=0;x2=0;y2=60;x3=35;y3=60;angle=105;bc=#2F559D;pw=0;"

  shape[9] = "func=tri;x=401;y=258;x1=14;y1=0;x2=0;y2=36;x3=28;y3=36;angle=295;bc=#000000;pw=0;"

  shape[10] = "func=ell;x=397;y=243;width=20;height=21;bc=#000000;pw=0;"

  shape[11] = "func=ell;x=367;y=203;width=15;height=40;angle=112;bc=#2F559D;pw=0;"

  shape[12] = "func=ell;x=361;y=271;width=12;height=32;angle=58;bc=#2F559D;pw=0;"

  shape[13] = "func=line;x=43;y=130;x1=39;y1=0;x2=0;y2=72;pc=#2F559D;pw=8;"

  shape[14] = "func=line;x=0;y=201;x1=0;y1=74;x2=44;y2=0;pc=#2F559D;pw=8;"

  shape[15] = "func=line;x=2;y=238;x1=0;y1=35;x2=42;y2=0;pc=#2F559D;pw=8;"

  shape[16] = "func=line;x=41;y=242;x1=18;y1=31;x2=0;y2=0;pc=#2F559D;pw=8;"

  shape[17] = "func=line;x=56;y=271;x1=0;y1=1;x2=30;y2=0;pc=#2F559D;pw=8;"

  shape[18] = "func=line;x=82;y=232;x1=0;y1=38;x2=1;y2=0;pc=#2F559D;pw=8;"

  shape[19] = "func=line;x=83;y=230;x1=0;y1=6;x2=65;y2=0;pc=#2F559D;pw=8;"

  shape[20] = "func=line;x=143;y=230;x1=0;y1=0;x2=79;y2=7;pc=#2F559D;pw=8;"

  shape[21] = "func=line;x=217;y=234;x1=0;y1=2;x2=98;y2=0;pc=#2F559D;pw=8;"

EndSub

Sub Math_CartesianToPolar

  ' Math | convert cartesian coodinate to polar coordinate

  ' param x, y - cartesian coordinate

  ' return r, a - polar coordinate

  r = Math.SquareRoot(x * x + y * y)

  If x = 0 And y > 0 Then

    a = 90 ' [degree]

  ElseIf x = 0 And y < 0 Then

    a = -90

  ElseIf x = 0 Then

    a = 0

  Else

    a = Math.ArcTan(y / x) * 180 / Math.Pi

  EndIf

  If x < 0 Then

    a = a + 180

  ElseIf x > 0 And y < 0 Then

    a = a + 360

  EndIf

EndSub

Sub SB_RotateWorkaround

  ' Small Basic | Rotate workaround for Silverlight

  ' param shp - current shape

  ' param x, y - original coordinate

  ' param alpha - angle [radian]

  ' returns x, y - workaround coordinate

  If shp["func"] = "tri" Then

    x1 = -Math.Floor(shp["x3"] / 2)

    y1 = -Math.Floor(shp["y3"] / 2)

  ElseIf shp["func"] = "line" Then

    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)

    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)

  EndIf

  ox = x - x1

  oy = y - y1

  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox

  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy

EndSub

Sub SB_Workaround

  ' Small Basic | Workaround for Silverlight

  ' returns silverlight - "True" if in remote

  color = GraphicsWindow.GetPixel(0, 0)

  If Text.GetLength(color) > 7 Then

    silverlight = "True"

    msWait = 300

  Else

    silverlight = "False"

  EndIf

EndSub

Sub Shapes_Add

  ' Shapes | add shapes as shapes data

  ' param iMin, iMax - shape indices to add

  ' param shape - array of shapes

  ' param scale - 1 if same scale

  ' return shWidth, shHeight - total size of shapes

  ' return shAngle - current angle of shapes

  Stack.PushValue("local", i)

  Stack.PushValue("local", x)

  Stack.PushValue("local", y)

  Shapes_CalcWidthAndHeight()

  s = scale

  For i = iMin To iMax

    shp = shape[i]

    GraphicsWindow.PenWidth = shp["pw"] * s

    If shp["pw"] > 0 Then

      GraphicsWindow.PenColor = shp["pc"]

    EndIf

    If Text.IsSubText("rect|ell|tri|text", shp["func"]) Then

      GraphicsWindow.BrushColor = shp["bc"]

    EndIf

    If shp["func"] = "rect" Then

      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)

    ElseIf shp["func"] = "ell" Then

      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)

    ElseIf shp["func"] = "tri" Then

      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)

    ElseIf shp["func"] = "line" Then

      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)

    ElseIf shp["func"] = "text" Then

      If silverlight Then

        fs = Math.Floor(shp["fs"] * 0.9)

      Else

        fs = shp["fs"]

      EndIf

      GraphicsWindow.FontSize = fs * s

      GraphicsWindow.FontName = shp["fn"]

      shp["obj"] = Shapes.AddText(shp["text"])

    EndIf

    x = shp["x"]

    y = shp["y"]

    shp["rx"] = x

    shp["ry"] = y

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      alpha = Math.GetRadians(shp["angle"])

      SB_RotateWorkaround()

      shp["wx"] = x

      shp["wy"] = y

    EndIf

    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)

    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then

      Shapes.Rotate(shp["obj"], shp["angle"])

    EndIf

    shape[i] = shp

  EndFor

  shAngle = 0

  y = Stack.PopValue("local")

  x = Stack.PopValue("local")

  i = Stack.PopValue("local")

EndSub

Sub Shapes_CalcRotatePos

  ' Shapes | Calculate position for rotated shape

  ' param["x"], param["y"] - position of a shape

  ' param["width"], param["height"] - size of a shape

  ' param ["cx"], param["cy"] - center of rotation

  ' param ["angle"] - rotate angle

  ' return x, y - rotated position of a shape

  _cx = param["x"] + param["width"] / 2

  _cy = param["y"] + param["height"] / 2

  x = _cx - param["cx"]

  y = _cy - param["cy"]

  Math_CartesianToPolar()

  a = a + param["angle"]

  x = r * Math.Cos(a * Math.Pi / 180)

  y = r * Math.Sin(a * Math.Pi / 180)

  _cx = x + param["cx"]

  _cy = y + param["cy"]

  x = _cx - param["width"] / 2

  y = _cy - param["height"] / 2

EndSub

Sub Shapes_CalcWidthAndHeight

  ' Shapes | Calculate total width and height of shapes

  ' param iMin, iMax - shape indices to add

  ' return shWidth, shHeight - total size of shapes

  For i = iMin To iMax

    shp = shape[i]

    If shp["func"] = "tri" Or shp["func"] = "line" Then

      xmin = shp["x1"]

      xmax = shp["x1"]

      ymin = shp["y1"]

      ymax = shp["y1"]

      If shp["x2"] < xmin Then

        xmin = shp["x2"]

      EndIf

      If xmax < shp["x2"] Then

        xmax = shp["x2"]

      EndIf

      If shp["y2"] < ymin Then

        ymin = shp["y2"]

      EndIf

      If ymax < shp["y2"] Then

        ymax = shp["y2"]

      EndIf

      If shp["func"] = "tri" Then

        If shp["x3"] < xmin Then

          xmin = shp["x3"]

        EndIf

        If xmax < shp["x3"] Then

          xmax = shp["x3"]

        EndIf

        If shp["y3"] < ymin Then

          ymin = shp["y3"]

        EndIf

        If ymax < shp["y3"] Then

          ymax = shp["y3"]

        EndIf

      EndIf

      shp["width"] = xmax - xmin

      shp["height"] = ymax - ymin

    EndIf

    If i = 1 Then

      shWidth = shp["x"] + shp["width"]

      shHeight = shp["y"] + shp["height"]

    Else

      If shWidth < shp["x"] + shp["width"] Then

        shWidth = shp["x"] + shp["width"]

      EndIf

      If shHeight < shp["y"] + shp["height"] Then

        shHeight = shp["y"] + shp["height"]

      EndIf

    EndIf

    shape[i] = shp

  EndFor

EndSub

Sub Shapes_Move

  ' Shapes | Move shapes

  ' param iMin, iMax - shape indices to add

  ' param shape - array of shapes

  ' param scale - to zoom

  ' param x, y - position to move

  ' return shX, shY - new position of shapes

  Stack.PushValue("local", i)

  s = scale

  shX = x

  shY = y

  For i = iMin To iMax

    shp = shape[i]

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      _x = shp["wx"]

      _y = shp["wy"]

    Else

      _x = shp["rx"]

      _y = shp["ry"]

    EndIf

    Shapes.Move(shp["obj"], shX + _x * s, shY + _y * s)

  EndFor

  i = Stack.PopValue("local")

EndSub

Sub Shapes_Remove

  ' Shapes | Remove shapes

  ' param iMin, iMax - shapes indices to remove

  ' param shape - array of shapes

  Stack.PushValue("local", i)

  For i = iMin To iMax

    shp = shape[i]

    Shapes.Remove(shp["obj"])

  EndFor

  i = Stack.PopValue("local")

EndSub

Sub Shapes_Rotate

  ' Shapes | Rotate shapes

  ' param iMin, iMax - shapes indices to rotate

  ' param shape - array of shapes

  ' param cx, cy - rotation center

  ' param scale - to zoom

  ' param angle - to rotate

  Stack.PushValue("local", i)

  Stack.PushValue("local", x)

  Stack.PushValue("local", y)

  s = scale

  param["angle"] = angle

  If cx <> "" Then

    param["cx"] = cx

  Else

    cx = ""   ' to avoid syntax error

    param["cx"] = shWidth / 2

  EndIf

  If cy <> "" Then

    param["cy"] = cy

  Else

    cy = ""   ' to avoid syntax error

    param["cy"] = shHeight / 2

  EndIf

  For i = iMin To iMax

    shp = shape[i]

    param["x"] = shp["x"]

    param["y"] = shp["y"]

    param["width"] = shp["width"]

    param["height"] = shp["height"]

    Shapes_CalcRotatePos()

    shp["rx"] = x

    shp["ry"] = y

    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then

      alpha = Math.GetRadians(angle + shp["angle"])

      SB_RotateWorkAround()

      shp["wx"] = x

      shp["wy"] = y

    EndIf

    Shapes.Move(shp["obj"], shX + x * s, shY + y * s)

    Shapes.Rotate(shp["obj"], angle + shp["angle"])

    shape[i] = shp

  EndFor

  y = Stack.PopValue("local")

  x = Stack.PopValue("local")

  i = Stack.PopValue("local")

EndSub


