' mandelbrot  fly-in by Rene_Miner 
' oct/14/2012 

gwWIDTH = 640
gwHEIGHT = 480

GraphicsWindow.Width = gwWIDTH
GraphicsWindow.Height = gwHEIGHT
GraphicsWindow.Title = "Mandelbrot - fly-in-view"

GraphicsWindow.BackgroundColor = "black"

GraphicsWindow.PenColor = "white"
GraphicsWindow.PenWidth = 1
GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor
GraphicsWindow.Show()
myCol[0] = GraphicsWindow.BackgroundColor

Timer.Tick = CountTime
Timer.Interval = 1000

startAllOver:

DEPTHMAX = 250  
X_MIN = -1.1 
X_MAX = 2.1
Y_MIN = -1.22
Y_MAX = Y_MIN + (X_MAX - X_MIN) * gwHEIGHT/gwWIDTH

oneLevelDeeper:

elapsed = 0
elapsMins = 0
elapsHours = 0

GraphicsWindow.Title = "Mandelbrot - Depth " + DEPTHMAX
colorScheme = Math.GetRandomNumber(6)

' set up a nice color-palette

For i1 = 1 to 127
  If colorScheme = 1 Then 
    myRed = 100 +  0.25 * i1 
    myGreen =40 + 1.5 * i1    
    myBlue =  160 +  i1 * 0.5
  ElseIf colorScheme = 2 then 
    myRed =  200 - i1   
    myGreen = 200 -  i1  
    myBlue = 128 + i1 
  elseif colorScheme = 3 then
    myRed = 50 +  i1  
    myGreen = 255 - i1 * 2 
    myBlue = 127 + i1   
  elseif colorScheme = 4 then 
    myRed =   i1 * 1.5   
    myGreen = 160 - i1   
    myBlue = 127 + i1 
  elseif colorScheme = 5 then 
    myRed =  2 * i1 
    myGreen = 128+ i1 
    myBlue =  255 -  i1 
  elseif colorScheme = 6 then 
    myRed =  127 + i1 
    myGreen = 127 + i1  
    myBlue =   200 - i1 
  EndIf
  myCol[i1] = GraphicsWindow.GetColorFromRGB(myRed, myGreen, myBlue)
endfor
'start drawing
deltaX = (X_MAX - X_MIN)/ gwWIDTH
deltaY = (Y_MAX - Y_MIN)/gwHEIGHT
realVal = X_MIN
imagineVal = Y_MIN
For iY = 0 to gwHEIGHT 
  For iX = 0 To gwWIDTH 
    iter = 0 
    xVal = 0    
    yVal = 0
    Xlen = 0
    Ylen = 0
    While (iter < DEPTHMAX) And (Xlen + Ylen < 8 )
      yVal = 2 * xVal * yVal - imagineVal
      xVal = Xlen - Ylen - realVal      
      Xlen =  math.Power(xVal,2)
      Ylen = Math.Power(Yval,2)
      iter = iter + 1
    EndWhile
    If iter >= DEPTHMAX Then
      selColor = 0
    Else      
      selColor = Math.Remainder(iter, 127)
    EndIf
    realVal = realVal + deltaX 
    GraphicsWindow.SetPixel(iX, iY, myCol[selColor])
  EndFor
  realVal = X_MIN
  imagineVal = imagineVal + deltaY
EndFor
If DEPTHMAX > 1000000 Then 
  Goto startAllOver   
EndIf 

While elapsed > 3599
  elapsHours = elapsHours + 1
  elapsed = elapsed - 3600
endwhile
While elapsed > 59
  elapsMins = elapsMins + 1
  elapsed = elapsed - 60
endwhile 
elapsTime = " elapsed Time "
If elapsHours > 0 then
  elapsTime = Text.Append(elapsTime, elapsHours + ":")
endif 
If elapsMins < 10 and elapsHours > 0 then 
  elapsTime = elapsTime +"0"
EndIf 

elapsTime = Text.Append(elapsTime, elapsMins +":")
If elapsed < 10 And elapsMins > 0 Then 
  elapsTime = elapsTime +"0"
EndIf 
elapsTime = Text.Append(elapsTime, elapsed)  

GraphicsWindow.Title = "Mandelbrot - Depth "+ DEPTHMAX + elapsTime
Program.Delay(4000)
GraphicsWindow.DrawRectangle(gwWIDTH * 0.61 - gwWIDTH * 0.25, gwHEIGHT * 0.5 - gwHEIGHT * 0.25, gwWIDTH * 0.5, gwHEIGHT * 0.5)
Program.Delay(4000)

dist = Math.Abs(X_MAX - X_MIN)
two3rd = X_MIN + 0.61 * dist 

X_MIN = two3rd - dist * 0.25 
X_MAX = two3rd + dist * 0.25 
Y_MIN = Y_MIN * 0.5 
Y_MAX = Y_MAX * 0.5 

GraphicsWindow.Clear()
DEPTHMAX = DEPTHMAX * 2
Goto oneLevelDeeper  

Sub countTime
  elapsed = elapsed + 1
EndSub
