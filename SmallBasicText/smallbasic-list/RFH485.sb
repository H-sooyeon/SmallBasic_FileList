'**************************************************************************
'Initialise
'*************************************************************************
GraphicsWindow.Hide()

GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp

movingBullet = "false"   'bullet fires when it's off screen

InitGW()

background = ImageList.LoadImage("http://s23.postimg.org/qhdyatkzv/My_Image_jpg.jpg")
gun = ImageList.LoadImage("http://s22.postimg.org/n4mh54o4h/Character_finished.gif")

'draw b/ground
GraphicsWindow.DrawResizedImage(background, 0, 0, gw, gh)  'to change b/ground try redrawing

'add and centre the Gun Man (sprite)
gunWidth = ImageList.GetWidthOfImage(gun)
gunHeight = ImageList.GetHeightOfImage(gun)
gun = Shapes.AddImage(gun)
gunX = gw/2 - gunWidth/2
gunY = gh - gunHeight
Shapes.Move(gun, gunX, gunY)

'add and hide the bullet. Will show when fired
bulletRadius = 10
bullet = Shapes.AddEllipse(bulletRadius * 2, bulletRadius * 2)
Shapes.HideShape(bullet)

'once Initial setup is complete, show the GW
GraphicsWindow.Show()

'*********************************************************************************
'Game Loop   - all position changes and corresponding Shape.Moves are done here.
'no Shape.Moves are done in the Event subs   
'*********************************************************************************
While "true"
  UpdateGamePlay()
  UpdateFrame()
  Program.Delay(20)  
EndWhile

'********************************************************************************
'Subroutines
'********************************************************************************
Sub UpdateGamePlay 
  If keyRight Then
    gunX = gunX + 5
  EndIf
  If keyLeft Then
    gunX = gunX - 5
  EndIf
  
  If movingBullet = "true" Then
    bulletY = bulletY - 20
  EndIf
  If bulletY < 0 Then
    Shapes.HideShape(bullet)
    movingBullet = "false"
  EndIf 
  
  If spaceKey Then
    movingBullet = "true"
    bulletX = Shapes.GetLeft(gun) + 5
    bulletY = Shapes.GetTop(gun) - 5
    Shapes.ShowShape(bullet)
    spaceKey = "false"
  EndIf  
EndSub

Sub UpdateFrame
  Shapes.Move(gun, gunX, gunY)
  Shapes.Move(bullet, bulletX, bulletY)
EndSub

Sub InitGW
  'you can put all the Initial Setup here  
  GraphicsWindow.Title = "Zombie Adventure"
  gw = 1000
  gh = 600  
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
  GraphicsWindow.CanResize  = "false"  
  GraphicsWindow.Left = (Desktop.Width - gw) / 2
  GraphicsWindow.Top  = 10 
EndSub

'*****************************************************************************
'Event Subroutines  - don't  Shapes.Move here, just flag that an event happened
'****************************************************************************
Sub OnKeyDown
  lastKey = GraphicsWindow.LastKey
  If lastKey = "Right" Then
    keyRight = "true"
  ElseIf lastKey = "Left" Then
    keyLeft = "true"
  ElseIf lastKey = "Space" And movingBullet = "false" Then
    'TextWindow.WriteLine("on space down")    'debug - see what's happening
    spaceKey = "true"
  EndIf
EndSub

Sub OnKeyUp
  lastKey = GraphicsWindow.LastKey
  If lastKey = "Right" Then
    keyRight = "false"
  ElseIf lastKey = "Left" Then
    keyLeft = "false"
  EndIf
EndSub


