num = Math.GetRandomNumber(40)  ' number of lines

spd = Math.GetRandomNumber(5)   ' speed



GraphicsWindow.PenWidth=2 



r1=Math.GetRandomNumber(6)+spd

r2=Math.GetRandomNumber(6)+spd

r3=Math.GetRandomNumber(6)+spd

r4=Math.GetRandomNumber(6)+spd



For line=num To 1 Step -1

  lines[line]["x1"]=0

  lines[line]["y1"]=0

  lines[line]["x2"]=GraphicsWindow.Width

  lines[line]["y2"]=GraphicsWindow.Height

  

  lines[line]["vx1"]=r1 'Math.GetRandomNumber(6)

  lines[line]["vy1"]=r2 'Math.GetRandomNumber(6)

  lines[line]["vx2"]=r3 'Math.GetRandomNumber(6)

  lines[line]["vy2"]=r4 'Math.GetRandomNumber(6)

  

  lines[line][0]=Shapes.AddLine(lines[line]["x1"],lines[line]["y1"],lines[line]["x2"],lines[line]["y2"])

  lines[line]["clr"]=GraphicsWindow.GetRandomColor()  ' line colour

  lines[line]["dlay"]=line*2+200  ' delay

EndFor



While "True"

  

  For line=1 To num

    If (lines[line]["x1"]+lines[line]["vx1"]>GraphicsWindow.Width) Or (lines[line]["x1"]+lines[line]["vx1"]<0) Then 

      lines[line]["vx1"]=-lines[line]["vx1"]

    EndIf 

    

    If (lines[line]["y1"]+lines[line]["vy1"]>GraphicsWindow.Height) Or (lines[line]["y1"]+lines[line]["vy1"]<0) Then 

      lines[line]["vy1"]=-lines[line]["vy1"]

    EndIf 

    

    If (lines[line]["x2"]+lines[line]["vx2"]>GraphicsWindow.Width) Or (lines[line]["x2"]+lines[line]["vx2"]<0) Then 

      lines[line]["vx2"]=-lines[line]["vx2"]

    EndIf 

    

    If (lines[line]["y2"]+lines[line]["vy2"]>GraphicsWindow.Height) Or (lines[line]["y2"]+lines[line]["vy2"]<0) Then 

      lines[line]["vy2"]=-lines[line]["vy2"]

    EndIf 

    

    ' delay each lines movement to prevent them all moving at the same time

    If lines[line]["dlay"]=0 Then

      lines[line]["x1"]=lines[line]["x1"]+lines[line]["vx1"]

      lines[line]["y1"]=lines[line]["y1"]+lines[line]["vy1"]

      lines[line]["x2"]=lines[line]["x2"]+lines[line]["vx2"]

      lines[line]["y2"]=lines[line]["y2"]+lines[line]["vy2"]

      

      Shapes.Remove(lines[line][0])

      GraphicsWindow.PenColor=lines[line]["clr"]

      lines[line][0] = Shapes.AddLine(lines[line]["x1"],lines[line]["y1"],lines[line]["x2"],lines[line]["y2"])

    Else

      lines[line]["dlay"] = lines[line]["dlay"] - 1

    EndIf

  EndFor

  

  'Program.Delay(10)

EndWhile


