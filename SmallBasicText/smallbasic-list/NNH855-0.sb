Initialise()



'=============================================

'MAIN LOOP

While 0=0

  If mouseDown Then

    SelectColor() 

    InitialisePriorMouseCoordinates()

    mouseDown = "False"

  EndIf

  

  If Mouse.IsLeftButtonDown Then    

    Paint()    

  EndIf  

  

  Program.Delay(20)

EndWhile



'===========================================================

'SUBROUTINES

Sub Paint

  GetMousePosition()  

  If mouseX > canvasX And mouseX < canvasX + canvasWidth And mouseY > canvasY And mouseY < canvasY + canvasHeight Then  

    If priorX = "" Then

      priorX = mouseX

      priorY = mouseY

    EndIf 

    

    GraphicsWindow.PenColor = colorSelected

    GraphicsWindow.DrawLine(mouseX, mouseY, priorX, priorY)

    priorX = mouseX

    priorY = mouseY  

  EndIf

EndSub



Sub SelectColor

  GetMousePosition()

  

  If mouseX <= ImageList.GetWidthOfImage(palet) And GraphicsWindow.GetPixel(mouseX, mouseY) <> "#000000" Then

    If mouseY <= ImageList.GetHeightOfImage(palet) Then

      colorSelected = GraphicsWindow.GetPixel(mouseX, mouseY)      

    EndIf

  EndIf

  

  UpdateSelectedColor()  

EndSub



Sub InitialisePriorMouseCoordinates 

  If mouseX > canvasX And mouseX < canvasX + canvasWidth And mouseY > canvasY And mouseY < canvasY + canvasHeight Then

    priorX = mouseX

    priorY = mouseY

  Else

    priorX = ""

    priorY = ""

  EndIf

EndSub



Sub GetMousePosition

  mouseX = GraphicsWindow.MouseX

  mouseY = GraphicsWindow.MouseY

EndSub



Sub UpdateSelectedColor

  GraphicsWindow.BrushColor = colorSelected

  GraphicsWindow.FillRectangle(10, ImageList.GetHeightOfImage(palet) + 25, 30, 30)

EndSub



'======================================================================

'SETUP PROGRAM

Sub Initialise

  LF = Text.GetCharacter(10)

  colorSelected = "black"

  

  path = "http://rickmurphy.byethost5.com/SmallBasic/ProgramListing/Paint/assets/images/color-palet.png"

  GraphicsWindow.DrawText(10, 10, "D/loading palet, wait a sec")

  palet = ImageList.LoadImage(path)

  

  If ImageList.GetWidthOfImage(palet) = 0 Then

    GraphicsWindow.ShowMessage("Image d/load failed!" + LF + "Click OK to end program.", "Network ERROR!")

    Program.End()

  EndIf

  

  GraphicsWindow.Clear()

  GraphicsWindow.DrawImage(palet, 0, 0)

  

  GraphicsWindow.DrawText(10, ImageList.GetHeightOfImage(palet) + 10, "Color selected")

  

  UpdateSelectedColor()  

  

  j = "Click on the Palet to select a color." + LF + "Then hold the Left button down to Paint on the canvas"

  GraphicsWindow.DrawText(ImageList.GetWidthOfImage(palet) +10, 10, j)

  

  canvasX = ImageList.GetWidthOfImage(palet) +10

  canvasY = 80

  canvasWidth = GraphicsWindow.Width - canvasX - 10

  canvasHeight = GraphicsWindow.Height - canvasY - 10

  

  GraphicsWindow.DrawRectangle(canvasX, canvasY, canvasWidth, canvasHeight)

  GraphicsWindow.PenWidth = 5

  

  'REGISTER EVENT(S)

  GraphicsWindow.MouseDown = OnMouseDown

EndSub



'================================

'EVENT HANDLER(S)

Sub OnMouseDown

  mouseDown = "True"

EndSub


