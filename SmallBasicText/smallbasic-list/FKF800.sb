' A Tree with Fruit - Orange
' Copyright Â© 2017 Nonki Takahashi.  The MIT License.
' Challenge 2017-05
' Last update 2017-05-19

GraphicsWindow.Title = "A Tree with Fruit - Orange"
gw = 598
gh = 428
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
' sky
GraphicsWindow.BackgroundColor = "SkyBlue"
' ground
GraphicsWindow.BrushColor = "#221100"
GraphicsWindow.FillRectangle(0, gh - 40, gw, 40)
bc = "#003300"
n = 400
DrawLeaves()
bc = "DarkGreen"
n = 300
DrawLeaves()
DrawTree()
bc = "Green"
n = 200
DrawLeaves()
n = 100
DrawFruits()

Sub DrawBranch
  If 0 < level Then
    Turtle.Move(len)
    Turtle.Turn(-a)
    level = level - 1
    len = len * 0.8
    pw = pw * 0.8
    GraphicsWindow.PenWidth = pw
    DrawBranch()
    Turtle.Turn(2 * a)
    DrawBranch()
    Turtle.Turn(-a)
    pw = pw / 0.8
    GraphicsWindow.PenWidth = pw
    len = len / 0.8
    level = level + 1
    Turtle.Turn(180)
    Turtle.Move(len)
    Turtle.Turn(180)
  EndIf
EndSub

Sub DrawFruits
  GraphicsWindow.BrushColor = "Orange"
  GraphicsWindow.PenWidth = 0
  xo = gw * 0.5
  yo = gh * 0.36
  lx = 220
  ly = 120
  For i = 1 To n
    a = Math.GetRandomNumber(360)
    _a = Math.GetRadians(a)
    rx = Math.GetRandomNumber(lx)
    ry = Math.GetRandomNumber(ly)
    x = xo + rx * Math.Cos(_a)
    y = yo + ry * Math.Sin(_a)
    fruit = Shapes.AddEllipse(10, 10)
    Shapes.Move(fruit, x - 5, y - 5)
  EndFor
EndSub

Sub DrawLeaves
  GraphicsWindow.BrushColor = bc
  GraphicsWindow.PenWidth = 0
  xo = gw * 0.5
  yo = gh * 0.36
  lx = 220
  ly = 120
  For i = 1 To n
    a = Math.GetRandomNumber(360)
    _a = Math.GetRadians(a)
    rx = Math.GetRandomNumber(lx)
    ry = Math.GetRandomNumber(ly)
    x = xo + rx * Math.Cos(_a)
    y = yo + ry * Math.Sin(_a)
    leaf = Shapes.AddEllipse(20, 10)
    Shapes.Move(leaf, x - 10, y - 5)
    Shapes.Rotate(leaf, a)
  EndFor
EndSub

Sub DrawTree
  pc = "#442200"
  pw = 20
  GraphicsWindow.PenColor = pc
  GraphicsWindow.PenWidth = pw
  level = 5
  len = 100
  a = 30
  Turtle.X = gw / 2
  Turtle.Y = gh - 40
  Turtle.Speed = 10
  DrawBranch()
  Turtle.Hide()
EndSub
  
