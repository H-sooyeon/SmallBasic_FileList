'jibba jabba   -  Sep 2013 Challenge



items = "empty stack"

valid = "1=+;2=-;3=*;4=/"



Header()

EnterNumberAndPush()

ReadMemoryAndHeader()



While Stack.GetCount(items) = 1  

  EnterOperatorToPush()

  

  While  Array.ContainsValue(valid, operator) <> "true"

    TextWindow.WriteLine("You've entered an invalid character.")

    TextWindow.Pause()

    ReadMemoryAndHeader()

    EnterOperatorToPush()

  EndWhile

  

  Stack.PushValue(items, operator)  

  EnterNumberAndPush()

  

  top = Stack.PopValue(items)

  operator = Stack.PopValue(items)

  bottom = Stack.PopValue(items)  

  

  If operator = "/" Then

    accumulator = bottom / top

  ElseIf operator = "*" Then

    accumulator = bottom * top

  ElseIf operator = "-" Then

    accumulator = bottom - top

  Else

    accumulator = bottom + top

  EndIf  

  

  Stack.PushValue(items, accumulator)

  ReadMemoryAndHeader()

EndWhile



'------------------------------------------Subroutines----------------------------

Sub ReadMemoryAndHeader

  TextWindow.Clear()

  Header()

  temp = Stack.PopValue(items)

  TextWindow.WriteLine("Memory =    : " + temp)

  Stack.PushValue(items, temp)

EndSub



Sub EnterNumberAndPush

  TextWindow.Write("Enter number: ")

  Stack.PushValue(items, TextWindow.ReadNumber())

EndSub



Sub EnterOperatorToPush

  TextWindow.Write("Enter +-*/  : ")

  operator = TextWindow.Read()

EndSub



Sub Header

  defaultTextColour = TextWindow.ForegroundColor

  TextWindow.ForegroundColor = "white"

  TextWindow.WriteLine("Memory Accumlator")

  TextWindow.ForegroundColor = defaultTextColour

  TextWindow.WriteLine("")

EndSub


