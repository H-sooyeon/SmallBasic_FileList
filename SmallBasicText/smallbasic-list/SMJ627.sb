'Test Zoom Animation to Shrink increment
'DEBUG()

boxSize = 3

GraphicsWindow.MouseDown = OnMouseDown
Timer.Tick = OnTick
Timer.Interval = 1000

InitialiseGraphicsWindow()
box = Shapes.AddRectangle(boxSize, boxSize)
ShowBox()               'initialise boxZoom and position, then hide.  ?????
Shapes.HideShape(box)

'GraphicsWindow.Show()

While "True"
  If flag = "Box Clicked On" Then
    Sound.PlayChime()
    Program.Delay(300)
    HideBox()
    flag = ""
  ElseIf flag = "Show Box" Then
    ShowBox()
    flag = ""
  EndIf
  Program.Delay(20)
EndWhile

Sub OnTick
  Timer.Interval = Math.GetRandomNumber(2500) + 500
  flag = "Show Box"
EndSub

Sub OnMouseDown
  If GraphicsWindow.MouseX >= boxX And GraphicsWindow.MouseX <= boxX + boxTrueSize Then
    If GraphicsWindow.MouseY >= boxY And GraphicsWindow.MouseY <= boxY + boxTrueSize Then      
      flag = "Box Clicked On"
    EndIf
  EndIf
EndSub

Sub ShowBox
  boxZoom = Math.GetRandomNumber(20 - 5) + 5    '5'20 
  boxZoomOffset = ((boxSize * boxZoom) - boxSize) / 2
  boxTrueSize = boxSize * boxZoom
  boxX = Math.GetRandomNumber(gw - (boxSize * boxZoom))
  boxY = Math.GetRandomNumber(gh - (boxSize * boxZoom))
  
  Shapes.Zoom(box, boxZoom, boxZoom)
  Shapes.Move(box, boxX + boxZoomOffset, boxY + boxZoomOffset)
  Shapes.ShowShape(box)
EndSub

Sub HideBox
  While boxZoom > 0.5
    boxZoom = boxZoom - 0.2
    Shapes.Zoom(box, boxZoom, boxZoom)
    Program.Delay(5)
  EndWhile
  Shapes.HideShape(box)
EndSub

Sub InitialiseGraphicsWindow
  'GraphicsWindow.Hide()
  gw = GraphicsWindow.Width
  gh = GraphicsWindow.Height
  GraphicsWindow.Left = (Desktop.Width - gw) / 2
  GraphicsWindow.Top = 10
  GraphicsWindow.BackgroundColor = "black"
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "yellow"
EndSub

Sub DEBUG
  TextWindow.Title = "DEBUGGING"
  TextWindow.Left = 5
  TextWindow.Top = 80
EndSub
