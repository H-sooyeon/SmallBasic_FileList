'Example with ideas from Dudeson creating smoke
'Use the mose to rotate (right mouse button), move and emit smoke with a left button press

'Create GraphicsWindow and TextWindow for debug output next to it
gh = 600
gw = 600
GraphicsWindow.Height = gh
GraphicsWindow.Width = gw
TextWindow.Left = GraphicsWindow.Left + 615
TextWindow.Top = GraphicsWindow.Top
Mouse.HideCursor()

'Create a circuler target for the smoke in the centre
radius = 25
circle  = Shapes.AddEllipse(2*radius,2*radius)
circleX = gw/2
circleY = gh/2
Shapes.Move(circle,circleX-radius,circleY-radius)

'Load smoke image
Steam_Skin = ImageList.LoadImage(Program.Directory + "\images\smoke1.png")
Pi = Math.Pi
Smoke_EmitAngle = Pi*(Emitter_Angle/180-0.5)

'Create triangular smoke emitter
GraphicsWindow.BrushColor = "Black"
Emitter = Shapes.AddTriangle(0,0,70,0,35,70)

GraphicsWindow.PenWidth=1
While "True"
  TimeStart = Clock.ElapsedMilliseconds
  
  Mouse_X=GraphicsWindow.mouseX
  Mouse_Y=GraphicsWindow.mouseY
  Shapes.Move(Emitter,Mouse_X-35,Mouse_Y-35)
  
  If Mouse.IsRightButtonDown Then
    Emitter_Angle = Emitter_Angle+5
    Smoke_EmitAngle = Pi*(Emitter_Angle/180-0.5)
    Shapes.Rotate(Emitter,Emitter_Angle)
  EndIF
  If Mouse.IsLeftButtonDown Then
    Steam_EmitDelay =  Steam_EmitDelay + 1
    If Steam_EmitDelay = 2 Then
      SteamEmitter()
      Steam_EmitDelay = 0
    endif
  endif
  
  SteamCode()
  
  TextWindow.Clear()
  TextWindow.WriteLine("Steam Sprites: " + (Steam_New-Steam_Old))
  TextWindow.WriteLine("Steam_New: " + Steam_New)
  TextWindow.WriteLine("Steam_Old: " + Steam_Old)
  
  'Even spaced fps delay
  TimeEnd = Clock.ElapsedMilliseconds
  PassedTime = TimeEnd - TimeStart
  TextWindow.WriteLine("PassedTime: " + PassedTime)
  PerfectDelay = 30-PassedTime
  If PerfectDelay > 0 Then
    Program.Delay(PerfectDelay)
  endif
endwhile

Sub SteamEmitter
  Steam_New = Steam_New + 1
  
  Steam[Steam_New] = Shapes.AddImage(Steam_Skin)
  Shapes.Move(Steam[Steam_New],-1000,-1000)
  
  Smoke_Velocity = 10+Math.GetRandomNumber(20)
  
  Steam_X[Steam_New] = Mouse_X+35*Math.Cos(Smoke_EmitAngle)
  Steam_Y[Steam_New] = Mouse_Y+35*Math.Sin(Smoke_EmitAngle)
  
  Steam_WX[Steam_New] = Math.Cos(Smoke_EmitAngle + (Math.GetRandomNumber(11)-6)*0.03) * Smoke_Velocity
  Steam_WY[Steam_New] = Math.Sin(Smoke_EmitAngle + (Math.GetRandomNumber(11)-6)*0.03) * Smoke_Velocity
  
  Steam_Opacity[Steam_New] = 100
  Steam_Angle[Steam_New] = Math.GetRandomNumber(360)-180
  Steam_Zoom[Steam_New] = Math.GetRandomNumber(5) * 0.1 + 0.8
endsub

Sub SteamCode
  For s = Steam_Old+1 To Steam_New
    Steam_WX[s] = Steam_WX[s] * 0.95
    Steam_WY[s] = Steam_WY[s] * 0.95
    Steam_X[s] = Steam_X[s] + Steam_WX[s]
    Steam_Y[s] = Steam_Y[s] + Steam_WY[s]
    
    Steam_Angle[s] = Steam_Angle[s] + Steam_Angle[s] * 0.006
    
    Steam_Distance[s] = math.SquareRoot((CircleX-Steam_X[s])*(CircleX-Steam_X[s])+(CircleY-Steam_Y[s])*(CircleY-Steam_Y[s]))
    If Steam_Distance[s] <= radius Then
      Collision()
    endif
    
    Steam_Zoom[s] = Steam_Zoom[s] + 0.1
    Steam_Opacity[s] = Steam_Opacity[s] - 2
    
    If Steam_Opacity[s] <= 0 And s = Steam_Old+1 Then
      RemoveSteam()
    endif
    
    Shapes.SetOpacity(Steam[s],Steam_Opacity[s])
    Shapes.Zoom(Steam[s],Steam_Zoom[s],Steam_Zoom[s])
    Shapes.Rotate(Steam[s],Steam_Angle[s])
    Shapes.Move(Steam[s],Steam_X[s]-35,Steam_Y[s]-35)
  endfor
endsub

Sub RemoveSteam
  Shapes.Remove(Steam[s])
  Steam[s] = ""
  Steam_X[s] = ""
  Steam_Y[s] = ""
  Steam_WX[s] = ""
  Steam_WY[s] = ""
  Steam_Opacity[s] = ""
  Steam_Angle[s] = ""
  Steam_Zoom[s] = ""
  Steam_Old = Steam_Old + 1
endsub

sub Collision
  'Make sure the starting point is outside the circle
  xP = Steam_X[s]-100*Steam_WX[s]
  yP = Steam_Y[s]-100*Steam_WY[s]
  
  'get incedence angle (theta) from veolcity
  If (Steam_WX[s] = 0) Then
    theta = Math.Pi/2
  Else
    theta = Math.ArcTan(Steam_WY[s]/Steam_WX[s])
  EndIf
  If (Steam_WX[s] < 0) Then
    theta = theta + Math.Pi
  EndIf
  
  'Get point of contact and reflection angle
  A = 1
  B = 2*(xP-circleX)*Math.Cos(theta) + 2*(yP-circleY)*Math.Sin(theta)
  C = (xP-circleX)*(xP-circleX)+(yP-circleY)*(yP-circleY)-radius*radius
  root = B*B-4*A*C
  dist2 = (xP-circleX)*(xP-circleX)+(yP-circleY)*(yP-circleY)-radius*radius
  
  If (root > 0 And dist2 > 0) Then
    r1 = (-B+Math.SquareRoot(root))/(2*A)
    r2 = (-B-Math.SquareRoot(root))/(2*A)
    If (Math.Abs(r1) < Math.Abs(r2)) Then
      r = r1
    Else
      r = r2
    EndIf
    If (r > 0) Then ' Only do collision if moving towards circle (-r is moving away)
      xC = xP+r*Math.Cos(theta)
      yC = yP+r*Math.Sin(theta)
      dx = circleX-xC
      dy = circleY-yC
      If (dx = 0) Then
        phi = pi/2
      Else
        phi = Math.ArcTan(dy/dx)
      EndIf
      If (dx < 0) Then
        phi = phi+pi
      EndIf
      psi = 2*phi-theta
      
      'The angle psi is already correct in radians
      emergentangle  = psi
      
      'Don't plot the direction lines
      'xR = xC-1000*Math.Cos(psi)
      'yR = yC-1000*Math.Sin(psi)
      'GraphicsWindow.DrawLine(xP,yP,xC,yC)
      'GraphicsWindow.DrawLine(xC,yC,xR,yR)
      
      'Reverse smoke direction using current velocity
      Smoke_Velocity = Math.SquareRoot(Steam_WX[s]*Steam_WX[s]+Steam_WY[s]*Steam_WY[s])
      Steam_WX[s] = -Math.Cos(emergentangle) * Smoke_Velocity
      Steam_WY[s] = -Math.Sin(emergentangle) * Smoke_Velocity
    EndIf
  EndIf
endsub

