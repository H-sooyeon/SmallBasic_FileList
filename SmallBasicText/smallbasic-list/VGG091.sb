' This program  can check word(s).    2011/08/22   by NaoChanON  

'This program refers  VFT761  and  GKN568



  Sentences_Sample()

  Show_Sample()

  Select_or_input()

  Check_Dictionary()



Sub Show_Sample

  GraphicsWindow.Clear()

  GraphicsWindow.BackgroundColor="Lightcyan"  

  GraphicsWindow.left=50

  GraphicsWindow.top=0

  GraphicsWindow.Height=700

  GraphicsWindow.Width=1200

  GraphicsWindow.FontName="Coorie new"

  GraphicsWindow.Show()

  GraphicsWindow.FontSize=18

  YP=40     'Sample listing pitch 

  XS=70     'Sample listing start point

  BW=850    'TextBox width

  BT=250    'TextBox  Y position

  BY=30     'textBox Height

  CW=150    'Checkbox Width

  

  For i = 1 To 5

    GraphicsWindow.DrawText(10,7+YP*(i-1),i)

    GraphicsWindow.drawRectangle(45,7+YP*(i-1),20,20)

    GraphicsWindow.DrawText(XS,5+YP*(i-1),sample[i])  'Sample sentences 

  endfor 

  

  textbox[1] = Controls.AddTextBox(XS,BT)    ' Sentences input box 

  Controls.SetSize(textbox[1] ,BW,BY)

  GraphicsWindow.BrushColor="Green"

  GraphicsWindow.FillRectangle(BW+XS+10,BT,CW,BY)

  GraphicsWindow.BrushColor="Yellow"

  GraphicsWindow.DrawText(BW+XS+15,BT+2,"  Check Start! ")

 

  i=6

  GraphicsWindow.BrushColor="Red"

  GraphicsWindow.DrawText(XS,15+YP*(i-1),sample[i]) ' Sample select or Input 

  GraphicsWindow.FontSize=18

Endsub  



Sub Select_or_input

  Loop1:

     GMX=GraphicsWindow.MouseX

     GMY=GraphicsWindow.MouseY

       If mouse.IsLeftButtonDown="true" Then

          If (45<GMX and GMX<65)  Then

             Goto Get_sample

          Elseif (BW+XS+10<GMX and GMX<BW+XS+CW+10 And BT<GMY And GMY<BT+BY) then

             Goto Get_yours

          EndIf

             Goto Loop1

       Endif    

   Goto Loop1 

  

  Get_sample:   '  gets sample sentences    

     S_No=Math.Round((GMY-5)/YP+0.5)

     GraphicsWindow.DrawText(47,8+YP*(S_No-1),"X")

     Program.Delay(500) 

     Goto End1

  

  Get_yours:           ' gets your sentences 

    S_No=7            

    Sample[S_No]= Controls.GetTextBoxText(textbox[1])

End1:

EndSub



Sub Check_Dictionary

  Review()

    Program.Delay(1000)

  Check_each_word()

    Program.Delay(1000)

  Attention()

EndSub



Sub Review    ' reject marks from sentences

  rjc_word="Rejected     ,  .  :  ?  !  -    etc...        except ( ' ) "

  GraphicsWindow.DrawText(2,BT+35,rjc_word)

  GraphicsWindow.DrawText(2,BT+60,"Residue")

  GraphicsWindow.DrawText(2,BT+100,"Result")

   

  TXT=Sample[S_No] +" "     'original sentences + " " 

  TXL=Text.GetLength(TXT)   ' Add  "  "  is a technic to catch endpoint  

    For i=1 To TXL

      M1=Text.GetSubText(TXT,i,1)      

      M2=Text.GetCharacterCode(M1)    '  ' :39    Large: 65- 90   Small : 97-  122  

       If M2<39 or (39<M2 and M2<65) or (90<M2 and M2<97) Or 122<M2  then    

          M1=" "   

       EndIf

      R_Sample=R_Sample+M1     '    Rejected residue

    Endfor

  

  GraphicsWindow.FontSize=20

  GraphicsWindow.BrushColor="navy"

  GraphicsWindow.DrawText(XS+10,BT+60,R_Sample)      ' Residue

EndSub



Sub Check_each_word

  start=1

  Loop2: 

  b=""   

  For i=start To TXL

     a=text.GetSubText(R_Sample,i,1)

     If a=" " Then

       Goto Skip_space

     endif  

     b=b+a       'Merge each "one" Character

  EndFor 



  Skip_space:   ' Skip space    ,    reject  pre-  '     post-  '    

    If b="" then   

       Goto Start_point

    endif

                        

    If Text.GetSubText(b,1,1)="'" Then 

      b=text.GetSubTextToEnd(b,2)

    EndIf         

    If Text.GetSubText(b,text.GetLength(b),1)="'" Then

      b=text.GetSubText(b,1, text.GetLength(b)-1)

    EndIf

    

  n=n+1       ' counts word(s)

  b[n]=b      ' memorizes  each word

  Result()    'Show results by animation   

 

  Start_point:

  start=i+1    ' change  startpoint 

    If start=TXL+1 Then 

       Goto end2

    EndIf

  Goto Loop2 

end2:

EndSub



Sub result

     c=Dictionary.GetDefinition(b[n])  '  Word check by Dictionary command

     TextWindow.Left=600

     TextWindow.Top=0

     TextWindow.BackgroundColor="White"

     TextWindow.ForegroundColor="Red"

     TextWindow.WriteLine(c)     ' Shows dictionary data into the  textwindow

        

     If c="" Then 

       GraphicsWindow.BrushColor="red"

       chk="Not correct"

     Else 

       GraphicsWindow.BrushColor="Navy"

       chk="Correct"

       mm=mm+1       'counts correct word(s)

     EndIf

    yloc=math.Remainder(n-1,10)            ' Yposition  

    chk[n]=shapes.AddText(b[n]+"="+chk) 

    Shapes.Move(chk[n],math.GetRandomNumber(Desktop.Width)/2,0)

    Shapes.Animate(chk[n],XS+10+30*(n-1),350+20*yloc,500)

EndSub



Sub Attention

  GraphicsWindow.BrushColor="Red"

  GraphicsWindow.FontSize=25

  If mm=n Then 

    msg="Congratulation!  All words  are correct! "

  Else 

    msg="Boo...  you'd better check incorrect word(s)"

  EndIf

  msg=shapes.AddText(msg)

  Shapes.Move(msg,math.GetRandomNumber(Desktop.Width)/3,0)

  Shapes.Animate(msg,400,600,1500)  

EndSub



Sub Sentences_Sample

 ' These samples ( Sample[2] -Sample[5] )   are written in Yahoo.UK head news. (2011/08/20 - 08/21 )    

  Sample[1]="Person's name might be judged to be an error. Exam: Lady-Gaga=OK but Shakespeare, Blair,Houston, Lennon=NG"

  Sample[2]="It was the ex-Harrods owner - not Charlie Sheen - who played the 'Un-Pharaoh' for one of the tasks."

  Sample[3]="Fiz finds herself in danger in prison in 'Corrie', while Whitney is shocked when Rob returns in 'EastEnders'"

  Sample[4]="Acidic foods such as tomatoes, citrus fruit juices and vinegars are not for everyone"

  Sample[5]="A lorry driver got the fright of his life when he crashed his vehicle through a third-floor window"

  Sample[6]="Select a sample (Check in the square box)   or   input sentences in the box and click check button!" 

EndSub




