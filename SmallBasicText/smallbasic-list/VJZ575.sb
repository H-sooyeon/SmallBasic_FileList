' Chime Programming System 0.1
' Program Copyright (c) 2014 Nonki Takahshi.
'
' License:
'  The MIT License (MIT)
'  http://opensource.org/licenses/mit-license.php
'
' History:
'  0.1a 2014-04-17 Created. ()
'
' Reference:
'  [1] Gakkou no Chaimu (Chime of School), se-053a.mp3, 
'      http://www.soundoffice.com/se/item/se-053.php
'
debug = "False"
CRLF = Text.GetCharacter(13) + Text.GetCharacter(10)
MAX = 40
gw = 598
gh = 428
GraphicsWindow.Width = gw
GraphicsWindow.Height = gh
GraphicsWindow.Title = "Chime Programming System 0.1"
GraphicsWindow.BackgroundColor = "LightGray"
url = "http://www.nonkit.com/smallbasic.files/"
chime = url + "se-053a.mp3"
' The following line could be harmful and has been automatically commented.
' path = File.GetSettingsFilePath()
GraphicsWindow.FontName = "Courier New"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.DrawText(10, 10, "Setting filename:" + CRLF + path)
' The following line could be harmful and has been automatically commented.
' settings = File.ReadContents(path)
GetCurrentTime()
otime = Shapes.AddText(ctime)
Shapes.Move(otime, 10, 50)
Timer.Tick = OnTick
GraphicsWindow.DrawText(10, 90, "Program:")
x = 10
y = 110
For i = 1 To MAX
  GraphicsWindow.DrawText(x, y, i)
  time[i] = Controls.AddTextBox(x + 20, y - 4)
  Controls.SetSize(time[i], 80, 24)
  If settings[i] <> "" Then
    Controls.SetTextBoxText(time[i], settings[i])
  EndIf
  x = x + 120
  If gw - 120 < x Then
    x = 10
    y = y + 26
  EndIf
EndFor
GetNextTime()
Controls.TextTyped = OnTextTyped
ring = "False"
changed = "False"
While "True"
  If ring Then
    Sound.Play(chime)
    GetNextTime()
    ring = "False"
  EndIf
  If changed Then
    GetNextTime()
    changed = "False"
  EndIf
EndWhile
Sub GetNextTime
  ' param cmin - current minutes
  ' return nmin - next minutes
  nrem = 1440
  nmin = 1440
  For i = 1 To MAX
    If settings[i] <> "" Then
      _h = Text.GetSubText(settings[i], 1, 2)
      _m = Text.GetSubText(settings[i], 4, 2)
      _min = _h * 60 + _m
      remain = _min - cmin
      If 0 < remain And remain < nrem Then
        nrem = remain
        nmin = _min
      EndIf
    EndIf
  EndFor
  If debug Then
    GraphicsWindow.Title = "nmin=" + nmin + " cmin=" + cmin
  EndIf
EndSub
Sub OnTextTyped
  tb = Controls.LastTypedTextBox
  For _i = 1 To MAX
    If time[_i] = tb Then
      settings[_i] = Controls.GetTextBoxText(tb)
      Goto break
    EndIf
  EndFor
  break:
' The following line could be harmful and has been automatically commented.
'   File.WriteContents(path, settings)
  changed = "True"
EndSub
Sub OnTick
  GetCurrentTime()
  Shapes.SetText(otime, ctime)
  If nmin <= cmin Then
    ring = "True"
  EndIf
EndSub
Sub GetCurrentTime
  h = Clock.Hour
  If h <= 9 Then
    h = Text.Append("0", h)
  EndIf
  m = Clock.Minute
  If m <= 9 Then
    m = Text.Append("0", m)
  EndIf
  ctime = "Current time: " + h + ":" + m
  cmin = h * 60 + m
  s = Clock.Second
  Timer.Interval = (60 - s) * 1000
EndSub
