Init()



EndofProgram = F

While EndofProgram = F

  checkbuttonclick()

EndWhile

Program.End()



Sub Init

  T = "True"

  F = "False"

  

  'Set the treeview data, the first index is the item number and the second is its parent (0 is top level)

  tree[1][0] = "Level 1"

  tree[2][1] = "Level 1 1"

  tree[3][1] = "Level 1 2"

  tree[4][3] = "Level 1 2 1"

  tree[5][0] = "Level 2"

  tree[6][5] = "Level 2 1"

  tree[7][5] = "Level 2 2"

  tree[8][0] = "Level 3"

  tree[9][0] = "End"

  

  'Create the treeview

  GraphicsWindow.Show()

  treeView = LDControls.AddTreeView(tree,200,200)

  Shapes.Move(treeView,50,50)

  

  'Add a textBox to show the current selection

  item = Controls.AddTextBox(50,260)

  

  'Set up the initial view

  LDControls.TreeViewItemChanged = OnTreeViewItemChanged 'Register selection event

  LDControls.TreeViewSelect(treeView,5) 'Select the second top level item "Level 2' - Note the OnTreeViewItemChanged is also fired with this action

  

  'Add buttons to toggle expand

  expand = Controls.AddButton("Expand All",50,20)

  collapse = Controls.AddButton("Collapse All",150,20)

  edit = Controls.AddButton("Edit", 250, 20)

  endedit = Controls.AddButton("End of edit", 350, 20)

  Controls.HideControl(endedit)

  Controls.ButtonClicked = OnButtonClicked

EndSub 



Sub OnTreeViewItemChanged

  index = Array.GetAllIndices(tree[LDControls.LastTreeViewIndex]) ' To get the second index (parent) for this item label

  i1 = LDControls.LastTreeViewIndex

  i2 = index[1]

  Controls.SetTextBoxText(item,tree[i1][i2])

  If i1 = 9 Then

    EndofProgram = T

  EndIf 

EndSub



Sub OnButtonClicked

  OBC = T

EndSub



Sub TextEdit

  Controls.HideControl(expand)

  Controls.HideControl(collapse)

  Controls.HideControl(edit)

  Controls.ShowControl(endedit)

  EOE = F

  LDFocus.SetFocus(item)

  While EOE = F 

    checkbuttonclick()

  EndWhile 

  tree[i1][i2] = Controls.GetTextBoxText(item)

  LDControls.TreeViewContent(treeView, tree)

  LDControls.TreeViewExpand(treeview,0,"True","True")

  Controls.HideControl(endedit)

  Controls.ShowControl(expand)

  Controls.ShowControl(collapse)

  Controls.ShowControl(edit)

EndSub 



Sub checkbuttonclick

  If OBC = T Then 

    OBC = F 

    If (Controls.LastClickedButton = expand) Then

      LDControls.TreeViewExpand(treeview,0,"True","True")

    ElseIf (Controls.LastClickedButton = collapse) Then

      LDControls.TreeViewExpand(treeview,0,"False","True")

    ElseIf (Controls.LastClickedButton = edit) Then

      TextEdit()

    ElseIf (Controls.LastClickedButton = endedit) Then

      EOE = T

    EndIf

  EndIf 

EndSub 


