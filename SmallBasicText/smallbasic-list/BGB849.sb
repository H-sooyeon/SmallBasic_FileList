'Small Basic challenge of the month, January 2019

'Tartan Pattern



'CONFIG

gw = 1200

gh = 800

Turtle.Speed = 9

scale = 1             '1 for original scale, increase or decrease to zoom.



GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

GraphicsWindow.Left = (Desktop.Width - gw) / 2

GraphicsWindow.Top = (Desktop.Height - gh) / 2

GraphicsWindow.CanResize = "False"

GraphicsWindow.BackgroundColor = "SteelBlue"

GraphicsWindow.Title = "Tartan"



'MAIN

createAssets()

scaleAssets()

drawStripes()



'SUBROUTINES

Sub createAssets      'This can be modified to change the pattern, the index must be numbered sequentialy

  asset[1] = "start=150;gap=300;width=150;colour=#32ADFF2F"

  asset[2] = "start=0;gap=150;width=50;colour=#80000000"

  asset[3] = "start=355;gap=600;width=5;colour=#80000000"

  asset[4] = "start=365;gap=600;width=5;colour=#80000000"

  asset[5] = "start=430;gap=600;width=5;colour=#80000000"

  asset[6] = "start=440;gap=600;width=5;colour=#80000000"

  asset[7] = "start=95;gap=300;width=10;colour=#64FF0000"

  asset[8] = "start=245;gap=300;width=10;colour=#64FFFF00"

EndSub



Sub scaleAssets       'Sets the zoom level using the scale setting above

  For i = 1 To Array.GetItemCount(asset)

    asset[i]["start"] = asset[i]["start"] * scale

    asset[i]["gap"] = asset[i]["gap"] * scale

    asset[i]["width"] = asset[i]["width"] * scale

  EndFor

EndSub



Sub drawStripes

  For a = 1 To Array.GetItemCount(asset)

    GraphicsWindow.PenColor = asset[a]["colour"]

    width = asset[a]["width"]

    GraphicsWindow.PenWidth = width

    For i = asset[a]["start"] To Math.Max(gw, gh) Step asset[a]["gap"]

      If (i - width / 2 < gw) Then      'Draw horizontal stripes

        Turtle.PenUp()

        Turtle.MoveTo(i + width / 2, 0)

        Turtle.Angle = 180

        Turtle.PenDown()

        Turtle.Move(gh + 10)

      EndIf

      If (i - width / 2 < gh) Then      'Draw vertical stripes

        Turtle.PenUp()

        Turtle.MoveTo(0, i + width / 2)

        Turtle.Angle = 90

        Turtle.PenDown()

        Turtle.Move(gw + 10)

      EndIf

    EndFor

  EndFor

  Turtle.Hide()

EndSub


