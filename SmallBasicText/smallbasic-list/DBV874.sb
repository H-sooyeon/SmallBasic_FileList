Anfang: 
start = Math.GetRandomNumber(2) 'Bestimmen ob der Spieler  start=1 oder das Programm start=2 beginnt
aa100init() 'Initialisiern Parameter
aa200rahmen() 'Aufabu Fenster
aa050hilfe()
aa300schiffesetzens() 'Setzen der Schiffe des Spielers
GraphicsWindow.Clear()
aa350eingabefeldschiffeok() 'Bestätigen der eingegebenen Schiffe des Spielers
If weiter = 0 then
  Goto Anfang
endif
aa400schiffesetztenp() 'Setzen der Schiffe des Programms
spiel = ""
While spiel= "" ' Spieldialog 
  schusszähler = schusszähler + 1   
  If trace = 1 Then
    TextWindow.WriteLine(schusszähler)
  EndIf
  If start = 2 Then
    start = 1
    Goto aprog
  EndIf
  aa500eingabefeld() 'Schuss des Spielers eingeben
  aa550suchenp()
  If verszs = 10  Then
    aa900schlussbild()
    spiel = "true"
    Goto eprog
  EndIf 
  aprog:
  If tschussep = 2 Then
    aa600trefferschuss() 'Folgeschuss nach einem Treffer
  else 
    If schussep = 2 then
      aa600trefferschuss()
    else
      aa700Schussprogramm() ' Schuss des Programms ermitteln  -  ergebnis ysq xsq
    EndIf
  EndIf
  aa800antwortfeld() ' Ausgabe des Schussergebnis des Programms
  If verszp = 10  Then
    aa900schlussbild()
    spiel = "true"
  EndIf 
  eprog: 
endwhile
Goto Anfang



'Unterprogramme



Sub aa050hilfe 'Hilfe und Grundeinstellungen
  
  GraphicsWindow.BrushColor = col[4]
  GraphicsWindow.DrawText(40, 20, "Wählen Sie das Hilfethema")
  hi1 = Controls.AddButton("Spielmodus     ", 40, 80)
  hi2 = Controls.AddButton("Schiffe setzen ", 40, 120)
  hi3 = Controls.AddButton("Spielen            ", 40, 160)
  'hi4 = Controls.AddButton("Spielen          ", 40, 200)
  'hi5 = Controls.AddButton("Analyse         ", 40, 240)
  'hi6 = Controls.AddButton("Meister          ", 40, 280)
  hi7 = Controls.AddButton("Spielmodus  ", 40, 320)
  GraphicsWindow.DrawText(200,320, Modus[Spielmodus])
  hi8 = Controls.AddButton("Spiel starten", 40, 460)
  hi9 = Controls.AddButton("Ende             ",40,500)
  
  bStart = ""
  Controls.ButtonClicked = ak050OnButtonClicked
  While bStart = ""
    Program.Delay(500)
  EndWhile
  GraphicsWindow.Clear() 
EndSub


Sub ak050OnButtonClicked
  oBu = Controls.LastClickedButton
  GraphicsWindow.BrushColor = col[7]
  GraphicsWindow.FillRectangle(200,60,900,600) 'löschen des Textbereichs
  If oBu = hi1 Then   
    GraphicsWindow.BrushColor = col[9]
    GraphicsWindow.DrawText(220,70,"Sie können auswählen wer das Spiel beginnt:")
    GraphicsWindow.DrawText(240,110,"- Der Spieler")
    GraphicsWindow.DrawText(240,150,"- Das Programm")
    GraphicsWindow.DrawText(240,190,"- Sie überlassen es dem Zufall wer das Spiel beginnt")
  ElseIf oBu = hi2 Then
    GraphicsWindow.BrushColor = col[9]
    GraphicsWindow.DrawText(220,70,"Sie können Ihre Schiffe auf zwei Arten setzen:")
    GraphicsWindow.DrawText(240,100,"- automatisch")
    GraphicsWindow.DrawText(240,130,"- manuell")
    GraphicsWindow.DrawText(220,170,"Wählen Sie automatisch, dann werden eventuell bereits manuell eingegebene Schiffe gelöscht.")
    GraphicsWindow.DrawText(220,200,"Die Eingabe ist damit abgeschlossen.")
    GraphicsWindow.DrawText(220,250,"Bei der manuellen Eingabe werden Sie vom Programm aufgefordert welche Art von Schiff Sie eingeben sollen.")
    GraphicsWindow.DrawText(220,280,"Für jedes Schiff sind 3 Angaben erforderlich:")
    GraphicsWindow.DrawText(220,310,"Startkoordinate Zeile: A - J  - anklicken links vom Eingabefeld")
    GraphicsWindow.DrawText(220,340,"Startkoordinate Spalte: 0 - 9  - anklicken unterhalb vom Eingabefeld")
    GraphicsWindow.DrawText(220,370,"Eingaberichtung: nach rechts ´>´   oder  nach unten ´V´. Bei 1-er Schiffen ist dies vorgegeben.")
    GraphicsWindow.DrawText(220,400,"Schiffe dürfen nicht direkt nebeneinander liegen, sondern müssen durch Wasser getrennt sein.")
    GraphicsWindow.DrawText(220,430,"Ist eine Eingabe nicht vollständig oder nicht regelkonform, dann bleibt die aktuelle Eingabe aktiv.")
    GraphicsWindow.DrawText(220,460,"Die Eingabe bestätigen Sie mir OK..")
    GraphicsWindow.DrawText(220,490,"Sind alle Schiffe eingegeben, dann wird das Ergebnis angezeigt.")
    GraphicsWindow.DrawText(220,540,"Sie können diese Setzung verwerfen oder das Spiel starten.")
  elseif oBu= hi3 then
    GraphicsWindow.BrushColor = col[9]
    GraphicsWindow.DrawText(220,70,"Auf dem Spielfeld wird links das Schussfeld des Spielers und recht das des Programms angezeigt.")
    GraphicsWindow.DrawText(220,100,"Abwechselnd erfolgt ein Schuss durch den Spieler und ein Schuss durch das Programm.")
    GraphicsWindow.DrawText(220,130,"Das Schussergebnis des Programms muss bestätigt werden, damit das Spiel fortgesetzt wird.")
    GraphicsWindow.DrawText(220,160,"Der Spieler gibt die Schusskoordinaten über die Spalten- und Zeilenkennung ein.")
    GraphicsWindow.DrawText(220,190,"Sind beide Angaben gemacht kann der Schuss bestätigt werden.")
    GraphicsWindow.DrawText(220,220,"Sind die Angaben für den Schuss nicht vollständig, bleibt das Programm im Eingabemodus.")
    GraphicsWindow.DrawText(220,250,"Ein Schuss wird durch einen grünen Kreis dargestellt. Nur beim Schuss durch das Programm.")
    GraphicsWindow.DrawText(220,280,"Handelt es sich um einen Treffer so wird der Kreis rot.")
    GraphicsWindow.DrawText(220,310,"Wird ein Schiff versenkt so wird dies rot im jeweiligen Spielfeld angezeigt.")
    GraphicsWindow.DrawText(220,340,"Ein Treffer im Wasser verändert die Farbe von hellblau auf dunkelblau.")
    GraphicsWindow.DrawText(220,370,"Bei einem Treffer oder Versenken werden die um das Schiff liegenden Wasserfelder automatisch dunkelblau.")
    GraphicsWindow.DrawText(220,400,"Im rechten Spielfeld werden alle noch nicht getroffenen Schiffe grau angezeit.")
    GraphicsWindow.DrawText(220,430,"Wird das zehnte Schiff versenk, so erfolgt eine entsprechende Meldung und das Spiel ist beendet.")
    GraphicsWindow.DrawText(220,460,"Hat das Programm das Spiel gewonnen, werden grau die noch nicht getroffenen Schiffe angezeigt.")
    GraphicsWindow.DrawText(220,490,"Sie können jetzt ein neues Spiel starten oder das Programm beenden.")
    '  elseif oBu = hi4 then
    '    GraphicsWindow.BrushColor = col[9]
    '    GraphicsWindow.DrawText(220,70,"hi4")
    '  elseif oBu = hi5 then
    '    GraphicsWindow.BrushColor = col[9]
    '    GraphicsWindow.DrawText(220,70,"hi5.")
    'elseif oBu = hi6 then
    '  GraphicsWindow.DrawText(240,60,"aaaa6")
  elseif oBu = hi7 then
    Spielmodus = Spielmodus + 1
    If Spielmodus = 4 then
      Spielmodus = 1
    EndIf
    If Spielmodus = 1 then
      start = Math.GetRandomNumber(2)
    EndIf
    If Spielmodus = 2 then
      start = 1
    EndIf
    If Spielmodus = 3 then
      start = 2
    EndIf
    GraphicsWindow.BrushColor = col[7]
    GraphicsWindow.FillRectangle (200,320,200,20)
    GraphicsWindow.BrushColor = col[4]
    GraphicsWindow.DrawText(200,320, Modus[Spielmodus])
  Elseif oBu = hi8 then
    bStart = "True"
  elseif oBu = hi9 then
    Program.End()
  Else
    bStart = ""  
  EndIf
EndSub



Sub aa100init 'Initalisieren der Parameter
  GraphicsWindow.Clear()
  Titel = " Schiffe versenken "
  GraphicsWindow.Title =Titel
  kox = 40 'Anfangskoordinate des Spielfelds x
  koy = 60 'Anfangskoordinate des Spielfelds y
  qukox = 40
  qukoy = 60
  schiffkox = 40
  schiffkoy = 60
  schiffl = 4 'Schiffflänge
  schiffla = 0 'Schifflänge für die Anzeige
  schiffx = 1 'Schiff x-Koordinate
  schiffy = 1 'Schiff y-Koordinate
  schiffr = 0 'Schiff nach rechts
  schifft = 0 'Schiff nach unten
  schiffc = 3 'Farbe des Schiffs
  schusses = 0 'Schussergebnis Spieler - Antwort der Programms 1 = Wasser, 2 = Treffer, 3 versenkt
  schusszähler = 0 'Zählt wieviele Schüsse das Programm abgegeben hat
  sz = 0 'Schiffszähler bei der Eingabe in die Matrix
  trace = 0 'Schussart protokollieren - 1 = ein  0 = aus
  GraphicsWindow.FontSize = 15 
  Col[1] = "PowderBlue"
  Col[2] = "navy"
  Col[3] = "gray"
  Col[4] = "black"
  Col[5] = "Red"
  Col[6] = "Green"
  Col[7] = "White"
  gkr = 34 'Grösse des Schusskreis - Wert muss gerade sein
  poss = 0
  posz = 0
  poszk = " "
  posdir = " "
  Spielmodus = 1
  Modus [1]= "automatisch"
  Modus [2] = "Spieler beginnt"
  Modus [3] = "Programm beginnt"
  tri = 1 'Schussrichtung nach Treffer
  trz2q = 0 'Zähler wievieler 2er versenkt sind durch das Programm
  trz3q = 0 'Zähler wievieler 3er versenkt sind durch das Programm
  trz4q = 0 'Zähler wievieler 4er versenkt sind durch das Programm
  tschussep = 0 'Treffer noch nicht bis versenkt bearbeitet durch das Programm
  verszs = 0 ' Zähler versenkte Schiffe  Spieler
  verszp = 0 'Zähler versenkte Schiffe  Programm
  For i = 1 To 10 'Liste der jeweiligen Schiffe
    For k = 1 To 6 '1 Länge, 2 KOX, 3 KOY, 4 nach rechts, 5 nach unten, 6 AZ Treffer
      eschiffs [i][k] = 0 'Schiffe Spieler
      eschiffp [i][k] = 0 'Schiffe Programm
    EndFor
  EndFor
  For i = 1 To 13 'setzen des Randes mit Wasser
    For k = 1 To 13
      If i = 13 Or k = 13 Then
        rx = 1
      Else
        rx = 0
      EndIf    
      mschiffs [i][k] = rx 'Matrix Spieler
      mschiffp [i][k] = rx 'Matrix Programm
      xschiffs [i][k] = rx 'Matrix für das Programmschussergebnis
    EndFor
  EndFor 
EndSub



Sub aa200rahmen 'Aufbau Fenster für das Spiel
  GraphicsWindow.Top= 10
  GraphicsWindow.Left = 10
  GraphicsWindow.Width = 1100
  GraphicsWindow.Height = 650
EndSub



Sub aa300schiffesetzens 'Setzen der Schiffe des Spielers 
  schiffl = 5
  For ii = 1 to 4 
    schiffl = schiffl -1
    For kk = 1 To ii 
      ab300eingabefeldschiffe()
      sz = sz + 1
      eschiffs [sz][1] = schiffl
      eschiffs [sz][2] = poss
      eschiffs [sz][3] = posz
      eschiffs [sz][4] = posre
      eschiffs [sz][5] = posdw
      eschiffs [sz][6] = 0  
      For ll = 1 to schiffl
        mschiffs [poss][posz] = sz + 10
        If posre = 1 Then
          poss = poss +1
        EndIf
        If posdw = 1 Then
          posz = posz + 1
        EndIf
      endfor    
      poss = 0
      posz = 0
      posre = 0
      posdw = 0 
      posdir = "" 
    EndFor
  Endfor   
  For ii = 1 To 12 'setzen des Randes mit Wasser 
    mschiffs [1][ii] = 1
    xschiffs [1][ii] = 1
    mschiffs [12][ii] = 1  
    xschiffs [12][ii] = 1   
    mschiffs [ii][1] = 1
    xschiffs [ii][1] = 1
    mschiffs [ii][12] = 1 
    xschiffs [ii][12] = 1  
  EndFor
EndSub



Sub ab300eingabefeldschiffe 'Eingabedialog für das Setzen der Schiffe des Spielers
  qukox = kox
  qukoy = koy
  ah100quadrate()
  For ix = 1 To 10
    If eschiffs [ix][1] <> 0 Then
      schiffx = eschiffs [ix][2] - 1 'Schiff x-Koordinate
      schiffy = eschiffs [ix][3] - 1 'Schiff y-Koordinate
      schiffr = eschiffs [ix][4] 'Schiff nach rechts
      schifft = eschiffs [ix][5] 'Schiff nach unten
      schiffla = eschiffs [ix][1]
      schiffkox = kox
      schiffkoy = koy    
      ah200schiff()      
    EndIf
  EndFor
  If schiffl = 1 Then 
    posre = 0
    posdw = 1
    posdir = "V" 
  EndIf
  GraphicsWindow.BrushColor=(col[7])
  GraphicsWindow.FillRectangle(kox + 40,koy - 25,200,40)
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText(kox + 45,koy - 30,"Eingabe "+ schiffl +"-er Schiff")    
  za = Controls.AddButton("A",kox,koy + 44)
  zb = Controls.AddButton("B",kox,koy + 84)
  zc = Controls.AddButton("C",kox,koy + 124)   
  zd = Controls.AddButton("D",kox,koy + 164)
  ze = Controls.AddButton("E",kox,koy + 204)
  zf = Controls.AddButton("F",kox,koy + 244)
  zg = Controls.AddButton("G",kox,koy + 284)
  zh = Controls.AddButton("H",kox,koy + 324)
  zi = Controls.AddButton("I",kox,koy + 364)
  zj = Controls.AddButton("J",kox,koy + 404)
  sd = Controls.AddButton("V",kox,koy + 444)
  ok = Controls.AddButton("Schiff setzen",kox,koy + 484)
  xf = Controls.AddButton("Schiffe automatisch setzen lassen",kox,koy + 524)
  z0 = Controls.AddButton("0",kox + 49,koy + 444)
  z1 = Controls.AddButton("1",kox + 89,koy + 444)
  z2 = Controls.AddButton("2",kox + 129,koy + 444)
  z3 = Controls.AddButton("3",kox + 169,koy + 444)
  z4 = Controls.AddButton("4",kox + 209,koy + 444)
  z5 = Controls.AddButton("5",kox + 249,koy + 444)
  z6 = Controls.AddButton("6",kox + 289,koy + 444)
  z7 = Controls.AddButton("7",kox + 329,koy + 444)
  z8 = Controls.AddButton("8",kox + 369,koy + 444)
  z9 = Controls.AddButton("9",kox + 409,koy + 444)
  sr = Controls.AddButton(">",kox + 449,koy + 444)
  ah300Zeilemarkieren()
  bStart = ""
  Controls.ButtonClicked = ak300OnButtonClickeds
  While bStart = ""
    Program.Delay(500)
  EndWhile
EndSub



Sub ak300OnButtonClickeds
  oBu = Controls.LastClickedButton
  If oBu = xf Then
    aa320schiffesetztens()
    bStart = "True"
    kk = ii
    ii = 4
  ElseIf oBu = za Then
    posz= 2
    poszk = "A"
  ElseIf oBu = zb Then
    posz = 3
    poszk = "B"
  elseif oBu= zc then
    posz = 4
    poszk = "C"
  elseif oBu = zd then
    posz = 5
    poszk = "D"
  elseif oBu = ze then
    posz = 6
    poszk = "E"
  elseif oBu = zf then
    posz = 7
    poszk = "F"
  elseif oBu = zg then
    posz = 8
    poszk = "G"
  Elseif oBu = zh then
    posz= 9  
    poszk = "H"
  ElseIf oBu = zi Then
    posz= 10
    poszk = "I"
  elseif oBu= zj then
    posz = 11
    poszk = "J"
  elseif oBu = z0 then
    poss = 2
  elseif oBu = z1 then
    poss = 3
  elseif oBu = z2 then
    poss = 4
  elseif oBu = z3 then
    poss = 5
  Elseif oBu = z4 then
    poss = 6
  elseif oBu = z5 then
    poss = 7
  elseif oBu = z6 then
    poss = 8
  elseif oBu = z7 then
    poss = 9
  elseif oBu = z8 then
    poss = 10
  Elseif oBu = z9 then
    poss = 11
  elseif oBu = sd then
    posre = 0
    posdw = 1
    posdir = "V"
  Elseif oBu = sr then
    posre = 1
    posdw = 0
    posdir = ">"
  elseif oBu = ok then
    If poss * posz * (posre + posdw) <> 0 then
      ac300checkschiffs()
      If checkok = 1 then       
        bStart = "True"
      Else
        posre = 0
        posdw = 0
        posdir = ""
        poss = 0
        posz = 0
        If schiffl = 1 Then 
          posre = 0
          posdw = 1
          posdir = "V" 
        EndIf
      EndIf
    EndIf
  Else
    bStart = "" 
  EndIf
  ah300Zeilemarkieren()
EndSub



Sub ac300checkschiffs 'Prüfen ob die vom Spieler gesetzten Schiffe den Spielbedingungen unterliegen
  checkok = 1
  If posre = 1 Then
    For xx = poss - 1 To poss + schiffl
      For yy = posz - 1 To posz + 1
        If mschiffs [xx][yy] <> 0 then
          checkok = 0
          xx = poss + schiffl
          yy = posz + 1
        EndIf
      EndFor
    EndFor
  EndIf
  If posdw = 1 Then
    For xx = poss - 1 To poss + 1
      For yy = posz - 1 To posz + schiffl
        If mschiffs [xx][yy] <> 0 then
          checkok = 0
          xx = poss + 1
          yy = posz + schiffl
        EndIf
      EndFor
    EndFor
  EndIf
EndSub



Sub aa320schiffesetztens 'Setzen der Schiffe für den Spieler
  schiffl = 5
  sz = 0
  For xii = 1 to 4 
    schiffl = schiffl -1
    For xkk = 1 To xii 
      ab320zufallschiff()
      sz = sz +1
      eschiffs [sz][1] = schiffl
      eschiffs [sz][2] = poss
      eschiffs [sz][3] = posz
      eschiffs [sz][4] = posre
      eschiffs [sz][5] = posdw
      eschiffs [sz][6] = 0  
      For ll = 1 to schiffl
        mschiffs [poss][posz] = sz + 10
        If posre = 1 Then
          poss = poss +1
        EndIf
        If posdw = 1 Then
          posz = posz + 1
        EndIf
      endfor    
      poss = 0
      posz = 0
      posre = 0
      posdw = 0 
      posdir = "" 
    EndFor
  Endfor 
  For ii = 1 To 12  
    mschiffs[1][ii] = 1
    mschiffs[12][ii] = 1
    mschiffs[ii][1] = 1
    mschiffs[ii][12] = 1    
  EndFor
EndSub



Sub ab320zufallschiff 'Ermitteln der Startdaten für ein Schiff des Spielers
  zloops:
  poss = Math.GetRandomNumber (10) + 1
  posz = Math.GetRandomNumber (10) + 1 
  posre = Math.GetRandomNumber (2) - 1
  posdw = 0
  If posre = 0 Then
    posdw = 1
  EndIf
  ac300checkschiffs()
  If checkok = 0 Then
    poss = 0
    posz = 0
    posre = 0
    posdw = 0 
    posdir = "" 
    Goto zloops
  EndIf
EndSub



Sub ac320checkschiffs 'Prüfen ob das Schiff des Programms den Regeln entspricht
  checkok = 1
  If posre = 1 Then
    For xx = poss - 1 To poss + schiffl
      For yy = posz - 1 To posz + 1
        If mschiffs [xx][yy] <> 0 then
          checkok = 0
          xx = poss + schiffl
          yy = posz + 1
        EndIf
      EndFor
    EndFor
  EndIf
  If posdw = 1 Then
    For xx = poss - 1 To poss + 1
      For yy = posz - 1 To posz + schiffl
        If mschiffs [xx][yy] <> 0 then
          checkok = 0
          xx = poss + 1
          yy = posz + schiffl
        EndIf
      EndFor
    EndFor
  EndIf
EndSub




Sub aa350eingabefeldschiffeok 'Abfrage ob die gestzten Schiffe des Spieler so in Ordnung sind
  qukox = kox
  qukoy = koy
  ah100quadrate()
  For ix = 1 To 10
    If eschiffs [ix][1] <>0 Then
      schiffx = eschiffs [ix][2] - 1 'Schiff x-Koordinate
      schiffy = eschiffs [ix][3] - 1 'Schiff y-Koordinate
      schiffr = eschiffs [ix][4] 'Schiff nach rechts
      schifft = eschiffs [ix][5] 'Schiff nach unten
      schiffla = eschiffs [ix][1]
      schiffkox = kox
      schiffkoy = koy        
      ah200schiff()      
    EndIf
  EndFor
  GraphicsWindow.BrushColor=(col[4])
  ok = Controls.AddButton("OK",kox,koy + 484)
  verw = Controls.AddButton("Eingabe verwerfen",kox + 350,koy + 484) 
  bStart = ""
  Controls.ButtonClicked = ak350OnButtonClickedsok
  While bStart = ""
    Program.Delay(500)
  EndWhile
EndSub



Sub ak350OnButtonClickedsok
  oBu = Controls.LastClickedButton
  bStart = "True"
  If oBu = verw then
    weiter = 0
  ElseIf oBu = ok then
    weiter = 1
  Else
    bStart = "" 
  EndIf
EndSub



Sub aa400schiffesetztenp 'Setzen der Schiffe des Programms
  schiffl = 5
  sz = 0
  For ii = 1 to 4 
    schiffl = schiffl -1
    For kk = 1 To ii 
      ab400zufallschiff()
      sz = sz +1
      eschiffp [sz][1] = schiffl
      eschiffp [sz][2] = poss
      eschiffp [sz][3] = posz
      eschiffp [sz][4] = posre
      eschiffp [sz][5] = posdw
      eschiffp [sz][6] = 0  
      For ll = 1 to schiffl
        mschiffp [poss][posz] = sz + 10
        If posre = 1 Then
          poss = poss + 1
        EndIf
        If posdw = 1 Then
          posz = posz + 1
        EndIf
      endfor    
      poss = 0
      posz = 0
      posre = 0
      posdw = 0 
      posdir = "" 
    EndFor
  Endfor 
  For ii = 1 To 12  
    mschiffp[1][ii] = 1
    mschiffp[12][ii] = 1
    mschiffp[ii][1] = 1
    mschiffp[ii][12] = 1    
  EndFor  
EndSub



Sub ab400zufallschiff 'Ermitteln der Startdaten für ein Schiff des Programms
  zloop:
  poss = Math.GetRandomNumber (10) + 1
  posz = Math.GetRandomNumber (10) + 1 
  posre = Math.GetRandomNumber (2) - 1
  posdw = 0
  If posre = 0 Then
    posdw = 1
  EndIf
  ac400checkschiffp()
  If checkok = 0 Then
    poss = 0
    posz = 0
    posre = 0
    posdw = 0 
    posdir = "" 
    Goto zloop
  EndIf
EndSub



Sub ac400checkschiffp 'Prüfen ob das Schiff des Programms den Regeln entspricht
  checkok = 1
  If posre = 1 Then
    For xx = poss - 1 To poss + schiffl
      For yy = posz - 1 To posz + 1
        If mschiffp [xx][yy] <> 0 then
          checkok = 0
          xx = poss + schiffl
          yy = posz + 1
        EndIf
      EndFor
    EndFor
  EndIf
  If posdw = 1 Then
    For xx = poss - 1 To poss + 1
      For yy = posz - 1 To posz + schiffl
        If mschiffp [xx][yy] <> 0 then
          checkok = 0
          xx = poss + 1
          yy = posz + schiffl
        EndIf
      EndFor
    EndFor
  EndIf
EndSub



Sub aa500eingabefeld 'Dialogfeld für das eigentliche Spiel Schiessen durch den Spieler
  GraphicsWindow.Clear() 
  qukox = kox
  qukoy = koy
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText (qukox + 150,qukoy - 5,"Schiffe des Programms")
  GraphicsWindow.DrawText (qukox + 650,qukoy - 5,"Schiffe des Spielers")
  ah100quadrate()
  ab800schussergebnis()
  qukox = kox + 500
  qukoy = koy
  ah100quadrate()
  ab500Schiffespieler()
  ac500schussergebnisp()
  poss = 0
  posz = 0
  poszk = ""
  GraphicsWindow.BrushColor=(col[4])
  za = Controls.AddButton("A",kox,koy + 44)
  zb = Controls.AddButton("B",kox,koy + 84)
  zc = Controls.AddButton("C",kox,koy + 124)   
  zd = Controls.AddButton("D",kox,koy + 164)
  ze = Controls.AddButton("E",kox,koy + 204)
  zf = Controls.AddButton("F",kox,koy + 244)
  zg = Controls.AddButton("G",kox,koy + 284)  
  zh = Controls.AddButton("H",kox,koy + 324)
  zi = Controls.AddButton("I",kox,koy + 364)
  zj = Controls.AddButton("J",kox,koy + 404)
  ok = Controls.AddButton("schiessen",kox,koy + 484)
  z0 = Controls.AddButton("0",kox + 49,koy + 444)
  z1 = Controls.AddButton("1",kox + 89,koy + 444)
  z2 = Controls.AddButton("2",kox + 129,koy + 444)
  z3 = Controls.AddButton("3",kox + 169,koy + 444)
  z4 = Controls.AddButton("4",kox + 209,koy + 444)
  z5 = Controls.AddButton("5",kox + 249,koy + 444)
  z6 = Controls.AddButton("6",kox + 289,koy + 444)
  z7 = Controls.AddButton("7",kox + 329,koy + 444)
  z8 = Controls.AddButton("8",kox + 369,koy + 444)   
  z9 = Controls.AddButton("9",kox + 409,koy + 444)
  ah300Zeilemarkieren()
  bStart = ""
  Controls.ButtonClicked = ak500OnButtonClicked
  While bStart = ""
    Program.Delay(500)
  EndWhile
EndSub



Sub ak500OnButtonClicked
  oBu = Controls.LastClickedButton
  If oBu = za Then
    posz = 2
    poszk = "A"
  ElseIf oBu = zb Then
    posz = 3
    poszk = "B"
  elseif oBu= zc then
    posz = 4
    poszk = "C"
  elseif oBu = zd then
    posz = 5
    poszk = "D"
  elseif oBu = ze then
    posz = 6
    poszk = "E"
  elseif oBu = zf then
    posz = 7
    poszk = "F"
  elseif oBu = zg then
    posz = 8
    poszk = "G"
  Elseif oBu = zh then
    posz = 9  
    poszk = "H"
  ElseIf oBu = zi Then
    posz = 10
    poszk = "I"
  elseif oBu= zj then
    posz = 11
    poszk = "J"
  elseif oBu = z0 then
    poss = 2
  elseif oBu = z1 then
    poss = 3
  elseif oBu = z2 then
    poss = 4
  elseif oBu = z3 then
    poss = 5
  Elseif oBu = z4 then
    poss = 6
  elseif oBu = z5 then
    poss = 7
  elseif oBu = z6 then
    poss = 8
  elseif oBu = z7 then
    poss = 9
  elseif oBu = z8 then
    poss = 10
  Elseif oBu = z9 then
    poss = 11
  elseif oBu = ok then
    If poss * posz <> 0 then
      bStart = "True"
    EndIf
  Else
    bStart = ""  
  EndIf
  ah300Zeilemarkieren()
EndSub



Sub ab500Schiffespieler ' Anzeigen der  Schiffe des Spielers auf dem Spielplan
  For ix = 1 To 10
    schiffx = eschiffs [ix][2] - 1 'Schiff x-Koordinate
    schiffy = eschiffs [ix][3] - 1 'Schiff y-Koordinate
    schiffr = eschiffs [ix][4] 'Schiff nach rechts
    schifft = eschiffs [ix][5] 'Schiff nach unten
    schiffla = eschiffs [ix][1]      
    If eschiffs [ix][1] <> eschiffs[ix][6] Then     
      schiffc = 3   
    Else
      schiffc = 5
    EndIf
    schiffkox = qukox
    schiffkoy = qukoy        
    ah200schiff()     
  EndFor 
EndSub



Sub ac500schussergebnisp 'Anzeige aller Schussergebnisdaten der Spielermatrix in Dialogfeld
  For lll = 2 To 11
    For iii = 2 To 11
      If mschiffs [lll][iii] = 1 Then
        GraphicsWindow.BrushColor=(col[2]) 
        GraphicsWindow.FillRectangle(qukox + 40 * (lll - 1),qukoy + 40 * (iii - 1),40,40)
        GraphicsWindow.BrushColor=(col[4]) 
        GraphicsWindow.DrawRectangle(qukox + 40 * (lll - 1),qukoy + 40 * (iii - 1),40,40)
      EndIf
      If mschiffs [lll][iii] > 30 And  mschiffs [lll][iii] < 41 Then
        ' GraphicsWindow.BrushColor = (col[5]) 
        xkr = qukox + 40 * (lll - 1)
        ykr = qukoy + 40 * (iii - 1)
        ckr = 5
        zkr = 5
        ah400Kreis()
      EndIf    
    EndFor
  EndFor
EndSub



Sub ab510Schiffeprogramm ' Anzeigen der  Schiffe des Programms auf dem Spielplan
  For ix = 1 To 10
    schiffx = eschiffp [ix][2] - 1 'Schiff x-Koordinate
    schiffy = eschiffp [ix][3] - 1 'Schiff y-Koordinate
    schiffr = eschiffp [ix][4] 'Schiff nach rechts
    schifft = eschiffp [ix][5] 'Schiff nach unten
    schiffla = eschiffp [ix][1]      
    If eschiffp [ix][1] <> eschiffp[ix][6] Then     
      schiffc = 3   
    Else
      schiffc = 5
    EndIf
    schiffkox = qukox
    schiffkoy = qukoy        
    ah200schiff()     
  EndFor 
EndSub



Sub aa550suchenp 'Setzen das Schussergebnis des Spielers in die Matrix des Programms
  If mschiffp [poss][posz] = 0 Then
    mschiffp [poss][posz] = 1
    schusses = 1
  EndIf  
  If (mschiffp [poss][posz] > 10) And ( mschiffp [poss][posz] < 21) Then
    si = mschiffp [poss][posz] - 10
    if eschiffp [si][1] = 1 Then
      eschiffp [si][6] = 1
      mschiffp [poss][posz] = mschiffp [poss][posz] + 40    
      schusses = 3
      verszs = verszs +1
      ad550wasserp()
    EndIf
    if eschiffp [si][1] > 1 Then
      eschiffp [si][6] = eschiffp [si][6] + 1
      If eschiffp [si][1] = eschiffp [si][6] Then
        mschiffp [poss][posz] = mschiffp [poss][posz] + 40    
        schusses = 3
        verszs = verszs + 1
        ad550wasserp()        
      Else
        mschiffp [poss][posz] = mschiffp [poss][posz] + 20       
        schusses = 2
        ad550wasserp()
      EndIf        
    EndIf
  EndIf  
EndSub



Sub ad550wasserp 'Platzieren von Wasserfeldern in der Spielermatrix bei Treffer und versenkt durch das Programm
  If schusses = 2 Then
    mschiffp [poss - 1][posz - 1] = 1
    mschiffp [poss - 1][posz + 1] = 1    
    mschiffp [poss + 1][posz - 1] = 1
    mschiffp [poss + 1][posz + 1] = 1
  EndIf 
  If schusses = 3 Then
    mschiffp [poss - 1][posz - 1] = 1
    mschiffp [poss - 1][posz + 1] = 1    
    mschiffp [poss + 1][posz - 1] = 1
    mschiffp [poss + 1][posz + 1] = 1   
    sil = eschiffp [si][1]
    six = eschiffp [si][2]
    siy = eschiffp [si][3]      
    If eschiffp [si][4] = 1 then  
      mschiffp [six - 1][siy] = 1
      mschiffp [six + sil][siy] = 1    
      mschiffp [six][siy + 1] = 1
      mschiffp [six][siy - 1] = 1 
      For x = 1 To sil
        mschiffp [six + x - 1][siy] = si + 50
      EndFor 
    EndIf
    If eschiffp [si][5] = 1 Then
      mschiffp [six][siy - 1] = 1
      mschiffp [six][siy + sil] = 1    
      mschiffp [six + 1][siy] = 1
      mschiffp [six - 1][siy] = 1
      For x = 1 To sil
        mschiffp [six][siy + x - 1] = si + 50
      EndFor 
    EndIf
  EndIf  
EndSub



Sub aa600trefferschuss ' Schuss nach einem Treffer des Programms
  If trace = 1 Then
    TextWindow.WriteLine("Trefferschuss")
  EndIf
  If tschussep = 0 Then  
    tschussep = 2
    xalt = xsq + 1
    yalt = ysq + 1
    tri = Math.GetRandomNumber(2)
    'tri = 1
  EndIf 
  If tri = 1 Then   
    ab600NachTrefferrechts()  
    Goto loopende
  EndIf
  If tri = 3 Then   
    ab620NachTrefferlinks()  
    Goto loopende
  EndIf
  If tri = 2 Then    
    ab640NachTrefferdown()   
    Goto loopende   
  EndIf 
  If tri = 4 Then    
    ab660NachTrefferup()   
    Goto loopende   
  EndIf 
  loopende: 
EndSub



sub ab600NachTrefferrechts 
  If schussep = 2 Then
    If xschiffs [xalt + 1][yalt] = 1 Then     
      schussep = 1
    else
      if xschiffs [xalt + 1][yalt] = 0 Then
        xsq = xalt 
      else
        if xschiffs [xalt + 1][yalt] = 2 Then
          If xschiffs [xalt + 2][yalt] = 1 Then     
            schussep = 1
          else          
            If  xschiffs [xalt + 2][yalt] = 0 then
              xsq = xalt + 1
            Else
              If  xschiffs [xalt + 2][yalt] = 2  then
                If xschiffs [xalt + 3][yalt] = 1 Then     
                  schussep = 1
                else
                  if xschiffs [xalt + 3][yalt] = 0 Then
                    xsq = xalt + 2
                  EndIf
                EndIf
              EndIf
            EndIf
          EndIf 
        endif
      EndIf
    EndIf
  EndIf
  If schussep = 1 Then
    tri = 3
    If xschiffs [xalt -1][yalt] = 1 Then
      If xschiffs [xalt][yalt + 1] = 1 Then
        tri = 4
        ysq = yalt - 2
        xsq = xalt - 1
      Else
        tri = 2
        ysq = yalt
        xsq = xalt - 1
      EndIf    
    EndIf
    if xschiffs [xalt - 1][yalt] = 0 Then
      xsq = xalt - 2
    else
      if xschiffs [xalt - 1][yalt] = 2 Then
        If  xschiffs [xalt - 2][yalt] = 0 then
          xsq = xalt - 3
        Else
          If  xschiffs [xalt - 2][yalt] = 2 then 
            if xschiffs [xalt - 3][yalt] = 0 Then
              xsq = xalt - 4
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf
EndSub



Sub ab620NachTrefferlinks
  If schussep = 2 Then
    if xschiffs [xalt - 1][yalt] = 0 Then
      xsq = xalt - 2
    else
      if xschiffs [xalt - 1][yalt] = 2 Then
        If  xschiffs [xalt - 2][yalt] = 0 then
          xsq = xalt - 3
        Else
          If  xschiffs [xalt - 2][yalt] = 2  then
            if xschiffs [xalt - 3][yalt] = 0 Then
              xsq = xalt - 4
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf 
  If schussep = 1 Then
    tri = 2
    If xschiffs [xalt][yalt + 1] = 1 Then
      tri = 4 
      xsq = xalt - 1
      ysq = yalt - 2     
    EndIf   
    if xschiffs [xalt][yalt + 1] = 0 Then
      ysq = yalt
      xsq = xalt - 1
    EndIf
  EndIf
EndSub   



Sub ab640NachTrefferdown
  If schussep = 2 Then
    If xschiffs [xalt][yalt + 1] = 1 Then
      schussep = 1
    else
      if xschiffs [xalt][yalt + 1] = 0 Then
        ysq = yalt 
      else
        if xschiffs [xalt][yalt + 1] = 2 Then
          If xschiffs [xalt][yalt + 2] = 1 Then
            schussep = 1
          else
            If  xschiffs [xalt][yalt + 2] = 0 then
              ysq = yalt + 1
            Else
              If  xschiffs [xalt][yalt + 2] = 2  then
                If xschiffs [xalt][yalt + 3] = 1 Then
                  schussep = 1
                else
                  if xschiffs [xalt][yalt + 3] = 0 Then
                    ysq = yalt + 2
                  EndIf
                EndIf
              EndIf
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf
  If schussep = 1 Then
    tri = 4
    If xschiffs [xalt][yalt - 1] = 1 Then
      If xschiffs [xalt + 1][yalt] = 1 Then
        tri = 3
        xsq = xalt - 2
        ysq = yalt - 1
      Else
        tri = 1
        xsq = xalt
        ysq = yalt - 1
      EndIf    
    EndIf
    if xschiffs [xalt][yalt - 1] = 0 Then
      ysq = yalt - 2
    else
      if xschiffs [xalt][yalt - 1] = 2 Then
        If  xschiffs [xalt][yalt - 2] = 0 then
          ysq = yalt - 3
        Else
          If  xschiffs [xalt][yalt - 2] = 2 then 
            if xschiffs [xalt][yalt - 3] = 0 Then
              ysq = yalt - 4
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf 
EndSub



Sub ab660NachTrefferup
  If schussep = 2 Then
    if xschiffs [xalt][yalt - 1] = 0 Then
      ysq = yalt - 2
    else
      if xschiffs [xalt][yalt - 1] = 2 Then
        If  xschiffs [xalt][yalt - 2] = 0 then
          ysq = yalt - 3
        Else
          If  xschiffs [xalt][yalt - 2] = 2  then
            if xschiffs [xalt][yalt - 3] = 0 Then
              ysq = yalt - 4
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf 
  If schussep = 1 Then
    tri = 1
    If xschiffs [xalt + 1][yalt] = 1 Then
      tri = 3 
      xsq = xalt - 2
      ysq = yalt - 1     
    EndIf     
    if xschiffs [xalt + 1][yalt] = 0 Then
      xsq = xalt
      ysq = yalt - 1
    EndIf
  EndIf
EndSub    



Sub aa700Schussprogramm  'Ermitteln des Schusses durch das Programm -Suchalgorhythmus
  IF trz4q = 1 AND trz3q = 2 AND trz2q = 3 THEN
    ab720Schuss1er()
    Goto a700
  EndIf 
  If schusszähler > 5 then    
    ab730Optimierungsschuss()
    Goto a700
  EndIf 
  ab710zufallschuss()
  a700:
  If xschiffs [xq+1][yq+1] <> 0 Then 
    ab710zufallschuss()    
  EndIf
  xsq = xq
  ysq = yq 
EndSub



Sub ab710zufallschuss    
  If trace = 1 Then
    TextWindow.WriteLine("Zufallschuss")
  EndIf
  While q1 = "" 
    xq = Math.GetRandomNumber(10)
    yq = Math.GetRandomNumber(10)
    If xschiffs [xq + 1][yq + 1] = 0 Then 
      q1 = "true"      
    EndIf
  EndWhile  
  q1 = ""
  'xq=7 ' Testdaten
  'yq = 1 'Testdaten     
EndSub



Sub ab720Schuss1er
  xxq = 0
  emaxq = 0
  FOR x1 = 1 TO 10
    FOR y1 = 1 TO 10 
      IF xschiffs [x1+1][y1+1] = 0 THEN
        xxq = xxq + 1
        emaxq [xxq][1] = x1
        emaxq [xxq][2] = y1
      ENDIF
    Endfor
  EndFor
  zeq = Math.GetRandomNumber(xxq)
  xq =emaxq [zeq][1]
  yq =emaxq [zeq][2]
  If trace = 1 Then
    TextWindow.WriteLine("Schuss auf 1er")
    TextWindow.WriteLine(emaxq)
    TextWindow.WriteLine(zeq)
  EndIf
EndSub



Sub ab730Optimierungsschuss
  bwq = 0
  cwq = 0
  dwq = 0
  bmaxq = 0
  dmaxq = 0
  If trace= 1 Then
    TextWindow.WriteLine("Optinierungsschuss")
  EndIf
  FOR iiq = 1 TO 10
    xxq = 0
    FOR kiq = 1 TO 10 
      IF xschiffs [iiq + 1][kiq + 1] = 0 THEN
        xxq = xxq + 1
      ELSE
        xxq = 0
      ENDIF
      bwq[iiq][kiq] = xxq
    Endfor
  Endfor   
  FOR kiq = 1 TO 10 
    xxq = 0
    FOR iiq = 1 TO 10 
      IF xschiffs [iiq + 1][kiq + 1] = 0 THEN
        xxq = xxq + 1
      ELSE
        xxq = 0
      ENDIF
      dwq[iiq][kiq] = xxq
    Endfor 
  Endfor   
  FOR iiq = 1 TO 10 
    yyaq = 0
    yyq = 0     
    FOR kiq = 10 TO 1 Step -1 
      IF bwq[iiq][kiq] = 0 THEN
        yyq = 0
        yyaq = 0
      ELSEIF yyaq < bwq[iiq][kiq] THEN
        yyq = bwq[iiq][kiq]
        yyaq = bwq[iiq][kiq]
      ENDIF
      cwq[iiq][kiq] = yyq
    Endfor 
  Endfor   
  FOR kiq = 1 TO 10 
    yyaq = 0
    yyq = 0
    FOR iiq = 10 TO 1 Step - 1 
      IF dwq[iiq][kiq] = 0 THEN
        yyq = 0
        yyaq = 0
      ELSEIF yyaq < dwq[iiq][kiq] THEN
        yyq = dwq[iiq][kiq]
        yyaq = dwq[iiq][ kiq]
      ENDIF
      cwq[iiq][kiq] = cwq[iiq][kiq] + yyq
    Endfor 
  Endfor 
  zbq = 0
  zdq = 0
  maxq = 0 
  FOR iiq = 1 TO 10 
    FOR kiq = 1 TO 10 
      IF cwq[iiq][ kiq] > maxq THEN
        maxq = cwq[iiq][kiq]
      ENDIF
    Endfor 
  Endfor 
  FOR iiq = 1 TO 10 
    FOR kiq = 1 TO 10 
      IF cwq[iiq][kiq] = maxq THEN
        zbq = zbq + 1
        bmaxq[zbq][1] = iiq
        bmaxq[zbq][2] = kiq
      ENDif 
    Endfor 
  Endfor   
  FOR iiq = 1 TO 10 
    FOR kiq = 10 TO 1 Step -1
      IF bwq[iiq][kiq] = 0 THEN
        cwq[iiq][kiq] = 0
      ELSE
        yyaq = bwq[iiq][kiq]
        yybwq = kiq
        FOR owq = 1 TO yyaq 
          cwq[iiq][yybwq] = owq * bwq[iiq][yybwq]
          yybwq = yybwq - 1
        Endfor 
        kiq = yybwq + 1
      ENDIF
    Endfor 
  Endfor    
  FOR kiq = 1 TO 10 
    FOR iiq = 10 TO 1 Step -1
      IF dwq[iiq][kiq] = 0 THEN
        cwq[iiq][kiq] = 0
      ELSE
        yyaq = dwq[iiq][kiq]
        yybwq = iiq
        FOR owq = 1 TO yyaq 
          cwq[yybwq][kiq] = owq * dwq[yybwq][kiq] * cwq[yybwq][kiq]
          yybwq = yybwq - 1
        Endfor 
        iiq = yybwq + 1
      ENDIF
    Endfor 
  Endfor 
  maxq = 0
  FOR iiq = 1 TO 10 
    FOR kiq = 1 TO 10 
      IF cwq[iiq][kiq] > maxq THEN
        maxq = cwq[iiq][kiq]
      ENDIF
    Endfor 
  Endfor 
  FOR iiq = 1 TO 10 
    FOR kiq = 1 TO 10 
      IF cwq[iiq][kiq] = maxq THEN
        zdq = zdq + 1
        dmaxq[zdq][1] = iiq
        dmaxq[zdq][2] = kiq
      ENDIF
    Endfor 
  Endfor 
  IF zdq < zbq THEN
    iiq = math.GetRandomNumber(zdq)
    xq = dmaxq[iiq][1]
    yq = dmaxq[iiq][2]
  ELSE
    iiq = Math.GetRandomNumber(zbq)
    xq = bmaxq[iiq][1]
    yq = bmaxq[iiq][2]
  ENDIF
EndSub



Sub aa800antwortfeld  'Anzeige Schussergebnis des Programms und Warten auf Bestätigung
  GraphicsWindow.Clear() 
  qukox = kox
  qukoy = koy
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText (qukox + 150,qukoy - 5,"Schiffe des Programms")
  GraphicsWindow.DrawText (qukox + 650,qukoy - 5,"Schiffe des Spielers")
  ah100quadrate()
  ab800schussergebnis()
  qukox = kox+500
  qukoy = koy
  ah100quadrate()
  ab500Schiffespieler()
  ac500schussergebnisp()
  poss = xsq + 1
  posz = ysq + 1
  ac800suchens()
  GraphicsWindow.BrushColor = (col[7]) 
  GraphicsWindow.FillRectangle (qukox + 218,koy + 448,250,30)  
  GraphicsWindow.BrushColor = (col[4]) 
  If schussep = 3 Then
    GraphicsWindow.FontSize=25 
    GraphicsWindow.BrushColor=(col[5])   
    GraphicsWindow.DrawText (qukox + 220,koy + 455,"Versenkt")
    GraphicsWindow.FontSize=15 
    GraphicsWindow.BrushColor=(col[4])
  EndIf
  If schussep = 2 Then
    GraphicsWindow.BrushColor=(col[5])   
    GraphicsWindow.DrawText (qukox + 220,koy + 450,"Treffer")
    GraphicsWindow.BrushColor=(col[4])
  EndIf
  If schussep = 1 Then
    GraphicsWindow.BrushColor=(col[2])   
    GraphicsWindow.DrawText (qukox + 220,koy + 450,"Wasser")
    GraphicsWindow.BrushColor=(col[4])
  EndIf
  xkr = qukox + 40 * xsq
  ykr = qukoy + 40 * ysq
  ckr = 6
  zkr = 20
  ah400Kreis()  
  GraphicsWindow.BrushColor=(col[4])
  OK2 = Controls.AddButton("bestätigen",kox,koy + 484)
  bStart = ""
  Controls.ButtonClicked = ak800OnButtonClickedaf
  While bStart = ""
    Program.Delay(500)
  EndWhile
EndSub



Sub ak800OnButtonClickedaf
  oBu = Controls.LastClickedButton
  If oBu = ok2 Then
    bStart = "True"
  EndIf 
EndSub



Sub ab800schussergebnis 'Anzeige aller Schussergebnisdaten der Programmmatrix in Dialogfeld
  For iii = 2 To 11
    For lll = 2 To 11
      If mschiffp [iii][lll] = 1 Then
        GraphicsWindow.BrushColor=(col[2]) 
        GraphicsWindow.FillRectangle(kox + 40 * (iii - 1),koy + 40 * (lll - 1),40,40)
        GraphicsWindow.BrushColor=(col[4]) 
        GraphicsWindow.DrawRectangle(kox + 40 * (iii - 1),koy + 40 * (lll - 1),40,40)
      EndIf
      If mschiffp [iii][lll] > 30 And  mschiffp [iii][lll] < 41 Then
        xkr = qukox + 40 * (iii - 1)
        ykr = qukoy + 40 * (lll - 1)
        ckr = 5
        zkr = 5
        ah400Kreis()  
      EndIf    
      If mschiffp [iii][lll] > 50 And  mschiffp [iii][lll] < 61 Then
        si = mschiffp [iii][lll] - 50
        schiffc = 5     
        schiffx = eschiffp[si][2] - 1
        schiffy = eschiffp[si][3] - 1
        schiffla = eschiffp[si][1]
        schiffr = eschiffp[si][4]
        schifft = eschiffp[si][5] 
        schiffkox = kox
        schiffkoy = koy       
        ah200schiff()      
      EndIf         
    EndFor
  EndFor
EndSub



Sub ac800suchens 'Setzen das Schussergebnis des Programms in die Matrix des Spielers
  If mschiffs [poss][posz] = 0 Then
    mschiffs [poss][posz] = 1
    xschiffs [poss][posz] = 1
    schussep = 1
  EndIf  
  If (mschiffs [poss][posz] > 10) And ( mschiffs [poss][posz] < 21) Then
    si = mschiffs [poss][posz] - 10
    if eschiffs [si][1] = 1 Then
      eschiffs [si][6] = 1
      mschiffs [poss][posz] = mschiffs [poss][posz] + 40
      xschiffs [poss][posz] = 3    
      schussep = 3
      tschussep = 0
      verszp = verszp + 1
      ad800wassers()
    EndIf
    if eschiffs [si][1] > 1 Then
      eschiffs [si][6] = eschiffs [si][6] + 1
      If eschiffs [si][1] = eschiffs [si][6] Then
        mschiffs [poss][posz] = mschiffs [poss][posz] + 40
        xschiffs [poss][posz] = 3    
        schussep = 3
        tschussep = 0
        verszp = verszp + 1
        If eschiffs [si][1] = 4 Then
          trz4q = trz4q + 1
        EndIf
        If eschiffs [si][1] = 3 Then
          trz3q = trz3q + 1
        EndIf   
        If eschiffs [si][1] = 2 Then
          trz2q = trz2q + 1
        EndIf       
        ad800wassers()        
      Else
        mschiffs [poss][posz] = mschiffs [poss][posz] + 20
        xschiffs [poss][posz] = 2       
        schussep = 2
        ad800wassers()
      EndIf        
    EndIf
  EndIf  
EndSub



Sub ad800wassers 'Platzieren von Wasserfeldern in der Programmmatrix bei Treffer und versenkt durch den Spieler
  If schussep = 2 Then
    mschiffs [poss - 1][posz - 1] = 1
    xschiffs [poss - 1][posz - 1] = 1  
    mschiffs [poss - 1][posz + 1] = 1 
    xschiffs [poss - 1][posz + 1] = 1  
    mschiffs [poss + 1][posz - 1] = 1
    xschiffs [poss + 1][posz - 1] = 1
    mschiffs [poss + 1][posz + 1] = 1
    xschiffs [poss + 1][posz + 1] = 1
  EndIf 
  If schussep = 3 Then
    mschiffs [poss - 1][posz - 1] = 1
    xschiffs [poss - 1][posz - 1] = 1
    mschiffs [poss - 1][posz + 1] = 1 
    xschiffs [poss - 1][posz + 1] = 1        
    mschiffs [poss + 1][posz - 1] = 1
    xschiffs [poss + 1][posz - 1] = 1   
    mschiffs [poss + 1][posz + 1] = 1 
    xschiffs [poss + 1][posz + 1] = 1    
    sil = eschiffs [si][1]
    six = eschiffs [si][2]
    siy = eschiffs [si][3]    
    If eschiffs [si][4] = 1 then  
      mschiffs [six - 1][siy] = 1
      xschiffs [six - 1][siy] = 1 
      mschiffs [six + sil][siy] = 1   
      xschiffs [six + sil][siy] = 1 
      mschiffs [six][siy + 1] = 1
      xschiffs [six][siy + 1] = 1
      mschiffs [six][siy - 1] = 1    
      xschiffs [six][siy - 1] = 1 
      For x = 1 To sil
        mschiffs [six + x - 1][siy] = si + 50
        xschiffs [six + x - 1][siy] = 3        
      EndFor 
    EndIf
    If eschiffs [si][5] = 1 Then
      mschiffs [six][siy - 1] = 1
      xschiffs [six][siy - 1] = 1
      mschiffs [six][siy + sil] = 1  
      xschiffs [six][siy + sil] = 1   
      mschiffs [six + 1][siy] = 1
      xschiffs [six + 1][siy] = 1
      mschiffs [six - 1][siy] = 1
      xschiffs [six - 1][siy] = 1
      For x = 1 To sil
        mschiffs [six][siy + x - 1] = si + 50
        xschiffs [six][siy + x - 1] = 3        
      EndFor 
    EndIf
  EndIf  
EndSub



Sub aa900schlussbild  
  GraphicsWindow.Clear() 
  qukox = kox
  qukoy = koy
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText (qukox + 150,qukoy - 5,"Schiffe des Programms")
  GraphicsWindow.DrawText (qukox + 650,qukoy - 5,"Schiffe des Spielers")
  ah100quadrate()
  ab800schussergebnis()
  ab510Schiffeprogramm()
  qukox = kox + 500
  qukoy = koy
  ah100quadrate()
  ab500Schiffespieler()
  ac500schussergebnisp()
  GraphicsWindow.FontSize = 25 
  GraphicsWindow.BrushColor=(col[5])
  If verszp = 10 Then
    GraphicsWindow.DrawText(kox + 260,qukoy - 50,"Das Programm hat das Spiel gewonnen")
  EndIf
  If verszs = 10 Then
    GraphicsWindow.DrawText(kox + 300,qukoy - 50,"Sie haben das Spiel gewonnen")
  EndIf  
  GraphicsWindow.FontSize = 15 
  GraphicsWindow.BrushColor=(col[4])
  ok = Controls.AddButton("Neues Spiel",kox,koy + 484)
  end = Controls.AddButton("Spiel beenden",kox + 200,koy + 484) 
  bStart = ""
  Controls.ButtonClicked = ak900OnButtonClicked
  While bStart = ""
    Program.Delay(500)
  EndWhile
EndSub



Sub ak900OnButtonClicked
  oBu = Controls.LastClickedButton
  If oBu = ok then
    bStart = "True"
  EndIf
  If oBu = end Then
    Program.End()
  EndIf
EndSub



Sub ah100quadrate 'Zeichen der Grundspielfelder
  For i = 1 To 10
    For k = 1 To 10
      GraphicsWindow.BrushColor=(col[1]) 
      GraphicsWindow.FillRectangle(qukox + 40 * i,qukoy + 40 * k,40,40)
      GraphicsWindow.BrushColor=(col[4]) 
      GraphicsWindow.DrawRectangle(qukox + 40 * i,qukoy + 40 * k,40,40)
    EndFor
  EndFor
EndSub



Sub ah200schiff ' Zeichnen eines Schiffs
  GraphicsWindow.BrushColor=(col[schiffc])
  x= schiffkox + 42 + (schiffx - 1) * 40
  y= schiffkoy + 42 + (schiffy - 1) * 40
  a= 36 + 40 * (schiffla - 1) * schiffr
  b= 36 + 40 * (schiffla - 1) * schifft
  GraphicsWindow.FillRectangle(x,y,a,b) 
EndSub



sub ah300Zeilemarkieren 'Ausgabe der Eingabekoordinaten beim Schiessen des Spielers
  GraphicsWindow.BrushColor=(col[7])
  GraphicsWindow.FillRectangle(kox,koy + 484,400,40)
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText(kox + 205,koy + 490,"Eingabe: ")   
  If posz <> 0 Then     
    GraphicsWindow.BrushColor=(col[4])   
    GraphicsWindow.DrawText(kox + 275,koy + 490,poszk) 
  EndIf  
  If poss <> 0 Then     
    GraphicsWindow.BrushColor=(col[4])
    GraphicsWindow.DrawText(kox + 300,koy + 490,poss - 2)   
  EndIf
  GraphicsWindow.BrushColor=(col[4])
  GraphicsWindow.DrawText(kox + 315,koy + 490,posdir)      
EndSub



Sub ah400Kreis
  'xkr x Koordinate
  'ykr y Koordiate
  'ckr Farbnummer
  'gkr Grösse Kreis nur gerade Zahlen 
  'zkr Geschwindigkeit - 0 sofort -  5 schnell -  20 normal
  GraphicsWindow.BrushColor = Col[ckr]
  GraphicsWindow.PenColor = "black"  
  For ikr = 2 To gkr Step 2
    Program.Delay(zkr)
    GraphicsWindow.FillEllipse(xkr+((40-ikr)/2),ykr+((40-ikr)/2),ikr,ikr)
    GraphicsWindow.DrawEllipse(xkr+((40-ikr)/2),ykr+((40-ikr)/2),ikr,ikr)
  EndFor
EndSub
