Initialisation()

Main()

Program.End()



Sub Main

  SkipEnd = F         'SkipEnd change to True to end program

  While SkipEnd = F   'Loop by SkipEnd equal False

    If Skip_OBC = T Then   

      Skip_OBC = F         'Button wasl clicked

      LastClickedButton = Controls.LastClickedButton

      If LastClickedButton = BT_End Then 

        SkipEnd = T   'EndButton was clicked so change SkipEnd to True

      ElseIf LastClickedButton = BT_Word Then

        MakeWordArray()  'Put the words into the array and show it

      EndIf 

    EndIf 

  EndWhile   

EndSub 'Main



Sub MakeWordArray

  Word = "" 

  Sentence = Controls.GetTextBoxText(MLTB_sentence) 'Read the Text from TextBox

  Length_Sentence = Text.GetLength (Sentence)

  Index = 1

  Skip_Delimiter = F

  For I = 1 To Length_Sentence

    Char = Text.GetSubText(Sentence, I, 1) 'The aktuel character from sentence

    If Array.ContainsValue(Delimiter, Char) = T Then

      If Skip_Delimiter = F Then 

        'By first Delimeter set next Index

        Index = Index + 1

      EndIf 

      Skip_Delimiter = T  

    Else 

      Skip_Delimiter = F

      'Add Char to the array Word

      Word[Index] = Text.Append(Word[Index], Char)    

    EndIf 

  EndFor  

  'Write all entry of word into the Textwindow

  For I = 1 To Array.GetItemCount(Word)

    TextWindow.WriteLine(Word[I])

  EndFor   

EndSub 'MakeWordArray



Sub Initialisation

  'Subroutine by Event

  Controls.ButtonClicked = OnButtonClicked

  'Windows

  'Max Width and Height from Desktop

  DW = Desktop.Width 

  DH = Desktop.Height 

  'Create Textwindow 

  TextWindow.Top = 0

  TextWindow.Left = DW / 2

  'Create GraphicWindow

  GraphicsWindow.Top = 0

  GraphicsWindow.Left = 0

  'Constante

  T = "True"

  F = "False"

  'Variables

  Skip_OBC = F 

  'Delimater by new word

  Delimiter[1] = " "

  Delimiter[2] = "."

  Delimiter[3] = ","

  Delimiter[4] = ";"

  Delimiter[5] = ":"

  Delimiter[6] = "!"

  Delimiter[6] = "?"

  'Create controls and shapes 

  SH_sentence = Shapes.AddText("Sentence:")

  Shapes.Move(SH_sentence, 10,0)

  MLTB_sentence = Controls.AddMultiLineTextBox(10,20)

  Controls.SetSize(MLTB_sentence, GraphicsWindow.Width - 20, 200)

  BT_Word = Controls.AddButton("Word", 10, GraphicsWindow.Height -30)

  BT_End = Controls.AddButton("End", 200, GraphicsWindow.Height -30)  

EndSub 'Initialisation



Sub OnButtonClicked

  Skip_OBC = T

EndSub 'OnButtonClicked


