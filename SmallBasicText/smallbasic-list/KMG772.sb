' Settings
url = "http://kerovia.net/sarah2"
title = "Yvonne Show 1.0"
tt = 10000
width = 0
height = 0

' The main part/start of the program
Sub Start
  LoadImageAndInfo()
  GraphicsWindow.Left = (Desktop.Width / 2) - (width / 2)
  GraphicsWindow.Top = (Desktop.Height / 2) - (height / 2)
  GraphicsWindow.CanResize = 0
  GraphicsWindow.Title = title
  GraphicsWindow.MouseDown = Settings
  GraphicsWindow.Show()
  GetShowImage()
  Timer.Tick = GetShowImage 
  Timer.Interval = tt
EndSub

' Updates the new image in the window
Sub GetShowImage
  GraphicsWindow.Clear()
  GraphicsWindow.Width = width
  GraphicsWindow.Height = height
  GraphicsWindow.DrawImage(img, 0, 0)
  LoadImageAndInfo()
EndSub

' Loads the next image and sets the window dimensions
Sub LoadImageAndInfo
  Timer.Pause()
  img = ImageList.LoadImage(url)
  width = ImageList.GetWidthOfImage(img)
  height = ImageList.GetHeightOfImage(img)    
  Timer.Resume()
EndSub

' Allows users to change where images load from
Sub Settings
  Timer.Pause()
  TextWindow.Show()
  TextWindow.Write("Enter the url to pull pictures from: ")
  url = TextWindow.Read()
  TextWindow.Write("Enter the seconds between each showing: ")
  tt = TextWindow.ReadNumber() * 1000
  TextWindow.Clear()
  TextWindow.Hide()
  Timer.Resume()
EndSub

' Runs the program
Start()
