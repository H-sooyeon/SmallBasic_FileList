'Initialisation Window

gw = 800

gh = 600

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh



'Initialise Key events

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp

keyLeft = 0

keyRight = 0

keyUp = 0

keyDown = 0



'Initialise ball

ball = Shapes.AddEllipse(50,50)

ballX = 100

ballY = 100

speed = 3



'Main loop - just move the ball

While ("True")

  start = Clock.ElapsedMilliseconds

  

  processKey()

  Shapes.Move(ball,ballX-25,ballY-25)

  

  delay = 20 - (Clock.ElapsedMilliseconds-start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile



'Key press event subroutines

'A separate Down and Up is checked for each key, this tells us the state for any key

'And isn't affected by auto-repeat delays for keys

Sub OnKeyDown

  key = GraphicsWindow.LastKey

  keyDown[key] = "True"

EndSub



Sub OnKeyUp

  key = GraphicsWindow.LastKey

  keyDown[key] = ""

EndSub



Sub processKey

  'Move object - note it can move diagonally if 2 keys are pressed

  If (keyDown["Left"]) Then

    ballX = ballX-speed

  EndIf

  If (keyDown["Right"]) Then

    ballX = ballX+speed

  EndIf

  If (keyDown["Up"]) Then

    ballY = ballY-speed

  EndIf

  If (keyDown["Down"]) Then

    ballY = ballY+speed

  EndIf

  

  'Check for hitting edges

  If (ballX < 0) Then

    ballX = ballX+gw

  EndIf

  If (ballX > gw) Then

    ballX = ballX-gw

  EndIf

  If (ballY < 0) Then

    ballY = ballY+gh

  EndIf

  If (ballY > gh) Then

    ballY = ballY-gh

  EndIf

EndSub


