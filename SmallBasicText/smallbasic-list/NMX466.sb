GraphicsWindow.Width=500 'Set up the graphicswindow
GraphicsWindow.Height=600
GraphicsWindow.Top=50

Emitter=Shapes.AddTriangle(0,50,25,0,50,50) 'Add Emitter Triangle
Shapes.Move(Emitter,225,400)
GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(230,0,0) 'Set the color for the "blood"
GraphicsWindow.PenWidth=0
For i = 1 to 10 'Add the "Blood" sprites
  Blood[i]=shapes.AddEllipse(10,10)
endfor
moving=0

While "True" 'Repeat the whole time:
  MouseControl()
  If moving < 10 then
    BloodMovement()
  endif
  Program.Delay(1)
endwhile

Sub MouseControl
  If Mouse.IsLeftButtonDown="True" Then 'Drag and drop the Emitter
    EmitterX=Shapes.GetLeft(Emitter)
    EmitterY=Shapes.GetTop(Emitter)
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If MX >= EmitterX And MX <= EmitterX+50 And MY >= EmitterY And MY <= EmitterY+50 then
      Shapes.Move(Emitter,MX-25,MY-25)
    endif
  endif
  If Mouse.IsRightButtonDown="True" Then 'Emitt the "blood" sprites
    moving = 0
    EmitterX=Shapes.GetLeft(Emitter)
    EmitterY=Shapes.GetTop(Emitter)
    For i = 1 to 10
      BWX[i]=0.1*Math.GetRandomNumber(21)-1.1
      BWY[i]=0.1*(Math.GetRandomNumber(30)-31)
      Shapes.Move(Blood[i],EmitterX+20,EmitterY-5)
    endfor
  endif
endsub

Sub BloodMovement 'Move the "blood" sprites around by their corresponding WX and WY
  For i = 1 To 10
    BX[i]=Shapes.GetLeft(Blood[i])
    BY[i]=Shapes.GetTop(Blood[i])
    If BY[i] > 550 Then
      BWX[i] = 0
      BWY[i] = 0
      moving=moving+1
    Else
      moving=moving-1
      If BWY[i]<3 Then
        BWY[i]=BWY[i]+0.05
      endif
      BWX[i] = BWX[i] * 0.998
    endif
    Shapes.Move(Blood[i],BX[i]+BWX[i],BY[i]+BWY[i])
  endfor
endsub
