Initialise()

Sub Initialise
  nRow = 3
  nCol = 8
  spacingWidth = 100
  spacingHeight = 100
  GraphicsWindow.Width = spacingWidth*nCol
  GraphicsWindow.Height = spacingHeight*nRow
' The following line could be harmful and has been automatically commented.
'   dataFile = File.GetSettingsFilePath()
  LoadData()
  
  For iRow = 1 To nRow
    For iCol= 1 To nCol
      MakeControls()
    EndFor
  EndFor
  
  Controls.ButtonClicked = OnButtonClicked
EndSub

Sub MakeControls
  xPos = (iCol-0.9)*spacingWidth
  yPos = (iRow-0.9)*spacingHeight
  
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.FontSize = 12*spacingHeight/100
  GraphicsWindow.PenWidth = 0
  
  addButton[iRow][iCol] = Controls.AddButton("Add",xPos,yPos)
  Controls.SetSize(addButton[iRow][iCol],0.8*spacingWidth,0.25*spacingHeight)
  
  okButton[iRow][iCol] = Controls.AddButton("OK",xPos,yPos)
  Controls.SetSize(okButton[iRow][iCol],0.8*spacingWidth,0.25*spacingHeight)
  Shapes.HideShape(okButton[iRow][iCol])
  
  dataTextBox[iRow][iCol] = Controls.AddTextBox(xPos,yPos+0.3*spacingHeight)
  Controls.SetSize(dataTextBox[iRow][iCol],0.8*spacingWidth,0.25*spacingHeight)
  Shapes.HideShape(dataTextBox[iRow][iCol])
  
  GraphicsWindow.BrushColor = "Red"
  dataBack[iRow][iCol] = Shapes.AddEllipse(0.5*spacingWidth,0.5*spacingHeight)
  Shapes.Move(dataBack[iRow][iCol],xPos+0.15*spacingWidth,yPos+0.35*spacingHeight)
  
  GraphicsWindow.BrushColor = "White"
  dataValue[iRow][iCol] = Shapes.AddText("")
  
  ShowData()
EndSub

Sub OnButtonClicked
  button = Controls.LastClickedButton
  For iRow = 1 To nRow
    For iCol= 1 To nCol
      xPos = (iCol-0.9)*spacingWidth
      yPos = (iRow-0.9)*spacingHeight
      If (button = addButton[iRow][iCol]) Then
        Shapes.HideShape(addButton[iRow][iCol])
        Shapes.HideShape(dataBack[iRow][iCol])
        Shapes.HideShape(dataValue[iRow][iCol])
        Shapes.ShowShape(okButton[iRow][iCol])
        Shapes.ShowShape(dataTextBox[iRow][iCol])
        Controls.SetTextBoxText(dataTextBox[iRow][iCol],"")
        LDFocus.SetFocus(dataTextBox[iRow][iCol])
      ElseIf (button = okButton[iRow][iCol]) Then
        Shapes.ShowShape(addButton[iRow][iCol])
        Shapes.ShowShape(dataBack[iRow][iCol])
        Shapes.ShowShape(dataValue[iRow][iCol])
        Shapes.HideShape(okButton[iRow][iCol])
        Shapes.HideShape(dataTextBox[iRow][iCol])
        
        value = Controls.GetTextBoxText(dataTextBox[iRow][iCol])
        If (LDUtilities.IsNumber(value)) Then
          data[iRow][iCol] = data[iRow][iCol] + value
        ElseIf (Text.ConvertToUpperCase(value) = "X") Then
          data[iRow][iCol] = ""
        EndIf
        ShowData()
        SaveData()
      EndIf
    EndFor
  EndFor  
EndSub

Sub ShowData
  txt = data[iRow][iCol]
  GraphicsWindow.FontSize = 20*spacingHeight/100*3/Math.Max(3,Text.GetLength(txt))
  LDShapes.Font(dataValue[iRow][iCol],GraphicsWindow.FontName,GraphicsWindow.FontSize,GraphicsWindow.FontBold,GraphicsWindow.FontItalic)
  width = LDText.GetWidth(txt)
  height = LDText.GetHeight(txt)
  Shapes.Move(dataValue[iRow][iCol],xPos+0.4*spacingWidth-width/2,yPos+0.6*spacingHeight-height/2)
  Shapes.SetText(dataValue[iRow][iCol],txt)
EndSub

Sub SaveData
' The following line could be harmful and has been automatically commented.
'   File.WriteContents(dataFile, data)
EndSub

Sub LoadData
' The following line could be harmful and has been automatically commented.
'   data = File.ReadContents(dataFile)
EndSub
