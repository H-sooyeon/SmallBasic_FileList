' Frog
' Version 0.2
' Copyright © 2016 Nonki Takahashi.  The MIT License.
' Program ID FMT726-0

GraphicsWindow.Title = "Frog"
GraphicsWindow.PenWidth = 12
param = "x=200;y=100;width=70;height=70;"
DrawEllipse()
param = "x1=230;y1=135;x2=240;y2=135;"
DrawLine()
param = "x=330;y=100;width=70;height=70;"
DrawEllipse()
param = "x1=360;y1=135;x2=370;y2=135;"
DrawLine()
GraphicsWindow.BrushColor = "Lime"
GraphicsWindow.FillEllipse(250, 270, 100, 150)
param = "x=250;y=270;width=100;height=150;"
DrawEllipse()
GraphicsWindow.FillEllipse(200, 170, 200, 100)
param = "x=200;y=170;width=200;height=100;"
DrawEllipse()
param = "x1=200;y1=220;x2=400;y2=220;"
DrawLine()
param = "x1=250;y1=310;x2=150;y2=210;"
DrawLine()
param = "x1=150;y1=240;x2=180;y2=240;"
DrawLine()
param = "x1=180;y1=210;x2=180;y2=240;"
DrawLine()

Sub DrawLine
  pc = GraphicsWindow.PenColor
  bc = GraphicsWindow.BrushColor
  GraphicsWindow.BrushColor = pc
  pw = GraphicsWindow.PenWidth
  x1 = param["x1"]
  y1 = param["y1"]
  x2 = param["x2"]
  y2 = param["y2"]
  len = Math.SquareRoot((x2 - x1) * (x2 - x1) + (y2 - y1) * (y2 - y1))
  For k = 0 To 1 Step pw / len / 3
    x = x1 * (1 - k) + x2 * k
    y = y1 * (1 - k) + y2 * k
    _dx = Math.GetRandomNumber(2) - 1
    _dy = Math.GetRandomNumber(2) - 1
    _pw = (Math.GetRandomNumber(2) / 10 + 0.9) * pw
    GraphicsWindow.FillEllipse(x - _pw / 2 + _dx, y - _pw / 2 + _dy, _pw, _pw)
  EndFor
  GraphicsWindow.BrushColor = bc
EndSub

Sub DrawEllipse
  pc = GraphicsWindow.PenColor
  bc = GraphicsWindow.BrushColor
  GraphicsWindow.BrushColor = pc
  pw = GraphicsWindow.PenWidth
  x = param["x"]
  y = param["y"]
  width = param["width"]
  height = param["height"]
  a = width / 2
  b = height / 2
  len = 2 * Math.Pi * Math.Max(a, b)
  For θ = 0 To 2 * Math.Pi Step pw / len * 3
    _x = (x + a) + a * Math.Cos(θ)
    _y = (y + b) + b * Math.Sin(θ)
    _dx = Math.GetRandomNumber(2) - 1
    _dy = Math.GetRandomNumber(2) - 1
    _pw = (Math.GetRandomNumber(2) / 10 + 0.9) * pw
    GraphicsWindow.FillEllipse(_x - _pw / 2 + _dx, _y - _pw / 2 + _dy, _pw, _pw)
  EndFor
  GraphicsWindow.BrushColor = bc
EndSub
