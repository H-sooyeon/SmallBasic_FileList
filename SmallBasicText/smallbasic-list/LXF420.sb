'Pong Game Sample without ball

'Created by Oskariok



'use up and down arrows to move left paddle

'use w and s keys to move right paddle 



Main()



Sub Main

  CreateGame()

  

  'Main Loop

  While "True"

    

    If MovingUpRightPad = "true" Then

      RightPaddleY = RightPaddleY - PadMovement

      NeedMoveRightPad = "true"

    EndIf

    If MovingDownRightPad = "true" Then

      RightPaddleY = RightPaddleY + PadMovement

      NeedMoveRightPad = "true"

    EndIf

    If MovingUpLeftPad = "true" Then

      LeftPaddleY = LeftPaddleY - PadMovement

      NeedMoveLeftPad = "true"

    EndIf

    If MovingDownLeftPad = "true" Then

      LeftPaddleY = LeftPaddleY + PadMovement

      NeedMoveLeftPad = "true"

    EndIf  

    

    SetPaddleYLimits() 

    

    If NeedMoveLeftPad = "true" Then

      MoveLeftPad()

    EndIf

    If NeedMoveRightPad = "true" Then

      MoveRightPad()

    EndIf

    

  EndWhile  

EndSub



Sub MoveLeftPad

  Shapes.Animate(LeftPaddle,5,LeftPaddleY,0)

  NeedMoveLeftPad = "false"

EndSub



Sub MoveRightPad

  Shapes.Animate(RightPaddle,GraphicsWindow.Width-25,RightPaddleY,0)

  NeedMoveRightPad = "false"

EndSub



Sub SetPaddleYLimits

  If LeftPaddleY > GraphicsWindow.Height - 125 Then

    LeftPaddleY = GraphicsWindow.Height - 125

  EndIf

  If LeftPaddleY < 85 Then

    LeftPaddleY = 85

  EndIf

  If RightPaddleY > GraphicsWindow.Height - 125 Then

    RightPaddleY = GraphicsWindow.Height - 125

  EndIf

  If RightPaddleY < 85 Then

    RightPaddleY = 85

  EndIf

EndSub





Sub CreateGame

  CrateWindow()

  CreatePads()

  

  'Set all moving false

  MovingUpRightPad = "false"

  MovingDownRightPad = "false"

  MovingUpLeftPad = "false"

  MovingDownLeftPad = "false"

  'set NeedMovePads to false

  NeedMoveRightPad = "false"

  NeedMoveLeftPad = "false"

  

  'In Small Basic GraphicsWindowlastkey is only "A" or "Z "not "a" or "z"

  

  'Set MoveUpRightPadKey as W Key

  MoveUpRightPadKey = "W"

  'Set MoveDownRightPadKey as S Key

  MoveDownRightPadKey = "S"

  'Set MoveUpLeftPadKey as Up arrow

  MoveUpLeftPadKey = "Up"

  'Set MoveDownLeftPadKey as Down arrow

  MoveDownLeftPadKey = "Down"

  

  'Set movement for Pads

  PadMovement = 0.3

  

  'Set Paddle Positions

  RightPaddleY = GraphicsWindow.Height/2-45

  LeftPaddleY = GraphicsWindow.Height/2-45

  

EndSub



Sub CrateWindow

  GraphicsWindow.Show()

  GraphicsWindow.BackgroundColor = "#000000"

  GraphicsWindow.Height = 600

  GraphicsWindow.Width = 800

  GraphicsWindow.Left = Desktop.Width/2-GraphicsWindow.Width/2

  GraphicsWindow.Top = Desktop.Height/2-GraphicsWindow.Height/2

  GraphicsWindow.CanResize = "false"

  GraphicsWindow.KeyDown = OnKeyDown

  GraphicsWindow.KeyUp = OnKeyUp

  GraphicsWindow.Title = "Pong"

  GraphicsWindow.PenColor = "#ffffff"

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.DrawLine(0,80,GraphicsWindow.Width+100,80)

  GraphicsWindow.DrawLine(GraphicsWindow.Width/2-1,80,GraphicsWindow.Width/2-1,GraphicsWindow.Height+100)

EndSub



Sub CreatePads

  GraphicsWindow.PenColor = "#ffffff"

  GraphicsWindow.BrushColor = "#000000"

  LeftPaddle = Shapes.AddRectangle(20,120)

  RightPaddle = Shapes.AddRectangle(20,120)

  Shapes.Move(LeftPaddle, 5,GraphicsWindow.Height/2-45)

  Shapes.Move(RightPaddle,GraphicsWindow.Width-25 ,GraphicsWindow.Height/2-45)

EndSub



Sub OnKeyDown  

  Key = GraphicsWindow.LastKey

  If (Key = MoveUpLeftPadKey) Then

    MovingUpLeftPad = "true"

  EndIf

  If (Key = MoveUpRightPadKey) Then

    MovingUpRightPad = "true"

  EndIf

  If (Key = MoveDownLeftPadKey) Then

    MovingDownLeftPad = "true"

  EndIf

  If (Key = MoveDownRightPadKey) Then

    MovingDownRightPad = "true"

  EndIf  

EndSub



Sub OnKeyUp

  Key = GraphicsWindow.LastKey

  If (Key = MoveUpLeftPadKey) Then

    MovingUpLeftPad = "false"

  EndIf

  If (Key = MoveUpRightPadKey) Then

    MovingUpRightPad = "false"

  EndIf

  If (Key = MoveDownLeftPadKey) Then

    MovingDownLeftPad = "false"

  EndIf

  If (Key = MoveDownRightPadKey) Then

    MovingDownRightPad = "false"

  EndIf    

EndSub


