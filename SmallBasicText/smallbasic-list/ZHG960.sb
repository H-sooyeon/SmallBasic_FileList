'Window
GraphicsWindow.Hide()
GraphicsWindow.Width = 1350
GraphicsWindow.Height = 689
GraphicsWindow.Left = 5
GraphicsWindow.Top = 5
GraphicsWindow.CanResize = "False"
GraphicsWindow.Title = "Card Snap"
GraphicsWindow.FontName = "Georgia"

'Variables
wWidth = 1350
wHeight = 689
MaximumImages = 33
ValuePbLs = 0
PbPosX = (wWidth - 460) / 2
ValueRecCoverLsOpacity = 100
MainMenuAdded = "False"
AnimateBtnHelp = "False"
HelpPage = 1
CardWidth = 132
CardHeight = 187
CardDifferenceX = 75
CardDifferenceY = 108
CanPlay = "False"
CanPressKeys = "False"
ScoreP1 = 0
ScoreP2 = 0
RemainingCards = 20
CardSpeed = 750

'Colors
ClrGrayBasic = GraphicsWindow.GetColorFromRGB(50, 50, 50)
ClrGrayBack = GraphicsWindow.GetColorFromRGB(100, 100, 100)
ClrGrayBorder = GraphicsWindow.GetColorFromRGB(150, 150, 150)
ClrGrayText = GraphicsWindow.GetColorFromRGB(200, 200, 200)

'Main
GraphicsWindow.BackgroundColor = ClrGrayBasic
Timer.Pause()
AddLoadingScreen()
GraphicsWindow.Show()
LoadResources()
Sound.Play(SndIntro)

'Finish loading
GraphicsWindow.DrawResizedImage(Load_ImgPbLs, PbPosX, 525, 460, 28)
Program.Delay(500)
GraphicsWindow.Clear()
Mouse.HideCursor()
AddMainMenu()

'Subroutines - Custom
'********************
'LoadResources
Sub LoadResources
  'Sounds
  SndMagicWand = "http://soundbible.com/grab.php?id=474&type=mp3"
  SndBlop = "http://soundbible.com/grab.php?id=2067&type=mp3"
  SndAnvil = "http://soundbible.com/grab.php?id=1742&type=mp3"
  SndIntro = "http://www.soundjay.com/misc/magic-chime-01.mp3"
  SndCorrect = "http://www.soundjay.com/misc/magic-chime-02.mp3"
  SndWrong = "http://soundbible.com/grab.php?id=1540&type=mp3"
  SndVictory = "http://soundbible.com/grab.php?id=1823&type=mp3"
  SndStartGame = "http://www.jewelbeat.com/free/free-sound-effects/musical%20effects/Trombone_5.mp3"
  'Images
  Load_ImgClockworkGiant = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/149/148/12201.png")
  AddProgressLsPb()
  Load_ImgMountainGiant = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/147/957/264.png")
  AddProgressLsPb()
  Load_ImgLanternOfPower = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/272/42/27469.png")
  AddProgressLsPb()
  Load_ImgFrostGiant = ImageList.LoadImage("https://media-Hearth.cursecdn.com/avatars/252/286/22262.png")
  AddProgressLsPb()
  Load_ImgGearMaster = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/274/995/27516.png")
  AddProgressLsPb()
  Load_ImgVarian = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/252/414/22342.png")
  AddProgressLsPb()
  Load_ImgTreeOfLife = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/148/858/12270.png")
  AddProgressLsPb()
  Load_ImgCenarios = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/148/473/605.png")
  AddProgressLsPb()
  Load_ImgMalygos = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/148/452/241.png")
  AddProgressLsPb()
  Load_ImgTirion = ImageList.LoadImage("http://media-Hearth.cursecdn.com/avatars/148/304/391.png")
  AddProgressLsPb()
  Load_ImgCardsBackside = ImageList.LoadImage("http://vignette1.wikia.nocookie.net/hearthstone/images/e/e6/Cards.png/revision/latest?cb=20130325035156")
  AddProgressLsPb()
  Load_ImgBtnPlay = ImageList.LoadImage("http://games.noaa.gov/oscar/media/playbutton.png")
  AddProgressLsPb()
  Load_ImgGameBack = ImageList.LoadImage("http://www.creativecrash.com/system/photos/000/035/094/35094/big/wood-l1.bmp?1251143899")
  AddProgressLsPb()
  Load_ImgNextTurn = ImageList.LoadImage("http://achievementgen.com/files/icons/hearthstone.png")
  AddProgressLsPb()
  Load_ImgPaper = ImageList.LoadImage("http://us.battle.net/hearthstone/static/images/body-content-bg.jpg")
  AddProgressLsPb()
  Load_ImgBorderFlag = ImageList.LoadImage("https://bnetcmsus-a.akamaihd.net/cms/gallery/585WHG4U28RI1401477381530.png")
  AddProgressLsPb()
  Load_ImgCursor = ImageList.LoadImage("http://www.hiveworkshop.com/forums/attachments/world-editor-help-zone-98/2323d1171206607-reskin-wow-cursor-arrow.png")
  AddProgressLsPb()
  Load_ImgEmptyCard = ImageList.LoadImage("http://t06.deviantart.net/0kBN9AFAPNDrXqp9umcq95XTRrY=/300x200/filters:fixed_height(100,100):origin()/pre10/bd1e/th/pre/f/2015/017/1/2/neutral_legendary_monster_empty_card_by_demaretc-d8ea24s.png")
  AddProgressLsPb()
  Load_ImgQuestionMark = ImageList.LoadImage("http://www.phonewareinc.com/wp-content/uploads/2012/06/blue-question-mark-100x100.png")
  AddProgressLsPb()
  Load_ImgLightEffect = ImageList.LoadImage("http://www.dundjinni.com/forums/uploads/Bogie/C37_Light_blue15_kpl.png")
  AddProgressLsPb()
  Load_ImgBorderLine = ImageList.LoadImage("http://vignette3.wikia.nocookie.net/wowwiki/images/7/7f/Fancy_separator-ML06M8222RGF1366383079159.png/revision/latest?cb=20140408152032")
  AddProgressLsPb()
  Load_ImgBackMm = ImageList.LoadImage("https://wallpaperscraft.com/image/warcraft_hearthstone_jaina_proudmoore_garrosh_hellscream_neltharion_deathwing_95362_2560x1080.jpg")
  AddProgressLsPb()
  Load_ImgBtnHelp = ImageList.LoadImage("http://ndismaps.nrel.colostate.edu/assets/images/i_help.png")
  AddProgressLsPb()
  Load_ImgPointer = ImageList.LoadImage("http://cdn.mysitemyway.com/etc-mysitemyway/icons/legacy-previews/icons/flat-gray-floral-icons-arrows/005174-flat-gray-floral-icon-arrows-hand-clear-pointer-left.png")
  AddProgressLsPb()
  Load_ImgBorders = ImageList.LoadImage("http://forums.nurfed.com/hearthstone/img/bg_frame.png")
  AddProgressLsPb()
  Load_ImgBlackboard = ImageList.LoadImage("http://4.bp.blogspot.com/_UdH8rcs3s1Q/TTxwVeLcQCI/AAAAAAAAANY/a8hxKDvcaTc/s1600/bgboard.png")
  AddProgressLsPb()
  Load_ImgLeftArrow = ImageList.LoadImage("http://findicons.com/icon/download/16003/back_2/64/png")
  AddProgressLsPb()
  Load_ImgRightArrow = ImageList.LoadImage("http://findicons.com/icon/download/15997/forward_2/64/png")
  AddProgressLsPb()
  Load_ImgBtnExitRot = ImageList.LoadImage("http://findicons.com/files/icons/1064/3d_cartoon_icons_ii/300/windows_close.png")
  AddProgressLsPb()
  Load_ImgCrown = ImageList.LoadImage("http://vignette1.wikia.nocookie.net/hearthstone/images/a/a6/MasterMedal.png/revision/latest?cb=20130826164930")
  AddProgressLsPb()
  Load_ImgWood = ImageList.LoadImage("http://www.wallpaperhi.com/thumbnails/detail/20130107/dark%20wood%20textures%20wood%20panels%20wood%20texture%201680x1050%20wallpaper_www.wallpaperhi.com_10.jpg")
  AddProgressLsPb()
  Load_ImgVictory = ImageList.LoadImage("https://solidsocietygaming.files.wordpress.com/2013/10/lol-victory.png?w=250&h=159")
  AddProgressLsPb()
  Load_imgcardback = ImageList.LoadImage("http://vignette1.wikia.nocookie.net/hearthstone/images/7/79/Card_back-Warlords.png/revision/latest?cb=20140822162428")
  AddProgressLsPb()
EndSub

'AddLoadingScreen
Sub AddLoadingScreen
  CurrentMenu = "LoadingScreen"
  Load_ImgBackLs = ImageList.LoadImage("http://static1.gamespot.com/uploads/screen_kubrick/1197/11970954/2372625-hearthstone+announcement+art.jpg")
  Load_ImgLightLs = ImageList.LoadImage("http://bio-net-calvados.pagesperso-orange.fr/suite/css/images/flare_fond.png")
  Load_ImgLsPbBack = "http://hearthstone.blizzplanet.com/wp-content/uploads/2015/03/Border2.png"
  Load_ImgPbLs = "http://cdn.1001freedownloads.com/vector/thumb/141864/ryanlerch_green_glossy_button_(gradient_based).png"
  GraphicsWindow.PenColor = ClrGrayBasic
  GraphicsWindow.BrushColor = ClrGrayBasic
  RecCoverLs1 = Shapes.AddRectangle(wWidth, 523)
  RecCoverLs2 = Shapes.AddRectangle(wWidth, 134)
  RecCoverLs3 = Shapes.AddRectangle(385, 32)  
  RecCoverLs4 = Shapes.AddRectangle(385, 32)
  RecCoverLs5 = Shapes.AddRectangle(580, 32)
  Shapes.Move(RecCoverLs2, 0, 555)
  Shapes.Move(RecCoverLs3, 0, 523)
  Shapes.Move(RecCoverLs4, 965 523)
  Shapes.Move(RecCoverLs5, 385 523)
  GraphicsWindow.DrawResizedImage(Load_ImgBackLs, 0, 0, wWidth, wHeight)
  GraphicsWindow.DrawImage(Load_ImgLsPbBack, (wWidth - 600) / 2, 500)
  Shapes.Remove(RecCoverLs5)
  ImgLightLs = Shapes.AddImage(Load_ImgLightLs)
  Controls.SetSize(ImgLightLs, 0, 0)
EndSub

'AddProgressLsPb
Sub AddProgressLsPb
  ValuePbLs = ValuePbLs + (460 / MaximumImages)
  GraphicsWindow.DrawResizedImage(Load_ImgPbLs, PbPosX, 525, ValuePbLs, 28)
  ValueRecCoverLsOpacity = ValueRecCoverLsOpacity - (100 / MaximumImages)
  OpacityImgLightLs = OpacityImgLightLs + (100 / MaximumImages)
  Shapes.SetOpacity(RecCoverLs1, ValueRecCoverLsOpacity)
  Shapes.SetOpacity(RecCoverLs2, ValueRecCoverLsOpacity)
  Shapes.SetOpacity(RecCoverLs3, ValueRecCoverLsOpacity)
  Shapes.SetOpacity(RecCoverLs4, ValueRecCoverLsOpacity)
  Controls.SetSize(ImgLightLs, ValuePbLs * 2, ValuePbLs * 2)
EndSub

'UpdateCursor
Sub UpdateCursor
  Shapes.Remove(ImgCursor)
  ImgCursor = Shapes.AddImage(Load_ImgCursor)
  Shapes.Move(ImgCursor, GraphicsWindow.MouseX + 1, GraphicsWindow.MouseY + 1)
EndSub

'AddMainMenu
Sub AddMainMenu
  CurrentMenu = "MainMenu"
  GraphicsWindow.PenColor = ClrGrayBasic
  GraphicsWindow.BrushColor = ClrGrayBasic
  RecCoverMm = Shapes.AddRectangle(wWidth, wHeight)
  Shapes.SetOpacity(RecCoverMm, 70)
  GraphicsWindow.DrawResizedImage(Load_ImgBackMm, -100, 0, wWidth + 200, wHeight)
  ImgBtnHelp = Shapes.AddImage(Load_ImgBtnHelp)
  Shapes.Move(ImgBtnHelp, 10, 10)
  ImgLightBtnPlay = Shapes.AddImage(Load_ImgLightEffect)
  Shapes.Zoom(ImgLightBtnPlay, 0.8, 0.8)
  Shapes.Move(ImgLightBtnPlay, (wWidth - ImageList.GetWidthOfImage(Load_ImgLightEffect)) / 2, -70)
  ImgBtnPlay = Shapes.AddImage(Load_ImgBtnPlay)
  Shapes.Move(ImgBtnPlay, 590, 100)
  Shapes.Zoom(ImgBtnPlay, 0.8, 0.8)
  If MainMenuAdded = "False" Then
    ImgFadingLight = Shapes.AddImage(Load_ImgLightEffect)
    Shapes.Zoom(ImgFadingLight, 0.8, 0.8)
    Shapes.Move(ImgFadingLight, (wWidth - ImageList.GetWidthOfImage(Load_ImgLightEffect)) / 2, -70)
    For Times = 0.8 To 3 Step 0.0002
      Shapes.Zoom(ImgFadingLight, Times, Times)
    EndFor
    Shapes.Remove(ImgFadingLight)
    ImgPointer = Shapes.AddImage(Load_ImgPointer)
    Shapes.Move(ImgPointer, 150, -90)
    Shapes.Zoom(ImgPointer, 0.5, 0.5)
    For Times = 0.5 To 1 Step 0.00003
      Shapes.Zoom(ImgPointer, Times, Times)
    EndFor
    For Times = 150 To 50 Step -0.01
      Shapes.Move(ImgPointer, Times, -90)
    EndFor
  EndIf
  UpdateCursor()
  MainMenuAdded = "True"
EndSub

'AddGame
Sub AddGame
  Sound.Stop(SndStartGame)
  Sound.Play(SndStartGame)
  CurrentMenu = "Game"
  ImgBackGameHeight = ImageList.GetHeightOfImage(Load_ImgGameBack)
  ImgBackGameWidth = ImageList.GetWidthOfImage(Load_ImgGameBack)
  For TimesX = 1 To 5
    For TimesY = 1 To 3
      GraphicsWindow.DrawImage(Load_ImgGameBack, TimesX * ImgBackGameWidth - ImgBackGameWidth, TimesY * ImgBackGameHeight - ImgBackGameHeight)
    EndFor
  EndFor
  GraphicsWindow.DrawResizedImage(Load_ImgPaper, 0, (wHeight - 250) / 2, wWidth, 250)
  For Loop = 1 To 2
    For Times = 1 To 5
      ImgBorderLine[Times] = Shapes.AddImage(Load_ImgBorderLine)
      Shapes.Move(ImgBorderLine[Times], - 300, Loop * 250 - 40)
      Shapes.Move(ImgBorderLine[Times], Times * 278 - 300, Loop * 250 - 40)
    EndFor
  EndFor
  CaptionsImages[1] = Load_ImgClockworkGiant
  CaptionsImages[2] = Load_ImgMountainGiant
  CaptionsImages[3] = Load_ImgLanternOfPower
  CaptionsImages[4] = Load_ImgFrostGiant
  CaptionsImages[5] = Load_ImgGearMaster
  CaptionsImages[6] = Load_ImgVarian
  CaptionsImages[7] = Load_ImgTreeOfLife
  CaptionsImages[8] = Load_ImgCenarios 
  CaptionsImages[9] = Load_ImgMalygos
  CaptionsImages[10] = Load_ImgTirion
  For AllCards = 1 To 10
    Player1Cards[AllCards] = Shapes.AddImage(CaptionsImages[AllCards])
    Shapes.Zoom(Player1Cards[AllCards], 0.5, 0.5)
    Shapes.HideShape(Player1Cards[AllCards])
    Player2Cards[AllCards] = Shapes.AddImage(CaptionsImages[AllCards])
    Shapes.Zoom(Player2Cards[AllCards], 0.5, 0.5)
    Shapes.HideShape(Player2Cards[AllCards])
    Shapes.Move(Player1Cards[AllCards], CardDifferenceX - 500, -CardDifferenceY + 10)
    Shapes.Move(Player2Cards[AllCards], CardDifferenceX - 500, -CardDifferenceY + 490)
    Shapes.ShowShape(Player1Cards[AllCards])
    Shapes.ShowShape(Player2Cards[AllCards])
  EndFor
  ShuffleCards()
  ImgCardsWonP1 = Shapes.AddImage(Load_ImgCardsBackside)
  Shapes.Zoom(ImgCardsWonP1, 0.4, 0.4)
  Shapes.Move(ImgCardsWonP1, -90, 130)
  ImgCardsWonP2 = Shapes.AddImage(Load_ImgCardsBackside)
  Shapes.Zoom(ImgCardsWonP2, 0.4, 0.4)
  Shapes.Move(ImgCardsWonP2, -90, 250)
  ImgLightBtnDeal = Shapes.AddImage(Load_ImgLightEffect)
  Shapes.Move(ImgLightBtnDeal, 1040, 140)
  Shapes.SetOpacity(ImgLightBtnDeal, 0)
  ImgDealCards = Shapes.AddImage(Load_ImgNextTurn)
  Shapes.Move(ImgDealCards, 1150, 255)
  Shapes.Zoom(ImgDealCards, 0.85, 0.85)
  GraphicsWindow.FontSize = 35
  GraphicsWindow.BrushColor = "Black"
  LblPlayBlack = Shapes.AddText("Play")
  Shapes.Move(LblPlayBlack, 1200, 320)
  Shapes.Zoom(LblPlayBlack, 0.85, 0.85)
  GraphicsWindow.BrushColor = "White"
  LblPlayWhite = Shapes.AddText("Play")
  Shapes.Move(LblPlayWhite, 1202, 322)
  Shapes.Zoom(LblPlayWhite, 0.85, 0.85)
  GraphicsWindow.FontSize = 30
  GraphicsWindow.BrushColor = "Black"
  LblCardsWonBlackP1 = Shapes.AddText("0")
  Shapes.Move(LblCardsWonBlackP1, 120, 275)
  GraphicsWindow.BrushColor = "White"
  LblCardsWonWhiteP1 = Shapes.AddText("0")
  Shapes.Move(LblCardsWonWhiteP1, 122, 277)
  GraphicsWindow.BrushColor = "Black"
  LblCardsWonBlackP2 = Shapes.AddText("0")
  Shapes.Move(LblCardsWonBlackP2, 120, 395)
  GraphicsWindow.BrushColor = "White"
  LblCardsWonWhiteP2 = Shapes.AddText("0")
  Shapes.Move(LblCardsWonWhiteP2, 122, 397)
  UpdateCursor()
  CanPlay = "True"
EndSub

'ShuffleCards
Sub ShuffleCards
  For Times = 1 To 10
    Start:
    RandomCard = Math.GetRandomNumber(10)
    If Shapes.GetOpacity(Player1Cards[RandomCard]) = 99 Then
      Goto Start
    Else
      Shapes.SetOpacity(Player1Cards[RandomCard], 99)
      Shapes.Animate(Player1Cards[RandomCard], Times * CardWidth - CardWidth - CardDifferenceX + 15, -CardDifferenceY + 15, 1500)
    EndIf
  EndFor
  For Times = 1 To 10
    Begin:
    RandomCard = Math.GetRandomNumber(10)
    If Shapes.GetOpacity(Player2Cards[RandomCard]) = 99 Then
      Goto Begin
    Else
      Shapes.SetOpacity(Player2Cards[RandomCard], 99)
      Shapes.Animate(Player2Cards[RandomCard], Times * CardWidth - CardWidth - CardDifferenceX + 15, -CardDifferenceY + 490, 1500)
    EndIf
  EndFor
  For Times = 1 To 10
    Shapes.SetOpacity(Player1Cards[Times], 100)
    Shapes.SetOpacity(Player2Cards[Times], 100)
  EndFor
EndSub

'PlayNextTurnAnimation
Sub PlayNextTurnAnimation
  CanPlay = "False"
  Sound.Stop(SndMagicWand)
  Sound.Play(SndMagicWand)
  ImgLightEffect = Shapes.AddImage(Load_ImgLightEffect)
  Shapes.Move(ImgLightEffect, (wWidth - 300) / 2 - 40, (wHeight - 300) / 2 - 50)
  Shapes.Zoom(ImgLightEffect, 0.5, 0.5)
  Shapes.SetOpacity(ImgLightEffect, 40)
  ImgFlag = Shapes.AddImage(Load_ImgBorderFlag)
  Shapes.Move(ImgFlag, (wWidth - 600) / 2, 200)
  Shapes.Zoom(ImgFlag, 0.5, 0.5)
  ImgQuestMark = Shapes.AddImage(Load_ImgQuestionMark)
  Shapes.Move(ImgQuestMark, (wWidth - 100) / 2, (wHeight - 100) / 2 - 20)
  ImgEmptyCard = Shapes.AddImage(Load_ImgEmptyCard)
  Shapes.Move(ImgEmptyCard, (wWidth - 141) / 2, (wHeight - 200) / 2)
  UpdateCursor()
  For Times = 0.5 To 1 Step 0.0001
    Shapes.Zoom(ImgFlag, Times, Times)
    Shapes.Zoom(ImgEmptyCard, Times, Times)
    Shapes.Zoom(ImgQuestMark, Times, Times)
    Shapes.Zoom(ImgLightEffect, Times, Times)
  EndFor
  For Times = 1 To 1.05 Step 0.000005
    Shapes.Zoom(ImgFlag, Times, Times)
    Shapes.Zoom(ImgEmptyCard, Times, Times)
    Shapes.Zoom(ImgQuestMark, Times, Times)
    Shapes.Zoom(ImgLightEffect, Times, Times)
  EndFor
  For Times = 1.05 To 0.5 Step -0.0001
    Shapes.Zoom(ImgFlag, Times, Times)
    Shapes.Zoom(ImgEmptyCard, Times, Times)
    Shapes.Zoom(ImgQuestMark, Times, Times)
    Shapes.Zoom(ImgLightEffect, Times, Times)
  EndFor
  Shapes.Remove(ImgFlag)
  Shapes.Remove(ImgEmptyCard)
  Shapes.Remove(ImgQuestMark)
  Shapes.Remove(ImgLightEffect)
  ThrowRandomCards()
EndSub

'ThrowRandomCards
Sub ThrowRandomCards
  RandomCardP1 = Math.GetRandomNumber(10)
  RandomCardP2 = Math.GetRandomNumber(10)
  P1CardLeft[RandomCardP1] = Shapes.GetLeft(Player1Cards[RandomCardP1])
  P2CardLeft[RandomCardP2] = Shapes.GetLeft(Player2Cards[RandomCardP2])
  P1CardTop[RandomCardP1] = Shapes.GetTop(Player1Cards[RandomCardP1])
  P2CardTop[RandomCardP2] = Shapes.GetTop(Player2Cards[RandomCardP2])
  If Shapes.GetOpacity(Player1Cards[RandomCardP1]) < 100 Or Shapes.GetOpacity(Player2Cards[RandomCardP2]) < 100 Then
    ThrowRandomCards()
  Else
    Shapes.Animate(Player1Cards[RandomCardP1], 420, 140, CardSpeed)
    Shapes.Animate(Player2Cards[RandomCardP2], 620, 140, CardSpeed)
  EndIf
  If RandomCardP1 = RandomCardP2 Then
  Else
    Timer.Resume()
  EndIf
  CanPressKeys = "True"
EndSub

'ReturnCurrentCards
Sub ReturnCurrentCards
  If RandomCardP1 = RandomCardP2 Then
    If RoundWonBy = "A" Then
      Sound.Stop(SndCorrect)
      Sound.Play(SndCorrect)
      For Times = 0.5 To 0.8 Step 0.0001
        Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
      EndFor
      For Times = 0.8 To 0.5 Step - 0.0001
        Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
      EndFor
      For Times = 0.5 To 0.3 Step - 0.0001
        Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
        Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
      EndFor
      Shapes.Animate(Player1Cards[RandomCardP1], -65, 80, 800)
      Shapes.Animate(Player2Cards[RandomCardP2], -65, 80, 800)
      For Times = 0 To 5 Step 0.0001
        Shapes.Rotate(Player2Cards[RandomCardP2], Times)
      EndFor
      Shapes.SetOpacity(Player1Cards[RandomCardP1], 0)
      Shapes.SetOpacity(Player2Cards[RandomCardP2], 0)
    ElseIf RoundWonBy = "L" Then
      Sound.Stop(SndCorrect)
      Sound.Play(SndCorrect)
      For Times = 0.5 To 0.8 Step 0.0001
        Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
      EndFor
      For Times = 0.8 To 0.5 Step - 0.0001
        Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
      EndFor
      For Times = 0.5 To 0.3 Step - 0.0001
        Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
        Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
      EndFor
      Shapes.Animate(Player1Cards[RandomCardP1], -65, 205, 800)
      Shapes.Animate(Player2Cards[RandomCardP2], -65, 205, 800)
      For Times = 0 To 5 Step 0.0001
        Shapes.Rotate(Player2Cards[RandomCardP2], Times)
      EndFor
      Shapes.SetOpacity(Player1Cards[RandomCardP1], 0)
      Shapes.SetOpacity(Player2Cards[RandomCardP2], 0)
    EndIf
    GraphicsWindow.DrawResizedImage(Load_ImgCardBack, P1CardLeft[RandomCardP1] + CardDifferenceX + 10, P1CardTop[RandomCardP1] + CardDifferenceY + 10, 110, 170)
    GraphicsWindow.DrawResizedImage(Load_ImgCardBack, P2CardLeft[RandomCardP2] + CardDifferenceX + 10, P1CardTop[RandomCardP1] + CardDifferenceY + 485, 110, 170)
  Else
    Shapes.Animate(Player1Cards[RandomCardP1], P1CardLeft[RandomCardP1], P1CardTop[RandomCardP1], 750)
    Shapes.Animate(Player2Cards[RandomCardP2], P2CardLeft[RandomCardP2], P2CardTop[RandomCardP2], 750)
  EndIf
  CanPressKeys = "False"
EndSub

'SetScore
Sub SetScore
  If RoundWonBy = "A" Then
    RemainingCards = RemainingCards - 2
    ScoreP1 = ScoreP1 + 2
    Shapes.SetText(LblCardsWonBlackP1, ScoreP1)
    Shapes.SetText(LblCardsWonWhiteP1, ScoreP1)
    For Times = 1 To 1.4 Step 0.0001
      Shapes.Zoom(LblCardsWonBlackP1, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP1, Times, Times)
    EndFor
    Sound.Stop(SndBlop)
    Sound.Play(SndBlop)
    For Times = 1.4 To 1 Step -0.0001
      Shapes.Zoom(LblCardsWonBlackP1, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP1, Times, Times)
    EndFor
    RoundWonBy = "None"
  ElseIf RoundWonBy = "L" Then
    RemainingCards = RemainingCards - 2
    ScoreP2 = ScoreP2 + 2
    Shapes.SetText(LblCardsWonBlackP2, ScoreP2)
    Shapes.SetText(LblCardsWonWhiteP2, ScoreP2)
    For Times = 1 To 1.4 Step 0.0001
      Shapes.Zoom(LblCardsWonBlackP2, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP2, Times, Times)
    EndFor
    Sound.Stop(SndBlop)
    Sound.Play(SndBlop)
    For Times = 1.4 To 1 Step -0.0001
      Shapes.Zoom(LblCardsWonBlackP2, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP2, Times, Times)
    EndFor
    RoundWonBy = "None"
  EndIf
  If RoundLostBy = "A" Then
    Sound.Stop(SndWrong)
    Sound.Play(SndWrong)
    For Times = 0.5 To 0.2 Step -0.0001
      Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
    EndFor
    Sound.Stop(SndBlop)
    Sound.Play(SndBlop)
    For Times = 0.2 To 0.5 Step 0.0001
      Shapes.Zoom(Player1Cards[RandomCardP1], Times, Times)
    EndFor
    ScoreP1 = ScoreP1 - 2
    Shapes.SetText(LblCardsWonBlackP1, ScoreP1)
    Shapes.SetText(LblCardsWonWhiteP1, ScoreP1)
    For Times = 1 To 1.4 Step 0.0001
      Shapes.Zoom(LblCardsWonBlackP1, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP1, Times, Times)
    EndFor
    For Times = 1.4 To 1 Step -0.0001
      Shapes.Zoom(LblCardsWonBlackP1, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP1, Times, Times)
    EndFor
    RoundLostBy = "None"
  ElseIf RoundLostBy = "L" Then
    Sound.Stop(SndWrong)
    Sound.Play(SndWrong)
    For Times = 0.5 To 0.2 Step -0.0001
      Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
    EndFor
    For Times = 0.2 To 0.5 Step 0.0001
      Shapes.Zoom(Player2Cards[RandomCardP2], Times, Times)
    EndFor
    ScoreP2 = ScoreP2 - 2
    Shapes.SetText(LblCardsWonBlackP2, ScoreP2)
    Shapes.SetText(LblCardsWonWhiteP2, ScoreP2)
    For Times = 1 To 1.4 Step 0.0001
      Shapes.Zoom(LblCardsWonBlackP2, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP2, Times, Times)
    EndFor
    Sound.Stop(SndBlop)
    Sound.Play(SndBlop)
    For Times = 1.4 To 1 Step -0.0001
      Shapes.Zoom(LblCardsWonBlackP2, Times, Times)
      Shapes.Zoom(LblCardsWonWhiteP2, Times, Times)
    EndFor
    RoundLostBy = "None"
  EndIf
EndSub

'PreTurnEffects
Sub PreTurnEffects
  Shapes.Remove(ImgLightEffect)
  Shapes.Remove(ImgEmptyCard)
  Shapes.Remove(ImgQuestMark)
  Shapes.Remove(ImgFlag)
  For Times = 0.85 To 0.65 Step -0.0002
    Shapes.Zoom(ImgDealCards, Times, Times)
    Shapes.Zoom(LblPlayBlack, Times, Times)
    Shapes.Zoom(LblPlayWhite, Times, Times)
  EndFor
  Shapes.SetOpacity(ImgLightBtnDeal, 0)
  For Times = 0.65 To 0.85 Step 0.0002
    Shapes.Zoom(ImgDealCards, Times, Times)
    Shapes.Zoom(LblPlayBlack, Times, Times)
    Shapes.Zoom(LblPlayWhite, Times, Times)
  EndFor
EndSub

'LoadHelp
Sub LoadHelp
  CurrentMenu = "Help"
  GraphicsWindow.Clear()
  GraphicsWindow.DrawResizedImage(Load_ImgBlackboard, (wWidth - 920) / 2, (wHeight - 556) / 2, 920, 536)
  GraphicsWindow.DrawResizedImage(Load_ImgBorders, (wWidth - 1024) / 2, (wHeight - 576) / 2, 1024, 576)
  GraphicsWindow.DrawImage(Load_ImgBtnHelp, (wWidth - 40) / 2, 45)
  GraphicsWindow.DrawResizedImage(Load_ImgBtnExitRot, 1090, 50, 70, 70)
  GraphicsWindow.FontSize = 15
  If HelpPage = 1 Then
    GraphicsWindow.BrushColor = "Black"
    GraphicsWindow.DrawBoundText(380, 200, 600, "This is the play button. Click it or press 'Space' to discard one random card from each player.")
    GraphicsWindow.BrushColor = "White"
    GraphicsWindow.DrawBoundText(382, 202, 600, "This is the play button. Click it or press 'Space' to discard one random card from each player.")
    GraphicsWindow.DrawResizedImage(Load_ImgNextTurn, (wWidth - 100) / 2, (wHeight - 100) / 2, 100, 100)
  ElseIf HelpPage = 2 Then
    GraphicsWindow.BrushColor = "Black"
    GraphicsWindow.DrawBoundText(380, 200, 600, "If the cards are the same then Player1 should press the 'L' key and Player2 should press the 'A' key. The player who strikes his key first wins the round and earns two points.")
    GraphicsWindow.BrushColor = "White"
    GraphicsWindow.DrawBoundText(382, 202, 600, "If the cards are the same then Player1 should press the 'L' key and Player2 should press the 'A' key. The player who strikes his key first wins the round and earns two points.")
    GraphicsWindow.DrawResizedImage(Load_ImgMountainGiant, 555, 280, 110, 150)
    GraphicsWindow.DrawResizedImage(Load_ImgMountainGiant, 695, 280, 110, 150)
  ElseIf HelpPage = 3 Then
    GraphicsWindow.BrushColor = "Black"
    GraphicsWindow.DrawBoundText(380, 200, 600, "In case the cards are different, the player who strikes his key first looses 2 points, so use it wisely!")
    GraphicsWindow.BrushColor = "White"
    GraphicsWindow.DrawBoundText(382, 202, 600, "In case the cards are different, the player who strikes his key first looses 2 points, so use it wisely!")
    GraphicsWindow.DrawResizedImage(Load_ImgMountainGiant, 555, 280, 110, 150)
    GraphicsWindow.DrawResizedImage(Load_ImgTirion, 695, 280, 110, 150)
  EndIf
  If HelpPage = 1 Then
    GraphicsWindow.DrawImage(Load_ImgRightArrow, (wWidth - 64) / 2 + 42, 500)
  ElseIf HelpPage = 3 Then
    GraphicsWindow.DrawImage(Load_ImgLeftArrow, (wWidth - 64) / 2 - 42, 500)
  Else
    GraphicsWindow.DrawImage(Load_ImgLeftArrow, (wWidth - 64) / 2 - 42, 500)
    GraphicsWindow.DrawImage(Load_ImgRightArrow, (wWidth - 64) / 2 + 42, 500)
  EndIf
  UpdateCursor()
EndSub

'LoadScore
Sub LoadScore
  Timer.Pause()
  CanPlay = "False"
  CanPressKeys = "False"
  RoundWonBy = "None"
  RoundLostBy = "None"
  RemainingCards = 20
  GraphicsWindow.Clear()
  CurrentMenu = "Score"
  Sound.Play(SndVictory)
  GraphicsWindow.DrawImage(Load_ImgWood, 275, 95)
  GraphicsWindow.DrawResizedImage(Load_ImgBorders, 255, 85, 840, 520)
  GraphicsWindow.DrawResizedImage(Load_ImgCrown, (wWidth - 128) / 2, 50, 128, 128)
  GraphicsWindow.DrawResizedImage(Load_ImgBtnExitRot, 1010, 80, 70, 70)
  GraphicsWindow.DrawResizedImage(Load_ImgLsPbBack, 350, 180, 300, 50)
  GraphicsWindow.DrawResizedImage(Load_ImgLsPbBack, 700, 180, 300, 50)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.FontSize = 25
  GraphicsWindow.DrawText(450, 160, "Player 1")
  GraphicsWindow.DrawText(800, 160, "Player 2")
  GraphicsWindow.DrawText(380, 440, "Scored: " + ScoreP1 + " points")
  GraphicsWindow.DrawText(730, 440, "Scored: " + ScoreP2 + " points")
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.DrawText(452, 162, "Player 1")
  GraphicsWindow.DrawText(802, 162, "Player 2")
  GraphicsWindow.DrawText(382, 442, "Scored: " + ScoreP1 + " points")
  GraphicsWindow.DrawText(732, 442, "Scored: " + ScoreP2 + " points")
  GraphicsWindow.DrawResizedImage(Load_ImgCardsBackside, 400, 230, 200, 200)
  GraphicsWindow.DrawResizedImage(Load_ImgCardsBackside, 750, 230, 200, 200)
  If ScoreP1 > ScoreP2 Then
    GraphicsWindow.DrawResizedImage(Load_ImgVictory, 415, 490, 150, 55)
  ElseIf ScoreP1 < ScoreP2 Then
    GraphicsWindow.DrawResizedImage(Load_ImgVictory, 765, 490, 150, 55)
  Else
    GraphicsWindow.DrawResizedImage(Load_ImgVictory, 415, 490, 150, 55)
    GraphicsWindow.DrawResizedImage(Load_ImgVictory, 765, 490, 150, 55)
  EndIf
  UpdateCursor()
  ScoreP1 = 0
  ScoreP2 = 0
EndSub

'Subroutines - System
'********************
'MouseDown
GraphicsWindow.MouseDown = MouseDown
Sub MouseDown
  x = GraphicsWindow.MouseX
  y = GraphicsWindow.MouseY
  Shapes.Rotate(ImgCursor, - 6)
  If CurrentMenu = "MainMenu" Then
    If x > 590 And x < 765 And y > 100 And y < 182 Then
      GraphicsWindow.Clear()
      AddGame()
    ElseIf x > 10 And x < 50 And y > 10 And y < 50 Then
      For Times = 1 To 0.7 Step -0.0001
        Shapes.Zoom(ImgBtnHelp, Times, Times)
      EndFor
      For Times = 0.7 To 1 Step 0.0001
        Shapes.Zoom(ImgBtnHelp, Times, Times)
      EndFor
      LoadHelp()
    EndIf
  ElseIf CurrentMenu = "Help" Then
    If y > 500 And y < 564 Then
      If x > 600 And x < 664 Then
        If HelpPage = 1 Then
        Else
          HelpPage = HelpPage - 1
          LoadHelp()
        EndIf
      ElseIf x > 685 And x < 749 Then
        If HelpPage = 3 Then
        Else
          HelpPage = HelpPage + 1
          LoadHelp()
        EndIf
      EndIf
    EndIf
    If x > 1090 And x < 1140 And y > 50 And y < 130 Then
      Sound.PlayClick()
      GraphicsWindow.Clear()
      AddMainMenu()
    EndIf
  ElseIf CurrentMenu = "Game" Then
    If x > 1150 And x < 1325 And y > 255 And y < 430 And CanPlay = "True"  Then
      If CanPlay = "True" Then
        CanPlay = "False"
        Sound.Stop(SndAnvil)
        Sound.Play(SndAnvil)
        PreTurnEffects()
        PlayNextTurnAnimation()
      EndIf
    EndIf
  ElseIf CurrentMenu = "Score" Then
    If x > 1010 And x < 1080 And y > 80 And y < 150 Then
      GraphicsWindow.Clear()
      AddMainMenu()
    EndIf
  EndIf
EndSub

'MouseUp
GraphicsWindow.MouseUp = MouseUp
Sub MouseUp
  Shapes.Rotate(ImgCursor, 0)
EndSub

'MouseMove
GraphicsWindow.MouseMove = MouseMove
Sub MouseMove
  x = GraphicsWindow.MouseX
  y = GraphicsWindow.MouseY
  Shapes.Move(ImgCursor, x + 1, y + 1)
  If CurrentMenu = "MainMenu" Then
    If x > 590 And x < 765 And y > 100 And y < 182 Then
      Shapes.Zoom(ImgLightBtnPlay, 1.3, 1.3)
      Shapes.Zoom(ImgBtnPlay, 1, 1)
      Shapes.SetOpacity(RecCoverMm, 30)
      Shapes.HideShape(ImgPointer)
      AnimateBtnHelp = "True"
    ElseIf x > 10 And x < 50 And y > 10 And y < 50 Then
      Shapes.HideShape(ImgPointer)
    Else
      Shapes.Zoom(ImgLightBtnPlay, 0.8, 0.8)
      Shapes.Zoom(ImgBtnPlay, 0.8, 0.8)
      Shapes.SetOpacity(RecCoverMm, 70)
      If AnimateBtnHelp = "True" Then
        AnimateBtnHelp = "False"
        For Times = 1 To 1.3 Step 0.0001
          Shapes.Zoom(ImgBtnHelp, Times, Times)
        EndFor
        For Times = 1.3 To 1 Step -0.0001
          Shapes.Zoom(ImgBtnHelp, Times, Times)
        EndFor
      EndIf
    EndIf
  ElseIf CurrentMenu = "Game" Then
    If x > 1150 And x < 1325 And y > 255 And y < 430 And CanPlay = "True"  Then
      Shapes.SetOpacity(ImgLightBtnDeal, 100)
    Else
      Shapes.SetOpacity(ImgLightBtnDeal, 0)
    EndIf
  EndIf
EndSub

'KeyDown
GraphicsWindow.KeyDown = KeyDown
Sub KeyDown
  LastKey = GraphicsWindow.LastKey
  If CurrentMenu = "Game" And CanPressKeys = "True"  Then
    If RandomCardP1 = RandomCardP2 Then
      If LastKey = "A" Then
        CanPressKeys = "False"
        RoundWonBy = "A"
        ReturnCurrentCards()
        SetScore()
        If RemainingCards > 0 Then
          CanPlay = "True"
        ElseIf RemainingCards = 0 Then
          Program.Delay(500)
          LoadScore()
        EndIf
      ElseIf LastKey = "L" Then
        CanPressKeys = "False"
        RoundWonBy = "L"
        ReturnCurrentCards()
        SetScore()
        If RemainingCards > 0 Then
          CanPlay = "True"
        ElseIf RemainingCards = 0 Then
          Program.Delay(500)
          LoadScore()
        EndIf
      EndIf
    Else
      If LastKey = "A" Then
        CanPressKeys = "False"
        RoundLostBy = "A"
        SetScore()
      ElseIf LastKey = "L" Then
        CanPressKeys = "False"
        RoundLostBy = "L"
        SetScore()
      EndIf
    EndIf
  EndIf
  If CurrentMenu = "Game" And LastKey = "Space" Then
    If CanPlay = "True" Then
      CanPlay = "False"
      Sound.Stop(SndAnvil)
      Sound.Play(SndAnvil)
      PreTurnEffects()
      PlayNextTurnAnimation()
    EndIf
  EndIf
EndSub

'Timer
Timer.Interval = 1500
Timer.Tick = Tick
Sub Tick
  ReturnCurrentCards()
  Timer.Pause()
  CanPlay = "True"
EndSub
