' binary    140817    WhTurner

While "True"

  TextWindow.Clear()

  TextWindow.WriteLine(" Binary calculation")

  TextWindow.WriteLine(" ==================")

  TextWindow.WriteLine("")

  Line="-----------------------------------"

  TextWindow.WriteLine("                      input 0 to stop")

  TextWindow.Write("Give  first binary value : ")

  B1=TextWindow.ReadNumber()

  If B1=0 then

    Program.End()

  endif

  TextWindow.Write("Give second binary value : ")

  B2=TextWindow.ReadNumber()

  TextWindow.Clear()

  TextWindow.WriteLine("====================")

  TextWindow.WriteLine(" Binary calculation")

  TextWindow.WriteLine("====================")

  TextWindow.WriteLine("")

  L1=Text.GetLength(B1)

  L2=Text.GetLength(B2)

  

  B=B1

  format()

  TextWindow.WriteLine(_B)

  B=B2

  format()

  TextWindow.WriteLine(_B)

  

  Sum()

  B=Bsum

  format()

  TextWindow.WriteLine(Line+" +")

  TextWindow.WriteLine(_B+" <-- sum")

  

  Multiply()

  TextWindow.WriteLine(Line+" *")

  B=Bprod

  format()

  TextWindow.WriteLine(_B+" <-- product")

  TextWindow.WriteLine("")

  TextWindow.Pause()

endwhile





Sub Multiply   ' in:  B1,B2   out:  Bprod

  Stack.PushValue("M",B1)

  Stack.PushValue("M",B2)

  fact=B2

  prod="00000000"

  BB1=B1

  For _j=Text.GetLength(BB1) To 1 Step -1

    bit1=Text.GetSubText(BB1,_j,1) 

    If bit1=1 Then

      B2=fact

      B1=prod

      Sum()

      prod=Bsum

    EndIf  

    fact=Text.Append(fact,"0")

  endfor

  B2=Stack.PopValue("M")

  B1=Stack.PopValue("M")

  Bprod=Bsum

EndSub



Sub Sum    ' in: B1,B2    out: Bsum

  L1=Text.GetLength(B1)

  L2=Text.GetLength(B2)

  If L1>L2 then 

    For _L=1 To L1-L2

      B2=Text.Append("0",B2)

    EndFor

    L2=L1

  ElseIf L1<L2 then

    For _L=1 To L2-L1

      B1=Text.Append("0",B1)

    EndFor

    L1=L2

  EndIf

  carry=0

  Bsum=""

  For _i=L1 to 1 Step -1

    bit1=Text.GetSubText(B1,_i,1) 

    bit2=Text.GetSubText(B2,_i,1)

    bit=bit1+bit2+carry

    carry=Math.Floor(bit/2)

    bit=Math.Remainder(bit,2)

    Bsum=Text.Append(bit,Bsum)

  EndFor

  Bsum=Text.Append(carry,Bsum)

EndSub



Sub format    ' in:  binary string in B --> out:  formatted in bytes in _B

  While Text.GetSubText(B,1,1)="0" 

    B=Text.GetSubTextToEnd(B,2)

  EndWhile

  _L=Text.GetLength(B)

  _C=Math.Ceiling(_L/8)

  _E="        "

  _B=""

  For _i=_C To 3   ' non significant bytes blank

    _B=Text.Append(_B,_E+" ")

  EndFor

  For _i=_L To 8*_C-1   'fill to 8 bits with 0 (zero)

    _B=Text.Append(_B,"0")

  EndFor

  _B=Text.Append(_B,Text.GetSubText(B,1,_L-8*(_C-1)))

  For _i=_L-(_C-1)*8+1 To _L Step 8

    _B=Text.Append(_B," "+Text.GetSubText(B,_i,8))

  EndFor

EndSub

    


