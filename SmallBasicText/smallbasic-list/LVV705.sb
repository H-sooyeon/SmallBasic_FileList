'Images for  this program can be downloaded from here:

'https://docs.google.com/open?id=0B4QdGY5z0AOfM2FmNjk3MGUtM2E4OC00OWJkLWJhZDAtODhhNzIxNzE5MTM2 



'Initialise graphics window



GraphicsWindow.Hide()

gw = 1024

gh = 768

GraphicsWindow.CanResize = "False"

GraphicsWindow.Top = (Desktop.Height-gh)/2

GraphicsWindow.Left = (Desktop.Width-gw)/2

GraphicsWindow.Title = "Carom Billiards"

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

Path = Program.Directory

background = ImageList.LoadImage( Path + "/hatter2.png")

GraphicsWindow.DrawImage(background, 0, 0)

'GraphicsWindow.BackgroundColor="#008000"

GraphicsWindow.Show()



GraphicsWindow.KeyDown=onKeyDown

GraphicsWindow.MouseDown=onMouseDown

GraphicsWindow.MouseUp=onMouseUp

GraphicsWindow.MouseMove=OnMouseMove



mozgat=1

lokes=2

kiiras=3



dt=0.5

atmero=40

sugar=atmero/2





N=4 'labdaszám



' fmstate=0 ha hiba

'fmstate=3 ha érvényes a lökés, vagyis poén lett



labda[1]=Shapes.AddImage(Path +"/afgolyo.png")

labda[2]=Shapes.AddImage(Path +"/asgolyo.png")

labda[3]=Shapes.AddImage(Path +"/apgolyo.png")

labda[4]=Shapes.AddImage(Path +"/apgolyo.png")

bf=112

jf=912

af=584

ff=184



balfal=bf+sugar

jobbfal=jf-sugar

alsofal=af-sugar

felsofal=ff+sugar



Kezd:

startgame()

While("True")

  If allapot=mozgat Then

    utkozes()' leadminisztrálja az esetleges ütközést

    ene=0

    For i=1 To N   

      x[i]=x[i]+vx[i]*dt 'kiszámolja a golyó x helyzetét

      y[i]=y[i]+vy[i]*dt 'kiszámolja a golyó y helyzetét

      

      visszapattan() 'ellenőrzi és kezeli a visszapattanást

      

      Shapes.Move(labda[i],x[i]-sugar,y[i]-sugar) 

      'súrlódás adminisztrálása

      v=Math.SquareRoot(vx[i]*vx[i]+vy[i]*vy[i])

      If (v-epsilon)> 0 then

        vx[i]=(v-epsilon)*vx[i]/v 

        vy[i]=(v-epsilon)*vy[i]/v

      else

        vx[i]=0

        vy[i]=0

      endif

      ene=ene+v*v  

    endFor

    '***********************************************

    '  GraphicsWindow.BrushColor="#008000"

    '  GraphicsWindow.FillRectangle(0,0,200,20)

    '  GraphicsWindow.BrushColor="Black"

    '  GraphicsWindow.DrawText(5,5,"fmstate: "+fmstate)

    '*********************************************  

    energia=ene

    If energia=0 then 

      allapot=kiiras

    EndIf

    Program.Delay(kesl)

  EndIf

  If allapot=kiiras Then

    GraphicsWindow.FontSize=20

    GraphicsWindow.FontName="Arial"

    GraphicsWindow.FontBold="False"

    GraphicsWindow.BrushColor="#000050"

    GraphicsWindow.FillRectangle(362,650,300,60)

    GraphicsWindow.PenColor="#007F7F"

    GraphicsWindow.PenWidth=1

    Shapes.AddLine(362,650,662,650)

    Shapes.AddLine(362,650,362,710)

    GraphicsWindow.PenColor="Black"

    Shapes.AddLine(362,710,662,710)

    Shapes.AddLine(662,650,662,710)

    GraphicsWindow.PenWidth=3

    If fmstate=4 then 

      If sargatert=1 then 

        score=score+1000

        GraphicsWindow.BrushColor="#FFFF00"

        GraphicsWindow.DrawText(400,655,"OK! Got +1000 points")

      Else

        score=score+5000

        GraphicsWindow.BrushColor="#FFFF00"

        GraphicsWindow.DrawText(400,655,"Well done! Got +5000 points")

      endif

      GraphicsWindow.BrushColor="#00FFFF"

      GraphicsWindow.DrawText(420,685,"Score:   "+score )

    else

      score=score-1000

      GraphicsWindow.BrushColor="#FF0000"

      GraphicsWindow.DrawText(420,655,"Foul! -1000 points")

      GraphicsWindow.BrushColor="#00FFFF"

      GraphicsWindow.DrawText(420,685,"Score:   "+score )

    endif

    allapot=lokes

  EndIf

  If allapot=lokes Then

    GraphicsWindow.PenWidth=3

    dakohossz=Math.SquareRoot((xm-x[1])*(xm-x[1])+(ym-y[1])*(ym-y[1]))

    If dakohossz<100 Then

      dakox=xm-x[1]

      dakoy=ym-y[1]

      vonal=Shapes.AddLine(x[1],y[1],xm,ym)

    Else

      dakox=100*(xm-x[1])/dakohossz

      dakoy=100*(ym-y[1])/dakohossz

      vonal=Shapes.AddLine(x[1],y[1],x[1]+dakox,y[1]+dakoy)

    EndIf

    Program.Delay(50)

    Shapes.Remove(vonal)

    GraphicsWindow.PenWidth=1

  EndIf

EndWhile

Sub StartGame

  GraphicsWindow.BrushColor="#FF0000"

  GraphicsWindow.PenWidth=1

  fmstate=1 'szabályt követő véges állapotú gép kezdőállapotának beállítása

  ' fmstate<>4 ha hiba

  'fmstate=4 ha érvényes a lökés

  epsilon=0.001

  energia=0

  sargatert=0

  kesl=5

  allapot=lokes

  score=0

  GraphicsWindow.DrawRectangle(bf,ff,jf-bf,af-ff) 

  x[1]=200

  x[2]=300

  x[3]=400

  x[4]=500

  

  y[1]=384

  y[2]=384

  y[3]=384

  y[4]=384

  

  For i=1 to N

    vx[i]=0

    vy[i]=0

    Shapes.Move(labda[i],x[i]-sugar,y[i]-sugar)  

  EndFor

  

  infoscreen()

  szabaly()

  GraphicsWindow.FontSize=20

  GraphicsWindow.FontName="Arial"

  GraphicsWindow.FontBold="False"

  GraphicsWindow.BrushColor="#000050"

  GraphicsWindow.FillRectangle(362,650,300,60)

  GraphicsWindow.PenColor="#007F7F"

  GraphicsWindow.PenWidth=1

  Shapes.AddLine(362,650,662,650)

  Shapes.AddLine(362,650,362,710)

  GraphicsWindow.PenColor="Black"

  Shapes.AddLine(362,710,662,710)

  Shapes.AddLine(662,650,662,710)

  GraphicsWindow.BrushColor="#00FFFF"

  GraphicsWindow.DrawText(420,685,"Score:   "+score )

  GraphicsWindow.PenWidth=3

  

EndSub 





Sub OnKeyDown

  bill = GraphicsWindow.LastKey

  If bill="Up" Then

    If kesl>1 then 

      kesl=kesl-1

    endif

    infoscreen()

  EndIf

  

  If bill="Down" Then

    If kesl<5 then 

      kesl=kesl+1

    endif

    infoscreen()

  EndIf

  

  If bill="Left" Then

    If epsilon>0.001 then 

      epsilon=epsilon-0.0001

    endif

    infoscreen()

  EndIf

  If bill="Right" Then

    If epsilon<0.003 then 

      epsilon=epsilon+0.0001

    endif

    infoscreen()

  EndIf

  If bill="F1" Then

    epsilon=0.001

    infoscreen()

  EndIf

  If bill="F2" Then

    epsilon=0.002

    infoscreen()

  EndIf

  If bill="F3" Then

    epsilon=0.003

    infoscreen()

  EndIf

  If bill="Back" Then

    Startgame()

  EndIf

  

  If bill="Escape" Then

    Program.End()

  EndIf

EndSub

Sub OnMouseMove

  xm = GraphicsWindow.MouseX

  ym = GraphicsWindow.MouseY

  

EndSub  

Sub OnMouseDown

  if allapot=lokes then

    

  EndIf

EndSub

Sub OnMouseUp

  If allapot=lokes Then 

    allapot=mozgat

    vx[1]=-dakox/25

    vy[1]=-dakoy/25

    'kiírás törlése

    GraphicsWindow.FontSize=20

    GraphicsWindow.FontName="Arial"

    GraphicsWindow.FontBold="False"

    GraphicsWindow.BrushColor="#000050"

    GraphicsWindow.FillRectangle(362,650,300,60)

    GraphicsWindow.PenColor="#007F7F"

    GraphicsWindow.PenWidth=1

    Shapes.AddLine(362,650,662,650)

    Shapes.AddLine(362,650,362,710)

    GraphicsWindow.PenColor="Black"

    Shapes.AddLine(362,710,662,710)

    Shapes.AddLine(662,650,662,710)

    GraphicsWindow.BrushColor="#00FFFF"

    GraphicsWindow.DrawText(420,685,"Score:   "+score )

    GraphicsWindow.PenWidth=3

    fmstate=1 'végesautomata kezdőállásba

    sargatert=0 'a sarga érintését űjból állítjuk

    allapot=mozgat

  endif

  

EndSub



Sub visszapattan

  For i1=1 To N

    If x[i1]<balfal Then

      x[i1]=2*balfal-x[i1]

      vx[i1]=-vx[i1]

    EndIf

    If x[i1]>jobbfal Then

      x[i1]=2*jobbfal-x[i1]

      vx[i1]=-vx[i1]

    EndIf 

    If y[i1]>alsofal Then

      y[i1]=2*alsofal-y[i1]

      vy[i1]=-vy[i1]

    EndIf

    If y[i1]<felsofal Then

      y[i1]=2*felsofal-y[i1]

      vy[i1]=-vy[i1]

    EndIf

  EndFor 

EndSub  

Sub utkozes

  For i=1 to N-1

    xi=x[i]

    yi=y[i]

    vxi=vx[i]

    vyi=vy[i]      

    

    For j=i+1 to N

      xj=x[j]

      yj=y[j]

      vxj=vx[j]

      vyj=vy[j]

      dxe=xi-xj

      dye=yi-yj

      deij=Math.SquareRoot(dxe*dxe+dye*dye)

      

      xvi=xi+vxi*dt

      yvi=yi+vyi*dt

      xvj=xj+vxj*dt

      yvj=yj+vyj*dt

      dxv=xvi-xvj

      dyv=yvi-yvj

      dvij=Math.SquareRoot(dxv*dxv+dyv*dyv)

      vxrel=vxi-vxj

      vyrel=vyi-vyj

      

      If (dvij<=atmero) And (vxrel*dxe+vyrel*dye<0) Then     'ütközés van!

        nevezo=deij-dvij

        px=atmero-dvij

        py=deij-atmero

        

        t0=dt*py/nevezo 'az utkozes idopontja dt-n belül

        

        'Ütközéskori pontos koordináták:

        x0i=(px*xi+py*xvi)/nevezo

        y0i=(px*yi+py*yvi)/nevezo

        x0j=(px*xj+py*xvj)/nevezo

        y0j=(px*yj+py*yvj)/nevezo

        ' az ütközés utáni sebességek számítása

        dx=x0i-x0j

        dy=y0i-y0j

        'nhossz=Math.SquareRoot(dx*dx+dy*dy)

        'nx=dx/nhossz

        'ny=dy/nhossz

        nx=dx/atmero

        ny=dy/atmero

        tx=-ny

        ty=nx

        'sebességek a normálvektor mentén:

        vni=nx*vxi+ny*vyi

        vnj=nx*vxj+ny*vyj

        'sebességek az érintővektor mentén:

        vti=tx*vxi+ty*vyi

        vtj=tx*vxj+ty*vyj

        'normálvektor irányában sebességcsere van:

        ujvni=vnj

        ujvnj=vni

        'uj sebességek x és y összetevői:

        ujvxi=nx*ujvni+tx*vti

        ujvxj=nx*ujvnj+tx*vtj

        ujvyi=ny*ujvni+ty*vti

        ujvyj=ny*ujvnj+ty*vtj

        'A golyók helyzete az ütközés után

        'ujxi=xi+ujvxi*(dt-t0)

        'ujyi=yi+ujvyi*(dt-t0)

        'ujxj=xj+ujvxj*(dt-t0)

        'ujyj=yj+ujvyj*(dt-t0)

        'Az ujkoordinatak ugrast okoznak, nem frissitem

        'x[i]=ujxi

        'y[i]=ujyi

        'x[j]=ujxj

        'y[j]=ujyj

        vx[i]=ujvxi

        vy[i]=ujvyi

        vx[j]=ujvxj

        vy[j]=ujvyj 

        If j=2 OR i=2 then

          sargatert=1

        EndIf

        'a végesállapotú gép itt kezd működni

        If i=1 then

          golyohit=j

        EndIf

        If fmstate=1 then

          If golyohit=2 then 

            fmstate=0 

          endif

          If golyohit=3 then 

            fmstate=2 

          endif

          If golyohit=4 then 

            fmstate=3 

          endif 

        EndIf

        If fmstate=2 then

          If golyohit=2 then 

            fmstate=0 

          endif

          If golyohit=3 then 

            fmstate=2 

          endif

          If golyohit=4 then 

            fmstate=4 

          endif 

        EndIf

        If fmstate=3 then

          If golyohit=2 then 

            fmstate=0 

          endif

          If golyohit=3 then 

            fmstate=4 

          endif

          If golyohit=4 then 

            fmstate=3 

          endif 

        EndIf

        'ha eddigre fmstate=4 akkor érvényes poén van

        'ha fmstate nem 4  akkor hiba van

      EndIf

    EndFor

  EndFor

EndSub  

Sub infoscreen

  GraphicsWindow.FontSize=12

  GraphicsWindow.PenWidth=1

  

  GraphicsWindow.FontName="Arial"

  GraphicsWindow.FontBold="False"

  GraphicsWindow.BrushColor="#000050"

  GraphicsWindow.FillRectangle(700,650,424,150)

  GraphicsWindow.PenColor="#007F7F"

  Shapes.AddLine(700,650,1024,650)

  Shapes.AddLine(700,650,700,768)

  GraphicsWindow.BrushColor="#00FFFF"

  GraphicsWindow.DrawText(705,655,"Animation speed: KeyUp-KeyDown")

  GraphicsWindow.DrawText(705,675,"Friction coeff: KeyRight-KeyLeft")

  GraphicsWindow.DrawText(705,695,"Levels: F1=Beginner F2=Intermediate F3=Master")

  GraphicsWindow.DrawText(705,715,"Animation speed value: "+(6-kesl))

  GraphicsWindow.DrawText(705,735,"Friction coeff value: "+epsilon)

  GraphicsWindow.DrawText(705,755,"ESC=Quit      BackSpace=Reset")

  GraphicsWindow.PenWidth=3

  GraphicsWindow.PenColor="Black"

EndSub

Sub szabaly

  GraphicsWindow.FontSize=12

  GraphicsWindow.PenWidth=1

  GraphicsWindow.FontName="Arial"

  GraphicsWindow.FontBold="False"

  GraphicsWindow.BrushColor="#000050"

  GraphicsWindow.FillRectangle(0,650,324,150)

  GraphicsWindow.PenColor="#007F7F"

  Shapes.AddLine(0,650,324,650)

  Shapes.AddLine(324,650,324,768)

  GraphicsWindow.BrushColor="#FFFF00"

  GraphicsWindow.DrawText(120,655,"Game rules:")

  GraphicsWindow.DrawText(5,675,"If white ball hits the two red balls first and yellow ball ")

  GraphicsWindow.DrawText(5,695,"isn't touched, then you got 5000 points. If the yellow ball ")

  GraphicsWindow.DrawText(5,715,"is hit by any ball after the two red balls were hit by the ")

  GraphicsWindow.DrawText(2,735,"white one, then you got only 1000 points. In any other cases")

  GraphicsWindow.DrawText(5,755,"you loose 1000 points. Have a nice game!")

  GraphicsWindow.BrushColor="#000000"

  GraphicsWindow.PenWidth=3

  GraphicsWindow.PenColor="Black"

EndSub


