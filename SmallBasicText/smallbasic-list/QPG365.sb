GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp

'Simplified dummy setup for images walking left and right - this is just to see something - a game would use images for each player etc
player1X = 50 'Initial X (Left of player)
player1Y = GraphicsWindow.Height - 60 'Initial Y (Top of player)
dir = Program.Directory+"\sidestroller\"
For i = 1 To 3
  player1WalkLeft[i] = Shapes.AddImage(ImageList.LoadImage(dir+"walkL"+i+".png"))
  Shapes.HideShape(player1WalkLeft[i])
  
  player1WalkRight[i] = Shapes.AddImage(ImageList.LoadImage(dir+"walkR"+i+".png"))
  Shapes.HideShape(player1WalkRight[i])
EndFor
'Show initial player
player1Frame = 1 'First frame
player1 = player1WalkRight[player1Frame]
Shapes.Move(player1,player1X,player1Y)
Shapes.ShowShape(player1)

'Main Loop
While("True")
  start = Clock.ElapsedMilliseconds
  
  movePlayers()
  
  delay = 25 - (Clock.ElapsedMilliseconds-start) 'Max delay of 25ms is 40 fps
  If (delay > 0) Then
    Program.Delay(delay)
  EndIf
EndWhile

'Move players
'player1 is the current active shown image (there is only ever one not hidden at a time)
Sub movePlayers
  If (keyDown["Left"]) Then
    player1X = player1X-2 'Move player position left
    If (Math.Remainder(iFrame,8) = 0) Then 'Update player every 8 frames of movement
      Shapes.HideShape(player1) 'Hide current player image (will be last player image)
      player1Frame = 1+Math.Remainder(player1Frame,3) 'Cycle player image in group (these for walking left)
      player1 = player1WalkLeft[player1Frame] 'The next player image in group is now the current player image
      Shapes.Move(player1,player1X,player1Y) 'Move the image before we show it
      Shapes.ShowShape(player1) 'Show current player image
    Else
      Shapes.Move(player1,player1X,player1Y) 'Just move player
    EndIf
    iFrame = iFrame+1
  ElseIf (keyDown["Right"]) Then
    player1X = player1X+2
    If (Math.Remainder(iFrame,8) = 0) Then
      Shapes.HideShape(player1)
      player1Frame = 1+Math.Remainder(player1Frame,3)
      player1 = player1WalkRight[player1Frame]
      Shapes.Move(player1,player1X,player1Y) 'Move the image before we show it
      Shapes.ShowShape(player1) 'Show current player image
    Else
      Shapes.Move(player1,player1X,player1Y)
    EndIf
    iFrame = iFrame+1
  Else
    iFrame = 0
  EndIf
EndSub

'Register events
Sub OnKeyDown
  key = GraphicsWindow.LastKey
  keyDown[key] = "True"
EndSub

Sub OnKeyUp
  key = GraphicsWindow.LastKey
  keyDown[key] = ""
EndSub
