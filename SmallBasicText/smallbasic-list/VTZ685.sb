' SmallBasic Version 1.2
' Program: BaseConverterGUI
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/dc6ef888-5d4c-4806-a58b-51248d6c4ee4/convert-between-decimal-and-hexatrigesimal-numbers-base10-and-base36
' ImportURL: http://smallbasic.com/program/?    (MJF819)
' Extension: LitDev
' Comment: https://stackoverflow.com/questions/34574203/c-sharp-base-converter
'  https://stackoverflow.com/questions/923771/quickest-way-to-convert-a-base-10-number-to-any-base-in-net
' Variables: 
' ToDo: add tb's & cb's as [] in loops
'  gw, gh  relativ controlPos
' ================================================================================
upperCase = "True"
bOnTop = "True"

For n = 2 To 36
  arrBase[n] = n
EndFor

BuildGUI()

Controls.ButtonClicked = OnButtonClick
GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.MouseDown = OnMouseDown


' //////////  SUBs  \\\\\\\\\\
Sub BuildGUI
  GraphicsWindow.CanResize = ""
  GraphicsWindow.Width = 175
  GraphicsWindow.Height = 110
  GraphicsWindow.BackgroundColor = "LightCyan"
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.FontSize = 11
  GraphicsWindow.FontBold = ""
  GraphicsWindow.Title = "BaseConverter"
  'LDGraphicsWindow.Icon = Program.Directory +"\img\BaseConverter.png"
  LDGraphicsWindow.TopMost = bOnTop
  
  tbIn = Controls.AddTextBox(5,5)
  Controls.SetSize(tbIn, 165,21)
  
  tbOut = Controls.AddTextBox(5,80)
  LDControls.TextBoxReadOnly(tbOut, "True")
  LDShapes.BrushColour(tbOut, "LightSalmon")
  Controls.SetSize(tbOut, 165,21)
  
  GraphicsWindow.DrawText(5,33, "Base  From")
  cbFrom = LDControls.AddComboBox(arrBase, 40,65)
  LDControls.ComboBoxSelect(cbFrom, 10)
  Controls.Move(cbFrom, 65,30)
  
  GraphicsWindow.DrawText(115,33, "To")
  cbTo = LDControls.AddComboBox(arrBase, 40,65)
  LDControls.ComboBoxSelect(cbTo, 10)
  Controls.Move(cbTo, 130,30)
  
  btn = Controls.AddButton("Convert", 5,50)
  btnW = LDShapes.Width(btn)
  btnClr = Controls.AddButton("Clear", 170-btnW,50)
  Controls.SetSize(btnClr, btnW,25)
  
  '' http://www.fileformat.info/info/unicode/char/1f4cc/index.htm
  'pin = "ðŸ“Œ"  ' PUSHPIN, U+1F4CC, 128204, UTF-16 (hex) 	0xD83D 0xDCCC (d83ddccc)
  shpPin = Shapes.AddText("ðŸ“Œ")
  Shapes.Move(shpPin, 85,55)
  LDShapes.PenColour(shpPin, LDLogic.Switch(LDGraphicsWindow.TopMost, "Gray", "Transparent"))
EndSub

Sub ConvertDecToBase  ' (val, base) -> res
  chars = "0123456789abcdefghijklmnopqrstuvwxyz"
  If upperCase Then
    chars = Text.ConvertToUpperCase(chars)
  EndIf
  
  If val < base Then
    res = Text.GetSubText(chars, val+1,1)
  Else
    While val <> 0
      id = Math.Remainder(val, base)
      res = Text.Append(Text.GetSubText(chars, id+1,1), res)
      val = Math.Floor(val / base)
    EndWhile
  EndIf
EndSub

Sub ConvertBaseToDec  ' (val, base) -> res
  chars = "0123456789abcdefghijklmnopqrstuvwxyz"  ' val AND chars, SAME upper-/lowerCase
  val = Text.ConvertToLowerCase(val)
  res = 0
  
  len = Text.GetLength(val)
  For pow = len-1 To 0 Step -1
    ch = Text.GetSubText(val, len-pow,1)
    GetCharIndex()  ' (chars, ch) -> id [0,35]
    res = res + id * Math.Power(base, pow)
  EndFor
EndSub

Sub GetCharIndex  ' (chars, ch) -> id [0,35]
  id = 0
  While Text.GetSubText(chars, id+1,1) <> ch
    id = id + 1
  EndWhile
EndSub

Sub Reset
  val = ""
  res = ""
  LDControls.ComboBoxSelect(cbFrom, 10)
  LDControls.ComboBoxSelect(cbTo, 10)
  Controls.SetTextBoxText(tbIn, "")
  Controls.SetTextBoxText(tbOut, "")
EndSub


' //////////  EVENTs  \\\\\\\\\\
Sub OnButtonClick
  lastBtn = Controls.LastClickedButton
  
  If lastBtn = btnClr Then
    Reset()
  Else
    val = Controls.GetTextBoxText(tbIn)
    If val <> "" Then
      baseFrom = LDControls.ComboBoxGetSelected(cbFrom)
      baseTo = LDControls.ComboBoxGetSelected(cbTo)
      
      If baseFrom <> 10 Then
        base = baseFrom
        ConvertBaseToDec()    ' (val, base) -> res
        val = res
        res = ""
      EndIf
      
      base = baseTo
      ConvertDecToBase()    ' (val, base) -> res
      Controls.SetTextBoxText(tbOut, res)
      res = ""
    EndIf
  EndIf
EndSub

Sub OnMouseDown
  If Mouse.IsRightButtonDown Then
    LDGraphicsWindow.TopMost = LDLogic.Not(LDGraphicsWindow.TopMost)
    LDShapes.PenColour(shpPin, LDLogic.Switch(LDGraphicsWindow.TopMost, "Gray", "Transparent"))
  EndIf
EndSub

Sub OnKeyDown
  If GraphicsWindow.LastKey = "Escape" Then
    Program.End()
  EndIf
EndSub
