DEBUG()

Initialise()



'register B/ground Thread

Timer.Interval = 100

Timer.Tick = DownloadThread    'Ticks once



Buffering()



startQuickPic = Clock.ElapsedMilliseconds  'TODO: init on a button click



'============================================================

'Event Loop

'============================================================

While runEventLoop  

  start = Clock.ElapsedMilliseconds

  

  UpdateProgram()

  UpdateFrame()

  

  delay = 20 - (Clock.ElapsedMilliseconds - start)

  If delay > 0 Then    

    Program.Delay(delay)

  EndIf

EndWhile

Program.End()



'=============================================================

'1st Level Subroutines

'=============================================================

Sub UpdateProgram  

  If start - startQuickPic > 495 Then      'program event       

    GetTotalPicsLoaded()    

    If slideIndex < totalPicsLoaded Then

      slideIndex = slideIndex + 1        'indexing the Slides

      show = slideIndex

    Else

      j = Math.GetRandomNumber(slideIndex)

      While j = show                    'don't show same pick twice in a row

        j = Math.GetRandomNumber(slideIndex)

      EndWhile

      show = j

    EndIf    

    startQuickPic = Clock.ElapsedMilliseconds

    drawQuickPic = "True"

  EndIf 

EndSub



Sub UpdateFrame

  If drawQuickPic Then

    inUse = "True"

    Program.Delay(5)  'allows variables already inUse to finish 

    GraphicsWindow.DrawResizedImage(pic[show], 0, 0, gw, gh)

    GraphicsWindow.DrawText(10, 10, show)

    TextWindow.WriteLine("draw " + show)   'doesn't draw all the pics???

    inUse = "False"

    drawQuickPic = "False"

  EndIf 

EndSub 



Sub Buffering

  While buffer > totalPicsLoaded

    GetTotalPicsLoaded()

  EndWhile

EndSub



'=============================================================

'2nd Level Subroutines

'=============================================================

Sub GetTotalPicsLoaded

  inUse = "True"

  Program.Delay(5)   'allows variables already inUse to finish

  totalPicsLoaded = picIndex  'isolate var from Timer Thread

  inUse = "False"

EndSub



'============================================================

'Timer Thread Subroutines

'============================================================

Sub DownloadThread

  Timer.Pause()

  

  While picIndex < 20

    TextWindow.WriteLine("d/loading")

    loadPic = ImageList.LoadImage(Flickr.GetRandomPicture("birds"))    

    

    While inUse    'wait till Event Loop is not using Pic & picIndex

    EndWhile

    

    picIndex = picIndex + 1

    pic[picIndex] = loadPic 

  EndWhile

EndSub



'============================================================

'Initialise & Debug Window

'============================================================

Sub Initialise

  GraphicsWindow.Hide()

  GraphicsWindow.Title = "Flickr Picture Viewer"

  

  'bug. Put set Resize before setting gwWidth. Else DrawResizedImage is not full window

  GraphicsWindow.CanResize = "False"  

  gw = 800

  gh = 580

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh   

  

  GraphicsWindow.Top = 10

  GraphicsWindow.BackgroundColor = "Black"  

  GraphicsWindow.FontSize = 18

  GraphicsWindow.BrushColor = "lime"  

  

  runEventLoop = "True"

  buffer = 5

  totalPicsLoaded = 0

  slideIndex = 0

  

  GraphicsWindow.Show()

  ' put Left after GW.Show().  Because uses  Desktop.Width

  GraphicsWindow.Left = (Desktop.Width - GraphicsWindow.Width) / 2

EndSub



Sub DEBUG

  TextWindow.Title = "DEBUG"

  TextWindow.Left = 10

  TextWindow.Top = 20

EndSub

  


