'factorial calculation   by NaochanON    FZQ904-1
'  far faster version
'  modified by Nonki Takahashi

t0=Clock.ElapsedMilliseconds
debug="false"
DIGITS=25
z="0000000000000000000000000"
div=Math.Power(10,DIGITS)
start=1000    '   1000!    
A=start

For N=2 to A-1
  s=""
  up=0
  L=Text.GetLength(start)
  m=Math.Ceiling(L/DIGITS)
  j=L-DIGITS+1
  len=DIGITS
  If j<1 Then
    j=1
    len=L
  EndIf
  For i=1 To m
    mid=Text.GetSubText(start,j,len)*N+up
    nz=len-Text.GetLength(mid)
    If nz>0 Then
      mid=Text.Append(Text.GetSubText(z,1,nz),mid)
    EndIf
    nup=Text.GetLength(mid)-DIGITS
    If nup<0 Then
      rem=mid
      up=0
    Else
      rem=Text.GetSubText(mid,nup+1,DIGITS)
      up=Text.GetSubText(mid,1,nup)
    EndIf
    s=Text.Append(rem,s)
    j=j-DIGITS
    If j<1 Then
      j=1
      len=Math.Remainder(L,DIGITS)
    EndIf
  EndFor
  If up>0 Then
    nz=DIGITS*(m-1)-Text.GetLength(s)
    If nz>0 Then
      s=Text.Append(Text.GetSubText(z,1,nz),s)
    EndIf
    start=Text.Append(up,s)
  Else
    start=s
  EndIf
EndFor

TextWindow.WriteLine("Result= "+start)
TextWindow.WriteLine((Clock.ElapsedMilliseconds-t0)+" mSec")
