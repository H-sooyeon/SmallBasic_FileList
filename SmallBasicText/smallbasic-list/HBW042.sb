'  This program is made by Amir originally .  
'  Background photo is absolu's.  (Mp3 Player Slider Position update issue. partially cropped.)
'  


DataExtension.Init()    '  this is not necessary          
init()                  '  init view
Control_button()        '  control button setting
Add_List()              ' write music(movie) dummy list in the ListBox
Open_Files()            '  gets music movie files
GameMouse.MouseDown=ClickandPlay

Player = Media.CreateMediaPlayer(Files_Array[Song_Id],40,60,680,360) '  Open first  file song_id=1

While "True"
    Media_Position= (Media.GetPosition(Player)*100)/Media.GetLength(Player) ' ##.# %
    GameControls.SetValue(Seek_Bar,Media_Position/10)                       ' #.## %
    Min = Math.Floor(Media.GetPosition(Player)/1000/60)                     ' ##  min
    Secs = Math.Floor(Math.Remainder(Media.GetPosition(Player)/1000,60))    ' ## sec
    GameControls.SetText(Time_Box,Min+"m "+Secs+"s")
    If Media_Position>=99.9 then                               '  End point  
       Play_Next() 
    EndIf
endwhile


Sub Open_Files
  MM=0                                               '  initialize
  Dir = Dialogs.AskForDirectory()                    ' Select a  music(movie) directory
  reset_list()                                        '  reset list
  RawFiles_Array = FilePlus.GetFiles(Dir)   '  gets visible files   &   RawFilesArray =  Original Array
  Total_Files = Array.GetItemCount(RawFiles_Array)    '  array number
  For i=1 To Total_Files
    CurrentExt=FilePlus.GetFileExtension(RawFiles_Array[i])   '  gets  extension name
    If  text.IsSubText(Musicext,text.ConvertToLowerCase(CurrentExt))="True" then   ' compare  if music( movie)  file exists
        MM=MM+1                                    
        Files_Array[MM]=RawFiles_Array[i]
    EndIf
  EndFor
  Song_Id=1                                     '  First file To Play
  PreListNo=1
  Scroll()                                      '  set  music(movie) list
  Stop_Song()                                   '  stop 
  SetVolume()                                   ' set volume
  Folder_name()                                 ' set folder+movie(song) name
EndSub

Sub Folder_name
 GameControls.SetText(Folder,Files_Array[Song_Id]) '  sets  name
 GameWindow.Title = "Small Music Player - By Amir   "+ Files_Array[Song_Id]  ' title
EndSub

Sub reset_list
  If Total_Files=0 Then 
    Total_Files=LN
  EndIf
  For i=1 To Total_Files
    Files_Array[i]=" "                           '  reset 
  EndFor
EndSub

Sub Play_Song
  If GameControls.GetText(Play_Button)="〓" Then 　　  '  pause  icon
    Media.Pause(Player)
    GameControls.SetText(Play_Button,"▲")              '  play icon 
  elseif GameControls.GetText(Play_Button)="▲" Then 
   Media.Resume(Player)
   GameControls.SetText(Play_Button,"〓")
  EndIf
EndSub

Sub ClickandPlay
  If 645<GameMouse.MouseX and GameMouse.MouseX<945 Then 
  ListNo= Math.Round((GameMouse.MouseY-T_LST)/HLN+0.5)   ' T_LST=110  450/LN  LN:25
  getnamefile= Files_Array[ListNo+NN]
  GameControls.SetFont(Song_name[PreListNo],"arial",FLN,"Black","False","true","Left")
  GameControls.SetFont(Song_name[ListNo],"arial",FLN,"red","False","true","Left")
  Media.SetMedia(Player,getnamefile) 
  Media.Play(player)
  Song_Id=ListNo+NN
  PreListNo=ListNo
  Folder_name() 
EndIf
EndSub

Sub Play_Next
   Media.Stop(Player)
   Song_Id = Song_Id + 1
   If Song_Id > MM Then
      Song_Id = MM
   EndIf
   Media.SetMedia(Player, Files_Array[Song_Id])   '  Song_ID is  already +1 
   Scroll() 
   GameControls.SetValue(Seek_Bar,0)              '  progress bar =0
   Media.Play(Player)
   Folder_name() 
   GameControls.SetText(Play_Button,"〓")
EndSub

Sub Play_Prev
   Media.stop(Player)
   Song_Id = Song_Id - 1
   If Song_Id < 1 Then
     Song_Id = 1
   EndIf
   Media.SetMedia(Player, Files_Array[Song_Id])   '  Song_ID is  already -1
   Scroll() 
   GameControls.SetValue(Seek_Bar,0)              '  progress bar =0
   GameControls.SetValue(Seek_Bar,0)
   Media.Play(Player)
   Folder_name() 
   GameControls.SetText(Play_Button,"〓")
endSub 

Sub Stop_Song
   Media.stop(Player)
   GameControls.SetText(Play_Button,"▲")
EndSub

Sub Volume_Set
   Vol_Value = GameControls.GetValue(Volume_Control_Bar)  '  gets current volume value
   Media.SetVolume(Player,Vol_Value/100)                 ' set
EndSub

Sub Show
  If GameControls.GetText(Show_PlayList)="<<" Then      '   close
   For i = 1200 To 645 Step -5
    GameWindow.Width = i
   EndFor
   GameControls.SetText(Show_PlayList,">>")
  else 
   For i = 645 To 1200 Step 5                           '  open 
    GameWindow.Width = i
   EndFor
   GameControls.SetText(show_PlayList,"<<")
   EndIf
EndSub

Sub Slider_Move
   SX= gamemouse.MouseX
   SN=math.Round((Sx-70)/450*1000)/10  ' gets current slider position
   Slider_Time_Position =(SN*Media.GetLength(Player))/100    '  position =  msec 
   Media.SetPosition(Player,Slider_Time_Position)            '  set  player
EndSub

Sub SetVolume
   Media.SetVolume(Player,50)                         
   GameControls.SetValue(Volume_Control_Bar,50/10)
EndSub

Sub Add_List   '   make dummy list  (space) 
  For i=1 To LN
      Song_name[i]= GameControls.addtext("  ",735,T_LST+HLN*(i-1),"arial",FLN,"False","true")
      GameControls.SetFont(Song_name[i],"arial",FLN,"black","False","true","Left")
  EndFor 
  GameControls.SetFont(Song_name[1],"arial",FLN,"red","False","true","Left") 
  PreListNo=1
EndSub

Sub Scroll
  NN=math.Round(GameControls.GetValue(Scroller)*Total_Files/100+0.5)
  For i=1 To LN
     S_name= FilePlus.GetFileName(Files_Array[i+NN])+FilePlus.GetFileExtension(Files_Array[i+NN])  
     If i+NN>MM Then 
       S_name=" "
     EndIf
     GameControls.SetText(Song_name[i],S_name)    '  sets  music(movie) name + extension 
     If Song_Id=i+NN Then 
       GameControls.SetFont(Song_name[i],"arial",FLN,"red","False","true","Left") '  current 
     Else
       GameControls.SetFont(Song_name[i],"arial",FLN,"black","False","true","Left")
     endif  
  EndFor
EndSub

Sub get_movie   '  Chk1 
  Musicext=".mp4:.avi:.wmv:.mpg:.mov" 
  Open_Files()
  GameControls.SetVisibility(chk1,"False")
  GameControls.SetVisibility(chk2,"True")
  GameControls.SetVisibility(chk3,"True")
EndSub

Sub get_song    'chk2
  Musicext=".mp3:.wma:.wav:.mid" 
  Open_Files()
  GameControls.SetVisibility(chk1,"True")
  GameControls.SetVisibility(chk2,"False")
  GameControls.SetVisibility(chk3,"True")
EndSub

Sub get_photo    'chk3
  Musicext=".jpg:.png:.gif" 
  Open_Files()
  GameControls.SetVisibility(chk1,"True")
  GameControls.SetVisibility(chk2,"True")
  GameControls.SetVisibility(chk3,"false")
EndSub

Sub init
  url= Flickr.GetRandomPicture("Flower")  'program.Directory+"\absolu amir back.jpg"    '  back view  partially cropped.
  back=ImageList.LoadImage(url)
  GameWindow.Width =1220 
  GameWindow.Height = 680
  GameWindow.CanResize = "True"
  GameWindow.Theme = "XPsilver" '"ExpressionLight"
  Gback=GameControls.AddImage(back,0,0)
  GameControls.SetSize(Gback,1200,645)
  GameWindow.Show()
EndSub

Sub Control_button
  LN=25        '  ListNo
  HLN=450/LN   '  height of list
  FLN=math.floor(HLN)-1    '  fontsize 
  T_LST=110     ' List top position
  Musicext=".mp4:.avi:.wmv:.mpg:.mov"  '  movie   file extensions
  Scroller = GameControls.AddSlider(0,1200-410,325,460,25)   '  Scroller X position is changed by Rotation 
  GameControls.Rotate(Scroller,90)
  Seek_Bar = GameControls.AddSlider(0,170,445,350,22)          '  Slider for progress 
  Time_Box = GameControls.AddTextBox("Text",620,446,58,25)     '  time box
  Volume_Control_Bar = GameControls.AddSlider(0,530,485,75,25) '  volume bar 
  Show_PlayList = GameControls.AddButton("<<",640,485,36,30)   '  Show list mark
  chk1= GameControls.AddCheckBox("Movie",130,498)
  chk2= GameControls.AddCheckBox("Sound",130,513)
  chk3= GameControls.AddCheckBox("Photo",130,528)
  
  Stop_Button = GameControls.AddButton("■",325,500,30,30)    '  Stop button
  Prev_Button = GameControls.AddButton("－",235,500,30,30)    '  Previous button
  Play_Button = GameControls.AddButton("▲",280,500,30,30)    '  Play button  
  GameControls.Rotate(Play_Button,90)
  Next_Button = GameControls.AddButton("＋",365,500,30,30)    '  ＋Next button
  PrgEnd= GameControls.addButton("X",36,12,28,28)             '  Program end button
  Folder= GameControls.AddTextBox("folder",170,550,425,30)    ' TxtBox for Folder name  
  
  GameControls.RegisterMouseDownEvent(Play_Button,"Play_Song")
  GameControls.RegisterMouseDownEvent(Next_Button,"Play_Next")
  GameControls.RegisterMouseDownEvent(Prev_Button,"Play_Prev")
  GameControls.RegisterMouseDownEvent(Stop_Button,"Stop_Song")
  GameControls.RegisterValueChangedEvent(Scroller,"Scroll")
  GameControls.RegisterMousemoveEvent(Seek_Bar,"Slider_Move")
  GameControls.RegisterValueChangedEvent(Volume_Control_Bar,"Volume_Set")
  GameControls.RegisterMouseDownEvent(Show_PlayList,"Show")
  GameControls.RegisterMouseDownEvent(PrgEnd,"PRG_END")
  GameControls.RegisterMouseDownEvent(chk1,"Get_Movie")
  GameControls.RegisterMouseDownEvent(chk2,"Get_Song")
  GameControls.RegisterMouseDownEvent(chk3,"Get_Photo")
EndSub
  
Sub PRG_END 
  Program.End()  
EndSub
  
  
