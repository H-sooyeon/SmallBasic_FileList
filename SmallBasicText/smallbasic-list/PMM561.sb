' Compare the Crab Nebula expansion from years 1999 to 2012
'
' Use the following keys to try to match up the two images, then press spacebar to animate them:
'   arrow keys to move up/down/left/right
'   A and D keys to rotate
'   W and S keys to zoom
'
' More information can be seen here: http://apod.nasa.gov/apod/ap130905.html
' Hint, once you line everything up to match the images, look at the outer edges for expansion as compared
' to the background stars.  
'
' Since it's 10 light years across, how fast do you think it's expanding?
'

version = "version 1.0 - 9/12/2012 - Daddyo"

true = 1
false = 0

' Locally saved images or on the internet?
localResources = false

GraphicsWindow.Show()
GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.Width = Desktop.Width - 100
GraphicsWindow.Height = Desktop.Height - 100
 
centerX = GraphicsWindow.Width / 2
centerY = GraphicsWindow.Height / 2
 
GraphicsWindow.Left = Desktop.Width / 2 - centerX
GraphicsWindow.Top = 10

' Key controls
leftKey  = "Left"
rightKey = "Right"
upKey = "Up"
downKey = "Down"
quitKey = "Escape"
leftKeyPressed = false
rightKeyPressed = false
upKeyPressed = false
downKeyPressed = false
ccwKeyPressed = false
cwKeyPressed = false
zoomInKeyPressed = false
zoomOutKeyPressed = false

' Variables
play = true
afterImageX = 0
afterImageY = 0
ANGLE_INCREMENT = 0.1
ZOOM_INCREMENT = 1.003
zoom = 1.0
angle = 0
animate = false

' Load images
If localResources = true Then
  resourcePath = Program.Directory + "\"
EndIf
 
 If localResources = false Then
  GraphicsWindow.Title = "LOADING IMAGES FROM THE INTERNET, PLEASE WAIT..."
  resourcePath = "http://www.eso.org/public/archives/images/screen/"
EndIf
 
beforeImage = Shapes.AddImage(resourcePath + "eso9948f.jpg")
 
Shapes.Zoom(beforeImage, 0.5, 0.5)
Shapes.Move(beforeImage, -150, -150)
 
If localResources = false Then
  resourcePath = "http://skycenter.arizona.edu/sites/skycenter.arizona.edu/files/imagecache/temp_gallery/"
EndIf
 
afterImage = Shapes.AddImage(resourcePath + "m1_2012s.jpg")
Shapes.Move(afterImage, afterImageX, afterImageY)

GraphicsWindow.Title = "Images Loaded"

' Let 50% of the older image come through the new image
opacity = 50
tLast = Clock.Millisecond
 
' Enable keyboard press events
GraphicsWindow.KeyDown = OnKeyDown
GraphicsWindow.KeyUp = OnKeyUp

' Main loop 
While play = true
  tNow = Clock.Millisecond
  dt = tNow - tLast
  If dt < 0 Then
    dt = dt + 1000
  EndIf

  ' Every 100 milliseconds update the screen   
  If dt > 100 Then
    tLast = tNow
    
    ' Cycle opacity of new image
    If animate = true Then
      opacity = opacity + 2
      If opacity > 100 Then
        opacity = 0
      EndIf
    EndIf
    
    ' Handle key presses
    If leftKeyPressed = true Then
      afterImageX = afterImageX - 1
    EndIf
     
    If rightKeyPressed = true Then
      afterImageX = afterImageX + 1
    EndIf
     
    If upKeyPressed = true Then
      afterImageY = afterImageY - 1
    EndIf
     
    If downKeyPressed = true Then
      afterImageY = afterImageY + 1
    EndIf
   
    If ccwKeyPressed = true Then
      angle = angle - ANGLE_INCREMENT 
    EndIf
   
    If cwKeyPressed = true Then
      angle = angle + ANGLE_INCREMENT 
    EndIf
 
    If zoomInKeyPressed = true Then
      zoom = zoom * ZOOM_INCREMENT
    EndIf
   
    If zoomOutKeyPressed = true Then
      zoom = zoom / ZOOM_INCREMENT
    EndIf
    
    ' Update image 
    Shapes.Move(afterImage, afterImageX, afterImageY)
    Shapes.Rotate(afterImage, angle)
    Shapes.Zoom(afterImage, zoom, zoom)
    Shapes.SetOpacity(afterImage, opacity)
 
    ' Show the user's image manipulation values
    GraphicsWindow.Title = "X=" + afterImageX +  ", Y=" + afterImageY + ", Angle=" + angle +  ", Zoom=" + zoom
 
  EndIf   ' End if time to update screen
EndWhile  ' End main loop
 
' Exit program
Program.End()
 
' Determine if keyboard pressed
Sub OnKeyDown
  If(GraphicsWindow.LastKey = rightKey) Then
    rightKeyPressed = true
  endif

  If(GraphicsWindow.LastKey = leftKey) Then
    leftKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = upKey) Then
    upKeyPressed = true
  EndIf
    
  If(GraphicsWindow.LastKey = downKey) Then
    downKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = "A") Then
    ccwKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = "D") Then
    cwKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = "W") Then
    zoomInKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = "S") Then
    zoomOutKeyPressed = true
  EndIf
  
  If(GraphicsWindow.LastKey = "Space") Then
    If animate = true Then
      animate = false
    Else
      animate = true
    EndIf
  EndIf
  
  If(GraphicsWindow.LastKey = quitKey) Then
    play = false
  EndIf
 
  ' TextWindow.WriteLine(GraphicsWindow.LastKey)

EndSub
 
' Determine if keyboard released
Sub OnKeyUp
  if GraphicsWindow.LastKey = rightKey Then
    rightKeyPressed = false
  EndIf
  
  if GraphicsWindow.LastKey = leftKey Then
    leftKeyPressed = false
  EndIf
 
  if GraphicsWindow.LastKey = upKey Then
    upKeyPressed = false
  EndIf
  
  if GraphicsWindow.LastKey = downKey Then
    downKeyPressed = false
  EndIf
  
  If(GraphicsWindow.LastKey = "A") Then
    ccwKeyPressed = false
  EndIf
  
  If(GraphicsWindow.LastKey = "D") Then
    cwKeyPressed = false
  EndIf

  If(GraphicsWindow.LastKey = "W") Then
    zoomInKeyPressed = false
  EndIf
  
  If(GraphicsWindow.LastKey = "S") Then
    zoomOutKeyPressed = false
  EndIf
EndSub
