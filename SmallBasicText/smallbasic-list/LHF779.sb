
GraphicsWindow.KeyDown = OnKeyDown

GameOver = "False"

Alien["Size"] = 50
ResetAlien()

'setup player
GraphicsWindow.BrushColor = "Green"
Player["X"] = GraphicsWindow.Width / 2
Player["Y"] = GraphicsWindow.Height - 100
Player["Size"] = 50
Player["Speed"] = 2
Player1 = Shapes.AddTriangle(Player["Size"] / 2, 0, 0,  Player["Size"],  Player["Size"],  Player["Size"])
Shapes.Move(Player1, Player["X"], Player["Y"])

'setup bullet
Bullet["X"] = 0
Bullet["Y"] = 0
Bullet["Size"] = 5
Bullet["Speed"] = 5
Bullet1 = Shapes.AddRectangle(Bullet["Size"], Bullet["Size"] * 2)
Shapes.Move(Bullet1, -50, -50)

'main game loop
While (Input <> "Escape" And GameOver <> "True")
  If (BulletAlive = "True") Then
    MoveBullet()
  EndIf
  
  MoveAlien()
  
  Program.Delay(20)
EndWhile

GraphicsWindow.ShowMessage("GoodBye", "")
Program.End()

'--------------------------------------------------
Sub ResetAlien
  GraphicsWindow.BrushColor = "Gray"
  Alien["X"] = 50
  Alien["Y"] = 20
  Alien["Size"] = Alien["Size"] * 0.9 ' 90 percent of original
  If (Alien["Size"] < 10) Then
    Alien["Size"] = 10
  EndIf
  
  'remove old
  Shapes.Remove(Alien1)
  'make a UFO shape
  Alien1 = Shapes.AddEllipse(Alien["Size"], Alien["Size"] / 3)
  Shapes.Move(Alien1, Alien["X"], Alien["Y"])
  
  Alien["Direction"] = 2
  
EndSub

'--------------------------------------------------
Sub MoveAlien
  
  Alien["X"] = Alien["X"] + Alien["Direction"]
  
  'add a jerky effect to UFO
  'Alien["Y"] = Alien["Y"] + Math.GetRandomNumber(4) - 2
  
  
  'if alien at either end of the screen, reverse direction and move closer to bottom
  If (Alien["X"] < 0 Or Alien["X"] > (GraphicsWindow.Width - Alien["Size"])) Then
    Alien["Y"] = Alien["Y"] + Alien["Size"]
    Alien["Direction"] = Alien["Direction"] * -1
    If (Alien["Y"] > GraphicsWindow.Height - 100) Then
      GameOver = "True"
    EndIf
    
  EndIf
  Shapes.Move(Alien1, Alien["X"], Alien["Y"])
  
EndSub

'--------------------------------------------------
Sub MoveBullet
  
  Bullet["Y"] = Bullet["Y"] - Bullet["Speed"]
  If (Bullet["Y"] <= Bullet["Size"]) Then
    BulletAlive = "False"
    Bullet["X"] = -50
  EndIf
  
  If (BulletAlive = "True") Then
    Shapes.Move(Bullet1, Bullet["X"], Bullet["Y"])
  EndIf
  
  'if the bullet is too low or too high, don't bother checking for a collision
  If (Bullet["Y"] > Alien["Y"] And Bullet["Y"] < Alien["Y"] + Alien["Size"]) Then
    If (Bullet["X"] >= Alien["X"] And Bullet["X"] <= Alien["X"] + Alien["Size"]) Then
      'collision detected
      'move bullet out of view
      Bullet["X"] = -50
      Shapes.Move(Bullet1, Bullet["X"], 50)
      ResetAlien()
    EndIf
  EndIf
  
EndSub


'--------------------------------------------------
Sub OnKeyDown
  Input = GraphicsWindow.LastKey
  
  If (Input = "Space") Then
    BulletAlive = "True"
    Bullet["X"] = Player["X"]
    Bullet["Y"] = Player["Y"]
    
    
  ElseIf (Input = "Left") Then
    If (OldInput = "Left") Then
      Player["Speed"] = Player["Speed"] + 2
    Else
      Player["Speed"] = 2
    EndIf
    If (Player["X"] - Player["Speed"] > 0) Then
      Player["X"] = Player["X"] - Player["Speed"]
      Shapes.Move(Player1, Player["X"], Player["Y"])
    EndIf
  ElseIf (Input = "Right") Then
    If (OldInput = "Right") Then
      Player["Speed"] = Player["Speed"] + 2
    Else
      Player["Speed"] = 2
    EndIf
    
    If (Player["X"] + Player["Speed"] < GraphicsWindow.Width - Player["Size"] / 2) Then
      Player["X"] = Player["X"] + Player["Speed"]
      Shapes.Move(Player1, Player["X"], Player["Y"])
    EndIf
    
  EndIf
  
  OldInput = Input
EndSub

