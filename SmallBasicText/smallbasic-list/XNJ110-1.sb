'Egg Catcher Game V.5.1



'You can use Keyboard or mouse

MorK = "K" 'Use Mouse = "M" , Use Keyboard = "K"



'Update in line 248 , Now you can't use keyboard when you are using mouse.



Main()



Sub Main

  'Creates Game

  CreateGame()  

  Sound.PlayChimes()

  'MainLoop

  While (Level < LevelTarget)

    'Update Level

    Level = Level + 1

    Scores = 0

    NextLevelTarget = NextLevelTarget + Level

    DrawPanel()

    DefaultSpeed = DefaultSpeed + "0.1"      

    While (CatchedEggs < NextLevelTarget)      

      Shapes.Move(dropper, dropperX, 80)  

      Program.Delay(10)  

      'erinevalt viskab pallikesi, peaks vÃ¤hemalt        

      If (Math.GetRandomNumber(100) > 98) Then          

        Stack.PushValue("p", dropperX + 30)  

        DropEgg()        

      EndIf      

      MoveBucketUsingKeyboard()

      SetIncrement()        

      dropperX = dropperX + incrementX  

      'All Loops Breaks and GameOver = "true" if Lives = 0 or Lives < 0 

      If Lives = 0 Or Lives < 0 Then

        Sound.PlayChimes()

        GraphicsWindow.Hide()

        GraphicsWindow.ShowMessage("Your current level is "+Level+" and your scores is "+Scores+".","Game Over")

        GraphicsWindow.Hide()

        Program.End() 

      EndIf

    EndWhile

  EndWhile

  Sound.PlayChimes()

  GraphicsWindow.Hide()

  GraphicsWindow.ShowMessage("You have completed the Game!!!","Well Done")

  GraphicsWindow.Hide()

  Program.End()

EndSub



Sub CreateGame

  CreateWindow()

  CreateCatcher()

  CreateDropper()

  Scores = 0

  Lives = 5

  Level = 0

  NextLevelTarget = 9

  LevelTarget = 100

  DefaultSpeed = 20 

  CatchedEggs = 0

  KeyMovement = 16.5

  MovingRight = "false"

  MovingLeft = "false"

  Shapes.Move(dropper, 1, 80)

  Shapes.Move(bucket,1, GraphicsWindow.Height - 20)

  droppedBalls = 0

  dropperX = 1  

  Timer.Interval = 1000

  Timer.Tick = TimeUp  

  Seconds = -1

  Minutes = 0

  Hours = 0

  Days = 0  

  TimeUp()

  DrawPanel()

EndSub



Sub CreateWindow

  'Creates GraphicsWindow

  GraphicsWindow.Show()

  GraphicsWindow.BackgroundColor = "WhiteSmoke"

  GraphicsWindow.Width = 800

  GraphicsWindow.Height = 400

  GraphicsWindow.Title = "Egg Catcher Game V.5.1"

  GraphicsWindow.Left = Desktop.Width/2-GraphicsWindow.Width/2

  GraphicsWindow.Top = Desktop.Height/2-GraphicsWindow.Height/2

  GraphicsWindow.CanResize = "false"

  GraphicsWindow.FontSize = 24

  MouseIsHidden = "true"

  Mouse.HideCursor()  

  'Events

  GraphicsWindow.MouseMove = MoveBucket

  GraphicsWindow.MouseDown = Exit

  GraphicsWindow.KeyDown = KeyDown

  GraphicsWindow.KeyUp = KeyUp

EndSub



Sub SetIncrement    

  incr = Math.GetRandomNumber(30)  

  If ((dropperX + 80) > GraphicsWindow.Width) Then    

    incrementX = -incr    

  EndIf  

  If (dropperX <= 1) Then    

    incrementX = incr    

  EndIf    

EndSub



Sub MoveBucket

  'Hides mouse if mouseY > 70

  If MouseIsHidden = "false" And GraphicsWindow.MouseY > 70 Then

    Mouse.HideCursor()

    MouseIsHidden = "true"

  EndIf

  If MouseIsHidden = "true" And GraphicsWindow.MouseY < 70 Then

    Mouse.ShowCursor()

    MouseIsHidden = "false"

  EndIf  

  'If MouseIsHidden = "true" Then Move Bucket 

  If MouseIsHidden = "true" And MorK = "M" Then

    bucketX = GraphicsWindow.MouseX - 25    

    Shapes.Move(bucket, bucketX, GraphicsWindow.Height - 20)

  EndIf

EndSub



Sub DropEgg  

  'pallikese kordinaadid

  ThereIsEgg = "true"

  ThisEggFails = "false"

  ballX = Stack.PopValue("p")  

  ballY = 90  

  'eggs mooveing in right posision  

  GraphicsWindow.PenWidth = 0

  GraphicsWindow.BrushColor = "Khaki"  

  ball = Shapes.AddEllipse(20, 30)    

  Shapes.Move(ball, ballX, ballY)  

  dropSpeed = DefaultSpeed + Math.GetRandomNumber(1) 

  

  While (ballY - 20 < GraphicsWindow.Height)    

    Shapes.Move(ball, ballX, ballY)    

    Program.Delay(dropSpeed) 

    'test if balls fall   

    TestForBallInBucket()    

    MoveBucketUsingKeyboard()

    ballY = ballY + 5    

  EndWhile  

EndSub



Sub TestForBallInBucket

  If ThereIsEgg = "true" Then

    If ((ballY + 20) = GraphicsWindow.Height - 30) Or ((ballY + 20) > GraphicsWindow.Height - 30) Then  

      If (ballX < (bucketX + 50)) And (ballX = bucketX-15) Or (ballX < (bucketX + 50)) And (ballX > bucketX-15) And ThisEggFails = "false" Then 

        Scores = Scores + 1    

        CatchedEggs = CatchedEggs + 1

        'Play Click

        Sound.PlayClick()

        'Update Score And ReFresh Panel        

        DrawPanel()        

        'Remove Egg

        Shapes.Remove(ball)     

        ThereIsEgg = "false"

      ElseIf ThisEggFails = "false" Then

        Lives = Lives - 1   

        DrawPanel()

        ThisEggFails = "true"

      EndIf  

    EndIf

  EndIf

EndSub



Sub DrawPanel

  'Draws Panel

  GraphicsWindow.BrushColor = "DarkMagenta"  

  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, 75)

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.PenColor = GraphicsWindow.GetRandomColor()

  GraphicsWindow.DrawRectangle(1,1,GraphicsWindow.Width-1,75)

  GraphicsWindow.DrawLine(GraphicsWindow.Width-1,0,GraphicsWindow.Width-1,75)

  GraphicsWindow.BrushColor = "White" 

  'Draws Score And Level    

  GraphicsWindow.DrawText(5,5,"Scores: "+Scores+", Level: "+Level+", Lives: "+Lives)

  'Draw Exit

  GraphicsWindow.DrawText(GraphicsWindow.Width-55,5,"Exit")

  'Draw Time

  GraphicsWindow.DrawText(5,43,Time)

EndSub



Sub CreateCatcher

  'Create egg catcher

  GraphicsWindow.PenColor = "Red"

  GraphicsWindow.BrushColor = "Brown"

  bucket = Shapes.AddRectangle(50, 20)

EndSub



Sub CreateDropper

  'Create egg dropper

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.PenColor = "silver"

  GraphicsWindow.BrushColor = "Black"

  dropper = Shapes.AddRectangle(80, 40)

EndSub





Sub TimeUp

  Seconds = Seconds + 1

  If Seconds = 60 Then

    Seconds = 0

    Minutes = Minutes + 1

  EndIf

  If Minutes = 60 Then

    Minutes = 0

    Hours = Hours + 1

  EndIf

  If Hours = 24 Then

    Hours = 0

    Days = Days + 1

  EndIf

  Time =  "Game Time: Days: "+Days+", Hours: "+Hours+", Minutes: "+Minutes+", Seconds: "+Seconds

  DrawPanel()

EndSub



Sub Exit

  If MouseIsHidden = "false" And GraphicsWindow.MouseX > GraphicsWindow.Width-55 And Mouse.IsLeftButtonDown = "True" And Mouse.IsRightButtonDown = "False" Then

    Program.End()

  EndIf

EndSub



Sub KeyUp

  Key = GraphicsWindow.LastKey

  If Key = "Right" Then

    MovingRight = "false"

  EndIf

  If Key = "Left" Then

    MovingLeft = "false"

  EndIf

EndSub



Sub KeyDown

  Key = GraphicsWindow.LastKey

  If Key = "Right" Then

    MovingRight = "true"

  EndIf

  If Key = "Left" Then

    MovingLeft = "true"

  EndIf

EndSub



Sub MoveBucketUsingKeyboard  

  If MorK = "K" Then

  If MovingRight = "true" Then

    bucketX = bucketX + KeyMovement

  EndIf

  If MovingLeft = "true" Then

    bucketX = bucketX - KeyMovement

  EndIf

  SetLimits() 

  Shapes.Move(bucket, bucketX, GraphicsWindow.Height - 20)

  EndIf

EndSub



Sub SetLimits

  If bucketX < 1 Then

    bucketX = 1

  EndIf

  If bucketX > GraphicsWindow.Width-51 Then

    bucketX = GraphicsWindow.Width-51

  EndIf 

EndSub


