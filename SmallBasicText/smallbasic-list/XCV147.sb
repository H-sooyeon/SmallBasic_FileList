'==========================================

'INITIALISE

'==========================================

Initialise()



'==========================================

'MAIN GAME LOOP

'==========================================

start:

playing = "True"



While (playing)

  start = Clock.ElapsedMilliseconds

  speed = speed+0.01

  

  If (start-musicLast > 70000) Then

    Sound.Stop(music)

    Sound.Play(music)

    musicLast = start

  EndIf

  

  If (start-turn > 50) Then

    Shapes.Rotate(cars[numCar],0)

  EndIf

  

  MoveLanes()

  MoveCars()

  CrashCheck()

  

  score = score+speed/100

  Shapes.SetText(scoreBox,"Score "+Math.Floor(score)+" Crashes "+crashes)

  If (crashes = 5 Or keys["Escape"]) Then

    playing = "False"

  EndIf

  

  delay = 20-(Clock.ElapsedMilliseconds-start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile



NewGame()

Goto start



'==========================================

'SUBROUTINES

'==========================================

Sub Initialise

  laneWidth = 100

  numLane = 5

  gw = numLane*laneWidth

  gh = Desktop.Height-100

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

  GraphicsWindow.Top = 0

  GraphicsWindow.Left = (Desktop.Width-gw)/2

  GraphicsWindow.Title = "Lane Changer"

  GraphicsWindow.BackgroundColor = "LightGray"

  

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.PenWidth = 0

  For i = 1 To numLane-1

    For j = 1 To 8

      lane[i][j] = Shapes.AddRectangle(6,100)

      Shapes.Move(lane[i][j],laneWidth*i-3,200*j-100)

    EndFor

  EndFor

  

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = laneWidth/2

  loading = Shapes.AddText("Loading...")

  Shapes.Move(loading,gw/4,gh/2-100)

  

  GraphicsWindow.KeyDown = OnKeyDown

  GraphicsWindow.MouseDown = OnMouseDown

  GraphicsWindow.MouseUp = OnMouseUp

  

  Timer.Interval = 1000

  Timer.Tick = OnTick

  While (music = "")

    For i = 100 To 0 Step -1

      Shapes.SetOpacity(loading,i)

      Program.Delay(5)

    EndFor

    For i = 0 To 100

      Shapes.SetOpacity(loading,i)

      Program.Delay(5)

    EndFor

  EndWhile

  

  speed = 10

  score = 0

  crashes = 0

  GraphicsWindow.BrushColor = "#50FFAAAA"

  Shapes.AddRectangle(gw,30)

  GraphicsWindow.FontSize = 20

  GraphicsWindow.BrushColor = "DarkRed"

  scoreBox = Shapes.AddText("")

  Shapes.Move(scoreBox,gw/2-100,0)

  CR = Text.GetCharacter(10)  

  Shapes.Remove(loading)

EndSub



Sub GetResources

  numCar = 8

  For res = 1 To numCar

    resources[res] = "car"+res+".png"

  EndFor

  resources[9] = "backgroundMusic.mp3"

  resources[10] = "crashSound.mp3"

  

  path = Program.Directory+"/"

  web = "http://litdev.co.uk/game_images/"

' The following line could be harmful and has been automatically commented.

'   files = File.GetFiles(path)

  For res = 1 To Array.GetItemCount(resources)

    If (Array.ContainsValue(files,path+resources[res]) = "False") Then

      tempFile = Network.DownloadFile(web+resources[res])

' The following line could be harmful and has been automatically commented.

'       File.CopyFile(tempFile,path+resources[res])

' The following line could be harmful and has been automatically commented.

'       File.DeleteFile(tempFile)

    EndIf

    If (Text.EndsWith(resources[res],"png")) Then

      image = ImageList.LoadImage(web+"car"+res+".png")

      carWidth[res] = ImageList.GetWidthOfImage(image)

      carHeight[res] = ImageList.GetHeightOfImage(image)

      cars[res] = Shapes.AddImage(image)

      If (res = numCar) Then

        carLane[res] = Math.Ceiling(numLane/2)

        carY[res] = gh-carHeight[res]

      Else

        carLane[res] = Math.GetRandomNumber(numLane)

        carY[res] = gh

      EndIf

      Shapes.Move(cars[res],-carWidth[res]/2+laneWidth*(carLane[res]-0.5),carY[res])

    EndIf

  EndFor

  music = path+resources[9]

  crash = path+resources[10]

EndSub



Sub MoveLanes

  laneY = laneY+speed

  If (laneY > 200) Then

    laneY = laneY-200

  EndIf

  For i = 1 To numLane-1

    For j = 1 To 8

      Shapes.Move(lane[i][j],laneWidth*i-3,laneY+200*j-500)

    EndFor

  EndFor

EndSub



Sub MoveCars

  For i = 1 To numCar-1

    carY[i] = carY[i]+speed/2

    If (carY[i] >= gh) Then

      carLane[i] = Math.GetRandomNumber(numLane)

      carY[i] = -Math.GetRandomNumber(2500)

      For j = 1 To numCar-1

        If (i <> j And carLane[i] = carLane[j]) Then

          carY[i] = Math.Min(carY[i],carY[j]-800)

        EndIf

      EndFor

    EndIf

    Shapes.Move(cars[i],-carWidth[i]/2+laneWidth*(carLane[i]-0.5),carY[i])

  EndFor

  Shapes.Animate(cars[numCar],-carWidth[numCar]/2+laneWidth*(carLane[numCar]-0.5),carY[numCar],50)

EndSub



Sub CrashCheck

  For i = 1 To numCar-1

    If (carLane[i] = carLane[numCar] And carY[i]+carHeight[i] > gh-carHeight[numCar]) Then

      Sound.Stop(crash)

      Sound.Play(crash)

      carY[i] = gh

      speed = 10

      crashes = crashes+1

    EndIf

  EndFor

EndSub



Sub NewGame

  Sound.Stop(music)

  musicLast = 0

  

  score = Math.Floor(score)

' The following line could be harmful and has been automatically commented.

'   highscore = File.ReadContents(File.GetSettingsFilePath())

  If (score > highscore) Then

    highscore = score

' The following line could be harmful and has been automatically commented.

'     File.WriteContents(File.GetSettingsFilePath(),highscore)

  EndIf

  

  Program.Delay(500)

  GraphicsWindow.BrushColor = "#50AAAAFF"

  mask = Shapes.AddRectangle(gw,gh)

  GraphicsWindow.BrushColor = "DarkBlue"

  GraphicsWindow.FontSize = 20

  message = Shapes.AddText("      Your Score is "+score+CR+"      High Score is "+highscore+CR+"(Press a key to play again)")

  Shapes.Move(message,gw/2-135,50)

  Shapes.Animate(message,gw/2-135,gh/2,2000)

  

  keys = ""

  While (keys = "")

    Program.Delay(100)

  EndWhile

  Shapes.Remove(mask)

  Shapes.Remove(message)

  speed = 10

  score = 0

  crashes = 0

  For i = 1 To numCar-1

    carY[i] = gh

  EndFor

EndSub



'==========================================

'EVENTS

'==========================================

Sub OnTick

  Timer.Pause()

  GetResources()

EndSub



Sub OnKeyDown

  key = GraphicsWindow.LastKey

  keys[key] = "True"

  If (key = "Left" And carLane[numCar] > 1) Then

    carLane[numCar] = carLane[numCar]-1

    Shapes.Rotate(cars[numCar],-20)

  ElseIf (key = "Right" And carLane[numCar] < numLane) Then

    carLane[numCar] = carLane[numCar]+1

    Shapes.Rotate(cars[numCar],20)

  EndIf

  turn = Clock.ElapsedMilliseconds

EndSub



Sub OnKeyUp

  key = GraphicsWindow.LastKey

  keys[key] = ""

EndSub



Sub OnMouseDown

  If (Mouse.IsLeftButtonDown) Then

    keys["LeftMouse"] = "True"

    If (carLane[numCar] > 1) Then

      carLane[numCar] = carLane[numCar]-1

      Shapes.Rotate(cars[numCar],-20)

    EndIf

  ElseIf (Mouse.IsRightButtonDown) Then

    keys["RightMouse"] = "True"

    If (carLane[numCar] < numLane) Then

      carLane[numCar] = carLane[numCar]+1

      Shapes.Rotate(cars[numCar],20)

    EndIf

  EndIf

  turn = Clock.ElapsedMilliseconds

EndSub



Sub OnMouseUp

  keys["LeftMouse"] = ""

  keys["RightMouse"] = ""

EndSub


