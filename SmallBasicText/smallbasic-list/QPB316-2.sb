'Challenge of the Month - April 2017    Windmill  // by NaochanON  QPB316-2
'  added  4  ducks 
GraphicsWindow.Width = 1200
GraphicsWindow.Height = 650
GraphicsWindow.Left=10
GraphicsWindow.top=10
GraphicsWindow.BackgroundColor="skyblue"
GraphicsWindow.BrushColor="Darkgreen"
GraphicsWindow.FillRectangle(0,400,1200,300)
GraphicsWindow.BrushColor="Blue"
GraphicsWindow.PenWidth=0
GraphicsWindow.FillTriangle(250,400,500,650,300,400)
GraphicsWindow.FillTriangle(300,400,500,650,900,650)
Duck_Init()
dat=duck
Name="Duck"
s=ds
sx=dsx
sy=dsy
add_shapes()

windmill_init()
s=ws
sx=wsx
sy=wsy
Name="Windmill"
add_shapes()

While "True"
  For M=1 To Array.GetItemCount(ws) '  ' windmill
    SName=Name+"_"+M
    ss=ws[M]
    For i=10 to array.GetItemCount(dat)
      angle[Sname][i]=angle[Sname][i]-(3+(M-1)*2)
      _angle=math.GetRadians(angle[Sname][i])
      Shapes.Move(SHP[Sname][i],XYC[Sname]["X"]+DR[Sname][i]*Math.Cos(_angle)-dat[i]["wd"]*ss/2,XYC[Sname]["Y"]+DR[Sname][i]*Math.sin(_angle)-dat[i]["ht"]*ss/2)
      If i<22 Then 
        Shapes.Rotate( SHP[Sname][i],  90+angle[Sname][10])
      Else 
        Shapes.Rotate( SHP[Sname][i], angle[Sname][22]) 
      EndIf
    EndFor
    Program.Delay(20)
  EndFor
  
  For j=1 To Array.GetItemCount(ds)     '  duck
    Sname="Duck_"+j
    dx= Math.GetRandomNumber(10)/2
    dy= -0.1
    If Shapes.GetLeft(shp[SName][1])>590-20*J Then 
      dx=-0.05
      dY=0.3
    endif
    If Shapes.Gettop(shp[SName][1])<460 then 
      dx=Math.GetRandomNumber(10)/2
      dY=-0.1
    endif
    For k=1 To 10
      Shapes.Move(shp[SName][k],Shapes.GetLeft(shp[SName][k])+dx,Shapes.Gettop(shp[SName][k])-dy)
    EndFor
    If  Shapes.GetLeft(shp[SName][1])>1250 Then 
      NMB=text.GetSubTextToEnd(Sname,6)
      For L=1 to 10
        Shapes.Move(shp[SName][L],  duck[L]["x"]*ds[NMB]+dsx[NMB]-200, duck[L]["y"]*ds[NMB]+dsy[NMB] )
      endfor
    endif
  EndFor
endwhile

Sub  CenterPosition
  if Text.IsSubText(Sname,"Windmill") Then 
    XYC[Sname]="X="+(shapes.GetLeft(shp[Sname][9])+dat[9]["wd"]*ss/2)+";Y="+ (shapes.Gettop(shp[Sname][9])+dat[9]["ht"]*ss/2)  '  base point //  
    For i=  10 To array.GetItemCount(dat)
      ddx=shapes.GetLeft(SHP[Sname][i])+dat[i]["wd"]*ss/2 - XYC[Sname]["X"]  
      ddy=shapes.GetTop(SHP[Sname][i])+dat[i]["ht"]*ss/2  - XYC[Sname]["Y"] 
      If ddX<=0 Then 
        angle[Sname][i]=Math.GetDegrees(Math.ArcTan((ddY)/(ddX+0.0000000001)))-90
      Else
        angle[Sname][i]=Math.GetDegrees(Math.ArcTan((ddY)/(ddX+0.0000000001)))+90
      EndIf
      DR[Sname][i]=Math.SquareRoot((ddx)*(ddx)+(ddy)*(ddy))  
    endfor
  endif
endsub

Sub add_shapes
  For M=1 To Array.GetItemCount(s)
    ss=s[M]
    shx=sX[M]
    shy=sY[M]
    SName=Name+"_"+M
    for i=1 To Array.GetItemCount(dat) 
      GraphicsWindow.PenWidth = dat[i]["pw"]
      GraphicsWindow.BrushColor = dat[i]["bc"] 
      GraphicsWindow.penColor = dat[i]["pc"] 
      If dat[i]["func"]="el" Then 
        shp[SName][i] = Shapes.AddEllipse(dat[i]["wd"]*ss, dat[i]["ht"]*ss) 
      ElseIf dat[i]["func"]="rec" Then 
        shp[SName][i] = Shapes.AddRectangle(dat[i]["wd"]*ss, dat[i]["ht"]*ss)
      ElseIf dat[i]["func"]="tri" Then 
        shp[SName][i] = Shapes.Addtriangle(dat[i]["x1"]*ss, dat[i]["y1"]*ss,dat[i]["x2"]*ss, dat[i]["y2"]*ss, dat[i]["x3"]*ss, dat[i]["y3"]*ss) 
      ElseIf dat[i]["func"]="ln" Then 
        shp[SName][i] = Shapes.Addline(dat[i]["x1"]*ss, dat[i]["y1"]*ss,dat[i]["x2"]*ss, dat[i]["y2"]*ss)
      EndIf
      Shapes.Animate(shp[SName][i], dat[i]["x"]*ss+shx, dat[i]["y"]*ss+shy, 500) 
      Shapes.Rotate(shp[SName][i], dat[i]["angle"])    
    EndFor
    CenterPosition() 
  EndFor
EndSub

Sub windmill_init 
  ws="1=0.25;2=0.4;3=0.9"
  wsx="1=100;2=450;3=850"
  wsy="1=300;2=250;3=180"
  
  dat[1]="func=rec;X=0;Y=100;wd=260;ht=340;angle=0;bc=#BBC8E6;pc=#A8C97F;pw=2" 
  dat[2]="func=rec;X=0;Y=100;wd=120;ht=340;angle=0;bc=#9D896C;pc=#94846A;pw=1" 
  dat[3]="func=rec;X=55;Y=160;wd=40;ht=24;angle=0;bc=#FFF1CF;pc=#333631;pw=2" 
  dat[4]="func=rec;X=200;Y=160;wd=40;ht=24;angle=0;bc=#5B6356;pc=#716246;pw=2" 
  dat[5]="func=rec;X=190;Y=356;wd=40;ht=86;angle=0;bc=#5B6356;pc=#716246;pw=2" 
  dat[6]="func=el;X=190;Y=344;wd=40;ht=24;angle=0;bc=#5B6356;pc=#716246;pw=0" 
  dat[7]="func=el;X=0;Y=76;wd=260;ht=44;angle=0;bc=#1F3134;pc=#1F3134;pw=0" 
  dat[8]="func=tri;x1=0;y1=103;x2=124;y2=0;x3=260;y3=103;angle=0;bc=#1F3134;pc=#1F3134;pw=0" 
  dat[9]="func=el;X=108;Y=50;wd=40;ht=40;angle=0;bc=#3EB370;pc=#005243;pw=2" 
  '  Wing  -Up
  dat[10]="func=rec;X=126;Y=-154;wd=12;ht=204;angle=0;bc=#302833;pc=#302833;pw=0" 
  dat[11]="func=rec;X=134;Y=-154;wd=58;ht=178;angle=0;bc=#F8E58C;pc=#956F29;pw=2" 
  dat[12]="func=rec;X=152;Y=-154;wd=20;ht=178;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[13]="func=rec;X=134;Y=-126;wd=58;ht=124;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[14]="func=rec;X=134;Y=-98;wd=58;ht=62;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[15]="func=rec;X=134;Y=-66;wd=58;ht=4;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  ' Wing  -Down 
  dat[16]="func=rec;X=126;Y=90;wd=12;ht=204;angle=0;bc=#302833;pc=#302833;pw=0" 
  dat[17]="func=rec;X=68;Y=114;wd=58;ht=178;angle=0;bc=#F8E58C;pc=#956F29;pw=2" 
  dat[18]="func=rec;X=86;Y=114;wd=20;ht=178;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[19]="func=rec;X=68;Y=142;wd=58;ht=124;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[20]="func=rec;X=68;Y=160;wd=58;ht=62;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[21]="func=rec;X=68;Y=192;wd=58;ht=4;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  ' Wing  -Right
  dat[22]="func=rec;X=148;Y=64;wd=204;ht=12;angle=0;bc=#302833;pc=#302833;pw=0" 
  dat[23]="func=rec;X=174;Y=72;wd=178;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[24]="func=rec;X=174;Y=90;wd=178;ht=20;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[25]="func=rec;X=202;Y=72;wd=124;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[26]="func=rec;X=230;Y=72;wd=62;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[27]="func=rec;X=258;Y=72;wd=4;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  ' Wing  -Left
  dat[28]="func=rec;X=-96;Y=64;wd=204;ht=12;angle=0;bc=#302833;pc=#302833;pw=0" 
  dat[29]="func=rec;X=-96;Y=10;wd=178;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[30]="func=rec;X=-96;Y=28;wd=178;ht=20;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[31]="func=rec;X=-68;Y=10;wd=124;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[32]="func=rec;X=-40;Y=10;wd=62;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
  dat[33]="func=rec;X=-12;Y=10;wd=4;ht=58;angle=0;bc=Transparent;pc=#956F29;pw=2" 
EndSub

Sub Duck_Init  '  Duck   
  ds ="1=0.13;2=0.25;3=0.2;4=0.3"
  dsX ="1=98;2=84;3=75;4=65"
  dsY ="1=520;2=560;3=530;4=580"
  duck[1] = "func=tri;x=153;y=41;x1=47;y1=0;x2=0;y2=22;x3=95;y3=22;bc=red;pw=0" ';beck"
  duck[2] = "func=el;x=118;y=0;wd=91;ht=73;bc=#460E44;pw=0;"  ' Head
  duck[3] = "func=ln;x=172;y=36;x1=0;y1=0;x2=22;y2=0;pc=red;pw=2" ' ;blink"
  duck[4] = "func=el;x=172;y=25;wd=22;ht=22;bc=pink;pw=0" '  ;eye"
  duck[5] = "func=tri;x=132;y=58;x1=31;y1=0;x2=0;y2=45;x3=62;y3=45;bc=red;pw=0"  ' ;neck"
  duck[6] = "func=tri;x=0;y=80;x1=37;y1=0;x2=0;y2=32;x3=75;y3=32;angle=178;bc=red;pw=0" '  ;tail"
  duck[7] = "func=ln;x=91;y=134;x1=0;y1=0;x2=0;y2=36;pc=red;pw=8" ' ;Leg1"
  duck[8] = "func=el;x=33;y=72;wd=164;ht=82;bc=#00A497;pw=0" '  ;body"
  duck[9] = "func=tri;x=58;y=180;x1=46;y1=0;x2=0;y2=14;x3=93;y3=14;bc=red;pw=0" ';Leg3"
  duck[10] = "func=ln;x=90;y=169;x1=0;y1=0;x2=14;y2=15;pc=yellow;pw=8" '  ;Leg2"
EndSub
