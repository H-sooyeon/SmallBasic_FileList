'register Events
GraphicsWindow.MouseDown = OnMouseDown
Controls.ButtonClicked = OnButtonClick

Initialise()

While "True"
  start = Clock.ElapsedMilliseconds
  
  UpdateProgram()  
  RefreshScreen()
  FrameRate()  
EndWhile

'===================================================================
'Event Handlers
'===================================================================
Sub OnButtonClick
  buttonClicked = "True"
EndSub

Sub OnMouseDown
  mouseDown = "True"
EndSub

'=========================================================
'1st Order Subroutines
'=========================================================
Sub UpdateProgram
  If buttonClicked Then
    GetPizzaSizeAndCrust()    
    displayMessage = "True"  ' I have a Structure issue here. 
  EndIf
  
  If mouseDown Then
    SetRadioButton()
    GetPizzaSizeAndCrust()
  EndIf
  
  buttonClicked = "False"
  mouseDown = "False"
EndSub

Sub RefreshScreen
  If displayMessage Then
    ShowMessage()
  EndIf
  
  If radioButtonSet Then
    DrawRBDeselection()            
    DrawRBSelection() 
  EndIf
  
  If drawPizza Then
    DrawRescaledPizza()
  EndIf
  
  displayMessage = "False"
  radioButtonSet = "False"
  drawPizza = "False"  
EndSub

Sub FrameRate
  delay = 1000 / fps - (Clock.ElapsedMilliseconds - start)
  If delay > 0 Then 
    Program.Delay(delay)
  EndIf
EndSub

'--------------------------------------------------------------------------------------------------
'Setup & Initialise
'--------------------------------------------------------------------------------------------------
Sub Initialise  
  fps = 50
  
  'get some GW properties to set variables
  gw = GraphicsWindow.Width
  gh = GraphicsWindow.Height
  
  canContinue = "False"
  buttonClicked = "False"
  displayMessage = "False"
  
  'button Graphics properties & RO (relative offset)
  rbPropZ[1] = "W=15;PW=1;PC=Silver;BC=White;Op=100;RO=0" 
  rbPropZ[2] = "W=11;PW=0;BC=Gainsboro;Op=100;RO=2"
  rbPropZ[3] = "W=7;PW=0;BC=White;Op=70;RO=5"
  'onSelection properties
  rbPropZ[4] = "W=8;PW=0;BC=MediumBlue;Op=0;RO=3.5" 
  rbPropZ[5] = "W=4;PW=0;BC=PowderBlue;Op=0;RO=5" 
  zOrder = Array.GetItemCount(rbPropZ)
  
  'gui grid
  columns = 2  
  rows[1] = 3
  rows[2] = 2
  columnSpace = 150
  rowSpace = 50
  leftOffset = 30
  topOffset = 60
  
  'pizza Size & Crust data
  txtVal[1] = "1=Small;2=Medium;3=Large"
  txtVal[2] = "1=Thin;2=Thick"
  
  'zoom scale (size & crust)
  zoomPizza[1] = "1=1;2=1.5;3=2"
  zoomPizza[2] = "1=1;2=1.5"
  
  'add Shapes & Control
  pizza = Shapes.AddRectangle(200, 15)
  Shapes.Move(pizza, 200, 250)
  Shapes.HideShape(pizza)
  
  btnWidth = 100
  btn = Controls.AddButton("Continue", gw - btnWidth - 10, 10)
  Controls.SetSize(btn, btnWidth, 30)
  
  AddRadioButtons()
EndSub

'=========================================================
'2nd Order Subroutines
'=========================================================
Sub AddRadioButtons
  For c = 1 To columns
    X = leftOffset * c + (columnSpace * (c-1))
    rbX[c] = X
    For r = 1 To rows[c]
      Y = topOffset + (rowSpace * (r-1))
      rbY[c][r] = Y
      selection[c][r] = -1  'initialise -1  =  not selected
      GraphicsWindow.BrushColor = "Black"
      txt = Shapes.AddText(txtVal[c][r])
      Shapes.Move(txt, X + 20, Y)    
      For z = 1 To zOrder      
        GraphicsWindow.PenWidth = rbPropZ[z]["PW"]
        GraphicsWindow.PenColor = rbPropZ[z]["PC"]
        GraphicsWindow.BrushColor = rbPropZ[z]["BC"]
        rb[z][c][r] = Shapes.AddEllipse(rbPropZ[z]["W"], rbPropZ[z]["W"])
        Shapes.Move(rb[z][c][r], rbPropZ[z]["RO"] + X, rbPropZ[z]["RO"] + Y)
        Shapes.SetOpacity(rb[z][c][r], rbPropZ[z]["Op"])
      EndFor
    EndFor
  EndFor
EndSub

Sub DrawRBSelection
  For z = 4 To 5  'show selected dot using c & r onClick
    Shapes.SetOpacity(rb[z][group][choice], 100)
  EndFor
EndSub

Sub DrawRBDeselection
  For j = 1 To rows[group]
    For z = 4 To 5  'show selected dot using c & r onClick
      Shapes.SetOpacity(rb[z][group][j], 0)
    EndFor
  EndFor
EndSub

Sub ShowMessage
  If canContinue Then
    GraphicsWindow.ShowMessage(txtVal[1][showSize] + ", " + txtVal[2][showCrust] , "Proceed")
  Else
    GraphicsWindow.ShowMessage("You must select a size & crust", "Wait")
  EndIf
EndSub

Sub DrawRescaledPizza
  Shapes.ShowShape(pizza)     
  Shapes.Zoom(pizza, size, type)
EndSub

Sub SetRadioButton  
  mouseX = GraphicsWindow.MouseX
  mouseY = GraphicsWindow.MouseY
  For c = 1 To columns
    If mouseX >  rbX[c] And mouseX < rbX[c] + rbPropZ[1]["W"] Then
      For r = 1 To rows[c]
        If mouseY > rbY[c][r] And mouseY < rbY[c][r] + rbPropZ[1]["W"] Then          
          selection[c][r] = selection[c][r] * -1
          radioButtonSet = "True"
          'DeselectRBGroup()            
          'SelectRB() 
          group = c
          choice = r
          For j = 1 To rows[c]
            If j <> r Then    'unset the others
              selection[c][j] = -1
            EndIf
          EndFor                         
        EndIf
      EndFor
    EndIf
  EndFor
EndSub

Sub GetPizzaSizeAndCrust
  If Array.ContainsValue(selection[1], 1) And Array.ContainsValue(selection[2], 1) Then    
    canContinue = "True"
    drawPizza = "True" 
    
    For c = 1 To columns
      For r = 1 To rows[c]
        If selection[1][r] = 1 Then          
          size = zoomPizza[1][r]
          showSize = r
        EndIf
        If selection[2][r] = 1 Then
          type = zoomPizza[2][r]
          showCrust = r
        EndIf
      EndFor
    EndFor    
  EndIf
EndSub
