GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp



GraphicsWindow.FontSize = 20

GraphicsWindow.DrawLine(60,180,100,150) 'Z Axis

GraphicsWindow.DrawText(60,180,"Z")

GraphicsWindow.DrawLine(100,150,200,150) 'X Axis

GraphicsWindow.DrawText(200,150,"X")

GraphicsWindow.DrawLine(100,50,100,150) 'Y Axis

GraphicsWindow.DrawText(100,50,"Y")



GW = 600



TextWindow.Left = GraphicsWindow.Left

TextWindow.Top = TextWindow.Top + GW + 10

GraphicsWindow.Width = GW

GraphicsWindow.Height = GW





GraphicsWindow.PenWidth = 0

GraphicsWindow.BrushColor = "#77000000"

Shadow = Shapes.AddEllipse(40,20)

'Shadow2 = Shapes.AddEllipse(40,20)



Player_Y = 0

Player_Width = 20

Player_Height = 40

Player_Speed = 4

GraphicsWindow.BrushColor = "Blue"

Player = Shapes.AddRectangle(20,40)





Block_Amount = 1

For i = 1 to Block_Amount

  GraphicsWindow.PenWidth = 0

  GraphicsWindow.BrushColor = "DarkRed"

  Block_Height[i] = 80

  Block_Width[i] = 80

  

  Block_Left[i] = 300

  Block_Top[i] = 300

  Block_Right[i] = Block_Left[i] + Block_Width[i]

  Block_Bottom[i] = Block_Top[i] + Block_Height[i]

  

  GraphicsWindow.BrushColor = "DarkRed"

  GraphicsWindow.FillRectangle(Block_Left[i],Block_Top[i],Block_Width[i],Block_Height[i])

  Block[i] = Shapes.AddRectangle(Block_Width[i],Block_Height[i])

  GraphicsWindow.BrushColor = "Red"

  GraphicsWindow.FillRectangle(Block_Left[i],Block_Top[i]-Block_Height[i]/2,Block_Width[i],Block_Height[i]/2)

  Top_Block[i] = Shapes.AddRectangle(Block_Width[i],Block_Height[i]/2)

  

  Shapes.Move(Block[i],Block_Left[i],Block_Top[i])

  Shapes.Move(Top_Block[i],Block_Left[i],Block_Top[i]-Block_Height[i]/2)

  

  GraphicsWindow.PenColor = "Black"

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.DrawLine(Block_Left[i]-200,Block_Top[i]+Block_Height[i]/2,Block_Left[i],Block_Top[i]+Block_Height[i]/2)

  GraphicsWindow.DrawLine(Block_Left[i]-200,Block_Bottom[i],Block_Left[i],Block_Bottom[i])

endfor







GraphicsWindow.BrushColor = "Magenta"

GraphicsWindow.PenColor = "Red"

'Sensor[1] = Left Sensor

'Sensor[2] = Right Sensor



For s = 1 to 2

  Sensor[s] = Shapes.AddRectangle(5,5)

endfor



GraphicsWindow.PenWidth = 5

GraphicsWindow.BrushColor = "Transparent"

Crosshair = Shapes.AddEllipse(50,50)

GraphicsWindow.PenWidth = 1



While "True"

  

  Mouse_X = GraphicsWindow.MouseX

  Mouse_Y = GraphicsWindow.MouseY

  If Right = 1 Then 

    Player_WX = Player_Speed

  endif

  if Left = 1 then

    Player_WX = Player_Speed*-1

  endif

  If Right = 0 And Left = 0 Then

    Player_WX = 0

  endif

  If Down = 1 Then 

    Player_WZ = Player_Speed/2

  endif

  if Up = 1 then

    Player_WZ = (Player_Speed/2)*-1

  endif

  if Down = 0 And Up = 0 Then

    Player_WZ = 0

  endif

  If RightShift = 1 Then

    If RightShift_Pressed = 0 Then

      Player_WY = -20

      RightShift_Pressed = 1

    endif

  endif

  

  If Player_Y < Player_Level Then

    Player_WY = Player_WY + 2

  ElseIf Player_WY > 0 Then

    Player_Y = Player_Level

    Player_WY = -Player_WY*0.3

  endif

  

  Player_Y = Player_Y + Player_WY

  Player_Z = Player_Z + Player_WZ

  Player_X = Player_X + Player_WX

  

  Player_Right = Player_X + Player_Width

  

  

  Sensor_X[1] = Player_X

  Sensor_X[2] = Player_Right

  

  For s = 1 To 2

    Sensor_Y = Player_Z + Player_Y

    Shapes.Move(Sensor[s],Sensor_X[s]-2.5,Sensor_Y-2.5)

  endfor

  

  

  

  CollisionDetection()

  

  Shapes.Move(Player,Player_X,Player_Z + Player_Y - Player_Height)

  

  Shadow_Zoom = 1 + (Player_Y-Player_Level)/120

  Shapes.Zoom(Shadow,Shadow_Zoom,Shadow_Zoom)

  'Shapes.Zoom(Shadow2,Shadow_Zoom,Shadow_Zoom)

  Shapes.Move(Shadow,Player_X-10,Player_Z+Player_Level-10)

  'Shapes.Move(Shadow2,Player_X-10,Player_Z-10)

  

  TextWindow.Clear()

  TextWindow.WriteLine("Player_X: "+ Player_X)

  TextWindow.WriteLine("Player_Y: "+ Player_Y)

  TextWindow.WriteLine("Player_Z: "+ Player_Z)

  TextWindow.WriteLine("Player_Level: "+ Player_Level)

  

  Shapes.Remove(Aimline)

  Aimline = Shapes.AddLine(Player_X+Player_Width/2,Player_Z+Player_Y-Player_Height/2,Mouse_X,Mouse_Y)

  shapes.Move(Crosshair,Mouse_X-25,Mouse_Y-25)

  

  Program.Delay(20)

Endwhile



Sub CollisionDetection

  For i = 1 To Block_Amount

    

    If Player_Z < Block_Bottom[i]-5 and Player_Level >= 0 Then

      Shapes.SetOpacity(Top_Block[i],100)

      Shapes.SetOpacity(Block[i],100)

    Else

      Shapes.SetOpacity(Top_Block[i],0)

      Shapes.SetOpacity(Block[i],0)

    endif

    

    For s = 1 To 2

      Sensor_Touching[s] = 0

      if Player_Z >= Block_Top[i] + Block_Height[i]/2 and Player_Z <= Block_Bottom[i] Then

        if Sensor_X[s] >= Block_Left[i] and Sensor_X[s] <= Block_Right[i] then

          Sensor_Touching[s] = 1

        endif

      endif

    endfor

    

    

    If Sensor_Touching[1] = 1 and Sensor_Touching[2] = 0 And Player_Level >= 0 or Sensor_Touching[1] = 0 and Sensor_Touching[2] = 1 and Player_Level >= 0 Then

      If Sensor_Touching[1] = 1 Then

        Player_X = Block_Right[i]

      endif

      

      If Sensor_Touching[2] = 1 Then

        Player_X = Block_Left[i] - Player_Width

      endif

      

    ElseIf Sensor_Touching[1] = 1 and Sensor_Touching[2] = 1 and Player_Level >= 0 Then

      If Player_Z < Block_Top[i] + 70 Then

        Player_Z = Block_Bottom[i] - Block_Height[i]/2

      Else

        Player_Z = Block_Bottom[i]

      endif

    endif

    if Sensor_Touching[1] = 1 or Sensor_Touching[2] = 1 Then

      If Player_Level >= 0 And Player_Y <= -Block_Height[i] Then

        Player_Level = -Block_Height[i]

      endif

    Else

      Player_Level = 0

    endif

  endfor

  

  If Player_Z - Player_Height < 0 Then

    Player_Z = Player_Height

  ElseIf Player_Z > GW then

    Player_Z = GW

  endif

  If Player_X < 0 Then

    player_X = 0

  ElseIf Player_Right > GW then

    player_X = GW - Player_Width

  endif

endsub







Sub OnKeyDown

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    Left = 1

  ElseIf (Key = "Right") Then

    Right = 1

  ElseIf (Key = "Up") Then

    Up = 1

  ElseIf (Key = "Down") Then

    Down = 1

  ElseIf (Key = "RightShift") Then

    RightShift = 1

  endif

EndSub



Sub OnKeyUp

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    Left = 0

  ElseIf (Key = "Right") Then

    Right = 0

  ElseIf (Key = "Up") Then

    Up = 0

  ElseIf (Key = "Down") Then

    Down = 0

  ElseIf (Key = "RightShift") Then

    RightShift = 0

    RightShift_Pressed = 0

  endif

EndSub


