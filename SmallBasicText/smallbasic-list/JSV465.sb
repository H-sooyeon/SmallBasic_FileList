'Calculate the duration for various circular equatorial orbits & altitude for a geostationary orbit.

'G*M*m/r/r = m*v*v/r  ; C=2*pi*r  ; v = d/t ; R_geo = ((G*M*SRP^2)/4*pi^2)^(1/3)



G = Math.Round(6.67384 *100)/100 * Math.Power(10, -11) 'm3 * kg^-1 * s^-2  , bigG. rel uncertainty 1.2Ã—10?ˆ’4

M = 5.9736 * Math.Power(10, 24) 'kg,  Earth mass

eR = 6371 * Math.Power(10, 3) 'm,  Earths mean radius

SRP = (23 + (56/60))*60*60 + 4.1 ' s, "sidereal" rotation period (nearly = SD)

ELEO = 200 * Math.Power(10, 3) 'm, <= equatorial low earth orbit <= MEO

EMEO = 2000 * Math.Power(10, 3) 'm,<  equatorial medium earth orbit < GEO

L1 = 1.5 *Math.Power(10, 6+3) 'm,  Lagrange point 1 or 2



'-----------------------------------Main Program----------------------------------------------------------------------------------

Calculate_GEO() 'm,  geostationary orbit

Calculate_Duration()

TextWindow.PauseWithoutMessage()



'-------------------------------------Subroutines-------------------------------------------------------------------------------

Sub Calculate_GEO

  numer = Math.Round(G*M*Math.Power(SRP,2)/Math.Power(10,21))*Math.Power(10,21)'kg and s units cancel

  denom = 4*Math.Power(Math.Pi,2)

  GEO = Math.Floor(Math.Power( numer / denom, 1/3)/Math.Power(10,3))*Math.Power(10,3) - eR 'to "the" km

EndSub



Sub Calculate_Duration

  increment = 600 * Math.Power(10,3)

  ColourOn() 

  TextWindow.WriteLine("ELEO's")

  ColourOff()

  For altitude = ELEO To L1 Step increment

    If altitude = EMEO Then

      increment = 11262 * Math.Power(10,3)

      ColourOn()

      TextWindow.WriteLine("EMEO's")

      ColourOff()

    ElseIf altitude = GEO Then

      increment = (L1-GEO)/3

      ColourOn()

      TextWindow.WriteLine("GEO")

      GeoColour()

    ElseIf altitude = GEO + increment Then

      ColourOn()            

      TextWindow.WriteLine("EHEO's")

      ColourOff()

    ElseIf altitude = L1 Then

      ColourOn()

      TextWindow.WriteLine("EHEO with altitude = distance to L1 & L2 but on the Equ plane.")

      ColourOff()

    EndIf

    C = 2 * Math.Pi * (eR + altitude)'r

    t = C / Math.SquareRoot(G * M / (eR + altitude))

    TextWindow.Write("Altitude (km) = " + Math.Round(altitude/1000))

    TextWindow.WriteLine(", Duration (min) = " + Math.Ceiling(t/60))    

  EndFor

EndSub



Sub ColourOn

  TextWindow.ForegroundColor = "green"

  TextWindow.WriteLine("")

EndSub



Sub ColourOff

  TextWindow.ForegroundColor = "white"

EndSub



Sub GeoColour

  TextWindow.ForegroundColor = "red"

EndSub






