'  Paint
'  by CodingLikeCrazy
'  remix GoToLoop

'  Graphics Init:

GW = 1250
GH = 750
Sz = 4

GraphicsWindow.Width  = GW
GraphicsWindow.Height = GH
GraphicsWindow.Left = (Desktop.Width  - GW) / 2  '  Centralizes GraphicsWindow.
GraphicsWindow.Top  = (Desktop.Height - GH) / 2

GraphicsWindow.BackgroundColor = "Black"
GraphicsWindow.PenColor = "White"
GraphicsWindow.PenWidth = Sz

CreateColorButtons()
CreateFeatureButtons()

'  Event Raising Init:

Button = ""
Controls.ButtonClicked   = OnButtonClick

GraphicsWindow.MouseDown = OnMouseDown
GraphicsWindow.MouseMove = OnMouseMove

'  Button Wait Loop:
Loop:

If Button <> "" Then   '  Waits until a button is selected.
  CheckButtons()
  Button = ""
EndIf

Program.Delay(100)     '  A delay to cool CPU down!

Goto Loop

'   Button Feature Subroutines:
'--------------------------------------------------------------------------------------------------------------------------'
Sub CheckButtons
  
  If Button = "Custom" Then
    'Button = Dialogs.AskForColor()  ' <---  Uncomment to use this extension!
    SetPenColor()
    
  ElseIf Button = "Paper" Then
    GraphicsWindow.BackgroundColor = GraphicsWindow.PenColor
    
  ElseIf Button = "Size Up" Then
    If Sz < 30 Then
      Sz = Sz + 1
    EndIf
    GraphicsWindow.PenWidth = Sz
    
  ElseIf Button = "Size Dn" Then
    If Sz > 1 Then
      Sz = Sz - 1
    EndIf
    GraphicsWindow.PenWidth = Sz
    
  ElseIf Button = "Square" Then
    AskSquare()    '  <---   Replace this w/ a more appropriate graphics extension input dialog.
    GraphicsWindow.BrushColor = GraphicsWindow.PenColor
    GraphicsWindow.FillRectangle(XC,YC   W,H)
    
  ElseIf Button = "Clear" Then
    GraphicsWindow.Clear()
    CreateColorButtons()
    CreateFeatureButtons()
    
  ElseIf Button = "Quit" Then
    Sound.PlayChimeAndWait()
    Program.End()
    
  Else   '  After all of feature buttons dismissed, assumes it's some color button now!
    SetPenColor()
    
  EndIf
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub SetPenColor
  
  GraphicsWindow.PenColor = Button   '  Button's caption is the color itself!
  GraphicsWindow.Title    = Button + "  :  " + Sz
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub AskSquare
  
  LF = Text.GetCharacter(10)   '<---  Line Feed ASCII Code (jumps a line)
  
  TextWindow.Show()
  TextWindow.Clear()
  
  TextWindow.Write("Enter X Co-ordinate:  ")
  XC = Math.Abs( TextWindow.ReadNumber() )
  TextWindow.Write(LF + "Enter Y Co-ordinate:  ")
  YC = Math.Abs( TextWindow.ReadNumber() )
  TextWindow.Write(LF + "Enter Width:   ")
  W = Math.Abs( TextWindow.ReadNumber() )
  TextWindow.Write(LF + "Enter Height:  ")
  H = Math.Abs( TextWindow.ReadNumber() )
  
  TextWindow.Hide()
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub CreateColorButtons
  
  Colors = "0=Custom;1=Orange;2=Yellow;3=Red;4=Blue;5=Cyan;6=Green;7=Olive;8=Purple;"
  Colors = Colors + "9=Magenta;10=Pink;11=White;12=Gray;13=Black;14=SaddleBrown;"
  Count  = Array.GetItemCount(Colors) - 1
  
  For i=0 To Count
    GraphicsWindow.BrushColor = Colors[i]
    Controls.AddButton(Colors[i]   75*i+10,10)
  EndFor
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub CreateFeatureButtons
  
  GraphicsWindow.BrushColor = "Black"
  
  Controls.AddButton("Paper"    10, 50)
  Controls.AddButton("Size Up"  10,100)
  Controls.AddButton("Size Dn"  10,150)
  Controls.AddButton("Square"   10,200)
  Controls.AddButton("Clear"    10,250)
  Controls.AddButton("Quit"     10,300)
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'

'  Triggered Event Subs:
'-----------------------------------------------------------------------------------------------------------------------------'
Sub OnButtonClick
  
  Button = Controls.GetButtonCaption( Controls.LastClickedButton )
  '  Grabs caption text of the last button clicked.
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub OnMouseDown
  
  PrevX = GraphicsWindow.MouseX
  PrevY = GraphicsWindow.MouseY
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
Sub OnMouseMove
  
  X = GraphicsWindow.MouseX
  Y = GraphicsWindow.MouseY
  
  If Mouse.IsLeftButtonDown Or Mouse.IsRightButtonDown Then
    GraphicsWindow.DrawLine(PrevX,PrevY   X,Y)
  EndIf
  
  PrevX = X
  PrevY = Y
  
EndSub
'-----------------------------------------------------------------------------------------------------------------------------'
