' Create String for ASCII Art
GraphicsWindow.BackgroundColor = "LightGray"
GraphicsWindow.Width = 677
url = "http://www.nonkit.com/smallbasic.files/WhiteTextWindow.PNG"
GraphicsWindow.DrawImage(url, 0, 0)
x0 = 10
y0 = 33
cw = 8
ch = 12
code = 32
x1 = 10
y1 = 353
GraphicsWindow.BrushColor = "Black"
tbox = Controls.AddTextBox(x1 + cw + 10, y1)
Controls.SetSize(tbox, 677 - (x1 + cw + 20), 22)
x = x0
y = y0
min = cw * ch
For code = 32 To 126
  If code = 32 + 80 Then
    x = x0
    y = y0 + ch
  EndIf
  CountWhitePixelsInChar()
  If px < min Then
    min = px
  EndIf
  char[px] = code
  x = x + cw
EndFor
str = ""
For px = min To cw * ch
  If char[px] = "" Then
    str = str + Text.GetCharacter(lastChar)
  Else
    str = str + Text.GetCharacter(char[px])
    lastChar = char[px]
  EndIf
EndFor
Controls.SetTextBoxText(tbox, str)
GraphicsWindow.DrawText(x1, y1 + 22 + 10, "min = " + min + " max=" + (cw * ch))
Sub CountWhitePixelsInChar
  ' param x, y - top left of the character
  ' return px - white pixels in the character
  px = 0
  For dy = 0 To ch - 1
    For dx = 0 To cw - 1
      color = GraphicsWindow.GetPixel(x + dx , y + dy)
      If 7 < Text.GetLength(color) Then
        color = "#" + Text.GetSubTextToEnd(color, 4)
      EndIf
      If color = "#FFFFFF" Then
        px = px + 1
      EndIf
      GraphicsWindow.SetPixel(x1 + dx, y1 + dy, color)
    EndFor
  EndFor
EndSub  
