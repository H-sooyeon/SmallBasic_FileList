'  Click the Box Game

'  by EthanNetz



Start:



'  Constants:

'************

GW = 1100        '  Window Width

GH = 700         '  Window Height

Delay = 100      '  Box Display Delay

Time  = 20       '  Game Duration

Size  = 50       '  Clickable Box Dimension

Quantity = 50    '  # of Clickable Box

Zapped   = 0     '  # of Them Clicked



'  Box Position Limits:

'*********************

x = 0

y = 0

PosMin  = Size

PosWMax = GW - Size*2

PosHMax = GH - Size*2

PosWRange = PosWMax - PosMin

PosHRange = PosHMax - PosMin



'  Window Init:

'***************

GraphicsWindow.BackgroundColor = "Yellow"

GraphicsWindow.PenColor = "Red"

GraphicsWindow.FontSize = 15

GraphicsWindow.Width  = GW

GraphicsWindow.Height = GH

GraphicsWindow.Clear()



ZappedTxt = Shapes.AddText("Zapped: " + Zapped)

Shapes.Move(ZappedTxt  100,10)



TimerTxt  = Shapes.AddText("Timer: "  + Time)

Shapes.Move(TimerTxt   GW-200,10)



Program.Delay(1500)

Sound.PlayChimesAndWait()    '  Plays a sound to alert game has started

EmptyBuffer()                '  Certifies there's no previous mouse clicks left



'  Event Init:

'************

GraphicsWindow.MouseDown = MouseClick

GraphicsWindow.KeyDown   = Keyboard



Timer.Interval = 1000   '  1 second = 1000 miliseconds

Timer.Tick = Duration

Timer.Resume()          ' Resumes Timer



'  Clickable Box Creation Loop:

'******************************

For i = 1 To Quantity

  x[i] = Math.GetRandomNumber(PosWRange) + PosMin

  y[i] = Math.GetRandomNumber(PosHRange) + PosMin

  

  Rectangle[i] = Shapes.AddRectangle(Size,Size)

  Shapes.Move(Rectangle[i]  x[i],y[i])

  

  HitScan()   ' Checks if any mouse clicks hit a box

  Program.Delay(Delay)

EndFor



'  Wait Loops:

'*************

While Time > 0        '  Waits for Time to reach ZERO

  HitScan()           '  Keeps on processing mouse clicks

EndWhile



Timer.Pause()         '  Stops Timer

HiScore()             '  Registers HiScore



Looping = "True"

While Looping         '  Waits player to hit SPACE or ENTER keys

  Program.Delay(100)

EndWhile



Goto Start            '  Restarts game!



'  General Subs:

'***************

'--------------------------------------------------------------------------------------------------------------'

Sub HitScan

  

  While Stack.GetCount("Buffer") > 0 '  Checks weather exists a coordinate to process

    

    MX = Stack.PopValue("Buffer")    '  Retrieves mouse click coordinates

    MY = Stack.PopValue("Buffer")

    

    For l = 1 To i     '  Scans all boxes

      If MX > x[l] And MX < x[l]+Size And MY > y[l] And MY < y[l]+Size Then

        x[l] = ""      '  Erases Box's  Position

        y[l] = ""

        Shapes.Remove(Rectangle[l])

        Zapped = Zapped + 1

        Sound.PlayClick()

      EndIf

    EndFor

    

  EndWhile

  

  Shapes.SetText(ZappedTxt,  "Zapped: " + Zapped)

  

EndSub

'--------------------------------------------------------------------------------------------------------------'

Sub EmptyBuffer

  

  While Stack.GetCount("Buffer") > 0

    Stack.PopValue("Buffer")

  EndWhile

  

EndSub

'--------------------------------------------------------------------------------------------------------------'

Sub HiScore

  

  Record = Math.Max(Record  Zapped)

  GraphicsWindow.Title = "HiScore: " + Record

  

EndSub

'--------------------------------------------------------------------------------------------------------------'



'  Triggered Event Subs:

'***********************

'--------------------------------------------------------------------------------------------------------------'

Sub MouseClick

  

  Stack.PushValue("Buffer"  GraphicsWindow.MouseY)  '  Stores coordinates

  Stack.PushValue("Buffer"  GraphicsWindow.MouseX)

  

  If Time < 1 Then   '  Rings it's no use to click anymore! Time's up!!!

    Sound.PlayBellRing()

  EndIf

  

EndSub

'--------------------------------------------------------------------------------------------------------------'

Sub Duration

  

  If Time > 0 Then

    Time = Time - 1

  EndIf

  

  Shapes.SetText(TimerTxt,  "Timer: " + Time)

  

EndSub

'--------------------------------------------------------------------------------------------------------------'

Sub Keyboard

  

  Key = GraphicsWindow.LastKey

  

  If Key = "Escape" Then

    Sound.PlayChimeAndWait()

    Program.End()

  EndIf

  

  If Key = "Space" Or Key = "Return" Then

    Looping = "False"

  EndIf

  

EndSub

'--------------------------------------------------------------------------------------------------------------'


