
pageData[0] = "title=Main Menu;titleX=20;titleY=20;titleColor=Green;"
pageData[1] = "title=January;titleX=100;titleY=30;titleColor=Blue;"
pageData[2] = "title=February;titleX=450;titleY=100;titleColor=Purple;"
pageData[3] = "title=March;titleX=300;titleY=230;titleColor=Teal;"
pageData[4] = "title=April;titleX=170;titleY=230;titleColor=Olive;"
pageData[5] = "title=May;titleX=120;titleY=80;titleColor=Orange;"
pageData[6] = "title=June;titleX=360;titleY=170;titleColor=Yellow;"
pageData[7] = "title=July;titleX=240;titleY=90;titleColor=Aquamarine;"
pageData[8] = "title=August;titleX=90;titleY=340;titleColor=Lime;"
pageData[9] = "title=September;titleX=320;titleY=30;titleColor=SlateGray;"
pageData[10] = "title=October;titleX=130;titleY=240;titleColor=Indigo;"
pageData[11] = "title=November;titleX=270;titleY=160;titleColor=Goldenrod;"
pageData[12] = "title=December;titleX=310;titleY=100;titleColor=Red;"

GraphicsWindow.Left = 10
GraphicsWindow.Top = 0
GraphicsWindow.Width = Desktop.Width - 20
GraphicsWindow.Height = Desktop.Height - 64

numberOfPages = 12
numberOfButtons = numberOfPages + 2 'add Menu and Quit buttons
buttonYPosition = GraphicsWindow.Height - 50
buttonSpacing = 10
buttonSize = (GraphicsWindow.Width - numberOfButtons * buttonSpacing) / numberOfButtons
buttonNames = "0=MENU;1=JAN;2=FEB;3=MAR;4=APR;5=MAY;6=JUN;7=JUL;8=AUG;9=SEP;10=OCT;11=NOV;12=DEC;13=QUIT;"
GraphicsWindow.PenColor = "Black"
For i = 0 To numberOfButtons - 1
  GraphicsWindow.BrushColor = "White"
  GraphicsWindow.DrawRectangle(i*(buttonSize+buttonSpacing) + buttonSpacing / 2, buttonYPosition, buttonSize, GraphicsWindow.Height - buttonYPosition - 5)
  If (i = 0 Or i = numberOfButtons-1) Then
    GraphicsWindow.FontSize = 14
  Else
    GraphicsWindow.FontSize = 18
  EndIf  
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(i*(buttonSize+buttonSpacing) + buttonSpacing / 2 + 15, buttonYPosition + 10, buttonNames[i])
EndFor

currentPage = 0 'MainMenu
GraphicsWindow.BrushColor = "#55FF5555" 'semi-transparent
GraphicsWindow.PenColor = "Red"
GraphicsWindow.PenWidth = 8
currentPageRect = Shapes.AddRectangle(buttonSize + 6,  GraphicsWindow.Height - buttonYPosition + 1)
Shapes.Move(currentPageRect, currentPage * (buttonSize+buttonSpacing) + buttonSpacing / 2 - 3, buttonYPosition - 3)
GraphicsWindow.PenColor = "Black"
GraphicsWindow.PenWidth = 2

currentPage = 0
GraphicsWindow.MouseDown = OnMouseDown
Timer.Tick = OnTimer
Timer.Interval = 5000
Timer.Resume()

'main loop
notDone = "True" 
While (notDone = "True")
  
  If (currentPage = numberOfButtons - 1) Then
    Program.End()
  Else
    screenData = pageData[currentPage]
  EndIf
  
  'update the screen with the new information
  DrawPage()
  
  'wait for mouse event loop
  waitingForMouse = "True"
  While (waitingForMouse = "True")
    Program.Delay(100)
    SlideShowCounter = SlideShowCounter + 1
    'TextWindow.WriteLine(SlideShowCounter)
    If (SlideShowCounter > 100) Then 'wait 10 seconds after a mouseclick, before resuming timer
      Timer.Resume()
      SlideShowCounter = 0
    EndIf
  EndWhile
EndWhile

Sub OnMouseDown
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
  For j = 0 To numberOfButtons - 1
    If (my >= GraphicsWindow.Height-60 And mx >= j * (buttonSize+buttonSpacing) + buttonSpacing / 2 And mx <= j * (buttonSize+buttonSpacing) + buttonSpacing / 2 + buttonSize) Then
      currentPage = j
      Shapes.Animate(currentPageRect, currentPage * (buttonSize+buttonSpacing) + buttonSpacing / 2 - 3, buttonYPosition - 3,1000)
    EndIf
  EndFor
  waitingForMouse = "False"
  SlideShowCounter = 0
  Timer.Pause()
EndSub

Sub OnTimer
  If( currentPage < numberOfPages) Then
    currentPage = currentPage + 1
  Else
    currentPage = 1
  EndIf
  Shapes.Animate(currentPageRect, currentPage * (buttonSize+buttonSpacing) + buttonSpacing / 2 - 3, buttonYPosition - 3,1000)
  waitingForMouse = "False"
EndSub

Sub DrawPage
  'clear the current page but keep the buttons at the bottom
  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor
  GraphicsWindow.FillRectangle(0, 0, GraphicsWindow.Width, GraphicsWindow.Height - 61)
  
  'check for the data before trying to draw it
  If (screenData["title"] <> "") Then
    GraphicsWindow.BrushColor = screenData["titleColor"]
    GraphicsWindow.FontSize = 36
    GraphicsWindow.DrawText(screenData["titleX"], screenData["titleY"], screenData["title"])
  EndIf
  
EndSub
