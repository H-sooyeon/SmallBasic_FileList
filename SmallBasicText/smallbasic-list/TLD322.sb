GraphicsWindow.MouseDown = OnMouse       ' Define Mouse event

GraphicsWindow.Width = Desktop.Width

GraphicsWindow.Height = Desktop.Height

GraphicsWindow.Left = 0

GraphicsWindow.Top = 0

GraphicsWindow.BackgroundColor = "LightGreen"

GraphicsWindow.BrushColor = "Green"

GraphicsWindow.FillRectangle(30,30,200,60)

GraphicsWindow.FillRectangle(430,30,200,60)

GraphicsWindow.FillRectangle(830,30,90,60)

GraphicsWindow.BrushColor = "Red"

GraphicsWindow.FillRectangle(20,20,200,60)

GraphicsWindow.FillRectangle(420,20,200,60)

GraphicsWindow.FillRectangle(820,20,90,60)

GraphicsWindow.FontName = "Times New Roman"

GraphicsWindow.FontSize = 32

GraphicsWindow.FontBold = "True"

GraphicsWindow.BrushColor = "Gold"

GraphicsWindow.DrawText(30,30,"Stop  scroller")

GraphicsWindow.DrawText(430,30,"Edit  scroller")

GraphicsWindow.DrawText(830,30,"Quit")

GraphicsWindow.BrushColor = "Black"

Scroll = "This is a demo program, which demonstrates how to create a ''scroller''  and  ''3-D buttons''.                      "

GraphicsWindow.Title = Scroll

GraphicsWindow.DrawBoundText(20,120,1000,"This program demonstrates how to create a ''scroller'' and ''3-D buttons''.  Notice how the Start/Stop button text changes to display the appropriate function.  Also, notice the ''drop shadow'' beneath each button.  This is what gives the illusion that the buttons are ''hovering'' above the screen in ''3-D''.  The program also shows how to bring up a text window, and how to get information from it.  ")

Len = Text.GetLength(Scroll) 

Flag = 1 '  This is a variable that is used as a "flag".  It can be any type of  variable; text, numbers, etc....

' The term "flag" just means that it is only assigned one of two values.  These values are typically "0" and "1",

' though they can be any values which have meaning for your program, such as "on/off", "yes/no", "up/down",

' "set/reset", "true/false", etcetera.  For example, if you wrote a program with sounds, and you wanted the

' ability to turn them on or off, you might create a flag variable called "Sounds", and then set it's value to 1 for

' "on" or 0 for "off".  While you could simply use the values "on" and "off", the advantage of 1 and 0 is that they

' can be easily "toggled" or alternated back and forth using a special assignment statement called a "toggle".

' See the sub "OnMouse" for an example of this statement.



Start:

If Flag = 1 then 

  Scroller()

endif 

Goto Start





Sub OnMouse 

  If Mouse.MouseX > 810 then

    Program.End()

  EndIf 

  If Mouse.MouseX > 410 then

    EditScroll() 

  endif 

  GraphicsWindow.BrushColor = "Red"

  GraphicsWindow.FillRectangle(20,20,200,60)

  GraphicsWindow.BrushColor = "Gold"

  Flag = 1 - Flag ' This is the "toggle" statement mentioned earlier.  Each time it is executed, it "toggles"

  ' or "flips" the value of Flag from o to 1 or vice-versa.  To understand how it works, let's suppose that Flag

  ' starts out with the value 0.  Then, when this statement executes, it subtracts Flag ( 0 ) from 1, leaving 1,

  ' and then stores that value back in Flag.  The next time the statement is executed, it again subtracts Flag 

  ' from 1, except that this time, Flag starts out as 1, and of course, 1 - 1 = 0, so this 0 is then stored  back in

  ' Flag.   To use the "sounds" example mentioned earlier,  we could setup a sub-routine to run each time a

  ' certain key, such as "S", is pressed.  The sub then executes a toggle statement like;  "Sounds = 1 - Sounds",

  ' which of course "toggles" the value of Sounds from 0 to 1 and back.  Now, we just need an "if" statement,

  ' much like the one below, to check whether Sounds equals 1 or 0 ( "on" or "off" ).

   

  If Flag = 1 then 

    GraphicsWindow.DrawText(30,30,"Stop  scroller")

  else 

    GraphicsWindow.DrawText(30,30,"Start scroller")

  endif 

endsub 





Sub Scroller  

  Part1 = Text.GetSubText(Scroll,1,1)   '  "snip off" first letter / character

  Part2 = Text.GetSubText(Scroll,2,Len) '   get the "leftover" text

  Scroll = Part2 + Part1                '   tack the first character onto the end

  GraphicsWindow.Title = Scroll         '   display new scroll text

  Program.Delay(50)

EndSub 



Sub EditScroll

  TextWindow.BackgroundColor = "DarkBlue"

  TextWindow.ForegroundColor = "Yellow"

  TextWindow.Clear()

  TextWindow.Show()

  TextWindow.WriteLine("Type in your text, then hit Enter")

  Scroll = TextWindow.Read() + "                                         "

  TextWindow.Hide ()

  Flag = 0

EndSub 


