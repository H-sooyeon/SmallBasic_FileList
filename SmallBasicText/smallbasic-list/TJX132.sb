' Match Game

InitializeProgram()



'Start Events

Timer.Interval = 1000

Timer.Pause()

Timer.Tick = TickSub

GraphicsWindow.MouseDown = MouseDownSub

Controls.ButtonClicked = ButtonClickedSub



'============================================================

'GAME LOOP

'============================================================



While ("True")

  'Handle all events

  HandleButtonClicked()

  HandleMouseDown()

  HandleKeyDown()

  HandleTick()

  

  'Other game logic here

  

  'Delay to no mash cpu while waiting for input

  Program.Delay(20)

EndWhile



'============================================================

'SUBROURINES

'============================================================



Sub InitializeProgram

  'graphics window

  GraphicsWindow.Width = 825

  GraphicsWindow.Height = 530

  GraphicsWindow.BackgroundColor = "LightSkyBlue"

  'initialize box locations with covers

  Cover = "C:\New folder\gold.png"

  BoxW = 150

  BoxH = 100

  x = 5

  y = 5

  For I = 1 To 20

    BoxX[I] = x

    BoxY[I] = y

    GraphicsWindow.DrawResizedImage(Cover, BoxX[I], BoxY[I], BoxW, BoxH)

    x = x + BoxW + 5

    If (x > 4 * BoxW) Then

      x = 5

      y = y + BoxH + 5

    EndIf

  EndFor

  

  'load images

  Photo[1] = "C:\Users\John\Desktop\New folder (2)\gg.jpg"

  Photo[2] = "C:\Users\John\Desktop\New folder (2)\ggg.jpg"

  Photo[3] = "C:\Users\John\Desktop\New folder (2)\gggg.jpg"

  Photo[4] = "C:\Users\John\Desktop\New folder (2)\ggggg.jpg"

  Photo[5] = "C:\Users\John\Desktop\New folder (2)\gggggg.jpg"

  Photo[6] = "C:\Users\John\Desktop\New folder (2)\ggggggg.jpg"

  Photo[7] = "C:\Users\John\Desktop\New folder (2)\gggggggg.jpg"

  Photo[8] = "C:\Users\John\Desktop\New folder (2)\ggggggggg.jpg"

  Photo[9] = "C:\Users\John\Desktop\New folder (2)\gggggggggg.jpg"

  Photo[10] ="C:\Users\John\Desktop\New folder (2)\ggggggggggg.jpg"

  

  'headings

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontBold = "false"

  GraphicsWindow.FontSize = 20

  Heading[1] = Shapes.AddText("Player 1")

  Heading[2] = Shapes.AddText("Player 2")

  Shapes.Move(Heading[1], 670, 5)

  Shapes.Move(Heading[2], 670, 70)

  

  'scores

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.FillRectangle(670, 35, 100, 25)

  GraphicsWindow.FillRectangle(670, 100, 100, 25)

  GraphicsWindow.BrushColor = "Black"

  Score[1] = 0

  Score[2] = 0

  ScoreDisplay[1] = Shapes.AddText(Score[1])

  ScoreDisplay[2] = Shapes.AddText(Score[2])

  Shapes.Move(ScoreDisplay[1], 715, 35)

  Shapes.Move(ScoreDisplay[2], 715, 100)

  

  'Draw message area

  GraphicsWindow.BrushColor = "Yellow"

  GraphicsWindow.FillRectangle(630, 250, 185, 45)

  GraphicsWindow.PenColor = "Black"

  GraphicsWindow.PenWidth = 2

  GraphicsWindow.DrawRectangle(630, 250, 185, 45)

  GraphicsWindow.BrushColor = "Blue"

  GraphicsWindow.FontBold = "false"

  GraphicsWindow.FontSize = 18

  Message = "Game Stopped"

  MessageX = 30

  MessageArea = Shapes.AddText(Message)

  DisplayMessage()

  

  'define buttons

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 16

  StartStopButton = Controls.AddButton("Start Game", 630, 410)

  OptionsButton = Controls.AddButton("Change Options", 630, 450)

  ExitButton = Controls.AddButton("Exit", 630, 490)

  

  'Default Options

  NumberPlayers = 2

  PlayAlone = "true"

  Difficulty = 1

  SetTitle()

  GameStatus = "Stopped"

  

  For I = 1 To 10

    GraphicsWindow.DrawResizedImage(Photo[I], BoxX[I], BoxY[I], BoxW, BoxH)

  EndFor

  

  N = 20

  NRandomIntegers()

  PhotoIndex = NIntegers

  For I = 1 To 20

    If (PhotoIndex[I] > 10) Then 

      PhotoIndex[I] = PhotoIndex[I] - 10

    EndIf

  EndFor

  GraphicsWindow.DrawResizedImage(Photo[PhotoIndex[I]], BoxX[I], BoxY[I], BoxW, BoxH)

EndSub



Sub DisplayMessage

  Shapes.Move(MessageArea, 630 + MessageX, 260)

  Shapes.SetText(MessageArea, Message)

EndSub



Sub SetTitle

  If (NumberPlayers = 1) Then

    GraphicsWindow.Title = "Match Game - 1 Player, "

    If (PlayAlone) Then

      GraphicsWindow.Title = GraphicsWindow.Title + "Play Alone"

    Else

      GraphicsWindow.Title = GraphicsWindow.Title + "Play Computer"

    EndIf

  Else

    GraphicsWindow.Title = "Match Game - 2 Players"

  EndIf

EndSub



Sub SetOptions

  GraphicsWindow.Hide()

  TextWindow.Show()

  TextWindow.Title = "Match Game"

  TextWindow.CursorLeft = 3

  TextWindow.CursorTop = 3

  TextWindow.WriteLine("MATCH GAME OPTIONS")

  TextWindow.WriteLine("")

  GetPlayers:

  TextWindow.CursorLeft = 3

  TextWindow.WriteLine("With one player, you play alone or against the computer.")

  TextWindow.CursorLeft = 3

  TextWindow.WriteLine("With two players, you play against a friend.")

  TextWindow.CursorLeft = 3

  TextWindow.Write("How many players do you want (1 or 2)? ")

  NumberPlayers = TextWindow.ReadNumber()

  If (NumberPlayers < 1 Or NumberPlayers > 2) Then

    Goto GetPlayers

  EndIf

  If (NumberPlayers = 1) Then

    Heading[1] = "You"

    GetPlayAlone:

    TextWindow.WriteLine("")

    TextWindow.CursorLeft = 3

    TextWindow.WriteLine("You can play alone or play the computer.")

    TextWindow.CursorLeft = 3

    TextWindow.Write("Make your choice (1-Play Alone, 2-Play Computer)? ")

    T = TextWindow.ReadNumber()

    If (T < 1 Or T > 2) Then

      Goto GetPlayAlone

    EndIf

    If (T = 1) Then

      PlayAlone = "true"

      Heading[2] = "Guesses"

    Else

      PlayAlone = "false"

      Heading[2] = "Computer"

      GetDifficulty:

      TextWindow.WriteLine("")

      TextWindow.CursorLeft = 3

      TextWindow.WriteLine("You can set the game difficulty.")

      TextWindow.CursorLeft = 3

      TextWindow.Write("Make your choice (1-Easiest, 2-Easy, 3-Hard, 4-Hardest)? ")

      Difficulty = TextWindow.ReadNumber()

      If (Difficulty < 1 Or Difficulty > 4) Then

        Goto GetDifficulty

      EndIf

    EndIf

  Else

    Heading[1] = "Player 1"

    Heading[2] = "Player 2"

  EndIf

  SetTitle()

  TextWindow.Hide()

  GraphicsWindow.Show()

EndSub



Sub NRandomIntegers

  'Order all elements initially

  For I = 1 To N

    NIntegers[I] = I

  EndFor

  'J is the number of integers remaining

  For J = N To 2 Step -1

    I = Math.GetRandomNumber(J)

    T = NIntegers[J]

    NIntegers[J] = NIntegers[I]

    NIntegers[I] = T

  EndFor

EndSub



Sub StartGame

  GameStatus = "Playing"

  'clear screen

  GraphicsWindow.BrushColor = GraphicsWindow.BackgroundColor

  GraphicsWindow.FillRectangle(0, 0, 625, 530)

  Score[1] = 0

  Score[2] = 0

  PhotosRemaining = 20

  N = 20

  NRandomIntegers()

  PhotoIndex = NIntegers

  For I = 1 To 20

    If (PhotoIndex[I] > 10) Then 

      PhotoIndex[I] = PhotoIndex[I] - 10

    EndIf

    PhotoFound[I] = "false"

    GraphicsWindow.DrawResizedImage(Cover, BoxX[I], BoxY[I], BoxW, BoxH)

  EndFor

  PlayerNumber = 1

  ChoiceNumber = 1

  If (NumberPlayers = 1) Then

    Message = "Pick a Box"

    MessageX = 45

  Else

    Message = "Player 1, Pick a Box"

    MessageX = 10

  EndIf

  DisplayMessage()

  'reset buttons

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 16

  Controls.SetButtonCaption(StartStopButton, "Stop Game")

  Controls.HideControl(OptionsButton)

  Controls.HideControl(ExitButton)

  CanClick = "true"

EndSub



Sub StopGame

  'restore buttons

  GameStatus = "Stopped"

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 16

  Controls.SetButtonCaption(StartStopButton, "Start Game")

  Controls.ShowControl(OptionsButton)

  Controls.ShowControl(ExitButton)

  CanClick = "false"

  DisplayMessage()

EndSub



Sub ShowPhotoAndCheck

  'show image behind selected Photo box (BoxNumber)

  GraphicsWindow.DrawResizedImage(Photo[PhotoIndex[BoxNumber]], BoxX[BoxNumber], BoxY[BoxNumber], BoxW, BoxH)

  PhotoFound[BoxNumber] = "true"

  If (ChoiceNumber = 1) Then

    Choice[1] = BoxNumber

    ChoiceNumber = 2

    If (NumberPlayers = 2) Then

      Message = "Player " + PlayerNumber + ", Pick Another"

      MessageX = 5

      DisplayMessage()

    Else

      'one player logic

    EndIf

  Else

    CanClick = "false"

    Timer.Resume()

  EndIf

EndSub



'============================================================

'EVENT SUBROURINES

'============================================================



Sub TickSub

  TickEvent = "True"

EndSub



Sub MouseDownSub

  MouseDownEvent = "True"

EndSub



Sub ButtonClickedSub

  ButtonClickedEvent = "True"

EndSub



Sub KeyDownSub 'COMMENT Not Used?

  KeyDownEvent = "True"

EndSub



'============================================================

'EVENT HANDLER SUBROURINES - CALLED FROM GAME LOOP

'============================================================



Sub HandleButtonClicked

  If (ButtonClickedEvent) Then

    TextWindow.WriteLine("HandleButtonClicked")

    ButtonClickedEvent = "False"

    B = Controls.LastClickedButton

    If (GameStatus = "Stopped") Then

      If (B = ExitButton) Then

        Program.End()

      ElseIf (B = OptionsButton) Then

        SetOptions()

      ElseIf (B = StartStopButton) Then

        StartGame()

      EndIf

    ElseIf (GameStatus = "Playing") Then

      If (B = StartStopButton) Then

        'stop program

        Message = "Game Stopped"

        MessageX = 30

        StopGame()

      EndIf

    EndIf

  EndIf

EndSub



Sub HandleMouseDown

  If (MouseDownEvent) Then

    TextWindow.WriteLine("HandleMouseDown")

    MouseDownEvent = "False"

    If (GameStatus = "Playing" And CanClick) Then

      'find which box was clicked

      x = GraphicsWindow.MouseX

      y = GraphicsWindow.MouseY

      BoxNumber = 0

      For I = 1 To 20

        If (x > BoxX[I] And x < BoxX[I] + BoxW) Then

          If (y > BoxY[I] And y < BoxY[I] + BoxH) Then

            BoxNumber = I

            Goto GotIt

          EndIf

        EndIf

      EndFor

      GotIt:

      If (BoxNumber <> 0) Then

        'if already clicked then exit

        If (PhotoFound[BoxNumber]) Then

          Goto LeaveMouseDownSub

        EndIf

        ShowPhotoAndCheck()

      EndIf

    EndIf

    LeaveMouseDownSub:

  EndIf

EndSub



Sub HandleKeyDown

  If (KeyDownEvent) Then

    TextWindow.WriteLine("HandleKeyDown")

    KeyDownEvent = "False"

    K = Text.ConvertToUpperCase(GraphicsWindow.LastKey)

    If (GameStatus = "Stopped") Then

      If (K = "E") Then

        Program.End()

      ElseIf (K = "C") Then

        SetOptions()

      ElseIf (K = "S") Then

        StartGame()

      EndIf

    EndIf

  EndIf

EndSub



Sub HandleTick

  If (TickEvent) Then

    TextWindow.WriteLine("HandleTick")

    TickEvent= "False"

    Choice[2] = BoxNumber

    ChoiceNumber = 1

    If (PhotoIndex[Choice[1]] = PhotoIndex[Choice[2]]) Then

      'a match

      Sound.Stop(Program.Directory + "\tada.wav")

      Sound.PlayAndWait(Program.Directory + "\tada.wav")

      'blank out boxes

      GraphicsWindow.BrushColor = "White"

      GraphicsWindow.FillRectangle(BoxX[Choice[1]], BoxY[Choice[1]], BoxW, BoxH)

      GraphicsWindow.FillRectangle(BoxX[Choice[2]], BoxY[Choice[2]], BoxW, BoxH)

      Score[PlayerNumber] = Score[PlayerNumber] + 1

      PhotosRemaining = PhotosRemaining - 2

      If (PhotosRemaining = 0) Then

        Sound.Stop(Program.Directory + "\wow.wav")

        Sound.Play(Program.Directory + "\wow.wav")

        If (NumberPlayers = 2) Then

          If (Score[1] > Score[2]) Then

            Message = "Player 1 Wins!"

            MessageX = 30

          ElseIf (Score[2] > Score[1]) Then

            Message = "Player 2 Wins!"

            MessageX = 30

          Else

            Message = "It's a Tie!"

            MessageX = 45

          EndIf

        Else

          'one player logic

        EndIf

        StopGame()

      EndIf

      'another turn?

      If (PhotosRemaining <> 0) Then

        If (NumberPlayers = 2) Then

          Message = "Player " + PlayerNumber + ", Pick Again"

          MessageX = 10

          DisplayMessage()

        Else

          'one player logic

        EndIf

      EndIf

    Else

      'no match

      Sound.Stop(Program.Directory + "\boing.wav")

      Sound.PlayAndWait(Program.Directory + "\boing.wav")

      PhotoFound[Choice[1]] = "false"

      PhotoFound[Choice[2]] = "false"

      GraphicsWindow.DrawResizedImage(Cover, BoxX[Choice[1]], BoxY[Choice[1]], BoxW, BoxH)

      GraphicsWindow.DrawResizedImage(Cover, BoxX[Choice[2]], BoxY[Choice[2]], BoxW, BoxH)

      'swap players

      If (NumberPlayers = 2) Then

        If (PlayerNumber = 1) Then

          PlayerNumber = 2

        Else

          PlayerNumber = 1

        EndIf

        Message = "Player " + PlayerNumber + ", Pick a Box"

        MessageX = 10

        DisplayMessage()

      Else

        'one player logic

      EndIf

    EndIf

    CanClick = "true"

  EndIf

EndSub 








