' Pyramid 0.1
' Copyright (c) 2012 Nonki Takahashi.  All rights reserved.
'
' History : 
' 0.1 2012/07/09 Created.
'
s = 30      ' size of cube [pixels]
r = s / 2   ' margin for right side [pixels]
t = r / 2   ' margin for top side [pixels]
h = 10      ' height (steps) of pyramid
xo = 250
yo = 315
GraphicsWindow.BackgroundColor = "Wheat"
GraphicsWindow.Title = "Pyramid 0.1"
ShowCubes()

Sub AddCube
  ' param s - size of cube [pixels]
  ' param r - margin for right side [pixels]
  ' param t - margin for top side [pixels]
  ' param h - height (steps) of pyramid
  ' return cube - shapes for cube

  ' Add right back triangle
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "#F08528"       ' H=27, S=87, L=55
  x1 = s
  y1 = 0
  x2 = s + r
  y2 = t * -1
  x3 = s
  y3 = s * -1
  cube["oRightF"] = Shapes.AddTriangle(x1, y1, x2, y2, x3, y3)

  ' Add (right) bottom edge
  GraphicsWindow.PenWidth = 2
  GraphicsWindow.PenColor = "#D2680F"         'H=27, S=87, L=44
  cube["oBottom"] = Shapes.AddLine(x1, y1, x2, y2)

  ' Add right front triangle
  GraphicsWindow.PenWidth = 0
  x1 = s + r
  y1 = (s + t) * -1 
  cube["oRightB"] = Shapes.AddTriangle(x1, y1, x2, y2, x3, y3)

  ' Add vertical (back) edge
  GraphicsWindow.PenWidth = 2
  cube["oVert"] = Shapes.AddLine(x1, y1, x2, y2)

  ' Add top back triangle
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "#F7C091"       ' H=27, S=87, L=77
  x2 = r
  y2 = (s + t) * -1
  cube["oTopB"] = Shapes.AddTriangle(x1, y1, x2, y2, x3, y3)
  
  ' Add right (top) and horizontal (back) edges
  GraphicsWindow.PenWidth = 2
  cube["oRight"] = Shapes.AddLine(x3, y3, x1, y1)
  cube["oHorz"] = Shapes.AddLine(x1, y1, x2, y2)
  
  ' Add top front triangle
  GraphicsWindow.PenWidth = 0
  x1 = 0
  y1 = s * -1
  cube["oTopF"] = Shapes.AddTriangle(x1, y1, x2, y2, x3, y3)
  
  ' Add left (top) edge
  GraphicsWindow.PenWidth = 2
  cube["oLeft"] = Shapes.AddLine(x1, y1, x2, y2)
  
  ' Add front rectangle
  GraphicsWindow.BrushColor = "SandyBrown"    ' H=27, S=87, L=66
  cube["oFront"] = Shapes.AddRectangle(s, s)
EndSub
  
Sub ShowCubes
  ' param s - size of cube [pixels]
  ' param r - margin for right side [pixels]
  ' param t - margin for top side [pixels]
  ' param h - height (steps) of pyramid
  For iz = h To 1 Step -1
    For iy = 1 To iz
      For ix = 1 To iz
        AddCube()
        x = xo - iy * r + (ix - 1) * s + (h - iz) * (s - r) / 2
        y = yo + iy * t - (h - iz) * (s - t / 2)
        Shapes.Move(cube["oRightB"], x, y)
        Shapes.Move(cube["oRightF"], x, y)
        Shapes.Move(cube["oBottom"], x, y)
        Shapes.Move(cube["oVert"], x, y)
        Shapes.Move(cube["oTopB"], x, y)
        Shapes.Move(cube["oTopF"], x, y)
        Shapes.Move(cube["oRight"], x, y)
        Shapes.Move(cube["oHorz"], x, y)
        Shapes.Move(cube["oLeft"], x, y)
        Shapes.Move(cube["oFront"], x, y - s)
      EndFor
    EndFor
  EndFor
EndSub
