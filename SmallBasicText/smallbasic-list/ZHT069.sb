'=============================
'Data Extension Initialisation
DataExtension.Init()              
'=============================


'====================================================================
'Init Graphic Window
GW=925
GameWindow.Width = GW
GameWindow.Height = 422
GameWindow.CanResize = "False"
GameWindow.Theme = "ExpressionLight"
GameWindow.Show()

'GUI Controls
VideoBox = GameControls.AddRectangle(5,5,450,300)
PlayListBox = GameControls.AddRectangle(460,5,GW-475,380)
GameControls.SetColors(VideoBox,"White","Black",1)
GameControls.SetColors(PlayListBox,"White","Black",1)
Scroller = GameControls.AddSlider(0,GW-210,182.5,380,25)  '  Scroller X position is changed by Rotation 
GameControls.Rotate(Scroller,90)
'File Extension list
Musicext=".mp3:.wma:.mp4:.avi:.wmv:.mpg"
'Buttons
Show_PlayList = GameControls.AddButton(">>",427,360,30,25)
Hide_PlayList = GameControls.AddButton("<<",427,360,30,25)
Play_Button = GameControls.AddButton("Play",140,345,60,40)
Pause_Button = GameControls.AddButton("Pause",205,345,60,40)
Next_Button = GameControls.AddButton("Next",270,345,60,40)
Prev_Button = GameControls.AddButton("Prev",75,345,60,40)
Stop_Button = GameControls.AddButton("Stop",10,345,60,40)
Seek_Bar = GameControls.AddSlider(0,10,315,395,22)
Volume_Control_Bar = GameControls.AddSlider(0,350,345,75,22)

'Time text box
Time_Box = GameControls.AddTextBox("Text",410,315,45,22)

'Register Sub
  GameControls.RegisterMouseDownEvent(Play_Button,"Play_Song")
  GameControls.RegisterMouseDownEvent(Next_Button,"Play_Next")
  GameControls.RegisterMouseDownEvent(Prev_Button,"Play_Prev")
  GameControls.RegisterMouseDownEvent(Stop_Button,"Stop_Song")
  GameControls.RegisterMouseDownEvent(Pause_Button,"Pause")
  GameControls.RegisterMouseDownEvent(Hide_PlayList,"Hide")
  GameControls.RegisterMouseDownEvent(Show_PlayList,"Show")
  GameControls.RegisterMouseDownEvent(Seek_Bar,"Slider_Move")
  GameControls.RegisterValueChangedEvent(Volume_Control_Bar,"Volume_Set")
  GameControls.RegisterValueChangedEvent(Scroller,"Scroll")
  
'Sub To Call On Window load
Open_Files()
'=====================================================================

'Create Media Play Control
Player = Media.CreateMediaPlayer(Files_Array[1],7,7,446,296)  '  Open first music file

'=====================================================================
'Loading Video and Sound File into an array
Sub Open_Files
  start:
  Dir = Dialogs.AskForDirectory()  ' Select a directory that include music files
  Files_Array = FilePlus.GetAllFiles(Dir)
  Total_Files = Array.GetItemCount(Files_Array)
  For i=1 To Total_Files
     CurrentExt=FilePlus.GetFileExtension(Files_Array[i])
    If  text.IsSubText(Musicext,CurrentExt)="True" then
        MM=MM+1
        Files_Array[MM]=Files_Array[i]
      Else
        Files_Array[i]=" "  
    EndIf
  EndFor
  GameWindow.Title = "Small Music Player - By Amir   "+Dir
  Song_Id=1 'First file To Play
  CurrentExt= FilePlus.GetFileExtension(Files_Array[Song_Id])
  If  text.IsSubText(Musicext,CurrentExt)="True" then
  Else 
    Dialogs.ShowMessageBox("Please Select Media Directory First","No Files","OK","Error")
    Goto Start
  EndIf
  Add_List()
  SetVolume()
EndSub
'=========================================================================================

'=========================================================================================
'Play First song
Sub Play_Song
  If GameControls.GetText(Pause_Button) = "Paused" Then
    Media.Resume(Player)
  Else
   Media.stop(Player)
   Media.SetMedia(Player, Files_Array[i])  '  set  new music file
   GameControls.SetValue(Seek_Bar,Media_Position/10)               '  slider =0
   Media.Play(Player)
 EndIf
 GameControls.SetText(Play_Button,"Playing")
 GameControls.SetText(Pause_Button,"Pause")
 EndSub
 '=========================================================================================
 
 '=========================================================================================
 'Play next song
 Sub Play_Next
   Media.Stop(Player)
   Song_Id = Song_Id + 1
   If Song_Id > Total_Files Then
      Song_Id = Total_Files
    EndIf
   Media.SetMedia(Player, Files_Array[Song_Id])   '  Song_ID is  already +1 
   Scroll() 
   GameControls.SetValue(Seek_Bar,0)  '  progress bar =0
   Media.Play(Player)
   Scroller_Pos = Scroller_Pos + 1
   NNM=math.Round(Scroller_Pos-0.5)
   GameControls.SetValue(Scroller,NNM/10)
   GameControls.SetText(Play_Button,"Playing")
   GameControls.SetText(Pause_Button,"Pause")
 EndSub
 '===========================================================================================
 
 '===========================================================================================
 'Play previous song
 Sub Play_Prev
   Media.stop(Player)
   Song_Id = Song_Id - 1
   If Song_Id < 1 Then
     Song_Id = 1
   EndIf
   Media.SetMedia(Player, Files_Array[Song_Id])
   Scroll() 
   GameControls.SetValue(Seek_Bar,0)  '  progress bar =0
   GameControls.SetValue(Seek_Bar,0)
   Media.Play(Player)
   Scroller_Pos = Scroller_Pos - 1
   NNM=math.Round(Scroller_Pos-0.5)
   GameControls.SetValue(Scroller,NNM/10)
   GameControls.SetText(Play_Button,"Playing")
   GameControls.SetText(Pause_Button,"Pause")
EndSub 
'============================================================================================
     
'============================================================================================
'Stop Current Playing Song
Sub Stop_Song
  Media.stop(Player)
  GameControls.SetText(Play_Button,"Play")
  GameControls.SetText(Pause_Button,"Pause")
 EndSub
'============================================================================================
'Volume Settings
Sub Volume_Set
  Vol_Value = GameControls.GetValue(Volume_Control_Bar)
  Media.SetVolume(Player,Vol_Value/100)
  EndSub

'============================================================================================
'Pause Playing song
Sub Pause
  GameControls.SetText(Pause_Button,"Paused")
  Media.Pause(Player)
  GameControls.SetText(Play_Button,"Play")
EndSub

'============================================================================================
'Hide PlayList
Sub Hide
  For i = GW To 460 Step -5
    GameWindow.Width = i
    EndFor
  GameControls.SetVisibility(Hide_PlayList,"False")
  EndSub
'Show PlayList
Sub Show
  For i = 460 To 915 Step 5
    GameWindow.Width = i
    EndFor
  GameControls.SetVisibility(Hide_PlayList,"True")
EndSub
'============================================================================================
'Set Song possition
Sub Slider_Move
  Slider_Position = GameControls.GetValue(Seek_Bar)
  Slider_Time_Position = (Slider_Position*Media.GetLength(Player))/100
  Media.SetPosition(Player,Slider_Time_Position)
  EndSub
'============================================================================================
'Set Volume level at 50%
Sub SetVolume
Media.SetVolume(Player,50)
GameControls.SetValue(Volume_Control_Bar,50/10)
EndSub
'============================================================================================
'Prepare Playlist Contents
Sub Add_List
 For i=1 To 24
      Song_name[i]= GameControls.AddText(FilePlus.GetFileName(Files_Array[i])+FilePlus.GetFileExtension(Files_Array[i]),465,10+15*(i-1),"Arial",15,"False","False")
 EndFor 
   GameControls.SetColors(Song_name[Song_Id],"red","red",2) 
EndSub

Sub Scroll
    NN=math.Round(GameControls.GetValue(Scroller)+0.5)
    For i=1 To 24
      GameControls.SetText(Song_name[i],FilePlus.GetFileName(Files_Array[i+NN])+FilePlus.GetFileExtension(Files_Array[i+NN]))
      If Song_Id=i+NN Then 
        GameControls.SetColors(Song_name[i],"red","red",2)
      Else
        GameControls.SetColors(Song_name[i],"Black","Black",2)
      endif   
    EndFor
EndSub
'============================================================================================  
'Seek Bar update and next media on end
While "True"
    Media_Position= (Media.GetPosition(Player)*100)/Media.GetLength(Player) ' ##.# %
    GameControls.SetValue(Seek_Bar,Media_Position/10)            ' #.## %
    Min = Math.Floor(Media.GetPosition(Player)/1000/60)        ' ##  min
    Secs = Math.Floor(Math.Remainder(Media.GetPosition(Player)/1000,60))  ' ## sec
    GameControls.SetText(Time_Box,Min+" : "+Secs+"")
    If Media_Position>=99.9 then                               '  End point  
       Play_Next() 
     EndIf
endwhile
