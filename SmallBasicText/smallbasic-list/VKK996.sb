'  Shooting at flying bee 

  Init_View()
  Get_Player()
  
  GraphicsWindow.KeyDown=Move_Player
  Timer.Tick=Ontick
  Timer.Interval= 500
  
Sub  Move_Player 
     a=text.GetCharacterCode(GraphicsWindow.LastKey)
     If  a=76 or a=82 Then         '   Left;76   Right ;82 
       rotate()
     elseIf a=68 or a=85 Then     '     Down: 68    Up:  85
       FB_Move()
     elseif a=83 then             '    Space;83
       SN=SN+1
       shoot()
     endif 
EndSub

Sub Rotate    ' rotate facing direction 
     SN=0
     DR=(a-79)/3  ' +1 or -1  
     Rdeg=Rdeg+DR
     Shapes.Rotate(spd,Rdeg)
Endsub  

Sub FB_Move   'Forward/ Backward  Moving
    SN=0
    DXY=(a-76.5)/8.5        'DXY=-1 or +1  
    ddx=ddx+DXY*math.sin(Rdeg/180*Math.pi)
    ddy=ddy-DXY*math.cos(Rdeg/180*Math.pi)
    nextx=startx+ddX-28     ' shift x
    nexty=starty+ddY-30     ' shift  y
    Shapes.animate(spd,nextx,nexty,400)
  
EndSub

Sub shoot     '  missile shooting
    mis_time1[SN]=Clock.ElapsedMilliseconds   '  shooted time
    Shapes.ShowShape(mis[SN])
    Shapes.Move(mis[SN],nextx,nexty)          ' shooting start position 
    '   Length  from Bee to Stag beetle 
    L2=math.SquareRoot((Beex-nextx)*(Beex-nextx)+(Beey-nexty)*(Beey-nexty))
    mis_EndX=nextX+L2*(Math.sin(Rdeg/180*Math.pi))   ' target point X
    mis_EndY=nextY-L2*(Math.cos(Rdeg/180*Math.pi))   ' target point Y
    Shapes.Animate(mis[SN],mis_EndX,mis_EndY,800)
EndSub

Sub Init_View
     GW=800
     GH=700
     XC=math.Round(GW/2+0.5)
     YC=math.Round(GH/2+0.5)
     GraphicsWindow.Left=100
     GraphicsWindow.Top=0
     GraphicsWindow.Width=GW 
     GraphicsWindow.Height=GH 
     GraphicsWindow.BackgroundColor="#8D61B463"
     GraphicsWindow.Show()
     '  show fruits  
     BackWall=ImageList.LoadImage ("http://park18.wakwak.com/~osyare/fruits/all-fruits2.gif")
     GraphicsWindow.DrawImage(BackWall,XC/2,YC)
     '  show message 
     GraphicsWindow.BrushColor="Yellow"
     GraphicsWindow.FontSize=20
     msg1=Shapes.AddText(" Foward/Backward: Up/down arrow ")
     Shapes.Move(msg1,20,5)
     msg2=Shapes.AddText("Face rotation: Left/right arrow , Shooting: space key")
     Shapes.Move(msg2,20,25)
Endsub  

Sub Get_Player
    L1=math.Round(GH/2-50+0.5)    ' distance
    startx=XC
    startY=YC+L1
    '       spider or Stag beetle 
    'Path="http://www.azbugman.com/images/spider.png"  Spider : too big 
    Path="http://park18.wakwak.com/~osyare/musi2/kuwagata-f.png" 
    player=ImageList.LoadImage(Path)
    spd=shapes.AddImage(player)
    Shapes.Animate(spd,startx-28,starty-30,900)  ' center shift
    '       bee
    Path2=" http://park18.wakwak.com/~osyare/musi2/hati-js.png"
    bee=ImageList.LoadImage(Path2)    
    bee1=shapes.AddImage(bee)
    beex=XC
    beey=YC-150
    Shapes.animate(bee1,XC,YC,750)
    '      shooting missile
    GraphicsWindow.BrushColor="Yellow"
    For i= 1 To 100 
      GraphicsWindow.PenColor="Yellow"
    mis[i]=shapes.AddEllipse(15,15)
    Shapes.HideShape(mis[i])
    endfor
EndSub

Sub ontick
   ' delete missile if  time is over 800-milliseconds
    For i=1 To 100
      mis_time2[i]=Clock.ElapsedMilliseconds
      If mis_time2[i]-mis_time1[i]>800 then
        Shapes.HideShape(mis[i])  
      EndIf
    EndFor
  
  ' random moving of Bee
   PM=(10-Math.GetRandomNumber(20))/10
   R_Rad=Math.GetRandomNumber(90)/180*Math.pi
    BeeX=nextX+PM*GW/2*math.sin(R_Rad)
    BeeY=nextY-GH*math.cos(R_Rad)
    
    If BeeX<0 then 
      BeeX=XC/2
      If  GW<BeeX Then 
      BeeX=3*XC/2
      endif
    EndIf
    If BeeY<0 or GH<BeeY Then 
      BeeY=YC/2
      If nexty<YC Then
        BeeY=3*YC/2
      EndIf
    EndIf
    Shapes.Animate(bee1,BeeX,BeeY,800)
EndSub
