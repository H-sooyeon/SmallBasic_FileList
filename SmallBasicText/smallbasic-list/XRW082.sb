'Matthew L. Parets; Input routine with prompt and word wrap at the end of the line. 
'Showing off a use for Textwindow.Readkey()

prompt = "Enter: "
InputWrap()
TextWindow.Clear()
TextWindow.WriteLine(input)

Sub InputWrap
  TextWindow.Write(prompt)
  a = ""
  while a <> Text.GetCharacter(27) And a <> Text.GetCharacter(13)
    a = textwindow.readkey()
    If a = Text.GetCharacter(8) Then
      If Text.GetLength(input) >= 1 then
        If TextWindow.CursorLeft = 1 and Text.GetLength(input) > 0 then
          TextWindow.CursorTop = TextWindow.CursorTop - 1
          value = Stack.PopValue("lefts")
          TextWindow.CursorLeft = value
        else
          TextWindow.CursorLeft = TextWindow.CursorLeft - 1
          TextWindow.Write(" ")
          TextWindow.CursorLeft = TextWindow.CursorLeft - 1
        EndIf
        input = Text.GetSubText(input,1,Text.GetLength(input) - 1)
      endif
    ElseIf Text.GetCharacterCode(a) >= 32 And Text.GetCharacterCode(a) <= 126 then
      If TextWindow.CursorLeft >= 79 then
        pos = Text.GetLength(input)
        While Text.GetSubText(input,pos,1) <> " "
          TextWindow.CursorLeft = TextWindow.CursorLeft - 1
          TextWindow.Write(" ")
          TextWindow.CursorLeft = TextWindow.CursorLeft - 1 
          pos = pos - 1
        EndWhile
        Stack.PushValue("lefts",TextWindow.CursorLeft-1)
        TextWindow.WriteLine("")
        TextWindow.Write(Text.GetSubText(input,pos,Text.GetLength(input)))    
      EndIf
      TextWindow.Write(a)
      input = input + a
    EndIf
  EndWhile
endsub
