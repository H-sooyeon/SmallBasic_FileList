'Set events

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp



'Initialise key controls

Left = 0

Right = 0

Cannon_Left = 0

Cannon_Right = 0



'Load images and their sizes

PlayerImage = ImageList.LoadImage(Program.Directory+"\Warship.png")

PlayerW = ImageList.GetWidthOfImage(PlayerImage)

PlayerH = ImageList.GetHeightOfImage(PlayerImage)

Player=Shapes.AddImage(PlayerImage)

CannonImage = ImageList.LoadImage(Program.Directory+"\WarshipCannon2.png")

CannonW = ImageList.GetWidthOfImage(CannonImage)

CannonH = ImageList.GetHeightOfImage(CannonImage)

Cannon=Shapes.AddImage(CannonImage)



'Initial positions (ship centre) and rotations

PlayerX = 100 '  Centre of ship

PlayerY = 100 ' Centre of ship

PlayerAngle = 0 ' Rotation of ship

CannonAngle = 0 ' Rotation of cannon

CannonPos = PlayerH/4 ' Distance cannon is forward of the centre of ship



deg2rad = Math.Pi/180 ' Degrees to radians



'Main loop

While ("True")

  'Update ship and cannon angles

  If (Left = 1) Then

    PlayerAngle = PlayerAngle-1

  ElseIf (Right = 1) Then

    PlayerAngle = PlayerAngle+1

  EndIf

  If (Cannon_Left = 1) Then

    CannonAngle = CannonAngle-1

  ElseIf (Cannon_Right = 1) Then

    CannonAngle = CannonAngle+1

  EndIf

  'Redraw the ship

  Shapes.Move(Player,PlayerX-PlayerW/2,PlayerY-PlayerH/2)

  Shapes.Rotate(Player,PlayerAngle)

  'The tricky bit - get the centre of the cannon on the rotated ship

  CannonX = PlayerX+CannonPos*Math.Sin(PlayerAngle*deg2rad)

  CannonY = PlayerY-CannonPos*Math.Cos(PlayerAngle*deg2rad)

  'Draw the cannon

  Shapes.Move(Cannon,CannonX-CannonW/2,CannonY-CannonH/2)

  Shapes.Rotate(Cannon,PlayerAngle+CannonAngle)

  Program.Delay(20)

EndWhile



Sub OnKeyDown'=======================ON KEY DOWN================================

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    Left = 1

  ElseIf (Key = "Right") Then

    Right = 1

  ElseIf (Key = "D") Then

    Cannon_Right = 1

  ElseIf (Key = "A") Then

    Cannon_Left = 1

  endif

EndSub



Sub OnKeyUp'=======================ON KEY UP================================

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    Left = 0

  ElseIf (Key = "Right") Then

    Right = 0

  ElseIf (Key = "D") Then

    Cannon_Right = 0

  ElseIf (Key = "A") Then

    Cannon_Left = 0

  endif

EndSub




