false = "False"
true = "True"

' Key controls
leftKey  = "Left"
rightKey = "Right"
upKey = "Up"
downKey = "Down"
fireKey = "Space"

' Initialize our variables to indicate no keys pressed yet
leftKeyPressed = false
rightKeyPressed = false
upKeyPressed = false
downKeyPressed = false
fireKeyPressed = false 

' Remember the last key pressed if user pressed up and down, 
' or left and right at the same time.  If you have more than 1 player,
' you need another set of these variables for them.
leftRightPriority = rightKey  ' This is arbitrary at first
upDownPriority = upKey     ' same here

' Set our initial velocities to zero
playerVx = 0
playerVy = 0

' Set up event handler functions for keypresses
GraphicsWindow.KeyDown = OnKeyDown 
GraphicsWindow.KeyUp = OnKeyUp 

' Show the graphics window
GraphicsWindow.Show()

' Add a player to the screen
player = Shapes.AddEllipse(5, 5)
Shapes.Move(player, 300, 300)

While 1=1
  
  ' Call our subroutine to figure out what keys are pressed and 
  ' determine our velocity from them
  DoKey()
  
  ' Get the current top and left corner position of the player
  left = Shapes.GetLeft(player)
  top = Shapes.GetTop(player)
  
  ' Move player by it's velocity
  left = left + playerVx
  top = top + playerVy
  
  ' Put the player in the new position
  Shapes.Move(player, left, top)
  
  ' Have a little delay to make game work the same on all computers
  Program.Delay(10)
  
EndWhile 

' Read key event
' Note that key priority is remembered in case both up/down pressed 
' we will do what the last key press indicates.
' Also note that this is an event handler, so key presses can
' interrupt code running elsewhere at any time, must be careful not to 
' change variables etc that could affect code undesireably.
Sub OnKeyDown
  
  If(GraphicsWindow.LastKey = rightKey) Then
    rightKeyPressed = true 
    
    ' If both left & right pressed, this has higher priority since pressed last
    leftRightPriority = rightKey 
  endif 

  If(GraphicsWindow.LastKey = leftKey) Then
    leftKeyPressed = true
    leftRightPriority = leftKey
  EndIf 
  
  If(GraphicsWindow.LastKey = upKey) Then
    upKeyPressed = true
    upDownPriority = upKey
  EndIf 
    
  If(GraphicsWindow.LastKey = downKey) Then
    downKeyPressed = true 
    upDownPriority = downKey
  EndIf 
  
  If(GraphicsWindow.LastKey = fireKey) Then
    fireKeyPressed = true 
  endif

EndSub

' Run on key release, see note above
Sub OnKeyUp

  if GraphicsWindow.LastKey = rightKey Then
    rightKeyPressed = false
  EndIf 
  
  if GraphicsWindow.LastKey = leftKey Then
    leftKeyPressed = false
  EndIf 

  if GraphicsWindow.LastKey = upKey Then
    upKeyPressed = false
  EndIf 
  
  if GraphicsWindow.LastKey = downKey Then
    downKeyPressed = false
  EndIf 
  
  If(GraphicsWindow.LastKey = fireKey) Then
    fireKeyPressed = false  
  endif 

EndSub 

' Process user controls on our own time 
' (called typically from an endless While loop)
Sub DoKey
  
  If rightKeyPressed = true And leftKeyPressed = true then
    ' Figure out which direction based on last pressed or released
    If leftRightPriority = rightKey Then
      playerVx = 1
    Else 
      playerVx = -1
    EndIf 
  Else
    If rightKeyPressed = true Then
      playerVx = 1
    ElseIf leftKeyPressed = true Then
      playerVx = -1
    Else
      playerVx = 0
    EndIf 
  endif 
  
  If upKeyPressed = true And downKeyPressed = true then 
    ' Figure out which direction based on last pressed or released
    If upDownPriority = upKey Then
      playerVy = -1
    Else
      playerVy = 1
    EndIf 
  else
    If upKeyPressed = true Then
      playerVy = -1
    ElseIf downKeyPressed = true Then
      playerVy = 1
    Else
      playerVy = 0
    EndIf 
  endif 
  
endsub
