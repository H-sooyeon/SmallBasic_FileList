gw = 500

gh = 500

GraphicsWindow.Width = gw

GraphicsWindow.Height = gh

radius = 10

speed = 0.1

next = 0



GraphicsWindow.MouseDown = OnMouseDown

GraphicsWindow.MouseUp = OnMouseUp



While ("True")

  start = Clock.ElapsedMilliseconds

  GraphicsWindow.Title = count

  

  If (mouseDown) Then

    AddBullet()

  EndIf

  MoveBullets() 

  CheckBullets()

  

  delay = 20-(Clock.ElapsedMilliseconds-start)

  If (delay > 0) Then

    Program.Delay(delay)

  EndIf

EndWhile



Sub OnMouseDown

  mouseDown = "True"

EndSub



Sub OnMouseUp

  mouseDown = "False"

EndSub



Sub AddBullet

  mx = GraphicsWindow.MouseX

  my = GraphicsWindow.MouseY

  'New bullet

  next = next+1

  bullet[next] = Shapes.AddEllipse(2*radius, 2*radius)

  'Position

  bulletX[next] = gw/2

  bulletY[next] = gh/2

  'Velocity

  bulletU[next] = mx-bulletX[next]

  bulletV[next] = my-bulletY[next]

  'Update active bullets

  count = Array.GetItemCount(bullet)

  indices = Array.GetAllIndices(bullet)

EndSub



Sub MoveBullets

  For i = 1 To count

    index = indices[i]

    bulletX[index] = bulletX[index]+speed*bulletU[index]

    bulletY[index] = bulletY[index]+speed*bulletV[index]

    Shapes.Move(bullet[index],bulletX[index]-radius,bulletY[index]-radius)

  EndFor

EndSub



Sub CheckBullets

  For i = 1 To count

    index = indices[i]

    If (bulletX[index] < -radius Or bulletX[index] > gw Or bulletY[index] < -radius Or bulletY[index] > gh) Then

      Shapes.Remove(bullet[index])

      bullet[index] = ""

      bulletX[index] = ""

      bulletY[index] = ""

      bulletU[index] = ""

      bulletV[index] = ""

    EndIf

  EndFor

  'Update active bullets

  count = Array.GetItemCount(bullet)

  indices = Array.GetAllIndices(bullet)

EndSub


