'**************
' NyanSnake
' by GAniman
'**************

' ===============================================================================
' Initialization
' ===============================================================================

Path= Program.Directory + "/
'Path= "http://ximages.net/images/
gw= 600
gh= 400
GraphicsWindow.Width=  gw
GraphicsWindow.Height= gh
GraphicsWindow.Left= (Desktop.Width  - gw)/2
GraphicsWindow.Top=  (Desktop.Height - gh)/2
GraphicsWindow.CanResize= "True
GraphicsWindow.BackgroundColor= "Black
GraphicsWindow.Title= "NyanSnake

score= 0
delay= 80
skip=  20
x= gw/2
y= gh/2
delX= skip
delY= 0
Running= "True
Direction= 2
LastDirection= Direction

CreateShapes()
Keyboard()
GraphicsWindow.KeyDown= Keyboard

' ===============================================================================
' Main Loop
' ===============================================================================

Loop:

gw= GraphicsWindow.Width
gh= GraphicsWindow.Height

CheckKeys()
CheckBorders()
InspectFood()

If Running Then
  x = x + delX
  y = y + delY
  Sound.PlayClick()
EndIf

If RainBow Then
  GraphicsWindow.PenColor= GraphicsWindow.GetRandomColor()
  GraphicsWindow.DrawEllipse(x + size/2, y + size/4, 2, 2)
EndIf

MoveSprites()
Program.Delay(delay)

Goto Loop

' ===============================================================================
' Subroutines
' ===============================================================================

Sub CreateShapes
  
  Nyan1= ImageList.LoadImage(Path + "28930020147868715423.png")
  Nyan2= ImageList.LoadImage(Path + "74412177564379571315.png")
  size=  ImageList.GetWidthOfImage(Nyan1)
  If size < 4 Then
    size= 32
  EndIf
    
  Sprite[1]= Shapes.AddImage(Nyan1)    ' Left
  Sprite[2]= Shapes.AddImage(Nyan2)    ' Right
  Sprite[3]= Shapes.AddImage(Nyan1)    ' Down (same image as left)
  Sprite[4]= Shapes.AddImage(Nyan2)    ' Up      (same image as right)
  Shapes.Rotate(Sprite[3],270)         ' Pre-rotate it downwards
  Shapes.Rotate(Sprite[4],270)         ' Pre-rotate it upwards
  
  GraphicsWindow.PenColor=   "Gold
  GraphicsWindow.BrushColor= "BlueViolet
  foodSize= 10
  food = Shapes.AddEllipse(foodSize, foodSize)
  GenerateFood()
  
  GraphicsWindow.FontSize=20
  GraphicsWindow.BrushColor = "SpringGreen
  display= Shapes.AddText("0")
  Shapes.Move(display, gw/2, 20)
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub HideSprites
  
  For index= 1 To 4
    Shapes.HideShape(Sprite[index])
  EndFor
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub MoveSprites
  
  If LastDirection = Direction Then
    Shapes.Animate(Sprite[Direction], x,y, delay)
  Else
    Shapes.Move   (Sprite[Direction], x,y)
    LastDirection= Direction
  EndIf
  
  Shapes.ShowShape(Sprite[Direction])
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub GenerateFood
  
  foodX= Math.GetRandomNumber(gw-foodSize)
  foodY= Math.GetRandomNumber(gh-foodSize)
  Shapes.Move(food, foodX,foodY)
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub InspectFood
  
  GraphicsWindow.Title= "("+x+","+y+")   ("+foodX+","+foodY+") 
  
  If x+size >= foodX And x <= foodX+foodSize And y+size >= foodY And y <= foodY+foodSize Then
    score= score + 1
    Shapes.SetText(display,score)
    Sound.PlayChimesAndWait()
    GenerateFood()
  EndIf
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub CheckBorders
  
  If x < -size Then
    x= gw - size
    LastDirection= 0
  EndIf
  If x > gw + size Then
    x= 0
    LastDirection= 0
  EndIf
  If y < -size Then
    y= gh - size
    LastDirection= 0
  EndIf
  If y > gh + size Then
    y= 0
    LastDirection= 0
  EndIf
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub CheckKeys
  
  If Key = "Left" Then
    Direction= 1
    delX= -skip
    delY= 0
  EndIf
  
  If Key = "Right" Then
    Direction= 2
    delX= skip
    delY= 0
  EndIf
  
  If Key = "Down" Then
    Direction= 3
    delX= 0
    delY= skip
  EndIf
  
  If Key = "Up" Then
    Direction= 4
    delX= 0
    delY= -skip
  EndIf
  
EndSub
' ----------------------------------------------------------------------------------------------------------------------------------------
Sub Keyboard
  
  Key= GraphicsWindow.LastKey
  HideSprites()
  
  If Key = "Escape" Then
    Sound.PlayChimeAndWait()
    Program.End()
  EndIf
  
  If Key = "Space" Then
    If RainBow Then
      RainBow= "False
    Else
      RainBow= "True
    EndIf
  EndIf
  
  If Key = "Return" Then
    If Running Then
      Running= "False
    Else
      Running= "True
    EndIf
  EndIf
  
EndSub

' ===============================================================================
