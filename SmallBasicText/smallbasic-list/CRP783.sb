'Part 4:  limit the balls movment to only within the walls -  using AND in the If condition

'--------------------------------------------Settings---------------------------------------
gw = GraphicsWindow.Width              '
gh = GraphicsWindow.Height
wallWidth = 20

ballRadius = 20              
ballDiameter = ballRadius * 2
displacement = 5  

X = GraphicsWindow.Width / 2 - ballRadius
Y = GraphicsWindow.Height / 2 - ballRadius

'---------------------------------------------Draw Graphics & Add Shape-------------------------------
GraphicsWindow.FillRectangle(0, 0, wallWidth, gh)               'left wall
GraphicsWindow.FillRectangle(gw - wallWidth, 0, wallWidth, gh)  'right wall
GraphicsWindow.FillRectangle(0, 0, gw, wallWidth)               'top wall
GraphicsWindow.FillRectangle(0, gh - wallWidth, gw, wallWidth)  'bottom wall

ball = Shapes.AddEllipse(ballDiameter, ballDiameter)     'add the ball
Shapes.Move(ball, X, Y)                                  'position ball in the centre 

'-----------------------------------------------Event---------------------------------------------------
GraphicsWindow.KeyDown = OnKeyDown

'-----------------------------------------Event Reaction----------------------------
Sub OnKeyDown
  keyDown = GraphicsWindow.LastKey
  If keyDown = "Right" And X < gw - ballDiameter - wallWidth Then
    X = X + displacement
  ElseIf keyDown = "Left" And X > wallWidth Then
    X = X - displacement
  ElseIf keyDown = "Up" And Y > wallWidth Then
    Y = Y - displacement
  ElseIf keyDown = "Down" And Y < gh - ballDiameter - wallWidth Then
    Y = Y + displacement
  EndIf
EndSub

'-------------------------------------------Main Loop----------------------------------
While "True"
  Shapes.Move(ball, X, Y)
  Program.Delay(5)
EndWhile
