'Random Number Generator - Created by Matthew L. Parets aka codingCat. No rights reserved. Use and distribute at your pleasure

'This program generates a set of random numbers in a range

'selected by drawing a line on a graph

'Developed for the monthly challenge - December 2017

InitializeWindow()

GraphicsWindow.MouseDown = onMouseDown

GraphicsWindow.MouseUp = onMouseUp



'--------------------------------------------------------------------

'Detect when mouse button is pressed

'Record the mouse pointer location if within the graph

Sub onMouseDown

  mx = GraphicsWindow.MouseX

  my = GraphicsWindow.MouseY  

  If mx > topx And mx < topx+wid And my > topy And my < topy+hei Then

    fx = mx

    fy = my

  Else

    fx = -1

    fy = -1

  EndIf

EndSub



'--------------------------------------------------------------------

'Detect when mouse button is released

'Record the mouse point location and generate random

'numbers in the range described  by the distance

'between the click and release locations

Sub onMouseUp

  nx = GraphicsWindow.MouseX

  ny = GraphicsWindow.MouseY  

  

  If fx >= 0 and nx > topx and nx < topx+wid and ny > topy and ny < topy+hei then

    mx = GraphicsWindow.MouseX

    my = GraphicsWindow.MouseY  

    

    GraphicsWindow.DrawLine(fx,fy,mx,my)

    

    dx = Math.Abs(fx-mx)

    dy = Math.max(fy-topY,my-topy) / 10

    

    Start = math.Floor(Math.Min(fx,mx) - topX)

    Range = math.Floor(dx)

    Count = math.Floor(dy) + 1

    

    GenerateRandoms()

  EndIf

EndSub



'--------------------------------------------------------------------

'Generate the list of random numbers requested

Sub GenerateRandoms

  nl = Text.GetCharacter(13) + Text.GetCharacter(10)

  msg = "" + Count + " random numbers From " + Start + " To " + (start + Range) + ": " + nl

  For i = 1 To count

    num = Math.GetRandomNumber(Range) + Start

    msg = msg + " " + num

    If Math.Remainder(i,10) = 0 Then

      msg = msg + nl

    EndIf

  EndFor

  Controls.SetTextBoxText(box,msg)

EndSub



'--------------------------------------------------------------------

'Pop open the window. Draw a graph. Place a control

'for the results

Sub InitializeWindow

  GraphicsWindow.Show()

  GraphicsWindow.BackgroundColor = "lightyellow"

  GraphicsWindow.Title = "Random Number Generator"

  

  topX = 65

  topY = 20

  wid = 500

  hei = 300

  

  DrawGraph()

  LabelGraph()

  

  GraphicsWindow.FontSize = 12

  GraphicsWindow.BrushColor = "black"

  box = Controls.AddMultiLineTextBox(10,topy+hei+30)

  Controls.SetSize(box,GraphicsWindow.Width-20,GraphicsWindow.Height - topY - hei - 40)

  GraphicsWindow.PenColor = "brown"

EndSub



'--------------------------------------------------------------------

'Draw the graph describing the range

Sub DrawGraph

  GraphicsWindow.DrawRectangle(topX,topY,wid,hei)

  GraphicsWindow.PenColor = "pink"

  yStep = 100

  dispNum = 0

  GraphicsWindow.BrushColor = "darkgreen"

  For i = 0 to hei step yStep

    If i >= yStep and i <= hei-yStep then

      GraphicsWindow.DrawLine(topx, topY + i, topx + wid, topY + i)

    EndIf

    GraphicsWindow.DrawText(topx-20,topy+i-10,dispNum)

    dispNum = dispNum + 10

  endfor

  

  xStep = 50

  dispNum = 0

  GraphicsWindow.BrushColor = "darkblue"

  For i = 0 to wid step 50

    If i >=xStep And i <= wid - xStep Then

      GraphicsWindow.DrawLine(topX + i, topy, topX + i, topy+hei)

    Endif

    GraphicsWindow.DrawText(topx+i-10, topy-17,dispNum)

    dispNum = dispNum + 50

  endfor

EndSub



'--------------------------------------------------------------------

'Label the axis of the range graph

Sub LabelGraph

  GraphicsWindow.FontSize = 20

  kerningLen = 15

  yMsg = "How Many"

  ystart = topY + ((hei/2) - ((Text.GetLength(yMsg) * kerningLen) / 2))

  For i = 1 to Text.GetLength(yMsg)

    GraphicsWindow.DrawText(topX - 50, ystart + (i * kerningLen) - 25, Text.GetSubText(yMsg,i,1))

  endfor

  

  xMsg = "Range"

  xstart = topX + ((wid/2) - ((Text.GetLength(xMsg) * kerningLen) / 2))

  For i = 1 to Text.GetLength(xMsg)

    GraphicsWindow.DrawText(xstart + (i * kerningLen) - 15, topy+hei , Text.GetSubText(xMsg,i,1))

  endfor

EndSub


