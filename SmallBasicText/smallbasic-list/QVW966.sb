GraphicsWindow.KeyDown = KeyDown
GraphicsWindow.KeyUp=KeyUp
GraphicsWindow.CanResize = "False"
GraphicsWindow.Title="Menu"
GraphicsWindow.Width="200"
GraphicsWindow.Height="155"
GraphicsWindow.BrushColor="#696969"
Square=Shapes.AddRectangle(20,20)
SinglePlayer=Shapes.AddText("SinglePlayer")
Multiplayer=Shapes.AddText("MultiPlayer")
Guide=Shapes.AddText("Guide")
Exit=Shapes.AddText("Exit")
Shapes.Zoom(SinglePlayer,2,2)
Shapes.Zoom(Multiplayer,2,2)
Shapes.Zoom(Guide,2,2)
Shapes.Zoom(Exit,2,2)
Shapes.Move(SinglePlayer,80,10)
Shapes.Move(Multiplayer,76,50)
Shapes.Move(Guide,59,90)
Shapes.Move(Exit,53,130)
Shapes.Move(Square,15,10)
Rotate=0
Select=1
Gamemode=1 
DeltaX=0.5
DeltaY=1.5
Speed=1
Score1=0
Score2=0
Up=0
Down=0
Up2=0
Down2=0
Gup=0
Timer.Interval = "10"
Timer.Resume()
Timer.Tick = EnemyMove
'Gamemodes, 1=Menu, 2=Guide, 3=SinglePlayer, 4=MultiPlayer

While "True"
  ScoreBoard()
  RotateSet()
  Ballmove()
  Change()
  KeyCheck()
  CoordinateCheck()
EndWhile

Sub ScoreBoard
  Shapes.SetText(ScoreDisplay1,Score1)
  Shapes.SetText(ScoreDisplay2,Score2)
  If Gamemode=3 Or Gamemode=4 Then
    If Score1=3 Then
      p1win=Shapes.AddText("Player 1 Wins!")
      Shapes.Zoom(p1win,3,3)
      Shapes.Move(p1win,135,90)
      Sound.PlayChimeAndWait()
      Gamemode=1
      CreateMenu()
    EndIf
    If Score2=3 Then
      p2win=Shapes.AddText("Player 2 Wins!")
      Shapes.Zoom(p2win,3,3)
      Shapes.Move(p2win,135,90)
      Sound.PlayChimeAndWait()
      Gamemode=1
      CreateMenu()
    EndIf
  EndIf
EndSub


Sub KeyDown
  LastKey=GraphicsWindow.LastKey
  If Gamemode = 1 Then
    If LastKey="Up" Then
      If Select>1 Then
        Select=Select-1
      EndIf
    EndIf
    If LastKey="Down" Then
      If Select<4 Then
        Select=Select+1
      EndIf
    EndIf
    If LastKey = "Return" Then
      If Gamemode=1 Then
        If Select=1 Then
          CreateGame()
          Gamemode=3
        ElseIf Select=2 Then
          CreateGame()
          Gamemode = 4
        ElseIf Select=3 Then
          Gamemode=2
          ShowHelp()
        ElseIf Select=4 Then
          Program.End()
        EndIf
      EndIf
    EndIf
    If Select=1 Then
      Shapes.Move(Square,15,10)
    ElseIf Select=2 Then
      Shapes.Move(Square,15,50)
    ElseIf Select=3 Then
      Shapes.Move(Square,15,90)
    ElseIf Select=4 Then
      Shapes.Move(Square,15,130)
    EndIf
  EndIf
  If Gamemode=3 Then
    If LastKey="Up" Then
      Up=1
    ElseIf LastKey="Down" Then
      Down=1
    EndIf
  EndIf
  If Gamemode=4 Then
    If LastKey="Up" Then
      Up=1
    ElseIf LastKey="Down" Then
      Down=1
    EndIf
    If LastKey="W" Then
      Up2=1
    ElseIf LastKey="S" Then
      Down2=1
    EndIf
  EndIf
  If Gup=1 Then
    If Gamemode=2 Then
      Gamemode=1
      CreateMenu()
      Gup=0
    EndIf
  EndIf
  If Gamemode=3 Or Gamemode=4 Then
    If LastKey = "Escape" Then
      CreateMenu()
    EndIf
  EndIf
EndSub

Sub KeyUp
  LastKey=GraphicsWindow.LastKey
  If Gamemode=3 Then
    If LastKey="Up" Then
      Up=0
    ElseIf LastKey="Down" Then
      Down=0
    EndIf
  EndIf
  If Gamemode=4 Then
    If LastKey="Up" Then
      Up=0
    ElseIf LastKey="Down" Then
      Down=0
    EndIf
    If LastKey="W" Then
      Up2=0
    ElseIf LastKey="S" Then
      Down2=0
    EndIf
  EndIf
  If Gamemode=2 Then
    If LastKey = "Return" Then
      Gup=1
    EndIf
  EndIf
EndSub

Sub CoordinateCheck
  Paddle1x=Shapes.GetLeft(Paddle1)
  Paddle1y=Shapes.GetTop(Paddle1)
  Paddle2x=Shapes.GetLeft(Paddle2)
  Paddle2y=Shapes.GetTop(Paddle2)
EndSub


Sub KeyCheck
  If Gamemode=3 or Gamemode=4 Then
    If Paddle1y > 0 Then
      If Up=1 Then
        Shapes.Move(Paddle1, Paddle1x, Paddle1y-Speed)
      EndIf
    EndIf
    If Paddle1y < 150 Then
      If Down=1 Then
        Shapes.Move(Paddle1, Paddle1x, Paddle1y+Speed)
      EndIf
    EndIf
    If Paddle2y > 0 Then
      If Up2=1 Then
        Shapes.Move(Paddle2, Paddle2x, Paddle2y-Speed)
      EndIf
    EndIf
    If Paddle2y < 150 Then
      If Down2=1 Then
        Shapes.Move(Paddle2, Paddle2x, Paddle2y+Speed)
      EndIf
    EndIf
  EndIf
EndSub

Sub RotateSet
  If Gamemode=1 Then
    Program.Delay(10)
    Rotate=Rotate+1
    Shapes.Rotate(Square,Rotate)
  EndIf
EndSub

Sub Change
  If Gamemode=3 or Gamemode=4 Then
    bx=Shapes.GetLeft(Ball)+5
    by=Shapes.GetTop(Ball)+5
    If gh-5<by Then
      DeltaY = -DeltaY
    EndIf
    If gw-5<bx Then
      Score2=Score2+1
      DeltaX = -DeltaX
    EndIf
    For i = 1 to 50
      If bx = Paddle1x-5 Then
        If Paddle1y+i=by+5 Then
          Sound.PlayClick()
          DeltaX = -DeltaX
        EndIf
      EndIf
    EndFor
    For i = 1 to 50
      If bx = Paddle2x+15 Then
        If Paddle2y+i=by+5 Then
          Sound.PlayClick()
          DeltaX = -DeltaX
        EndIf
      EndIf
    EndFor
    If 5>by Then
      DeltaY = -DeltaY
    EndIf
    If 5>bx Then
      Score1=Score1+1
      DeltaX = -DeltaX
    EndIf
  EndIf
EndSub

Sub Ballmove 
  If Gamemode=3 or Gamemode=4 Then
    Glitchstop=Glitchstop+1
    Program.Delay("6")
    x=x+DeltaX
    y=y+DeltaY
    Shapes.Move(Ball, x, y)
  EndIf
EndSub

Sub EnemyMove
  If Gamemode=3Then
    If Paddle2y+25 > by Then
      Shapes.Move(Paddle2, Paddle2x, Paddle2y-Speed)
    EndIf
    If Paddle2y+25 < by Then
      Shapes.Move(Paddle2, Paddle2x, Paddle2y+Speed)
    EndIf
  EndIf
EndSub

Sub CreateGame
  GraphicsWindow.Clear()
  GraphicsWindow.Title = "Pong"
  GraphicsWindow.Height="200"
  GraphicsWindow.Width="350"
  gw = GraphicsWindow.Width
  gh = GraphicsWindow.Height   
  x= gw - gw/2
  y= gh - gh/2
  ScoreDisplay1=Shapes.AddText("0")
  ScoreDisplay2=Shapes.AddText("0")
  Shapes.Move(ScoreDisplay1,125,10)
  Shapes.Move(ScoreDisplay2,205,10)
  Paddle1=Shapes.AddRectangle(10,50)
  Paddle2=Shapes.AddRectangle(10,50)
  Shapes.Move(Paddle1, 300, 70)
  Shapes.Move(Paddle2, 30, 70)
  Shapes.Zoom(ScoreDisplay1,4,4)
  Shapes.Zoom(ScoreDisplay2,4,4)  
  Paddle2x=Shapes.GetLeft(Paddle2)
  Paddle2y=Shapes.GetTop(Paddle2)
  Ball = Shapes.AddEllipse(10,10)
  Shapes.Move(Ball, x, y)
EndSub

Sub ShowHelp
  GraphicsWindow.Clear()
  GraphicsWindow.DrawText(0,0,"Controls:")
  GraphicsWindow.DrawText(0,15,"Player 1: Up and Down keys")
  GraphicsWindow.DrawText(0,30,"Player 2: W and S Keys")
  GraphicsWindow.DrawText(0,45,"Press Escape to exit the match")
  GraphicsWindow.DrawText(0,65,"Goal:")
  GraphicsWindow.DrawBoundText(0,80,190,"The Goal is to score against the opponent 5 times.")
  GraphicsWindow.DrawText(0,130,"Press Enter to continue...")
EndSub

Sub CreateMenu
  GraphicsWindow.Clear()
  Gamemode=1
  GraphicsWindow.Title="Menu"
  GraphicsWindow.Width="200"
  GraphicsWindow.Height="155"
  Square=Shapes.AddRectangle(20,20)
  Shapes.Move(Square,15,10)
  SinglePlayer=Shapes.AddText("SinglePlayer")
  Multiplayer=Shapes.AddText("MultiPlayer")
  Guide=Shapes.AddText("Guide")
  Exit=Shapes.AddText("Exit")
  Shapes.Zoom(SinglePlayer,2,2)
  Shapes.Zoom(Multiplayer,2,2)
  Shapes.Zoom(Guide,2,2)
  Shapes.Zoom(Exit,2,2)
  Shapes.Move(SinglePlayer,80,10)
  Shapes.Move(Multiplayer,76,50)
  Shapes.Move(Guide,59,90)
  Shapes.Move(Exit,53,130)
  Rotate=0
  Select=1
EndSub
