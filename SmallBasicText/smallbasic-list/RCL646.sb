''''' countdown and timer

' idea: absolu    upgraded: WhTurner 2013-03-11

GraphicsWindow.Width = 1000

GraphicsWindow.Height = 300

GraphicsWindow.Left =(Desktop.Width / 2) - (GraphicsWindow.Width / 2)

GraphicsWindow.Top =(Desktop.Height / 2) - (GraphicsWindow.Height / 2)

GraphicsWindow.CanResize = "False"

GraphicsWindow.Title = "COUNTDOWN"

GraphicsWindow.BackgroundColor = "black"

GraphicsWindow.FontName = "DS-Digital"

Std()

strt=0

GraphicsWindow.DrawText(160,20,"Input startingtime / clocktime")

GraphicsWindow.DrawText(180,45,"h:")

GraphicsWindow.DrawText(245,45,"m:")

GraphicsWindow.DrawText(320,45,"s:")

BX1=Controls.AddTextBox(200,45)

Controls.SetSize(BX1,40,30)

BX2=Controls.AddTextBox(270,45)

Controls.SetSize(BX2,40,30)

BX3=Controls.AddTextBox(340,45)

Controls.SetSize(BX3,40,30)

B01=Controls.AddButton("Start countdown",10,10)

B02=Controls.AddButton("Run to clocktime",10,45)

B03=Controls.AddButton("Run Timer from start",10,80)

Controls.ButtonClicked=Btn

While strt=0

endwhile

''     now running

deltah=Controls.GetTextBoxText(BX1)

deltam=Controls.GetTextBoxText(BX2)

deltas=Controls.GetTextBoxText(BX3)

GraphicsWindow.Clear()

If strt=1 then  ''countdown

  B1=Controls.AddButton("Pause",100,0)

  B9=Controls.AddButton("Reset",200,0)

elseif strt=2 then   ''countdown to clock

  deltah=deltah-Clock.Hour

  If deltah<0 then

    deltah=deltah+24

  endif

  deltam=deltam-Clock.Minute

  If deltah=0 and deltam<0 then

    deltah=deltah+23  

    deltam=deltam+60

  endif

  deltas=deltas-Clock.Second

  strt=1

elseif strt=-1 then   ''timer

  

  GraphicsWindow.Title = "  T I M E R"

  B1=Controls.AddButton("Pause",100,0)

  B9=Controls.AddButton("Reset",200,0) 

endif



Bexit=Controls.AddButton("Exit",300,0)



Big()

deltat=3600*deltah+60*deltam+deltas

update()

Timer.Interval=1000

Timer.Tick=update



Sub Btn

  If Controls.LastClickedButton=B01 Then

    strt=1

  ElseIf Controls.LastClickedButton=B02 then

    strt=2

  ElseIf Controls.LastClickedButton=B03 then

    strt=-1

  elseif controls.LastClickedButton=B1 Then

    Timer.Pause()

    Std()

    B2= Controls.AddButton(" Cont ",100,0)

  ElseIf Controls.LastClickedButton=B2 then

    Timer.Resume()

    Std()

    B1=controls.AddButton("Pause",100,0)

    Big()

  elseif Controls.LastClickedButton=B9 then

    deltat=3600*deltah+60*deltam+deltas+strt

    Big()

    update()

  elseif Controls.LastClickedButton=Bexit then

    Program.End()

  endif  

  Timer.Tick=Update

EndSub



Sub update

  GraphicsWindow.BrushColor="Black"

  GraphicsWindow.FillRectangle(20,50,960,200)

  GraphicsWindow.BrushColor="Red"

  deltat=deltat-strt

  If deltat>=0 then

    h="x  "+math.Floor(deltat/3600)

    h=text.GetSubTextToEnd(h,text.GetLength(h)-1)

    m=Text.GetSubTextToEnd(100+Math.Floor(deltat/60)-60*h,2)

    s= Text.GetSubTextToEnd(100+deltat-3600*h-60*m,2)

    GraphicsWindow.DrawText (40,20, h+":"+m+":"+s) 

  Else

    ala=0

    Timer.Tick=Alarm

  endif

EndSub



Sub Big     '' set big red characters

  GraphicsWindow.BrushColor = "red"

  GraphicsWindow.FontSize = 200

EndSub

Sub Std     '' set standard characters

  GraphicsWindow.BrushColor = "Green"

  GraphicsWindow.FontSize = 15

EndSub



Sub Alarm

  If Ala=0 Then

    ala=1

    GraphicsWindow.DrawText(80,20,"ALARM")

    Sound.PlayMusic("C8 F4 C8")

  Else

    ala=0

    GraphicsWindow.BrushColor="Black"

    GraphicsWindow.FillRectangle(20,10,960,200)

    GraphicsWindow.BrushColor="Red"

  EndIf

EndSub


