' Fish

' Version 0.1

' Copyright © 2016 Nonki Takahashi.  The MIT License.

' Program ID



GraphicsWindow.Title = "Fish"

Init()

GraphicsWindow.PenWidth = 15

GraphicsWindow.BackGroundColor = "#3F48CC"

DrawGrid()

' back fin

param = "x1=200;y1=100;x2=400;y2=20;x3=400;y3=150;"

DrawTriangle()

' tail fin

param = "x1=400;y1=225;x2=550;y2=150;x3=550;y3=300;"

DrawTriangle()

' body

GraphicsWindow.BrushColor = "#99D9EA"

GraphicsWindow.FillEllipse(100, 70, 350, 300)

param = "x=100;y=70;width=350;height=300;"

DrawEllipse()

' eye

GraphicsWindow.BrushColor = "Black"

GraphicsWindow.FillEllipse(150, 160, 40, 40)

param = "x=150;y=160;width=40;height=40;"

DrawEllipse()

' mouth

param = "x1=110;y1=260;x2=140;y2=240;"

DrawLine()

' gill

param = "x1=220;y1=120;x2=245;y2=200;"

DrawLine()

param = "x1=245;y1=200;x2=200;y2=300;"

DrawLine()

' pectoral fin

param = "x1=220;y1=300;x2=280;y2=275;x3=280;y3=325;"

DrawTriangle()



Sub Init

  gw = 598

  gh = 428

  GraphicsWindow.Width = gw

  GraphicsWindow.Height = gh

EndSub



Sub DrawGrid

  pwSaved = GraphicsWindow.PenWidth

  pcSaved = GraphicsWindow.PenColor

  GraphicsWindow.PenWidth = 1

  GraphicsWindow.PenColor = "Cyan"

  For x = 0 To gw Step 50

    GraphicsWindow.DrawLine(x, 0, x, gh)

  EndFor

  For y = 0 To gh Step 50

    GraphicsWindow.DrawLine(0, y, gw, y)

  EndFor

  GraphicsWindow.PenColor = pcSaved

  GraphicsWindow.PenWidth = pwSaved

EndSub



Sub DrawTriangle

  paramSaved = param

  _x1 = param["x1"]

  _y1 = param["y1"]

  _x2 = param["x2"]

  _y2 = param["y2"]

  _x3 = param["x3"]

  _y3 = param["y3"]

  param = "x1=" + _x1 + ";y1=" + _y1 + ";x2=" + _x2 + ";y2=" + _y2 + ";"

  DrawLine()

  param = "x1=" + _x2 + ";y1=" + _y2 + ";x2=" + _x3 + ";y2=" + _y3 + ";"

  DrawLine()

  param = "x1=" + _x3 + ";y1=" + _y3 + ";x2=" + _x1 + ";y2=" + _y1 + ";"

  DrawLine()

  param = paramSaved

EndSub



Sub DrawLine

  pc = GraphicsWindow.PenColor

  bc = GraphicsWindow.BrushColor

  GraphicsWindow.BrushColor = pc

  pw = GraphicsWindow.PenWidth

  x1 = param["x1"]

  y1 = param["y1"]

  x2 = param["x2"]

  y2 = param["y2"]

  len = Math.SquareRoot((x2 - x1) * (x2 - x1) + (y2 - y1) * (y2 - y1))

  For k = 0 To 1 Step pw / len / 3

    x = x1 * (1 - k) + x2 * k

    y = y1 * (1 - k) + y2 * k

    _dx = Math.GetRandomNumber(2) - 1

    _dy = Math.GetRandomNumber(2) - 1

    _pw = (Math.GetRandomNumber(2) / 10 + 0.9) * pw

    GraphicsWindow.FillEllipse(x - _pw / 2 + _dx, y - _pw / 2 + _dy, _pw, _pw)

  EndFor

  GraphicsWindow.BrushColor = bc

EndSub



Sub DrawEllipse

  pc = GraphicsWindow.PenColor

  bc = GraphicsWindow.BrushColor

  GraphicsWindow.BrushColor = pc

  pw = GraphicsWindow.PenWidth

  x = param["x"]

  y = param["y"]

  width = param["width"]

  height = param["height"]

  a = width / 2

  b = height / 2

  len = 2 * Math.Pi * Math.Max(a, b)

  For θ = 0 To 2 * Math.Pi Step pw / len * 3

    _x = (x + a) + a * Math.Cos(θ)

    _y = (y + b) + b * Math.Sin(θ)

    _dx = Math.GetRandomNumber(2) - 1

    _dy = Math.GetRandomNumber(2) - 1

    _pw = (Math.GetRandomNumber(2) / 10 + 0.9) * pw

    GraphicsWindow.FillEllipse(_x - _pw / 2 + _dx, _y - _pw / 2 + _dy, _pw, _pw)

  EndFor

  GraphicsWindow.BrushColor = bc

EndSub


