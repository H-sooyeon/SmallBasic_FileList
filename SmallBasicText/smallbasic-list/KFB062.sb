' SmallBasic Version 1.2
' Program: ImportSBCode
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/6dbe4a45-95e5-4ba4-a18e-287df4f8698c/publish-and-import-code-from-httpwwwsmallbasiccomsmallbasiccomprogram
' ImportURL: http://smallbasic.com/program/?
' Extension: LitDev (, ESL)
' Comment:  http://regexlib.com/CheatSheet.aspx
'  
' Variables: 
' ToDo: 
' ================================================================================
While done = ""
  TextWindow.WriteLine("")
  TextWindow.Write("ProgramID eg. aBc123, XyZ049-12 [Return to Quit] ?: ")
  id = LDText.Trim(TextWindow.Read())
  If id = "" Then
    TextWindow.WriteLine("Aborted.")
    TextWindow.PauseWithoutMessage()
    Program.End()
  Else
    If LDRegex.Match(id, "^[a-z]{3}[0-9]{3}(-\d+)?$","") = "" Then
      TextWindow.WriteLine("INVALID Program ID! Retry...")
      done = ""
    Else
      done = "True"
    EndIf
  EndIf
EndWhile

url = "http://www.smallbasic.com/program/"       ' or
'url = "http://www.smallbasic.com/smallbasic.com/program/"

'html = ESLNetworking.GETRequest(url, id)                           ' or
html = Network.GetWebPageContents(url +"?"+ id)

TextWindow.WriteLine("")
TextWindow.WriteLine("CLEAN Import:")
TextWindow.WriteLine("")

StripHtml()
TextWindow.ForegroundColor = 11 ' Cyan
TextWindow.WriteLine(html)
TextWindow.WriteLine("")

'savePath = Program.Directory +"\SBProgram_"+ id +".sb"
'' Perhaps first auto-rename filename in savePath to eg.  ... _X.sb  (X = 2,3,4, etc) to avoid overwrite
' The following line could be harmful and has been automatically commented.
' 'File.WriteContents(savePath, html)


' //////////  SUB  \\\\\\\\\\
Sub StripHtml
  qu = Text.GetCharacter(34)
  str = "<div id="+ qu +"codeListing"+ qu +">"
  'len = Text.GetLength(str)  ' 22
  html = Text.GetSubTextToEnd(html, Text.GetIndexOf(html, str) + 22)  ' + len)
  
  html = LDRegex.Replace(html, "</?span.*?>", "","")
  html = LDRegex.Replace(html, "<br */?>", "","")
  html = LDRegex.Replace(html, "[ \t]*</?div.*>", "","")
  html = LDRegex.Replace(html, "&nbsp;", " ","")
  
  html = Text.GetSubText(html, 1, Text.GetIndexOf(html, "</form>") - 1)
  html = LDText.Trim(html)
  
  ' .... and perhaps remove '..harmful..' comments:
  html = LDRegex.Replace(html, "' The following line could be harmful.*\r\n\s*'", "","")
EndSub

