'Challenge of the Month - April 2014   ---  Physics Challenge  --     by  NaochanON

init()

Controls.ButtonClicked=Onclicked



Sub Onclicked

  distance=0

  Velocity=0

  NN=0

  Result="Distance:Velocity"+CRLF

  profile= text.ConvertToLowerCase(Controls.GetTextBoxText(box[1]))+","

  Len= Text.GetLength(profile)

  While Len>0 

    Comma_P= Text.GetIndexOf(profile,",")

    NN=NN+1

    travel= Text.GetSubText(profile,1,Comma_P-1)

    '---------------------------------------------------------------------------- 

    acc_P= Text.GetIndexOf(travel,"acc")

    acc= Text.GetSubText(travel,1,acc_P-1)

    multi_P= Text.GetIndexOf(travel,"*")

    sec_P=  Text.GetIndexOf(travel,"s")  '  small    s

    sec= Text.GetSubText(travel,multi_P+1,sec_P - multi_P-1)  

    '---------------------------------------------------------------------------- 

    distance= distance+Velocity*sec+acc*sec*sec/2

    Velocity= velocity+acc*sec

    result=result+distance+"   :   "+Velocity+CRLF

    '---------------------------------------------------------------------------- 

    profile= Text.GetSubTextToEnd(profile,Comma_P+1)

    Len= Text.GetLength(profile)

  endwhile

  Controls.SetTextBoxText(box[2],"Distance= "+distance+ " m")

  Shapes.SetText(Sresult,result)

EndSub



Sub init

  GraphicsWindow.BackgroundColor="Lightcyan"

  GraphicsWindow.Width=900

  GraphicsWindow.Height=300 

  GraphicsWindow.FontSize=20

  GraphicsWindow.BrushColor="Red"

  msg=Shapes.AddText("Input acceleration value (acc = m/s/s) and time (sec) like below")

  Shapes.Move(msg,50,50)

  Sresult= Shapes.AddText(" ")

  Shapes.Move(Sresult,300,140)

  Controls.AddButton("Calc",770,80)

  GraphicsWindow.FontSize=16

  box[1]= Controls.AddTextBox(50,80)

  Controls.SetSize(box[1],700,40) '

  Controls.SetTextBoxText(box[1],"20 acc * 12 s , -10 acc * 4 s , -40 acc * 8 s")  '  2 acc * 5 s,  0 acc * 3 s,-4 acc * 1 s

  box[2]= Controls.AddTextBox(50,180)

  Controls.SetSize(box[2],200,40)

  Controls.SetTextBoxText(box[2],"Distance= ") 

  CRLF= Text.GetCharacter(13)+Text.GetCharacter(10)

EndSub


