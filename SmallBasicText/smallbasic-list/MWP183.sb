'text decryption by S Paget

' These are the normal  frequencies for each letter in English text
target[1] = 8.167
target[2] = 1.492
target[3]=2.782
target[4]=4.253
target[5]=12.702
target[6]=2.228
target[7]=2.015
target[8]=6.094
target[9]=6.966
target[10]=0.153
target[11]=0.772
target[12]=4.025
target[13]=2.406
target[14]=6.749
target[15]=7.507
target[16]=1.929
target[17]=0.095
target[18]=5.987
target[19]=6.327
target[20]=9.056
target[21]=2.758
target[22]=0.978
target[23]=2.360
target[24]=0.150
target[25]=1.974
target[26]=0.074

'draw as a graph
GraphicsWindow.left=0
GraphicsWindow.top=0
GraphicsWindow.width=600
GraphicsWindow.height=600
targetoriginx = 50
targetoriginy=250
GraphicsWindow.DrawText(250,10,"Normal Letter Frequencies")
GraphicsWindow.DrawLine(targetoriginx, targetoriginy,targetoriginx+540,targetoriginy)
GraphicsWindow.DrawLine(targetoriginx, targetoriginy,targetoriginx,targetoriginy-200)
For i = 1 To 26
  GraphicsWindow.FillRectangle(targetoriginx+(i*20),targetoriginy-(target[i]*15),15,target[i]*15)
  GraphicsWindow.DrawText(targetoriginx+(i*20)+5,targetoriginy+20,Text.GetCharacter(96+i))
EndFor


' load the encrypted file
TextWindow.top=0
TextWindow.left=650
textWindow.WriteLine("Encrypted text:")
' The following line could be harmful and has been automatically commented.
' mytext = File.ReadContents("encryptedtext.txt")
TextWindow.Writeline(mytext)
TextWindow.WriteLine("")

'count letters  in sample text
For i = 1 To Text.GetLength(mytext)
  charval = Text.GetCharacterCode(Text.GetSubText(mytext,i,1)) - 96
  count[charval]=count[charval]+1
EndFor
'convert to percentage frequencies
For letter = 1 To 26
  freq[letter] = count[letter]/text.GetLength(mytext)*100
EndFor

'run through all possible offsets
min = 3000
testoriginx = 50
testoriginy=550
For offset = 1 To 26
  graphicswindow.BrushColor="white"
  GraphicsWindow.FillRectangle(0,testoriginy-200,600,200)
  GraphicsWindow.brushcolor="red"
  if math.Round(offset/2) = offset/2 Then
    GraphicsWindow.DrawText(250,testoriginy-200,"Analysing cipher text...")
  EndIf
  GraphicsWindow.DrawLine(testoriginx, testoriginy,testoriginx+540,testoriginy)
  GraphicsWindow.DrawLine(testoriginx, testoriginy,testoriginx,testoriginy-200)
  For i = 1 To 26
     chartbar = i+offset
    If chartbar > 26 Then
      chartbar = (chartbar-26)
    EndIf
    GraphicsWindow.FillRectangle(testoriginx+(i*20),testoriginy-(freq[chartbar]*15),15,freq[chartbar]*15)
    GraphicsWindow.DrawText(testoriginx+(i*20)+5,testoriginy+20,Text.GetCharacter(96+i))
  EndFor
  
  Program.Delay(200)
  'for each offset, add up the total amount of "difference" between the actual frequency
  'and the target frequency
  difference[offset] = 0
  For letter = 1 To 26
    comparisonfreq = letter + offset
    If comparisonfreq>26 Then
      comparisonfreq = 0 + comparisonfreq-26  
    EndIf
    difference[offset] = difference[offset] + math.Abs(target[letter] - freq[comparisonfreq])
  EndFor
  If difference[offset]<min Then
    min = difference[offset]
    bestoffset = offset
  EndIf
endfor

  graphicswindow.BrushColor="white"
  GraphicsWindow.FillRectangle(0,testoriginy-200,600,200)
  GraphicsWindow.brushcolor="red"
  GraphicsWindow.DrawText(250,testoriginy-200,"Found closest match")
  GraphicsWindow.DrawLine(testoriginx, testoriginy,testoriginx+540,testoriginy)
  GraphicsWindow.DrawLine(testoriginx, testoriginy,testoriginx,testoriginy-200)
  
  For i = 1 To 26
     chartbar = i+bestoffset
    If chartbar > 26 Then
      chartbar = (chartbar-26)
    EndIf
    GraphicsWindow.FillRectangle(testoriginx+(i*20),testoriginy-(freq[chartbar]*15),15,freq[chartbar]*15)
    GraphicsWindow.DrawText(testoriginx+(i*20)+5,testoriginy+20,Text.GetCharacter(96+i))
  EndFor
  
TextWindow.WriteLine("The best offset was " + bestoffset)
TextWindow.WriteLine("")
TextWindow.WriteLine("So I reckon the text says:")
TextWindow.WriteLine("")

For i = 1 To Text.GetLength(mytext)
  char = Text.ConvertToLowerCase(text.GetSubText(mytext,i,1))
  charval = Text.GetCharacterCode(char) 
    enccode = charval-bestoffset
    If enccode<97 then
      enccode = 123-(97-enccode)
    EndIf
    freetext = freetext + Text.GetCharacter(enccode)
EndFor
TextWindow.WriteLine(freetext)
