GraphicsWindow.Title = "Tokyo 2021 Games Flag"
SB_Workaround()
Init()
Shapes_Init_Olympic()
name = "Olymic"
scale = 1 / 2.3
Group_Add()
x = 120
y = 80
i = nGroup
Group_Move()
OlympicFlag()
Shapes_Init_Paralympic()
name = "Paralymic"
Group_Add()
x = 470
y = 80
i = nGroup
Group_Move()
ParalympicFlag()

Sub Init
  Not = "False=True;True=False;"
  gw = 810
  gh = 540
  GraphicsWindow.Width = gw
  GraphicsWindow.Height = gh
EndSub

Sub OlympicFlag
  lx = 110
  ty = 300
  oh = 160
  ow = oh / 2 * 3
  GraphicsWindow.DrawRectangle(lx, ty, ow, oh)
  w = oh / 30
  r = oh / 6 - w / 2
  GraphicsWindow.PenWidth = w
  color = "1=#0067C0;2=#FFD400;3=#000000;4=#008000;5=#ED1A3D;"
  x[1] = ow / 2 - 3 * r - 2 * w
  y[1] = ow / 4 + w / 2
  x[2] = ow / 2 - 2 * r - w
  y[2] = ow / 4 + w / 2 + r
  x[3] = ow / 2 - r
  y[3] = ow / 4 + w / 2
  x[4] = ow / 2 + w
  y[4] = ow / 4 + w / 2 + r
  x[5] = ow / 2 + r + 2 * w
  y[5] = ow / 4 + w / 2
  GraphicsWindow.PenWidth = 0
  For a = -315 To 135
    If a = 45 Then
      a = 90
    EndIf
    _a = Math.GetRadians(a)
    For i = 1 To 5
      GraphicsWindow.BrushColor = color[i]
      cx = x[i] + r
      cy = y[i] + r
      px = lx + cx + r * Math.Cos(_a) - w / 2
      py = ty + cy + r * Math.Sin(_a) - w / 2
      GraphicsWindow.FillEllipse(px, py, w, w)
    EndFor
  EndFor
EndSub

Sub ParalympicFlag
  scale = 0.12
  ox = 530
  oy = 380
  color = "1=#AA272F;2=#00549F;3=#008542;"
  cubic[1] = "1=210.134,447.59;2=196.926,453.403;3=-10.03,468.122;4=11.45,291.223;5=32.953,114.378;"
  cubic[1] = cubic[1] + "6=347.595,-9.82;7=406.157,11.982;8=327.769,52.1;9=165.435,84.083;10=104.464,264.128;"
  cubic[1] = cubic[1] + "11=44.363,441.763;12=208.664,445.142;13=210.134,447.59;"
  cubic[2] = "1=530.521,540.879;2=r-3.939,13.918;3=r-125.69,182.398;4=r-247.112,52.159;5=r-121.44,-130.145;"
  cubic[2] = cubic[2] + "6=r-14.026,-451.576;7=r40.431,-482.614;8=r-19.893,86.01;9=r-99.744,231.226;10=r-1.349,393.744;"
  cubic[2] = cubic[2] + "11=419.517,664.543;12=527.785,540.61;13=530.521,540.879;"
  cubic[3] = "1=794.25,272.876;2=r12.281,7.49;3=r143.059,168.887;4=r-13.811,252.746;5=r-156.787,83.881;"
  cubic[3] = cubic[3] + "6=r-438.813,-103.251;7=r-454.597,-163.974;8=r77.776,41.397;9=r196.933,156.35;10=r379.146,103.12;"
  cubic[3] = cubic[3] + "11=884.521,412.343;12=793.129,275.432;13=794.25,272.876;"
  For j = 1 To 3
    points = ""
    GraphicsWindow.BrushColor = color[j]
    GetPoints()
    For i = 1 To (Array.GetItemCount(cubic[j]) - 1) / 3
      For k = 0 To 1 Step 1 / 8
        i1 = (i - 1) * 3 + 1
        i2 = (i - 1) * 3 + 2
        i3 = (i - 1) * 3 + 3
        i4 = (i - 1) * 3 + 4
        param = "1=" + i1 + ";2=" + i2 + ";3=1-2;"
        CalcMidPoint()
        param = "1=" + i2 + ";2=" + i3 + ";3=2-3;"
        CalcMidPoint()
        param = "1=" + i3 + ";2=" + i4 + ";3=3-4;"
        CalcMidPoint()
        param = "1=1-2;2=2-3;3=1-3;"
        CalcMidPoint()
        param = "1=2-3;2=3-4;3=2-4;"
        CalcMidPoint()
        param = "1=1-3;2=2-4;3=1-4;"
        CalcMidPoint()
        n = n + 1
        points[n][1] = x["1-4"]
        points[n][2] = y["1-4"]
      EndFor
    EndFor
    GraphicsWindow.PenWidth = 0
    Program.Delay(200)
    LDShapes.AddPolygon(points)
    n = 0
  EndFor
EndSub

Sub CalcMidPoint
  ' param k - 0..1
  x[param[3]] = x[param[1]] + k * (x[param[2]] - x[param[1]])
  y[param[3]] = y[param[1]] + k * (y[param[2]] - y[param[1]])
EndSub

Sub GetPoints
  GraphicsWindow.PenWidth = 2
  For i = 1 To Array.GetItemCount(cubic[j])
    p = cubic[j][i]
    c = Text.GetIndexOf(p, ",")
    x[i] = Text.GetSubText(p, 1, c - 1)
    y[i] = Text.GetSubTextToEnd(p, c + 1)
    If Text.StartsWith(x[i], "r") Then
      x[i] = Text.GetSubTextToEnd(x[i], 2)
      x[i] = xLast + x[i]
      y[i] = yLast + y[i]
    EndIf
    If Math.Remainder(i, 3) = 1 Then
      xLast = x[i]
      yLast = y[i]
    EndIf
    x[i] = x[i] * scale + ox
    y[i] = y[i] * scale + oy
  EndFor
EndSub

Sub Group_Add
  ' Group | add shapes to a group
  ' param name - group name
  ' param shX, shY, origin of shape array
  ' param scale - scale of group
  ' param shape[] - shape array
  ' param nGroup - number of group
  ' return nGroup - updated number of group
  ' return group - group array
  Stack.PushValue("local", i)
  Stack.PushValue("local", x)
  Stack.PushValue("local", y)
  nGroup = nGroup + 1
  grp = ""
  grp["name"] = name
  grp["x"] = shX
  grp["y"] = shY
  grp["angle"] = 0
  grp["dir"] = 1
  Shapes_CalcWidthAndHeight()
  grp["width"] = shWidth
  grp["cx"] = shWidth / 2
  grp["height"] = shHeight
  s = scale
  grp["scale"] = s
  For i = 1 To Array.GetItemCount(shape)
    shp = shape[i]
    GraphicsWindow.PenWidth = shp["pw"] * s
    If shp["pw"] > 0 Then
      GraphicsWindow.PenColor = shp["pc"]
    EndIf
    If Text.IsSubText("rect|ell|tri|text|btn", shp["func"]) Then
      GraphicsWindow.BrushColor = shp["bc"]
    EndIf
    If Text.IsSubText("text|btn", shp["func"]) Then
      If silverlight Then
        fs = Math.Floor(shp["fs"] * 0.9)
      Else
        fs = shp["fs"]
      EndIf
      GraphicsWindow.FontSize = fs * s
      GraphicsWindow.FontName = shp["fn"]
      If shp["fb"] = "False" Then
        GraphicsWindow.FontBold = "False"
      Else
        GraphicsWindow.FontBold = "True"
      EndIf
    EndIf
    If shp["func"] = "rect" Then
      shp["obj"] = Shapes.AddRectangle(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "ell" Then
      shp["obj"] = Shapes.AddEllipse(shp["width"] * s, shp["height"] * s)
    ElseIf shp["func"] = "tri" Then
      shp["obj"] = Shapes.AddTriangle(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s, shp["x3"] * s, shp["y3"] * s)
    ElseIf shp["func"] = "line" Then
      shp["obj"] = Shapes.AddLine(shp["x1"] * s, shp["y1"] * s, shp["x2"] * s, shp["y2"] * s)
    ElseIf shp["func"] = "text" Then
      shp["obj"] = Shapes.AddText(shp["text"])
    EndIf
    x = shp["x"]
    y = shp["y"]
    shp["rx"] = x
    shp["ry"] = y
    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      alpha = Math.GetRadians(shp["angle"])
      SB_RotateWorkaround()
      shp["wx"] = x
      shp["wy"] = y
    EndIf
    If shp["func"] = "btn" Then
      shp["obj"] = Controls.AddButton(shp["caption"], shX + x * s, shY + y * s)
    Else
      Shapes.Move(shp["obj"], shX + x * s, shY + y * s)
    EndIf
    If Text.IsSubText("rect|ell|tri|text", shp["func"]) And (shp["angle"] <> 0) And (shp["angle"] <> "") Then
      Shapes.Rotate(shp["obj"], shp["angle"])
    EndIf
    shape[i] = shp
  EndFor
  grp["shape"] = shape
  group[nGroup] = grp
  y = Stack.PopValue("local")
  x = Stack.PopValue("local")
  i = Stack.PopValue("local")
EndSub

Sub Group_Move
  ' Group | Move a group
  ' param group[i] - group to move
  ' param x, y - position to move
  ' return group[i] - updated group
  grp = group[i]
  s = grp["scale"]
  grp["x"] = x
  grp["y"] = y
  shape = grp["shape"]
  n = Array.GetItemCount(shape)
  For j = 1 To n
    shp = shape[j]
    If silverlight And Text.IsSubText("tri|line", shp["func"]) Then
      _x = shp["wx"]
      _y = shp["wy"]
    Else
      _x = shp["rx"]
      _y = shp["ry"]
    EndIf
    Shapes.Move(shp["obj"], grp["x"] + _x * s, grp["y"] + _y * s)
  EndFor
  group[i] = grp
EndSub

Sub SB_RotateWorkaround
  ' Small Basic | Rotate workaround for Silverlight
  ' param shp - current shape
  ' param x, y - original coordinate
  ' param alpha - angle [radian]
  ' returns x, y - workaround coordinate
  If shp["func"] = "tri" Then
    x1 = -Math.Floor(shp["x3"] / 2)
    y1 = -Math.Floor(shp["y3"] / 2)
  ElseIf shp["func"] = "line" Then
    x1 = -Math.Floor(Math.Abs(shp["x1"] - shp["x2"]) / 2)
    y1 = -Math.Floor(Math.Abs(shp["y1"] - shp["y2"]) / 2)
  EndIf
  ox = x - x1
  oy = y - y1
  x = x1 * Math.Cos(alpha) - y1 * Math.Sin(alpha) + ox
  y = x1 * Math.Sin(alpha) + y1 * Math.Cos(alpha) + oy
EndSub

Sub SB_Workaround
  ' Small Basic | Workaround for Silverlight
  ' returns silverlight - "True" if in remote
  color = GraphicsWindow.GetPixel(0, 0)
  If Text.GetLength(color) > 7 Then
    silverlight = "True"
    msWait = 300
  Else
    silverlight = "False"
  EndIf
EndSub

Sub Shapes_CalcWidthAndHeight
  ' Shapes | Calculate total width and height of shapes
  ' param shape[] - shape array
  ' return shWidth, shHeight - total size of shapes
  For i = i To Array.GetItemCount(shape)
    shp = shape[i]
    If shp["func"] = "tri" Or shp["func"] = "line" Then
      xmin = shp["x1"]
      xmax = shp["x1"]
      ymin = shp["y1"]
      ymax = shp["y1"]
      If shp["x2"] < xmin Then
        xmin = shp["x2"]
      EndIf
      If xmax < shp["x2"] Then
        xmax = shp["x2"]
      EndIf
      If shp["y2"] < ymin Then
        ymin = shp["y2"]
      EndIf
      If ymax < shp["y2"] Then
        ymax = shp["y2"]
      EndIf
      If shp["func"] = "tri" Then
        If shp["x3"] < xmin Then
          xmin = shp["x3"]
        EndIf
        If xmax < shp["x3"] Then
          xmax = shp["x3"]
        EndIf
        If shp["y3"] < ymin Then
          ymin = shp["y3"]
        EndIf
        If ymax < shp["y3"] Then
          ymax = shp["y3"]
        EndIf
      EndIf
      shp["width"] = xmax - xmin
      shp["height"] = ymax - ymin
    EndIf
    If i = 1 Then
      shWidth = shp["x"] + shp["width"]
      shHeight = shp["y"] + shp["height"]
    Else
      If shWidth < shp["x"] + shp["width"] Then
        shWidth = shp["x"] + shp["width"]
      EndIf
      If shHeight < shp["y"] + shp["height"] Then
        shHeight = shp["y"] + shp["height"]
      EndIf
    EndIf
    shape[i] = shp
  EndFor
EndSub

Sub Shapes_Init_Olympic
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 50  ' x offset
  shY = 50  ' y offset
  shape = ""
  shape[1] = "func=rect;x=249.99;y=441.33;width=47.77;height=47.77;angle=30;bc=#012063;pw=0;"
  shape[2] = "func=rect;x=244.62;y=383.07;width=58.51;height=33.78;angle=-45;bc=#012063;pw=0;"
  shape[3] = "func=rect;x=179.36;y=448.33;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[4] = "func=rect;x=184.73;y=376.07;width=47.77;height=47.77;angle=-30;bc=#012063;pw=0;"
  shape[5] = "func=rect;x=97.81;y=448.34;width=65.26;height=17.48;angle=30;bc=#012063;pw=0;"
  shape[6] = "func=rect;x=68.54;y=383.67;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[7] = "func=rect;x=73.91;y=311.4;width=47.77;height=47.77;angle=-30;bc=#012063;pw=0;"
  shape[8] = "func=rect;x=121.69;y=424.45;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[9] = "func=rect;x=125.06;y=351.04;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[10] = "func=rect;x=10.28;y=360.81;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[11] = "func=rect;x=13.65;y=287.4;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[12] = "func=rect;x=-21.75;y=241.25;width=65.26;height=17.48;angle=90;bc=#012063;pw=0;"
  shape[13] = "func=rect;x=10.88;y=232.51;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[14] = "func=rect;x=34.77;y=208.62;width=65.26;height=17.48;angle=30;bc=#012063;pw=0;"
  shape[15] = "func=rect;x=70.77;y=256.99;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[16] = "func=rect;x=27.77;y=139.18;width=47.77;height=47.77;angle=150;bc=#012063;pw=0;"
  shape[17] = "func=rect;x=78.92;y=178.81;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[18] = "func=rect;x=55.03;y=89.66;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[19] = "func=rect;x=116.92;y=115.29;width=47.77;height=47.77;angle=90;bc=#012063;pw=0;"
  shape[20] = "func=rect;x=97.81;y=34.17;width=65.26;height=17.48;angle=150;bc=#012063;pw=0;"
  shape[21] = "func=rect;x=166.44;y=26.02;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[22] = "func=rect;x=228.33;y=51.65;width=47.77;height=47.77;angle=90;bc=#012063;pw=0;"
  shape[23] = "func=rect;x=106.55;y=66.8;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[24] = "func=rect;x=166.44;y=91.29;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[25] = "func=rect;x=217.36;y=2.14;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[26] = "func=rect;x=277.25;y=26.62;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[27] = "func=rect;x=336.92;y=34.17;width=65.26;height=17.48;angle=210;bc=#012063;pw=0;"
  shape[28] = "func=rect;x=328.18;y=66.8;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[29] = "func=rect;x=336.92;y=99.44;width=65.26;height=17.48;angle=150;bc=#012063;pw=0;"
  shape[30] = "func=rect;x=275.03;y=91.29;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[31] = "func=rect;x=400.56;y=97.8;width=47.77;height=47.77;angle=270;bc=#012063;pw=0;"
  shape[32] = "func=rect;x=338.67;y=137.43;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[33] = "func=rect;x=427.82;y=161.32;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[34] = "func=rect;x=376.67;y=186.95;width=47.77;height=47.77;angle=210;bc=#012063;pw=0;"
  shape[35] = "func=rect;x=456.48;y=241.25;width=65.26;height=17.48;angle=270;bc=#012063;pw=0;"
  shape[36] = "func=rect;x=427.22;y=289.62;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[37] = "func=rect;x=376.07;y=315.26;width=47.77;height=47.77;angle=210;bc=#012063;pw=0;"
  shape[38] = "func=rect;x=423.85;y=232.51;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[39] = "func=rect;x=370.7;y=256.99;width=58.51;height=33.78;angle=315;bc=#012063;pw=0;"
  shape[40] = "func=rect;x=424.45;y=360.81;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[41] = "func=rect;x=371.3;y=385.3;width=58.51;height=33.78;angle=315;bc=#012063;pw=0;"
  shape[42] = "func=rect;x=336.92;y=448.34;width=65.26;height=17.48;angle=330;bc=#012063;pw=0;"
  shape[43] = "func=rect;x=313.04;y=424.45;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[44] = "func=rect;x=280.4;y=415.71;width=65.26;height=17.48;angle=270;bc=#012063;pw=0;"
  shape[45] = "func=rect;x=316.41;y=351.04;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[46] = "func=text;x=434.45;y=424.45;text=TM;fs=18.48;fn=Calibri;bc=#012063;"
  shape[47] = "func=text;x=-24.45;y=494.45;text=TOKYO 2021;fs=108.48;fn=Calibri;bc=#012063;"
EndSub

Sub Shapes_Init_Paralympic
  ' Shapes | Initialize shapes data
  ' return shX, shY - current position of shapes
  ' return shape - array of shapes
  shX = 50  ' x offset
  shY = 50  ' y offset
  shape = ""
  shape[1] = "func=rect;x=160.85;y=465.23;width=65.26;height=17.48;angle=30;bc=#012063;pw=0;"
  shape[2] = "func=rect;x=184.73;y=441.34;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[3] = "func=rect;x=217.36;y=432.6;width=65.26;height=17.48;angle=270;bc=#012063;pw=0;"
  shape[4] = "func=rect;x=250;y=441.34;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[5] = "func=rect;x=273.88;y=465.23;width=65.26;height=17.48;angle=150;bc=#012063;pw=0;"
  shape[6] = "func=rect;x=97.81;y=448.34;width=65.26;height=17.48;angle=30;bc=#012063;pw=0;"
  shape[7] = "func=rect;x=131.58;y=400.56;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[8] = "func=rect;x=188.1;y=367.93;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[9] = "func=rect;x=253.37;y=367.93;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[10] = "func=rect;x=309.89;y=400.56;width=58.51;height=33.78;angle=-45;bc=#012063;pw=0;"
  shape[11] = "func=rect;x=336.92;y=448.34;width=65.26;height=17.48;angle=330;bc=#012063;pw=0;"
  shape[12] = "func=rect;x=68.54;y=383.67;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[13] = "func=rect;x=136.95;y=328.3;width=47.77;height=47.77;angle=150;bc=#012063;pw=0;"
  shape[14] = "func=rect;x=226.1;y=304.41;width=47.77;height=47.77;angle=90;bc=#012063;pw=0;"
  shape[15] = "func=rect;x=315.26;y=328.3;width=47.77;height=47.77;angle=30;bc=#012063;pw=0;"
  shape[16] = "func=rect;x=372.93;y=383.67;width=58.51;height=33.78;angle=315;bc=#012063;pw=0;"
  shape[17] = "func=rect;x=10.28;y=360.81;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[18] = "func=rect;x=73.91;y=311.4;width=47.77;height=47.77;angle=-30;bc=#012063;pw=0;"
  shape[19] = "func=rect;x=164.22;y=278.78;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[20] = "func=rect;x=277.25;y=278.78;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[21] = "func=rect;x=378.3;y=311.4;width=47.77;height=47.77;angle=210;bc=#012063;pw=0;"
  shape[22] = "func=rect;x=424.45;y=360.81;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[23] = "func=rect;x=13.65;y=287.4;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[24] = "func=rect;x=101.18;y=261.88;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[25] = "func=rect;x=217.36;y=254.29;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[26] = "func=rect;x=340.29;y=261.88;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[27] = "func=rect;x=427.82;y=287.4;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[28] = "func=rect;x=-21.75;y=241.25;width=65.26;height=17.48;angle=90;bc=#012063;pw=0;"
  shape[29] = "func=rect;x=51.65;y=223.88;width=47.77;height=47.77;angle=90;bc=#012063;pw=0;"
  shape[30] = "func=rect;x=154.33;y=237.4;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[31] = "func=rect;x=280.4;y=237.4;width=65.26;height=17.48;angle=180;bc=#012063;pw=0;"
  shape[32] = "func=rect;x=400.56;y=223.88;width=47.77;height=47.77;angle=270;bc=#012063;pw=0;"
  shape[33] = "func=rect;x=456.48;y=241.25;width=65.26;height=17.48;angle=270;bc=#012063;pw=0;"
  shape[34] = "func=rect;x=14.25;y=176.59;width=58.51;height=33.78;angle=285;bc=#012063;pw=0;"
  shape[35] = "func=rect;x=102.8;y=198.25;width=58.51;height=33.78;angle=195;bc=#012063;pw=0;"
  shape[36] = "func=rect;x=338.67;y=198.25;width=58.51;height=33.78;angle=165;bc=#012063;pw=0;"
  shape[37] = "func=rect;x=427.22;y=176.59;width=58.51;height=33.78;angle=75;bc=#012063;pw=0;"
  shape[38] = "func=rect;x=10.28;y=121.7;width=65.26;height=17.48;angle=300;bc=#012063;pw=0;"
  shape[39] = "func=rect;x=76.14;y=136.95;width=47.77;height=47.77;angle=210;bc=#012063;pw=0;"
  shape[40] = "func=rect;x=376.07;y=136.95;width=47.77;height=47.77;angle=-30;bc=#012063;pw=0;"
  shape[41] = "func=rect;x=424.45;y=121.7;width=65.26;height=17.48;angle=60;bc=#012063;pw=0;"
  shape[42] = "func=rect;x=70.17;y=80.91;width=58.51;height=33.78;angle=315;bc=#012063;pw=0;"
  shape[43] = "func=rect;x=371.3;y=80.91;width=58.51;height=33.78;angle=45;bc=#012063;pw=0;"
  shape[44] = "func=rect;x=97.81;y=34.17;width=65.26;height=17.48;angle=150;bc=#012063;pw=0;"
  shape[45] = "func=rect;x=336.92;y=34.17;width=65.26;height=17.48;angle=210;bc=#012063;pw=0;"
  shape[46] = "func=text;x=434.45;y=424.45;text=TM;fs=18.48;fn=Calibri;bc=#012063;"
  shape[47] = "func=text;x=-24.45;y=494.45;text=TOKYO 2021;fs=108.48;fn=Calibri;bc=#012063;"
  shape[48] = "func=text;x=38.45;y=604.45;text=PARALYMPIC GAMES;fs=50.48;fn=Calibri;bc=#012063;"
EndSub
