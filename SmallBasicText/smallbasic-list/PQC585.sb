' SmallBasic Version 1.0

' Funktion: EffectCompare (+Extension Vs pure SB)

' Autor: Pappa Lapub

' Herkunft: 

' ImportURL: http://smallbasic.com/program/?

' Extension: LitDev

' Kommentar: In SBGray/SBSepia/SBFilter  for every Pixel (100*100) = 10000 cycles

'  http://www.codeproject.com/Articles/33838/Image-Processing-using-C

' Variablen: 

'  

' --------------------------------------------------------------------------------

' pic = "http://www.codeproject.com/KB/GDI-plus/ImageProcessing2/img.jpg"

' resized to 100x100 -> png

pic = Program.Directory + "\ImageProcessing_100.png"

W = ImageList.GetWidthOfImage(pic)-1

H = ImageList.GetHeightOfImage(pic)-1



GraphicsWindow.BrushColor = "Black"

GraphicsWindow.Width = 5*(W+10)

GraphicsWindow.Height = 2*H+80

GraphicsWindow.Title = "EffectCompare  Extension : SB"

GraphicsWindow.DrawText(0,0,"Original:")



Grayscale = Controls.AddButton("Grayscale", W+10,0)

Sepia = Controls.AddButton("Sepia", 2*(W+10),0)

Filter = Controls.AddButton("FilterColor", 3*(W+10),0)



GraphicsWindow.DrawText(4*(W+10),30+H/2, "Extension")

GraphicsWindow.DrawText(4*(W+10),40+3*H/2, "SB")



img = ImageList.LoadImage(pic)

GraphicsWindow.DrawImage(img, 0, 30)



Controls.ButtonClicked = OnButtonClick





' ===================================

' Event

Sub OnButtonClick

  lastBtn = Controls.LastClickedButton

  

  If lastBtn = Grayscale Then

    ExtensionGray()

    SBGray()

    

  ElseIf lastBtn = Sepia Then

    ExtensionSepia()

    SBSepia()

    

  ElseIf lastBtn = Filter Then

    'col = LDColours.LightSeaGreen     ' eg. default  FilterColor without dialog

    col = LDDialogs.Colour()

    colR = Math.Round(100*LDColours.GetRed(col)/255)/100    ' 0.00 - 1.00

    colG = Math.Round(100*LDColours.GetGreen(col)/255)/100

    colB = Math.Round(100*LDColours.GetBlue(col)/255)/100

    GraphicsWindow.DrawBoundText(4*(W+10),0, W, col +" ("+ colR +";"+ colG +";"+ colB +")")

    LDShapes.BrushColour(Filter,col)

    

    ExtensionFilter()

    SBFilter()

    

  EndIf

EndSub



'--------------------------------------------------------

' Subs

Sub ExtensionGray

  img1 = ImageList.LoadImage(pic)

  LDImage.EffectGray(img1)

  GraphicsWindow.DrawImage(img1, W+10, 30)

EndSub



Sub SBGray

  img2 = ImageList.LoadImage(pic)

  start = Clock.ElapsedMilliseconds

  For i = 0 To W

    For j = 0 To H

      c = LDImage.GetPixel(img2, i,j)

      cR = LDColours.GetRed(c)

      cG = LDColours.GetGreen(c)

      cB = LDColours.GetBlue(c)

      'gray = 0.33*cR + 0.33*cG + 0.33*cB                         ' average Mean, or...

      gray = 0.3*cR + 0.59*cG + 0.11*cB        ' weighted Mean, or...

      'gray = 0.299*cR + 0.587*cG + 0.114*cB                  ' weighted Mean, LitDev

      

      newCol = GraphicsWindow.GetColorFromRGB(gray, gray, gray)

      LDImage.SetPixel(img2, i,j, newCol)

    EndFor

  EndFor

  time = Math.Floor(Clock.ElapsedMilliseconds - start)/1000

  GraphicsWindow.DrawText(W+10,2*H+50, time)

  GraphicsWindow.DrawImage(img2, W+10, H+40)

EndSub



' .............................................................

Sub ExtensionSepia

  img3 = ImageList.LoadImage(pic)

  LDImage.EffectGray(img3)

  LDImage.Multiply(img3, 1, 0.95, 0.82)

  GraphicsWindow.DrawImage(img3, 2*(W+10), 30)

EndSub



Sub SBSepia

  img4 = ImageList.LoadImage(pic)

  start = Clock.ElapsedMilliseconds

  For i = 0 To W

    For j = 0 To H

      c = LDImage.GetPixel(img4, i,j)

      cR = LDColours.GetRed(c)

      cG = LDColours.GetGreen(c)

      cB = LDColours.GetBlue(c)

      'gray = 0.33*cR + 0.33*cG + 0.33*cB                         ' average Mean, or...

      gray = 0.3*cR + 0.59*cG + 0.11*cB        ' weighted Mean, or...

      'gray = 0.299*cR + 0.587*cG + 0.114*cB                   ' weighted Mean, LitDev

      

      newCol = GraphicsWindow.GetColorFromRGB(gray, 0.95*gray, 0.82*gray)

      LDImage.SetPixel(img4, i,j, newCol)

    EndFor

  EndFor

  time = Math.Floor(Clock.ElapsedMilliseconds - start)/1000

  GraphicsWindow.DrawText(2*(W+10),2*H+50, time)

  GraphicsWindow.DrawImage(img4, 2*(W+10), H+40)

EndSub



' ............................................................

Sub ExtensionFilter

  img5 = ImageList.LoadImage(pic)

  LDImage.EffectGray(img5)

  LDImage.Multiply(img5, colR, colG, colB)

  GraphicsWindow.DrawImage(img5, 3*(W+10), 30)

EndSub



Sub SBFilter

  img6 = ImageList.LoadImage(pic)

  start = Clock.ElapsedMilliseconds

  For i = 0 To W

    For j = 0 To H

      c = LDImage.GetPixel(img6, i,j)

      cR = LDColours.GetRed(c)

      cG = LDColours.GetGreen(c)

      cB = LDColours.GetBlue(c)

      'gray = 0.33*cR + 0.33*cG + 0.33*cB                         ' average Mean, or...

      gray = 0.3*cR + 0.59*cG + 0.11*cB        ' weighted Mean, or...

      'gray = 0.299*cR + 0.587*cG + 0.114*cB                   ' weighted Mean, LitDev

      

      newCol = GraphicsWindow.GetColorFromRGB(colR * gray, colG * gray, colB * gray)

      LDImage.SetPixel(img6, i,j, newCol)

    EndFor

  EndFor

  time = Math.Floor(Clock.ElapsedMilliseconds - start)/1000

  GraphicsWindow.DrawText(3*(W+10),2*H+50, time)

  GraphicsWindow.DrawImage(img6, 3*(W+10), H+40)

EndSub


