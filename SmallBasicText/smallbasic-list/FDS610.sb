'****Work In Progress****
'Key Stroke Recorder ver 0.1 by Ezra94 & Yan Grenier
GraphicsWindow.Title = "Key Stroke Recorder 0.1"
GraphicsWindow.CanResize = "False"
graphWinWidth = 600
graphWinHeight = 400 
GraphicsWindow.Width = graphWinWidth
GraphicsWindow.Height = graphWinHeight
GraphicsWindow.Left = (Desktop.Width - graphWinWidth)*0.50 
GraphicsWindow.Top = (Desktop.Height - graphWinHeight)*0.25 
Controls.ButtonClicked = onButtonClicked
Controls.TextTyped = onTextTypedInTextBox

mainMenuTextBoxWidth = graphWinWidth
mainMenuTextBoxHeight = graphWinHeight*0.71
mainMenuTextBoxX = (graphWinWidth - mainMenuTextBoxWidth)*0.50
mainMenuTextBoxY = (graphWinHeight - mainMenuTextBoxHeight)*0.50

mainMenuButtonsWidth = graphWinWidth*0.15
mainMenuButtonsHeight = graphWinHeight*0.08
mainMenuButtonsX = (graphWinWidth - mainMenuButtonsWidth)
mainMenuButtonsY = (graphWinHeight - mainMenuButtonsHeight)*0.07

buttonName = "0=Record;1=Play;2=Stop;"
playState = "0=None;1=Recording;2=Playing"

currentState = 0 
nextRecord = 0

createMainMenu()

While "True" 
  selectButtonInMainMenu()
  recordContent()
  playRecording()
  returnToMainMenu()
EndWhile 

Sub createMainMenu
  mainMenuTextBox = Controls.AddMultiLineTextBox(mainMenuTextBoxX,mainMenuTextBoxY)
  Controls.SetSize(mainMenuTextBox,mainMenuTextBoxWidth,mainMenuTextBoxHeight)
  
  For i = 1 To 0 Step -1
    button[i] = Controls.AddButton(buttonName[i],mainMenuButtonsX-(mainMenuButtonsWidth*i),mainMenuButtonsY)
    Controls.SetSize(button[i],mainMenuButtonsWidth,mainMenuButtonsHeight)
  EndFor
EndSub

Sub selectButtonInMainMenu
  While playState[currentState] = "None" 
    If (lastClicked = "Record") Then 
      record = "" 'Delete Temporary Data
      currentState = 1
      removeMainMenuButtons()
    ElseIf (lastClicked = "Play") Then 
      currentState = 2 
      removeMainMenuButtons()
    EndIf
    lastClicked = ""
    Program.Delay(50) 
  EndWhile 
EndSub 

Sub removeMainMenuButtons
  For i = 1 To 0 Step -1
    Controls.Remove(button[i])
  EndFor
  
  button[2] = Controls.AddButton(buttonName[2],mainMenuButtonsX,mainMenuButtonsY) 
  Controls.SetSize(button[2],mainMenuButtonsWidth,mainMenuButtonsHeight) 
EndSub 

Sub recordContent 
  inputTextInTextBox = "False" 'Prevent statement on line 76 from becoming true w/out user input
  startRecording = Clock.ElapsedMilliSeconds 'Must be placed immediately before recording starts 
  While playState[currentState] = "Recording"
    If (inputTextInTextBox = "True") Then 
      record[nextRecord]["time"] = Clock.ElapsedMilliseconds - startRecording
      record[nextRecord]["text"] = Controls.GetTextBoxText(mainMenuTextBox)
      nextRecord = nextRecord + 1
      inputTextInTextBox = "False" 
    EndIf 
    returnToMainMenu()
    Program.Delay(20) 
  EndWhile 
EndSub 

Sub playRecording 
  recordCount = Array.GetItemCount(record) 
  startReplay = Clock.ElapsedMilliseconds 'Must be placed immediately before recording starts
  While nextRecord < recordCount And playState[currentState] = "Playing" 
    elapsed = Clock.ElapsedMilliseconds - startReplay
    While record[nextRecord]["time"] <= elapsed And playState[currentState] = "Playing"
      If (nextRecord < recordCount) Then 
        Controls.SetTextBoxText(mainMenuTextBox,record[nextRecord]["text"])
        nextRecord = nextRecord + 1
      Else
        Program.Delay(20)
      EndIf 
      returnToMainMenu()
    EndWhile
    returnToMainMenu() 
    Program.Delay(50)
  EndWhile
EndSub 

Sub returnToMainMenu
  If (lastClicked = "Stop") Then 
    nextRecord = 0 
    currentState = 0 
    GraphicsWindow.Clear() 
    createMainMenu()
  EndIf 
EndSub 

Sub onButtonClicked 
  lastClicked = Controls.GetButtonCaption(Controls.LastClickedButton)
EndSub

Sub onTextTypedInTextBox
  inputTextInTextBox = "True" 
EndSub 
