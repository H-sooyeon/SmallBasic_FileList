targetTotal = 489.69

iterMax = 1000

valNum = 37

For i = 1 To valNum

' The following line could be harmful and has been automatically commented.

'   val[i] = File.ReadLine(Program.Directory+"\values.txt",i)

EndFor



'Assume value can only be used once

For incNum = 1 To valNum-1

  'Included numbers 

  inc = ""

  incTotal = 0

  For i = 1 To incNum

    inc[i] = val[i]

    incTotal = incTotal+inc[i]

  EndFor

  incDev = Math.Abs(incTotal-targetTotal)

  

  'Excluded numbers

  exc = ""

  excNum = valNum-incNum

  For i = incNum+1 To valNum

    exc[i-incNum] = val[i]

  EndFor

  

  swap = 1

  iter = 0

  While (iter < iterMax And incDev > 0 And swap = 1)

    iter = iter + 1

    swap = 0

    'Test all combinations of included and excluded numbers to see if we can do better

    For i = 1 To incNum

      For j = 1 To excNum

        newDev = Math.Abs(incTotal-inc[i]+exc[j]-targetTotal)

        If (newDev < incDev) Then

          incTotal = incTotal-inc[i]+exc[j]

          incDev = newDev

          swap = 1

          temp = inc[i]

          inc[i] = exc[j]

          exc[j] = temp

        EndIf

      EndFor

    EndFor

  EndWhile

  

  'Output the results

  TextWindow.ForegroundColor = "Red"

  TextWindow.WriteLine("Best answer for "+incNum+" numbers in "+iter+" iterations is "+incTotal+" deviation "+(incTotal-targetTotal))

  TextWindow.ForegroundColor = "White"

  For i = 1 To incNum

    TextWindow.Write(inc[i]+" ")

  EndFor

  TextWindow.WriteLine("")

EndFor

  


