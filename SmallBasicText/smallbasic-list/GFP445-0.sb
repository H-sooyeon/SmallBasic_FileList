' Moving dots

' Version 0.2

' Program ID GFP445-0



gw = GraphicsWindow.Width

gh = GraphicsWindow.Height

ox = gw / 2

oy = gh / 2

r = 70

size = 10

GraphicsWindow.BackgroundColor = "Black"

GraphicsWindow.PenWidth = 0

k = 1

a = 0

b = 0

c = 0

For i = 1 To 7

  GraphicsWindow.BrushColor = "#330099FF"

  light[i] = Shapes.AddEllipse(2 * size, 2* size)

  GraphicsWindow.BrushColor = "White"

  dot[i] = Shapes.AddEllipse(size, size)

  MoveDot()

EndFor

While "True"

  GraphicsWindow.Title = "k=" + k

  Program.Delay(10)

  k = k + 0.01

  a = a + Math.Pi * 0.001

  b = b + Math.Pi * 0.002

  c = c + Math.Pi * 0.004

  For i = 1 To 7

    MoveDot()

  EndFor

EndWhile



Sub MoveDot

  x = r * Math.Cos(k * 2 * Math.Pi * (i - 1) / 7)

  y = -r * Math.Sin(k * 2 * Math.Pi * (i - 1) / 7)

  z = 0

  Mapping()

  Shapes.Move(light[i], x - size, y - size)

  Shapes.Move(dot[i], x - size / 2, y - size / 2)

EndSub



Sub Mapping

  ' param x, y, z - point to map 

  ' param a - angle around x axis [rad]

  ' param b - angle around y axis [rad]

  ' param c - angle around z axis [rad]

  _y = y

  _z = z

  y = Math.Cos(a) * _y - Math.Sin(a) * _z

  z = Math.Sin(a) * _y + Math.Cos(a) * _z

  _z = z

  _x = x

  z = Math.Cos(b) * _z - Math.Sin(b) * _x

  x = Math.Sin(b) * _z + Math.Cos(b) * _x

  _x = x

  _y = y

  x = Math.Cos(c) * _x - Math.Sin(c) * _y

  y = Math.Sin(c) * _x + Math.Cos(c) * _y

  x = ox + x

  y = oy - y

EndSub


