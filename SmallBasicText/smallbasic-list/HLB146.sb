' Set up the graphics windows

GraphicsWindow.BackgroundColor = "Green"

GraphicsWindow.Height = 600

GraphicsWindow.Width = 600



' Set up the default font size and colour (for the big numbers)

GraphicsWindow.FontBold = "True"

GraphicsWindow.FontName = "Courier New"

GraphicsWindow.FontSize = 300

GraphicsWindow.BrushColor = "Black"



' Enable the graphics window to respond to mouse and keyboard events.

GraphicsWindow.MouseDown = OnMouseDown

GraphicsWindow.KeyDown = OnKeyDown



pause = 20

canclick = "True" 'flag to only allow clicks when a new pair appear

p1score = 0

p2score = 0

number1 = 0

number2 = 0



While ("True")

  GraphicsWindow.Clear() '  Clear the screen to remove the old numbers

  GraphicsWindow.BackgroundColor = "Cyan"

  p1clicked = "False"

  p2clicked = "False"

  canclick = "True"

  number1 = Math.GetRandomNumber(6)

  number2 = Math.GetRandomNumber(6)

  GraphicsWindow.DrawText(75,125, number1)

  GraphicsWindow.DrawText(325,125, number2)

  Updatep1Score()

  Updatep2Score()

  For i = 1 To 50 ' 50 * 20ms = 1 second delay between number pairs

    Program.Delay(pause)

    If canclick = "True" And p1clicked = "True" And number1 = number2 Then

      GraphicsWindow.BackgroundColor = "Green"

      p1score = p1score + 1

      Updatep1Score()

      canclick = "False"

    ElseIf canclick = "True" And p2clicked = "True" And number1 = number2 Then

      GraphicsWindow.BackgroundColor = "Green"

      p2score = p2score + 1

      Updatep2Score()

      canclick = "False"

    ElseIf canclick = "True" And p1clicked = "True" Then

      GraphicsWindow.BackgroundColor = "Red"

      p1score = p1score - 1

      If p1score < 0 Then

        p1score = 0

      EndIf

      Updatep1Score()

      canclick = "False"

    ElseIf canclick = "True" And p2clicked = "True" Then

      GraphicsWindow.BackgroundColor = "Red"

      p2score = p2score - 1

      If p2score < 0 Then

        p2score = 0

      EndIf

      Updatep2Score()

      canclick = "False"

    EndIf

  EndFor

EndWHile

  

Sub OnMouseDown

  p1clicked = "True"

EndSub



Sub OnKeyDown

  if GraphicsWindow.LastKey = "Space" Then

    p2clicked = "True"

  EndIf

EndSub



Sub Updatep1Score

  GraphicsWindow.FillRectangle(10,10,80,50)

  GraphicsWindow.FontSize = 50

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.DrawText(20,10, p1score)

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 18

  GraphicsWindow.DrawText(12, 62, "P1 Click Mouse")

  GraphicsWindow.FontSize = 300

EndSub



Sub Updatep2Score

  GraphicsWindow.FillRectangle(510,10,80,50)

  GraphicsWindow.FontSize = 50

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.DrawText(520,10, p2score)

  GraphicsWindow.BrushColor = "Black"

  GraphicsWindow.FontSize = 18

  GraphicsWindow.DrawText(435, 62, "P2 Press Space")

  GraphicsWindow.FontSize = 300

EndSub


