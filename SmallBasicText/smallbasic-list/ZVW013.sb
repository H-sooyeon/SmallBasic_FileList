GraphicsWindow.Width = 640 
GraphicsWindow.Height = 480
GraphicsWindow.BackgroundColor = "blue"
GraphicsWindow.Show()

maxcircles = 50
fps = 0
exit = 0
scenario = 1  'change to 0 for performance drop !!

For i = 0 To maxcircles
   circle[i] = Shapes.AddEllipse(20,20)           
   circletime[i] = Math.GetRandomNumber(300)   
EndFor

' Here comes the main loop
fpstime = Clock.ElapsedMilliseconds
While exit = 0
  
  fps = fps + 1
  If Clock.ElapsedMilliseconds - fpstime >= 1000 Then    
    fpstime = Clock.ElapsedMilliseconds
    GraphicsWindow.BrushColor = "blue"
    GraphicsWindow.FillRectangle(0,0,50,17)
    GraphicsWindow.BrushColor = "white"
    GraphicsWindow.DrawText(0, 0, "fps "+ fps )
    fps = 0
  EndIf
  
  For i = 0 to maxcircles
    circletime[i] = circletime[i] + 1
    If circletime[i] > 300 then
      angle = Math.GetRandomNumber(360)  
      circlexv[i] = Math.Cos(angle *0.0174)  
      circleyv[i] = Math.Sin(angle *0.0174 )    
      circlex[i] = 320
      circley[i] = 240
      Shapes.Move(circle[i], circlex[i] , circley[i])
      circletime[i] = 0     
    EndIf
   
    'Move circles 
    If scenario = 0 then
       Shapes.Move(circle[i], Shapes.GetLeft(circle[i]) + circlexv[i], Shapes.GetTop(circle[i]) + circleyv[i]) 
     Else     
        circlex[i] = circlex[i] + circlexv[i]
        circley[i] = circley[i] + circleyv[i]
        Shapes.Move(circle[i], circlex[i], circley[i])  
      EndIf    
    EndFor  
EndWhile
  
