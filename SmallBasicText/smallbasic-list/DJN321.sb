gameRunning = "True"

CreateBattleArray()

CreateTextArray()



While gameRunning = "True"

  TextWindow.Clear()

  PrintIntro()

  GetHumanWeapon()

  GetComputerWeapon()

  DoBattle()

  DetermineVictory()

  PlayAgain()

EndWhile



Sub PrintIntro

  TextWindow.ForegroundColor = "Gray"

  TextWindow.WriteLine("")

  TextWindow.WriteLine(" Welcome to the ROCK, PAPER, SCISSORS game.")

  TextWindow.WriteLine("")

  TextWindow.WriteLine(" Please select your 'weapon.'")

  TextWindow.WriteLine("")

  TextWindow.WriteLine(" 1. Rock")

  TextWindow.WriteLine(" 2. Paper")

  TextWindow.WriteLine(" 3. Scissors")

EndSub



Sub GetHumanWeapon

  TextWindow.WriteLine("")

  TextWindow.Write(" >")

  humanWeapon = TextWindow.ReadNumber()

  If humanWeapon < 1 Or humanWeapon > 3 Then

    humanWeapon = Math.GetRandomNumber(3)

    TextWindow.WriteLine(" You have made an invalid choice.  A choice has been made for you.")

  EndIf

EndSub



Sub GetComputerWeapon

  computerWeapon = Math.GetRandomNumber(3)

  TextWindow.WriteLine("")

  TextWindow.WriteLine(" The computer has selected a " + computerWeapon + ".")

EndSub



Sub DoBattle

  victory = battleArray[humanWeapon][computerWeapon]

  TextWindow.WriteLine("")

  TextWindow.WriteLine("")

  TextWindow.ForegroundColor = "Red"

  TextWindow.WriteLine(textArray[humanWeapon][computerWeapon])

EndSub



Sub DetermineVictory

  TextWindow.WriteLine("")

  If victory = 1 Then

    TextWindow.ForegroundColor = "Green"

    TextWindow.WriteLine(" You won!")

  ElseIf victory = 2 then

    TextWindow.ForegroundColor = "Blue"

    TextWindow.WriteLine(" The computer won!")

  Else

    TextWindow.ForegroundColor = "Gray"

    TextWindow.WriteLine(" A draw! No one wins!")

  EndIf

  TextWindow.ForegroundColor = "Gray"

EndSub



Sub CreateBattleArray

  ' First element is the human weapon.  1 = rock, 2 = paper, 3 = scissors

  ' Second element is the computer weapon.  1 = rock, 2 = paper, 3 = scissors

  ' The array value is the victory condition.  1 = human victory, 2 = computer victory, 3 = draw

  battleArray[1][1] = 3

  battleArray[2][1] = 1

  battleArray[3][1] = 2

  battleArray[1][2] = 2

  battleArray[2][2] = 3

  battleArray[3][2] = 1

  battleArray[1][3] = 1

  battleArray[2][3] = 2

  battleArray[3][3] = 3

EndSub



Sub CreateTextArray

  textArray[1][1] = " Rock vs. Rock."

  textArray[2][1] = " Paper vs. Rock.  Paper covers Rock."

  textArray[3][1] = " Scissors vs. Rock.  Rock break Scissors."

  textArray[1][2] = " Rock vs. Paper.  Paper covers Rock."

  textArray[2][2] = " Paper vs. Paper."

  textArray[3][2] = " Scissors vs. Paper.  Scissors cut Paper."

  textArray[1][3] = " Rock vs. Scissors.  Rock break Scissors."

  textArray[2][3] = " Paper vs. Scissors.  Scissors cut Paper."

  textArray[3][3] = " Scissors vs. Scissors."

EndSub



Sub PlayAgain

  TextWindow.ForegroundColor = "Gray"

  TextWindow.WriteLine("")

  TextWindow.Write(" Would you like to play again (y/n)?")

  answer = TextWindow.Read()

  If answer <> "y" Then

    Program.End()

  EndIf

EndSub


