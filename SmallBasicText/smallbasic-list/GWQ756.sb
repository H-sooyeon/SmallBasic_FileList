'  Easy    Tunnel  Game       by   NaochanON                   2012/09/30

GUI()


While "true"  
  NN=NN+1
  Shapes.move(player,GraphicsWindow.MouseX,GraphicsWindow.MouseY) 
  For i=startx To endx
    Shapes.Move(rect[i],Shapes.GetLeft(rect[i])+BW/4,Shapes.Gettop(rect[i]))
    collisioncheck()
    edgecheck()
  EndFor
  Program.Delay(50)
endWhile

Sub collisioncheck
  If  math.Abs(Shapes.GetLeft(rect[i])-Shapes.GetLeft(player))<20 Then 
    If Math.abs(  Shapes.Gettop(player)+SH/2-(Shapes.Gettop(rect[i])+ LY[i]/2) )>LY[i]/2 then
      GraphicsWindow.Title=" Score = "+NN +" points     Crashed  !!!!  "
      Sound.PlayClickAndWait()  
      Shapes.move(player,Shapes.GetLeft(player)+5,Shapes.Gettop(player)) 
      Program.Delay(1000)
      NN=0
    Else   
      GraphicsWindow.Title=" Score = "+NN +" points "
    EndIf  
  endif  
endsub

sub edgecheck
  If BW*22<Shapes.GetLeft(rect[i]) Then              '  if rect goes over right screen edge
    Shapes.Move(rect[i],startx*BW,startY+(2-Math.GetRandomNumber(3))*20)
    startY=Shapes.Gettop(rect[i])
    If starty<20 then 
      startY=25
    elseif startY>400 then 
      startY=370
    EndIf
  EndIf
endsub

Sub GUI
  GraphicsWindow.Width=800
  GraphicsWindow.Height=500
  GraphicsWindow.BackgroundColor="Darkgreen"
  GraphicsWindow.BrushColor="Khaki"
  GraphicsWindow.PenColor="Khaki"
  BW=40
  startx=-4
  endx=24
  For i=startx To endx
    LY[i]=85+Math.GetRandomNumber(90)
    rect[i]= Shapes.AddRectangle(BW,LY[i])
    Shapes.Move(rect[i],BW*i,150-LY[i]/2)
  EndFor  
  startY= Shapes.Gettop(rect[startx])
  url=ImageList.LoadImage("http://illustration-free.net/thumb/png/ifn0019.png") '  UFO image
  player= Shapes.AddImage(url)
  sW= ImageList.GetWidthOfImage(url)
  sH= ImageList.GetHeightOfImage(url)
  Mouse.HideCursor()
EndSub
