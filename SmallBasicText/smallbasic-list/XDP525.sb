'Program: Names
'by Martin Menze
'
'Use LitDev Extentions
'
'06/2015

GraphicsWindow.Show()                            'Before do anny thinks show the graphic window. It is a old Small Basic bug.
'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
'Events                                                                                                 'Events can start a Subroutine. This program use the following events. 
'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
GraphicsWindow.KeyUp = KeyUp                     'Start Sub KeyUp if a key was uped.
Controls.ButtonClicked = ButtonClicked           'Start Sub ButtonClickes if a button was clicked.

'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
'Initialisation
'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
Initialisation()                                 'All objects, variable and so on will define in the subroutine Initialisation.

'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
'Main Program                                                                                
'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
Controls.ShowControl(ArrTextBox[VarNumFirstName])'Show the texbox to input the first name.
VarInputIndex = VarNumFirstName                  'Fill the variable VarInputIndex with the same value it is in the variable VarNumFirstName. It is 1.   
LDFocus.SetFocus(ArrTextBox[VarInputIndex])      'Set the fokus to the textbox to input the first name. The cursor is blinking in this text box.
SetFocus()                                       'Start the subroutine SetFocus.
Fokus()                                          'Start the subroutine Fokus.
While VarEndOfProgram = F                        'Begin of the main loop. Lopp in the main loop as long as the variable VarEndOfProgram is equal F. F is "False".
  Fokus()                                        'Start the subroutine Fokus.  
  CheckEvents()                                  'Start the subroutine CheckEvents
EndWhile                                         'End of the main loop. 
Program.End()                                    'The End of this Program.

'▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀▀
'Subroutines
'▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄���▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine Fokus
'Notize the value form the last name of the textbox in the variable VarOldFocus.
'Notize the value from the actual name of the textbox in the variable VarFokus.
'Is the Focus changed then identify in a loop the name of the actual textbox. 
'Has it found, set the loop variable of the end value. This optimize the loop cycle.
'Start the subroutine Set Focus.
'______________________________________________________________________
Sub Fokus                                        'Begin of subroutine Fokus
  VarOldFokus = VarFokus 
  VarFokus = LDFocus.GetFocus()                  'Get the name of the object with the actualy focus.
  If VarFokus <> VarOldFokus Then                'If the focus was changed do the folowing.
    For i = VarNumFirstName To VarNumLastInput   'Loop as long as i is the same as VarNumLastInput. Begin with i equal the same as in VarNumFirstName
      If VarFokus = ArrTextBox[i] Then           'Is the Name of the actual object the same as the textbox 
        VarOldInputIndex = VarInputIndex         'Put the value from variable VarInputIndex into the variable VarOldInputIndex
        VarInputIndex = i                        'Put the value from variable i into the variable VarInputIndex
        SetFocus()                               'Start the subroutine SetFocus. 
      EndIf                                      'End the last If
    EndFor                                       'If the value from i equal VarNumLastInput it is the end of the loop else the add 1 to the vallue of i.
  EndIf                                          'End the last if 
EndSub 'Fokus                                                                                    'go back to the next line after the Start of the subroutine Fokus()

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine CheckEvents
'The subroutine CheckEvents check the events skips for events.
'Was a button clicked it start the subroutine WichButtonWasClicked.
'Was a key pressed and key uped then start the subroutine WichKeyWasPressed.
'______________________________________________________________________
Sub CheckEvents
  If VarButtonClicked = T Then
    VarButtonClicked = F
    VarLastClickedButton = Controls.LastClickedButton 
    WichButtonWasClicked() 
  EndIf   
  If VarKeyUP = T Then 
    VarKeyUP = F 
    VarLastKey = GraphicsWindow.LastKey 
    WichKeyWasUped()
  EndIf 
EndSub 'CheckEvents

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine WhichButtonWasClicked
'There are 3 buttons (Next, Save and Exit).
'Was clicked Next then start the subroutines SetFocusToNextTextBox and SetFocus.
'Was clicked Save then start the subroutines SetFocustoToNextTextBox and SetFocus.
' Start the subroutine DataToDatabase and ClearObjects. At last by button Save 
' to set the focus to the first text box.
' Was clicked Exit then put T (True) to the variable VarEndOfProgram. So the main loop will ended
'______________________________________________________________________
Sub WichButtonWasClicked
  If VarLastClickedButton = ArrButton[VarNumNextField] Then
    SetFocusToNextTextBox()
    SetFocus()   
  ElseIf VarLastClickedButton = ArrButton[VarNumSave] Then 
    SetFocusToNextTextBox()
    SetFocus()
    ArrData[VarNumLastName] = LDText.Trim(ArrData[VarNumLastName])
    If ArrData[VarNumLastName] = "" Then 
      GraphicsWindow.ShowMessage("Please input a last name.           ", " No save without last name.")
      VarMessage = "Data: " + ArrData[VarNumFirstName] + " " + ArrData[VarNumLastName] + " was not saved."
      LDFocus.SetFocus(ArrTextBox[VarNumLastName])
    Else   
      DataToDatabase()
      VarMessage = "Data: " + ArrData[VarNumFirstName] + " " + ArrData[VarNumLastName] + " was saved."
      ClearObjects()
      LDFocus.SetFocus(ArrTextBox[VarNumFirstName])
    EndIf 
    ShowMessage()
  ElseIf VarLastClickedButton = ArrButton[VarNumExit] Then 
    VarEndOfProgram = T      
  EndIf
EndSub 'WichButtonWasClicked

Sub ShowMessage
  Shapes.SetText(MessageShape, VarMessage)
  Shapes.ShowShape(MessageShape)
EndSub 'ShowMessage

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine WhichKeyWasUped
'Was it key Return, then start the subroutine SetFocusToNextTextBox. The same doing by key 
'Tab (Tabulator).
'By the combination of key tab and a shift key (right or left) the variable VarInputIndex substract 1.
'Is this lower then 1 the value will set with the last number ob text boxes. At last the Focus set in
'the actualy text box.
'______________________________________________________________________
Sub WichKeyWasUped
  If VarLastKey = "Return" Then 
    SetFocusToNextTextBox()   
  ElseIf VarLastKey = "Tab" Then
    If GraphicsWindow.LastKey = "RightShift" or GraphicsWindow.LastKey = "LeftShift" Then      
      VarInputIndex = VarInputIndex - 1
      If VarInputIndex < 1 Then 
        VarInputIndex = VarNumLastInput 
      EndIf
      SetFocus()
    Else
      SetFocusToNextTextBox()   
    EndIf   
  EndIf
  SetFocus()
  Shapes.HideShape(MessageShape)
EndSub 'WichKeyWasUped

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine SetFocus
'Put the Input from the text box into the array for database data.
'hide the old objects and shows the actualy objects.
'______________________________________________________________________
Sub SetFocus
  ArrData[VarOldInputIndex] = Controls.GetTextBoxText(ArrTextBox[VarOldInputIndex])
  Controls.HideControl(ArrTextBox[VarOldInputIndex])
  Shapes.SetText(ArrDataShape[VarOldInputIndex], ArrData[VarOldInputIndex])
  Shapes.ShowShape(ArrDataShape[VarOldInputIndex])
  Shapes.HideShape(ArrDataShape[VarInputIndex])
  Controls.SetTextBoxText(ArrTextBox[VarInputIndex], ArrData[VarInputIndex])
  Controls.ShowControl(ArrTextBox[VarInputIndex])
  LDFocus.SetFocus(ArrTextBox[VarInputIndex])
EndSub 'SetFocus

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine SetFocutToNextTextBox
'Add 1 to the value of the variable VarInputIndex. Is this greater as the last text box number then set
' it to the first input number VarNumFirstName.
'At last start the subroutine SetFocus.
'______________________________________________________________________
Sub SetFocusToNextTextBox
  VarInputIndex = VarInputIndex + 1
  If VarInputIndex > VarNumLastInput Then 
    VarInputIndex = VarNumFirstName 
  EndIf
  SetFocus()
EndSub 'SetFocusToNextTextBox

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine DataToDatabase
'For database data it is not possible to write a ' into a database. If there one ' replace it with two ' 
'then it will be functions.
'Set the variable command with the sql-command to put the data FirstName and LastName into
'the database.
'At last show all data from the table Names in the liestView.
'______________________________________________________________________
Sub DataToDatabase
  For i = VarNumFirstName To VarNumLastInput 
    LDText.Replace(ArrData[i], "'", "''")
  EndFor 
  command = "BEGIN TRANSACTION;"
  command = command + "INSERT INTO Names(FirstName,LastName)"
  command = command + "VALUES('"+ArrData[VarNumFirstName]+"','"+ArrData[VarNumLastName]+"');"
  command = command + "COMMIT;"
  LDDataBase.Command(database,command)  
  LDDataBase.Query(database, "SELECT * FROM Names;", listView, "False")
EndSub 'DataToDatabase

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯��¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine ClearObjects
'Set nothing in the database data shape, in the text box and in the variable for the database data.
'______________________________________________________________________
Sub ClearObjects
  For i = VarNumFirstName to VarNumLastInput 
    Shapes.SetText(ArrDataShape[i], "")
    Controls.SetTextBoxText(ArrTextBox[i], "")
    ArrData[i] = ""
  EndFor 
EndSub 'ClearObjects

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine KeyUp
'By event KeyUP set the event skip to T equal "True"
'______________________________________________________________________
'Event Subroutines
Sub KeyUp
  VarKeyUP = T 
EndSub 'KeyUp

'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯��¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
'Subroutine ButtonClicked
'By event ButtonClicked set the event skip to T equal "True"
'______________________________________________________________________
Sub ButtonClicked
  VarButtonClicked = T
EndSub 'ButtonClicked

Sub Initialisation
  '████████████████████████████
  'Initialisation
  '████████████████████████████
  
  '▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
  'Variables
  '▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
  'Bolen
  T = "True"
  F = "False"
  
  'Program controls
  VarEndOfProgram = F
  VarButtonClicked = F
  VarKeyUP = F 
  VarNumInputIndex = 1
  
  'Varible Input
  VarNumFirstName = 1
  VarNumLastName = 2
  VarNumLastInput = 2
  VarNumTabText = 80
  
  'Variable Button
  VarNumNextField = 1
  VarNumSave = 2
  VarNumExit = 3
  VarLastButton = 3
  
  '▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
  'Array
  '▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
  'Position on Desktop
  VarXPositionShape[VarNumFirstName] = 10
  VarYPositionShape[VarNumFirstName] = 10
  VarXPositionShape[VarNumLastName] = 10
  VarYPositionShape[VarNumLastName] = 30
  
  VarXPositionButton[VarNumNextField] = 10
  VarXPositionButton[VarNumSave] = 110
  VarXPositionButton[VarNumExit] = 210
  
  'Data
  ArrData[VarNumFirstName] = ""
  ArrData[VarNumLastName] = ""
  
  'Text
  ArrInputText[VarNumFirstName] = "First name"
  ArrInputText[VarNumLastName] = "Last name"
  
  ArrTextButton[VarNumNextField] = "Next"
  ArrTextButton[VarNumSave] = "Save"
  ArrTextButton[VarNumExit] = "Exit"
  
  'Schapes, TextBoxes and the array with the Database data
  For i = VarNumFirstName To VarNumLastInput   
    'Set a new shape and fill it with the text to put out before the text boxes.
    ArrShape[i] = Shapes.AddText(ArrInputText[i]+":") 
    'Set the shape to her position
    Shapes.Move(ArrShape[i], VarXPositionShape[i], VarYPositionShape[i])
    'Set a new shape with database data and fill it with nothing.
    ArrDataShape[i] = Shapes.AddText("")
    'Hide the database data shape
    Shapes.HideShape(ArrDataShape[i])
    'Set the shape to her position
    Shapes.Move(ArrDataShape[i], VarXPositionShape[i]+VarNumTabText, VarYPositionShape[i])
    'Set a new text box on his position.
    ArrTextBox[i] = Controls.AddTextBox(VarXPositionShape[i]+VarNumTabText-6, VarYPositionShape[i]-3)
    'Hide the text box.
    Controls.HideControl(ArrTextBox[i]) 
  EndFor 
  
  MessageShape = Shapes.AddText("")
  Shapes.HideShape(MessageShape)
  Shapes.Move(MessageShape, 5, GraphicsWindow.Height - 30)
  
  'Button
  For i = VarNumNextField To VarLastButton  
    'Set a new button on his position
    ArrButton[i] = Controls.AddButton(ArrTextButton[i], VarXPositionButton[i], GraphicsWindow.Height -65)
  EndFor 
  
  'Database
  'Create a new list view
  listView = LDDataBase.AddListView(GraphicsWindow.Width,GraphicsWindow.Height-200)
  'Set the list view on his position.
  Controls.Move(listView, 0, 100)
  'Set a fullpath for a database
  datFile = Program.Directory+"\database.db"
  
  'Is it a new database, so create it with a new table and table fields. Other wise open it.
' The following line could be harmful and has been automatically commented.
'   If LDFile.Exists(datFile) = F Then 
    database = LDDataBase.ConnectSQLite(datFile)
    command = "BEGIN TRANSACTION;"
    'The databse table Names has the fields Id with the field definition PRIMARY KEY, FirstName and LastName both with the field defination TEXT.  
    command = command + "CREATE TABLE Names(Id INTEGER PRIMARY KEY, FirstName TEXT, LastName TEXT);"
    command = command + "COMMIT;"
    LDDataBase.Command(database,command)
  Else 
    database = LDDataBase.ConnectSQLite(datFile)  
  EndIf   
  
EndSub 'Initialisation
