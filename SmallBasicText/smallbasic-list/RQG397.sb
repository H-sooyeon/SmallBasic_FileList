GraphicsWindow.Width = 800

GraphicsWindow.Height = 600



Wheel = Shapes.AddRectangle(40,100)

Shapes.Rotate(Wheel,0)

Shapes.Move(Wheel,100,200)



LDPhysics.AddMovingShape(Wheel,0.8,0.8,0.8)

LDPhysics.SetVelocity(Wheel,20,20)



LDPhysics.SetGravity(0,0)



For i = 1 To 5

  Text[i] = Shapes.AddText("")

  Shapes.Move(Text[i],100,20*i)

EndFor



While 1 = 1

  Program.Delay(10)

  

  LDPhysics.DoTimestep()

  GlobalVelocityArray = LDPhysics.GetVelocity(Wheel)

  

  GlobalVelocity = Math.SquareRoot((GlobalVelocityArray[1]*GlobalVelocityArray[1]) + (GlobalVelocityArray[2]*GlobalVelocityArray[2]))

  

  

  'Get movement direction

  OldX = X

  OldY = Y

  X = LDShapes.GetLeft(Wheel)

  Y = LDShapes.GetTop(Wheel)

  MovementAngle = Math.Round(Zock77Math.GetAngle(X,OldX,Y,OldY))

  

  'Get Lateral Direction

  LateralAngle = LDPhysics.GetAngle(Wheel) + 90

  If LateralAngle >= 360 Then

    LateralAngle = LateralAngle - 360

  EndIf

  

  'Get angle difference

  Difference = Math.Remainder(Math.abs(LateralAngle - MovementAngle),360)

  if(Difference > 180) Then

    Difference = 360 - Difference

  EndIf



  

  'Get the Lateral Velocity 

  LateralVelocity = Math.Sin(Math.GetRadians(Difference+90)) * GlobalVelocity

  

  'Get the X and Y impulses

  YImpulse = Math.Cos(Math.GetRadians(LateralAngle)) * (LateralVelocity * LDPhysics.GetMass(Wheel))

  XImpulse = Math.Sin(Math.GetRadians(LateralAngle)) * (LateralVelocity * LDPhysics.GetMass(Wheel))

  

  'Aplly the impulses

 LDPhysics.SetImpulse(Wheel,-XImpulse,YImpulse)

  

  Shapes.SetText(Text[1],"GlobalVelocity: " + GlobalVelocity)

  Shapes.SetText(Text[2],"MovementAngle: " + MovementAngle)

  Shapes.SetText(Text[3],"XImpulse: " + -XImpulse)

  Shapes.SetText(Text[4],"YImpulse: " + -YImpulse)

  Shapes.SetText(Text[5],"LateralVelocity: " + LateralVelocity)

  

  If LDUtilities.KeyDown("Right") Then

    LDPhysics.SetRotation(Wheel,10)

  EndIf

  If LDUtilities.KeyDown("Left") Then

    LDPhysics.SetRotation(Wheel,-10)

  EndIf

EndWhile


