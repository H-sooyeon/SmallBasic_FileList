' SmallBasic Version 1.0

' Funktion: PracticalLogical (DecLogicCalc)

' Autor: Pappa Lapub

' Herkunft: http://social.msdn.microsoft.com/Forums/en-US/7c56ed35-f71e-417b-9c01-9b14d8ddf7d9/

' ImportURL: http://smallbasic.com/program/?

' Extension: LitDev, ESL Ext

' Kommentar: Die BinTB[N] später erst hinzugefügt und werden nicht abgefragt. Dienen nur zur dualen Ausgabe.

'  http://www.lyricsfreak.com/s/supertramp/the+logical+song_20133850.html

' Variablen: 

'  

' --------------------------------------------------------------------------------

GraphicsWindow.Height = 136

GraphicsWindow.Width = 320

GraphicsWindow.Title = "How to be .. PracticalLogical"

GraphicsWindow.BackgroundColor = "LightSteelBlue"



GraphicsWindow.BrushColor = "SandyBrown"

GraphicsWindow.FillRectangle(220,20,60,22)

GraphicsWindow.FillRectangle(220,50,60,22)

GraphicsWindow.FillRectangle(220,90,60,26)



GraphicsWindow.BrushColor = "Black"

BTxt = "1=AND;2=OR;3=XOR;4=NOT;5=C;"

For N = 1 To 3

  TB[N] = Controls.AddTextBox(100*N-80,20)

  Controls.SetSize(TB[N],60,22)

  BinTB[N] = Controls.AddTextBox(100*N-80,50)

  Controls.SetSize(BinTB[N],60,22)

EndFor



For N = 1 To 5

  ' Btn[N] = Controls.AddButton(BTxt[N],20,35*N+20)  ' senkrecht

  Btn[N] = Controls.AddButton(BTxt[N],50*N-30,90)   ' waagrecht

  Controls.SetSize(Btn[N],40,26)

EndFor

GraphicsWindow.DrawText(195,25,"=")

GraphicsWindow.DrawText(290,25,"dek")   ' "10"

GraphicsWindow.DrawText(195,55,"=")

GraphicsWindow.DrawText(290,55,"bin")   ' "  2"



Shapes.SetOpacity(TB[3],80)

Shapes.SetOpacity(BinTB[3],80)

Shapes.SetOpacity(Btn[5],70)

Controls.SetSize(Btn[5],60,26)

LDDialogs.ToolTip(Btn[4],"Nur für Wert in 2.TextBox")   ' Tooltip zu NOT-Button



Controls.ButtonClicked = ButtonClick



' /////  SUBs  \\\\\

Sub ButtonClick

  GraphicsWindow.BrushColor = "LightSteelBlue"

  GraphicsWindow.FillRectangle(81,20,38,22)

  GraphicsWindow.FillRectangle(81,50,38,22)

  

  For N = 1 To 2

    If Controls.GetTextBoxText(TB[N]) = "" Then

      Controls.SetTextBoxText(TB[N],0)

    EndIf

  EndFor

  

  If Controls.LastClickedButton = Btn[5] Then   ' C

    For N = 1 To 3

      Controls.SetTextBoxText(TB[N],"")

      Controls.SetTextBoxText(BinTB[N],"")

    EndFor

    

  Else

    ValCheck()

    If IsNumer = "true" Then

      Val1 = Controls.GetTextBoxText(TB[1])

      Val2 = Controls.GetTextBoxText(TB[2])

      For N = 1 To 2

        Controls.SetTextBoxText(BinTB[N], LDMath.Decimal2Base(Controls.GetTextBoxText(TB[N]),2))

      EndFor

      

      If Controls.LastClickedButton = Btn[1] Then   ' AND

        Res = ESLMaths.BitwiseAND(Val1,Val2)

        Calc = BTxt[1]

      ElseIf Controls.LastClickedButton = Btn[2] Then ' OR

        Res = ESLMaths.BitwiseOR(Val1,Val2)

        Calc = BTxt[2]

      ElseIf Controls.LastClickedButton = Btn[3] Then ' XOR

        Res = ESLMaths.BitwiseXOR(Val1,Val2)

        Calc = BTxt[3]

      ElseIf Controls.LastClickedButton = Btn[4] Then   ' NOT

        'Controls.SetTextBoxText(TB[1],"******")

        Controls.SetTextBoxText(TB[1],"---------")

        Controls.SetTextBoxText(BinTB[1],"---------")

        Res = -1-Val2

        Calc = "  NOT"      'Calc = BTxt[4]

      EndIf

      

      GraphicsWindow.BrushColor = "FireBrick"

      GraphicsWindow.DrawText(85,24,Calc)

      GraphicsWindow.DrawText(85,54,Calc)

      Controls.SetTextBoxText(TB[3],Res)

      Controls.SetTextBoxText(BinTB[3], LDMath.Decimal2Base(Res,2))

      

      'For N = 1 To 3

      '  Controls.SetTextBoxText(BinTB[N], LDMath.Decimal2Base(Controls.GetTextBoxText(TB[N]),2))

      'EndFor

      

    EndIf ' IsNumber

    

  EndIf ' Any calculation Button

  

EndSub





Sub ValCheck

  IsNumer = "true"

  For N = 1 To 2

    Temp = Controls.GetTextBoxText(TB[N])

    If (Temp + 0 <> Temp) Or (1 * Temp <> Temp) Then

      IsNumer = "false"

      GraphicsWindow.ShowMessage("Mind. eine Eingabe ist keine Zahl!", "Falsche Eingabe")

    EndIf

  EndFor

EndSub


