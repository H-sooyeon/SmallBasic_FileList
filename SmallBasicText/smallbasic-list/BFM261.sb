Initialise()  'copied from build gui



While "True"  

  

  'Where's the mouse cursor for this cycle?

  mseX = GraphicsWindow.MouseX

  mseY = GraphicsWindow.MouseY

  

  'If the mseBtn is down check to see if it's on a box  

  If Mouse.IsLeftButtonDown Then    

    If selected = "" And clicked = "False" Then

      'Loop thru the boxes and select a part

      For i = 1 To Array.GetItemCount(part)

        If mseX >= boxX[i] And mseX <= boxX[i] + boxWidth And mseY >= boxY[i] And mseY <= boxY[i] + boxHeight Then      

          selected = i      

        EndIf

      EndFor

    EndIf

    clicked = "True"  'the btns has been clicked for this cylce

    Shapes.Move(part[selected], mseX, mseY)

  Else

    If Math.Round(mseX/50) = Math.Round(Shapes.GetLeft(flower[selected])/50) Then   'changed mseX/10 to mseX/50 etc

      If selected = 3 And mseY >= flowerY[3] And mseY < flowerY[3] + 90 Then 'added this for stem

        Shapes.Remove(part[3])

        Shapes.SetOpacity(flower[3], 100)

      EndIf      

      If Math.Round(mseY/50) = Math.Round(Shapes.GetTop(flower[selected])/50) Then

        Shapes.Remove(part[selected])

        Shapes.SetOpacity(flower[selected], 100)

      EndIf      

    EndIf   

    

    Shapes.Move(part[selected], boxX[selected] + offsetPartX[selected], boxY[selected] + offsetPartY[selected])

    selected = ""   'deselect when we're finishised with the part

    clicked = "False"  'inits here

  EndIf

  

  Program.Delay(20)  

EndWhile



Sub Initialise

  GraphicsWindow.Width = 620

  GraphicsWindow.Height = 420

  gw = GraphicsWindow.Width

  gh = GraphicsWindow.Height

  

  'Set Variables  to use for drawing the boxes & frames

  margin = 10

  columns = 2

  rows = 4

  boxWidth = 90

  boxHeight = (gh - margin *4) / rows  'centre boxes

  

  'Draw the boxes

  For r = 1 To rows  'numbers boxes left to right, top to bottom

    Y = (margin *2) + (boxHeight * (r-1))  

    For c = 1 To columns 

      X = gw - (margin *2) - (boxWidth * (columns + 1 - c))

      GraphicsWindow.DrawRectangle(X, Y, boxWidth, boxHeight)    

      i = i + 1     'Index & Store box XY

      boxX[i] = X

      boxY[i] = Y

    EndFor

  EndFor

  

  'Frame the boxes

  X = gw - margin *3 - boxWidth *2

  height = margin *2 + boxHeight * rows

  GraphicsWindow.DrawRectangle(X, margin, margin *2 + boxWidth *2, height)

  

  'Frame the flower

  width = X - margin *2

  height = gh - margin *2

  GraphicsWindow.DrawRectangle(margin, margin, width, height)

  

  'Add & Position the flower

  flowerWidth = "1=70;2=68;3=3;4=20;5=40;6=68;7=44;8=68"

  flowerHeight = "1=25;2=42;3=65;4=20;5=65;6=41;7=68;8=22"

  flowerX = "1=183;2=100;3=180;4=170;5=156;6=192;7=157;8=110"

  flowerY = "1=280;2=135;3=233;4=143;5=77;6=130;7=165;8=280"

  angle = "1=-6;2=-2;5=-5;6=-8;7=8;8=8"

  

  GraphicsWindow.BrushColor = "Green"

  flower[1] = Shapes.AddEllipse(flowerWidth[1] + 5, flowerHeight[1] + 5) ' right leaf from box[1]   upSize Flower by +5

  flower[8] = Shapes.AddEllipse(flowerWidth[8] + 5, flowerHeight[8] + 5)  'left leaf

  GraphicsWindow.BrushColor = "Brown"

  flower[3] = Shapes.AddRectangle(flowerWidth[3] + 5, flowerHeight[3] + 90)  'stem

  GraphicsWindow.BrushColor = "Pink"

  flower[2] = Shapes.AddEllipse(flowerWidth[2] + 5, flowerHeight[2] + 5)  'left petal from box[2]

  flower[5] = Shapes.AddEllipse(flowerWidth[5] + 5, flowerHeight[5] + 5)  'top petal

  flower[6] = Shapes.AddEllipse(flowerWidth[6] + 5, flowerHeight[6] + 5)  'right petal

  flower[7] = Shapes.AddEllipse(flowerWidth[7] + 5, flowerHeight[7] + 5)  'bottom petal

  GraphicsWindow.BrushColor = "Yellow"

  flower[4] = Shapes.AddEllipse(flowerWidth[4] + 5, flowerHeight[4] + 5)  'stamin

  

  'index the above BrushColors for the parts in the boxes build in - (you could now revise the above to add all the flowers in a loop as well)

  color = "1=Green;2=Brown;3=Pink;4=Yellow"

  assignColorToPart = "1=1;2=3;3=2;4=4;5=3;6=3;7=3;8=1"

  

  offsetPartX = "1=10;2=10;3=40;4=35;5=25;6=10;7=20;8=10"  'offset the parts in the box

  offsetPartY = "1=30;2=25;3=10;4=35;5=15;6=25;7=15;8=35"

  

  For i = 1 To Array.GetItemCount(flower)  

    Shapes.Move(flower[i], flowerX[i], flowerY[i]) 

    Shapes.Rotate(flower[i], angle[i])

    

    'from the Mouse part of the project

    Shapes.SetOpacity(flower[i], 30)

    GraphicsWindow.BrushColor = color[assignColorToPart[i]]   'set the color from the above BrushColor arrays

    part[i] = Shapes.AddEllipse(flowerWidth[i], flowerHeight[i])  

    Shapes.Move(part[i], boxX[i] + offsetPartX[i], boxY[i] + offsetPartY[i])

    Shapes.Rotate(part[i], angle[i])  

  EndFor

EndSub


