'  Challenge of the month  August  2013    Curriculum 4.1's See Saw exercise   By NaochanON





GUI()

Controls.ButtonClicked=Onclicked

GraphicsWindow.KeyDown = OnKeyDown



While "true"

  If Play="True" Then 

    angle=angle+dangle

    BX=BX+ angle/5

    BY=XYC["Y"]+(BX-XYC["X"])*math.Tan(math.GetRadians(angle))-DR

    Shapes.Move(ball,BX,BY)

    Shapes.Rotate(ball,angle)

    Shapes.Rotate(rect,angle)

    '-----------------------------------------------------------

    et=Math.Floor((clock.ElapsedMilliseconds-t0)/100)/10

    Shapes.SetText(msg,"       Elapsed time= "+et+" sec")

    '-----------------------------------------------------------

    If Math.Abs(BX+DR/2-XYC["X"])>(RWH["W"]/2) then 

      play="False"

      Shapes.Animate(ball,BX,XYC["Y"]+1000,2000)  '  ball falls

    endif

  endif

  Program.Delay(5)

endwhile



Sub OnKeyDown

  If Play = "True" Then

    dangle = dang[GraphicsWindow.LastKey]

  EndIf

EndSub



Sub Onclicked

  Play="True" 

  angle=2*dx/math.Abs(dx)      '   +2 or  -2  

  t0=clock.ElapsedMilliseconds

EndSub



Sub GUI

  DR=30                                          '  ball  size

  RWH="W=500;H=15"                               '   bar  width height 

  XYC="X=300;Y=200"                              '   center position 

  dang="Left=-0.15;Right=0.1"                    '   bar rotating speed // not even 

  dx= 50.5-Math.GetRandomNumber(100)             '  start point  ...   avoid  center position 

  '-----------------------------------------------------------

  GraphicsWindow.BackgroundColor="Lightcyan"

  Tri= Shapes.AddTriangle(DR,0,0,DR,2*DR,DR)  

  '-----------------------------------------------------------

  GraphicsWindow.BrushColor="Darkgreen"

  GraphicsWindow.PenColor="Darkgreen"

  rect=shapes.AddRectangle(RWH["W"],RWH["H"])

  '-----------------------------------------------------------

  GraphicsWindow.BrushColor="Red"

  GraphicsWindow.PenColor="Red"

  ball=shapes.AddEllipse(DR,DR)

  '-----------------------------------------------------------

  Shapes.Move(ball,XYC["X"]+dx-DR/2,XYC["Y"]-DR)

  Shapes.Move(rect,XYC["X"]-RWH["W"]/2,XYC["Y"])

  Shapes.Move(tri,XYC["X"]-DR,XYC["Y"]+RWH["H"])

  '-----------------------------------------------------------

  GraphicsWindow.FontSize=20

  Controls.AddButton("Start",XYC["X"],XYC["Y"]+100)

  msg= Shapes.AddText("       *** Push Start button to play ***")

  '-----------------------------------------------------------

  Play="False"

  BX= Shapes.GetLeft(ball)

  BY= Shapes.GetTop(ball)

EndSub






