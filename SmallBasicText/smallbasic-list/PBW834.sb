'Small Basic challenge of the month February 2019

'   First write a program to draw a line on the GraphicsWindow following the mouse.

'   Then make the program color the line depending on which quadrant the mouse is in.



'CONFIG

xPad = 8  'Adjust to match the left window border size

ypad = 30 'Adjust to match to top window border and title bar size

quad = "11=Red;12=Blue;21=Green;22=Cyan"  'set tge colour for each of the 4 quadrants.



'INIT GRAPHICS

GraphicsWindow.Width = Desktop.Width * .75

GraphicsWindow.Height = Desktop.Height * .75

GraphicsWindow.Left = Desktop.Width * .125

GraphicsWindow.Top = Desktop.Height * .125

GraphicsWindow.BackgroundColor = "Black"

GraphicsWindow.Title = "Line coloured by quadrant"

GraphicsWindow.Show()



'MAIN

Program.Delay(100)  'Required delay to ensure that xlast and ylast register correctly.

reset = "False"

xLast = GraphicsWindow.MouseX

yLast = GraphicsWindow.MouseY



While ("True")

  'Checks to see if the mouse has gone off the window

  If (Mouse.MouseX <= GraphicsWindow.Left + xPad Or Mouse.MouseY <= GraphicsWindow.Top + ypad Or Mouse.MouseX >= GraphicsWindow.Width + GraphicsWindow.Left + xPad Or Mouse.MouseY >= GraphicsWindow.Height + GraphicsWindow.Top + ypad) Then

    reset = "True"

  EndIf

  If reset Then 'Waits for the mouse to return to the window and updtes xlast & ylast

    While (Mouse.MouseX <= GraphicsWindow.Left + xPad Or Mouse.MouseY <= GraphicsWindow.Top + ypad Or Mouse.MouseX >= GraphicsWindow.Width + GraphicsWindow.Left + xPad Or Mouse.MouseY >= GraphicsWindow.Height + GraphicsWindow.Top + ypad)

      Program.Delay(10)

    EndWhile

    xLast = GraphicsWindow.MouseX

    yLast = GraphicsWindow.MouseY

    reset = "False"

  Else  'Draws a line if the mouse cursor is inside the graphicsWindow.

    x = GraphicsWindow.MouseX

    y = GraphicsWindow.MouseY

    GraphicsWindow.PenColor = quad[(Math.Round(x / GraphicsWindow.Width) + 1) * 10 + Math.Round(y / GraphicsWindow.Height) + 1] 'Selects the quadrant colour

    GraphicsWindow.DrawLine(x, y, xLast, yLast)

    xLast = x

    yLast = y

  EndIf

EndWhile


