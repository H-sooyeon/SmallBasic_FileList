' SmallBasic Version 1.1
' Program: RubberBand
' Changelog: 
' Author: Pappa Lapub
' Website: https://social.msdn.microsoft.com/Forums/en-US/a0e03c4d-a685-4b56-ab55-a4902fa20155/click-and-drag-to-make-rectangle
' ImportURL: http://smallbasic.com/program/?
' Extension: (LitDev,  3x LDCursors)
' Comment: ex. GWCapture.sb
'  http://photographyblogger.net/beautiful-sunset-pictures/
' Variables: dimensions of frame(x0,y0,  w0,h0 | w,h)
'   where x0;y0 is the current ancor point (left top) and
'   w;h  may be (+/-) signed, while  w0;h0 are |absolute|
'   x,y .. init ancor point of frame (from where began to enlarge 'in statu nascendi')
' ToDo: 
' ================================================================================
img = "http://www.photographyblogger.net/wp-content/uploads/2010/10/sunset17.jpg"
title = "Rubberband  "

gw = GraphicsWindow.Width
gh = GraphicsWindow.Height
GraphicsWindow.Title = title

'GraphicsWindow.BackgroundColor = GraphicsWindow.GetRandomColor()
GraphicsWindow.DrawResizedImage(img, 0,0, gw,gh)

GraphicsWindow.PenWidth = 1
GraphicsWindow.PenColor = "Red"
GraphicsWindow.BrushColor = "#40FFFF00"
frame = Shapes.AddRectangle(0,0)


GraphicsWindow.MouseMove = OnMouseMove
GraphicsWindow.MouseDown = OnMouseDown
GraphicsWindow.MouseUp = OnMouseUp


' //////////  EVENTs  \\\\\\\\\\
Sub OnMouseMove
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
  
  '' Use of following 'If' branch: To continue frame usage after MouseUp outside of GW and back into again
  If Mouse.IsLeftButtonDown <> "True" Then
    OnMouseUp()
  EndIf
  
  If doResize Then
    w = mx-x
    h = my-y
    w0 = Math.Abs(w)
    h0 = Math.Abs(h)
    Controls.SetSize(frame, w0,h0)
    
    If (mx < x) And (my < y) Then   ' II
      Shapes.Move(frame, mx,my)
      x0 = mx
      y0 = my
    ElseIf (mx < x) Then            ' III
      Shapes.Move(frame, mx,y)
      x0 = mx
    ElseIf (my < y) Then            ' I
      Shapes.Move(frame, x,my)
      y0 = my
    Else                            ' IV
      x0 = x
      y0 = y
    EndIf
  EndIf
  
  '' In this way, does'nt care if partly outside of GW
  If dragOn Then
    '' ... If fullyInsideGW ... Then ...
    x0 = mx-xOff
    y0 = my-yOff
    Shapes.Move(frame, x0,y0)
    '' ... Else (outside) ... Then ... EndIf
  EndIf
  
  UpdateTitle()
EndSub

Sub OnMouseDown
  x = mx
  y = my
  
  If (mx > x0 And mx < x0+w0) And (my > y0 And my < y0+h0) Then   ' cursor inside frame
    LDCursors.SizeAll()
    xOff = mx-x0
    yOff = my-y0
    dragOn = "True"
  Else
    LDCursors.Cross()
    Shapes.Move(frame, x,y)
    doResize = "True"
  EndIf
EndSub

'' NOT recognized if happens outside of GW, so check back under 'OnMouseMove'
Sub OnMouseUp
  LDCursors.Arrow()
  dragOn = ""
  doResize = ""
EndSub


' //////////  SUBs  \\\\\\\\\\
Sub UpdateTitle
  GraphicsWindow.Title = title + "  x0;y0: "+ x0 +";"+ y0 +" | w;h: "+ w0 +";"+ h0 '+ " || " +mx+"|"+my '+ " || "+x+"|"+y+" | "+w+"|"+h
EndSub
  
