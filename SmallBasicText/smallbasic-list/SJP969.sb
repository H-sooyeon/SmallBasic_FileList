'   Univerese  planets orbit  simulation   by NaochanON     2012/03/09
'   Planet data is obtained    by wiki  and   MITAKA http://4d2u.nao.ac.jp/html/program/mitaka/index_E.html

data()               '   planet data 
init()               '   initial view 
big_bang()           '  big bang    background stars
Location()           '  Planet setting 
Time_shift()         '  time shift slider setting

GraphicsWindow.MouseMove=onmousedown
GraphicsWindow.MouseUp=onmouseup

Sub Change_orbit
   DTNo=6-DT
   drdeg=ydeg[5]*360/ccl[3]*(20-Clock.day)  ' day adjustment  only  in March   1day=0.00274*360/1 degree =0.9864 
   For i=1 To 5   '   Mercuty Venus Earth Mars 
    If i<5 Then 
      Rdeg=math.GetRadians( DG[i]-drdeg +ydeg[DTNo]*360/ccl[i] )  '   
      orb_R=(AU*R[i]/Rratio+D[i]/2)
      X[i]=WX/2+orb_R*Math.Cos(Rdeg)    '  
      y[i]=WY/2+orb_R*Math.sin(-Rdeg)    '  
   ElseIf i=5 then      '    Luna
      drdeg5=13.1762*(20-Clock.day)    '  Luna    13.1762degree/day        ' today's degree
      Rdeg5=math.GetRadians(DG[5]-drdeg5 +ydeg[DTNo]*365.257*13.176)  ' 
      orb_R5=AU*R[3]/Rratio*R[5]+D[5]/2
      X[5]=X[3]+orb_R5*Math.Cos(Rdeg5)  '  
      y[5]=y[3]+orb_R5*Math.sin(-Rdeg5) 
    EndIf
    Shapes.Move(sstar[i],X[i],y[i])           '   planet location
    Shapes.Move(sN[i],X[i],y[i]-20)           '   name  location  
    Shapes.setText(msg,"Planet location from today....  "+shift[DTNo])  '   
  Endfor
EndSub
  
Sub Location  '  Today   2012/03/04  11:40
  For i=1 To 11
    endadgP=text.GetIndexOf(addeg," ")  
    ydeg[i]=Text.GetSubText(addeg,1,endadgP-1)      '  gets shift time 
    addeg=Text.GetSubTextToEnd(addeg,endadgP+1)
  EndFor
  GraphicsWindow.FontSize=15
  For i=1 To 5
    endCP=text.GetIndexOf(Color," ")          '  color 
    endcclP=text.GetIndexOf(cycle," ")        '  Cycle 
    endDP=text.GetIndexOf(Diameter," ")       '  Diameter
    endDGP=text.GetIndexOf(degree," ")        '  degree 
    endNP=text.GetIndexOf(Name," ")           ' name 
    endRP=text.GetIndexOf(radius," ")         '  Radius 
    C[i] =Text.GetSubText(color,1,endCP-1)     '  color 
    Ccl[i]=Text.GetSubText(cycle,1,endcclP-1)  '  Cycle 
    D[i] =Text.GetSubText(Diameter,1,endDP-1)  '  Diameter
    DG[i]=Text.GetSubText(degree,1,endDGP-1)   '    Degree 
    N[i] =Text.GetSubText(Name,1,endNP-1)      '  planet name 
      sN[i]= Shapes.AddText(N[i])
    R[i] =Text.GetSubText(radius,1,endRP-1)    '  Radius 
    
    GraphicsWindow.PenWidth=0.5
    GraphicsWindow.PenColor=C[i]
    If i<5 then
      OrbitR=AU*R[i]/Rratio  '-D[i]/4     '  orbit radius 
      GraphicsWindow.DrawEllipse(WX/2-OrbitR-4,WY/2-OrbitR-4,OrbitR*2+8,OrbitR*2+8)   '  orbit circle
    endif 
    GraphicsWindow.BrushColor=C[i]
    sstar[i]=Shapes.AddEllipse(D[i],D[i])            '  planets
    
    color=Text.GetSubTextToEnd(Color,endCP+1)
    cycle=Text.GetSubTextToEnd(cycle,endCclP+1)
    Diameter=Text.GetSubTextToEnd(Diameter,endDP+1)
    degree=Text.GetSubTextToEnd(degree,endDGP+1)
    Name=Text.GetSubTextToEnd(name,endNP+1)
    radius=Text.GetSubTextToEnd(radius,endRP+1)
  Endfor 
  
  Change_orbit()
  sun=Shapes.AddEllipse(20,20)
  Shapes.Move(sun,wx/2-10,WY/2-10)           '   sun is set center
  ssun= Shapes.AddText("Sun")
  Shapes.Move(ssun,wx/2-10,WY/2-30)
EndSub
  
Sub Time_shift  '  shift slider
  GraphicsWindow.FontSize=15
  GraphicsWindow.BrushColor="cyan"
  GraphicsWindow.PenColor="Lightcyan"
  GraphicsWindow.DrawLine(wx-145,wy/4,wx-145,wy/4*3)   '  slider line 
  For i=1 To 11
    gx=wx-160
    gy=WY/4+(i-1)*(wy/2/10)
    GraphicsWindow.DrawLine(gx,gy,gx+30,gy)
    midP= Text.GetIndexOf(text.GetSubTextToEnd(Tdirection,dx+1),",")
    shift[i]= Text.GetSubText(Tdirection,dx+1,midP-1) 
    GraphicsWindow.DrawText(gx+40,gy-10,shift[i])
    dx=dx+midP
  EndFor  
  srect=Shapes.AddRectangle(22,20)                     '  rectangle pointer
  srx=wx-156
  sry=wy/2-10
  Shapes.Move(srect,srx,sry)                           '   move rectangle 
  GraphicsWindow.DrawText(WX-160,WY/4-40,"Future")
  GraphicsWindow.DrawText(WX-160,WY/4*3+10,"Past")
EndSub

Sub onmousedown
  MX= GraphicsWindow.MouseX
  MY= GraphicsWindow.MouseY
  If Mouse.IsLeftButtonDown and  math.Abs(MX-(srx+22/2))<22/2 and math.Abs(MY-(WY/2-20/2))<WY/4 Then 
    Shapes.Move(srect,srx,MY)    '   move rectangle 
  EndIf
EndSub  

Sub onmouseup
  DT= Math.Round(-(MY-WY/2)/(WY/4/5))  '  Future   +  direction  Past   -  direction
  If DT>5 Then 
    DT=5
  ElseIf DT<-5 then 
    DT=-5
  EndIf
  sry=WY/2-DT*(WY/4/5)-20/2
  Shapes.Move(srect,srx,sry)   '     slider pointer setting
  Change_orbit()                '   change  planet position 
EndSub

Sub init
  WX=1400
  WY=700 
  GraphicsWindow.Hide()
  GraphicsWindow.Top=0
  GraphicsWindow.Left=0
  GraphicsWindow.Width=WX
  GraphicsWindow.Height=WY
  GraphicsWindow.BackgroundColor="Black"
EndSub

Sub data               '   Planet data    by wiki  and   MITAKA planet orbit simulaing program
  '     1month=30  1week=6.899  1day=0.98557  1 year=365.257day = 360degree
  '   10years=10  1year=1   1month=0.08333  1week=0.0192     1day=0.00274 year
  '   Diameter="139        0.5            1.2          1.3       0.7     0.35   sun=1390,000Km    Luna = 3,474Km
  Rratio=50    '   to arrange planet orbit scale
  name="Mercuty Venus Earth Mars Luna "            '   Planet name    
  AU=14960                                         '   Revolution Radius    of Earth's    149,600,000Km
  Radius="0.39 0.72 1.0 1.5 0.1 "　　               '   Ratio vs Earth  
  Diameter="6 6 8 6 6 "　                           '  virtual diameter 
  Cycle="0.243 0.66 1.0 1.865 0.0748 "　            '  Year ratio vs Earth   Luna: 27day7hr43.1min=27.322day =13.1762deg/day
  Color="Lightcyan Cyan Green olive Yellow "        '  planet  color 
  Tdirection="+10Years,+1Year,+1Month,+1week,+1Day,Today,-1Day,-1week,-1Month,-1Year,-10Years,"
  addeg="10 1 0.0833 0.0192 0.00274 0 -0.00274 -0.0192 -0.0833 -1 -10 "   '  year     1day=0.00274year
  degree="312 315 360 350 155 "                     '  2012/03/20/12:00     at  Japanese time  standard degree  seen  from North Polar direction 
EndSub
  
Sub big_bang           '  background    spreading   stars
  GraphicsWindow.BrushColor="yellow"
  GraphicsWindow.FontItalic="True"
  GraphicsWindow.FontSize=30
  GraphicsWindow.penColor="white"
  GraphicsWindow.PenWidth=1
  stNo=360*4
  For i=1 To STNo
    str[i]= Shapes.AddEllipse(2,2)
    Shapes.Move(str[i],WX/2,WY/2)              '   stars   360*4 pieces
  EndFor
  GraphicsWindow.Show()
  msg=Shapes.AddText("13.7 billion years ago, the Big Bang occured!")
  Shapes.Move(msg,50,600)
 
 For i=1 To STNo
   RL=Math.GetRandomNumber(5000)+50
   sx[i]= RL*Math.Cos(Math.GetRadians(i))+WX/2     '    end point
   sY[i]= RL*Math.sin(Math.GetRadians(i))+WY/2
  EndFor
  DT=25
  For j=1 To DT
    For i=1 To STNo
      Shapes.Move(str[i],(sx[i]-WX/2)/DT*J+WX/2,(sy[i]-WY/2)/DT*J+WY/2) ' scatter stars
    EndFor
    Shapes.Move(msg,50,600-j*600/DT)
  endfor
EndSub  


