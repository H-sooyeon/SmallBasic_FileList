

'Set graphics window
gw=800'Desktop.Width
gh=600'Desktop.Height
GraphicsWindow.Width=gw
GraphicsWindow.Height=gh
GraphicsWindow.CanResize="false"
GraphicsWindow.BackgroundColor="Black"
GraphicsWindow.MouseMove = OnMouseMove

'Set suns
nsun=50
size=10
Ang=0.1
gsize=Math.Min(gw,gh)
For i=1 To nsun
  GraphicsWindow.BrushColor=GraphicsWindow.GetRandomColor()
  sun=shapes.AddEllipse(size,size)
  Array.SetValue("suns",i,sun)
  Array.SetValue("X",i,Math.GetRandomNumber(gsize/2))
  Array.SetValue("Y",i,Math.GetRandomNumber(gsize/2))
  Array.SetValue("Z",i,Math.GetRandomNumber(gsize/2))
  Array.SetValue("U",i,Ang*(Array.GetValue("Y",i)-gsize/2))
  Array.SetValue("V",i,-Ang*(Array.GetValue("X",i)-gsize/2))
  Array.SetValue("W",i,0)
EndFor

'Move suns
dt=1
grav=10
fric=0.0
While("true")
  For i=1 To nsun
    Xacc=0
    Yacc=0
    Zacc=0
    For j=1 To nsun
      If (i <> j) Then
        Xdist=Array.GetValue("X",j)-Array.GetValue("X",i)
        Ydist=Array.GetValue("Y",j)-Array.GetValue("Y",i)
        Zdist=Array.GetValue("Z",j)-Array.GetValue("Z",i)
        dist2=Xdist*Xdist+Ydist*Ydist+Zdist*Zdist
        dist2=Math.Max(dist2,100)
        Xacc=Xacc+grav*Xdist/dist2
        Yacc=Yacc+grav*Ydist/dist2
        Zacc=Zacc+grav*Zdist/dist2
      EndIf
    EndFor 
    array.SetValue("AccX",i,Xacc)
    array.SetValue("AccY",i,Yacc)
    array.SetValue("AccZ",i,Zacc)
  EndFor
  For i=1 To nsun
    array.SetValue("U",i,(1-fric)*array.GetValue("U",i)+dt*array.GetValue("AccX",i))
    array.SetValue("V",i,(1-fric)*array.GetValue("V",i)+dt*array.GetValue("AccY",i))
    array.SetValue("W",i,(1-fric)*array.GetValue("W",i)+dt*array.GetValue("AccZ",i))
    array.SetValue("X",i,array.GetValue("X",i)+dt*array.GetValue("U",i))
    array.SetValue("Y",i,array.GetValue("Y",i)+dt*array.GetValue("V",i))
    array.SetValue("Z",i,array.GetValue("Z",i)+dt*array.GetValue("W",i))
  EndFor
  While (Mouse.IsLeftButtonDown)
    For i=1 To nsun
      x=array.GetValue("X",i)*Math.Cos(Xeye)+array.GetValue("Z",i)*Math.Sin(Xeye)
      z=-array.GetValue("X",i)*Math.Sin(Xeye)+array.GetValue("Z",i)*Math.Cos(Xeye)
      array.SetValue("X",i,x)
      array.SetValue("Z",i,z)
      y=array.GetValue("Y",i)*Math.Cos(Yeye)+array.GetValue("Z",i)*Math.Sin(Yeye)
      z=-array.GetValue("Y",i)*Math.Sin(Yeye)+array.GetValue("Z",i)*Math.Cos(Yeye)
      array.SetValue("Y",i,y)
      array.SetValue("Z",i,z)
    EndFor
    x=0
    y=0
    For i=1 To nsun
      x=x+array.GetValue("X",i)
      y=y+array.GetValue("Y",i)
    EndFor
    x=x/nsun
    y=y/nsun
    For i=1 To nsun
      array.SetValue("X",i,array.GetValue("X",i)-(x-gw/2))
      array.SetValue("Y",i,array.GetValue("Y",i)-(y-gh/2))
      sun=array.GetValue("suns",i)
      Shapes.Move(sun,array.GetValue("X",i),array.GetValue("Y",i))
    EndFor
  EndWhile
  x=0
  y=0
  For i=1 To nsun
    x=x+array.GetValue("X",i)
    y=y+array.GetValue("Y",i)
  EndFor
  x=x/nsun
  y=y/nsun
  For i=1 To nsun
    array.SetValue("X",i,array.GetValue("X",i)-(x-gw/2))
    array.SetValue("Y",i,array.GetValue("Y",i)-(y-gh/2))
    sun=array.GetValue("suns",i)
    Shapes.Move(sun,array.GetValue("X",i),array.GetValue("Y",i))
  EndFor
EndWhile

Sub OnMouseMove
  Xeye=-(GraphicsWindow.MouseX-gw/2)/10000
  Yeye=-(GraphicsWindow.MouseY-gh/2)/10000
EndSub



