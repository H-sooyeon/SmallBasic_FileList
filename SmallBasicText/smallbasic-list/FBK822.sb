' Projectile motion
' Copyright (c) 2012 Nonki TAKAHASHI 
'
' History:
'  0.1 2012/08/05 Created.
'
' Reference :
' [1] Kunio HIRATA, BASIC ni yoru butsuri, 1983
'
Init()
While "True"
  Throw()
EndWhile

Sub Init
  size = 24   ' ball size [cm]
  m = 600     ' mass of ball [g]
  g = 9.8     ' acceleration of gravity [m/s^2]
  fx = 0      ' force of gravity [N]
  fy = -m * g ' force of gravity [N]
  ax = fx / m ' x acceleration of gravity [m/s^2]
  ay = fy / m ' y acceleration of gravity [m/s^2]
  dt = 0.05   ' delta time [s]
  GraphicsWindow.BackgroundColor = "LightGray"
  GraphicsWindow.Title = "Projectile Motion"
  x0 = GraphicsWindow.Width - size
  y0 = GraphicsWindow.Height - size
  GraphicsWindow.PenWidth = 0
  GraphicsWindow.BrushColor = "OrangeRed"
  oBall = Shapes.AddEllipse(size, size)
  Shapes.Move(oBall, x0, y0)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.DrawText(30, 30, "Speed:")
  oSpeed = Controls.AddTextBox(85, 30 - 4)
  Controls.SetTextBoxText(oSpeed, "5")
  Controls.SetSize(oSpeed, 60, 22)
  GraphicsWindow.DrawText(150, 30, "[m/s]")
  GraphicsWindow.DrawText(30, 60, "Angle:")
  oAngle = Controls.AddTextBox(85, 60 - 4)
  Controls.SetTextBoxText(oAngle, "60")
  Controls.SetSize(oAngle, 60, 22)
  GraphicsWindow.DrawText(150, 60, "[degree]")
  oThrow = Controls.AddButton("Throw", 30, 90)
  Controls.ButtonClicked = OnButtonClicked
  Timer.Interval = dt * 1000
  Timer.Tick = OnTick
EndSub

Sub MoveBall
  vx = vx + ax * dt / 2
  vy = vy + ay * dt / 2
  x = x - (vx * dt) * 100 ' [cm] = [pixel] in screen
  y = y - (vy * dt) * 100 ' [cm] = [pixel] in screen
  Shapes.Move(oBall, x - size / 2, y - size / 2)
  If x < -size / 2 Or x0 + size / 2 * 3 < x Then
    bInScreen="False"
  EndIf
  If y < -size / 2 Or y0 + size / 2 * 3 < y Then
    bInScreen="False"
  EndIf
EndSub

Sub OnButtonClicked
  speed = Controls.GetTextBoxText(oSpeed)
  angle = Controls.GetTextBoxText(oAngle)
  vx = speed * Math.Cos(angle / 180 * Math.Pi)  ' [m/s]
  vy = speed * Math.Sin(angle / 180 * Math.Pi)  ' [m/s]
  bWait = "False"
EndSub

Sub OnTick
  bTime = "True"
EndSub

Sub Throw
  ' Wait throw button
  bWait = "True"
  While bWait
    Program.Delay(200)
  EndWhile
  ' Throw
  x = x0 + size / 2
  y = y0 + size / 2
  t = 0
  bInScreen = "True"
  bTime = "False"
  While bInScreen
    Program.Delay(dt * 1000)
    If bTime Then
      bTime = "False"
      MoveBall()
    EndIf
  EndWhile
  Shapes.Move(oBall, x0, y0)
EndSub
