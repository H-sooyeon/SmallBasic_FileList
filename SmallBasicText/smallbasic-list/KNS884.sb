'carImage = ImageList.LoadImage(Program.Directory+"\car.png")

LDGraphicsWindow.State = 2

carImage = ImageList.LoadImage("http://litdev.co.uk/game_images/car.png")

MakeCar()

MakeObstacles()

LDPhysics.SetGravity(0,0)



While ("True")

  Update()

  LDPhysics.DoTimestep()

  info = LDPhysics.GetTireInformation(car)

  If (info["Skid"] > properties["AntiSkid"]) Then

    GraphicsWindow.Title = "*****  SKID *****"

  Else

    GraphicsWindow.Title = ""

  EndIf

  Program.Delay(20)

EndWhile



Sub Update

  If (LDUtilities.KeyDown("Left")) Then

    LDPhysics.TurnTire(car,-400)

  EndIf

  If (LDUtilities.KeyDown("Right")) Then

    LDPhysics.TurnTire(car,400)

  EndIf

  If (LDUtilities.KeyDown("Up")) Then

    LDPhysics.MoveTire(car,250)

  EndIf

  If (LDUtilities.KeyDown("Down")) Then

    LDPhysics.MoveTire(car,-250)

  EndIf

  If (LDUtilities.KeyDown("Space")) Then

    LDPhysics.BrakeTire(car)

  EndIf

EndSub



Sub MakeCar

  LDPhysics.LoadImagesAsCircles = "False"

  car = Shapes.AddImage(carImage)  

  LDPhysics.AddMovingShape(car,0.8,0.05,1)

  LDPhysics.SetPosition(car,GraphicsWindow.Width/2,GraphicsWindow.Height/2,0)  

  LDPhysics.SetTire(car)

  

  'Reduced skid from default (3) and reduced drag resistance from default (0.05)

  properties = LDPhysics.GetTireProperties(car)

  properties["AntiSkid"] = 10

  properties["Drag"] = 0.01

  LDPhysics.SetTireProperties(car,properties)

EndSUb



Sub MakeObstacles

  LDPhysics.LoadImagesAsCircles = "True"

  For i = 1 To 5

    cone = Shapes.AddEllipse(40,40)

    LDPhysics.AddMovingShape(cone,1,0,10)

    LDPhysics.SetPosition(cone,100+Math.GetRandomNumber(GraphicsWindow.Width-200),100+Math.GetRandomNumber(GraphicsWindow.Height-200),0)  

    LDPhysics.SetDamping(cone,1,1) 'Sticky cones

  EndFor

EndSub


