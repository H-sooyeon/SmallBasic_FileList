'  Challenge of the month   May 2013  ...   A Shape (car)  Rotation     ....     by  NaochanON  

Make_shape_car()

While "true"
  Rotate_Car()
  Program.Delay(200)
endwhile

Sub Rotate_Car
  NN=NN+5     '  d_sita= 5  degrees
  For i=  1 To 9
    newX[i]=DR[i]*Math.Cos(Math.GetRadians(DS0[i]-NN))+XYC["X"]           '   new position .. X //center of the shape
    newY[i]=DR[i]*Math.sin(Math.GetRadians(DS0[i]-NN))+XYC["Y"]           '   new position .. Y //center of the shape
    If i=7 Then   '  7 = Light
      GraphicsWindow.PenColor="Yellow"
      GraphicsWindow.DrawLine(newX[i],newY[i],XYC["X"],XYC["Y"])          '   to show where is the Light // rotating angle 
    endif
    If SHP[i]["func"] <>"tri" Then 
      Shapes.Move(SHP[i]["obj"],newx[i]-shp[i]["width"]*s/2, newY[i]-shp[i]["height"]*s/2)  '  move the shape to  Left:Top  position 
    Elseif  SHP[i]["func"]="tri" then  
      Shapes.Move(SHP[i]["obj"],newx[i]-(shp[i]["x1"]*s+shp[i]["x2"]*s)/2, newY[i]-(shp[i]["y1"]*s+shp[i]["y2"]*s)/2)
    EndIf
    Shapes.Rotate(SHP[i]["obj"],-NN)
  endfor
  If Math.Remainder(NN,360)=0 Then 
    GraphicsWindow.BrushColor="Darkgreen"
    GraphicsWindow.FillRectangle(0,0,1200,700)                             '   erase  lines
  EndIf
EndSub


Sub Make_shape_car
  GraphicsWindow.BackgroundColor="Darkgreen"
  GraphicsWindow.Top=10
  GraphicsWindow.Left=50
  GraphicsWindow.Width=1200
  GraphicsWindow.Height=700 
  '--------------------------------------------------------------------------------------------------------------------------------------------------------------------------- 
  shp[1] = "func=rect;x=4;y=23;width=130;height=25;bc=#00F0CA;pc=#000000;pw=2;"             '  body
  shp[2] = "func=ell;x=20;y=33;width=23;height=23;bc=#9D756A;pc=#000000;pw=2;"              '  tire  Left 
  shp[3] = "func=ell;x=95;y=32;width=23;height=23;bc=#9D756A;pc=#000000;pw=2;"              '  tire  Right
  shp[4] = "func=rect;x=48;y=2;width=54;height=23;bc=#E0F0CA;pc=#000000;pw=2;"              '  window 
  shp[5] = "func=tri;x=42;y=5;x1=0;y1=17;x2=7;y2=0;x3=7;y3=17;bc=#E0F0CA;pc=#E0F0CA;pw=2;"  '  front window //Note : X1 <  Y3<=X2
  shp[6] = "func=tri;x=100;y=5;x1=0;y1=0;x2=9;y2=17;x3=0;y3=17;bc=#E0F0CA;pc=#E0F0CA;pw=2;" '  rear  window  //Note : Y1 <  Y3<=Y2
  shp[7] = "func=ell;x=0;y=26;width=11;height=12;bc=#FFFF00;pc=#FF0000;pw=2;"               '  Light
  shp[8] = "func=ell;x=58;y=5;width=10;height=15;bc=#6A5ACD;pc=#6A5ACD;pw=4;"               '  a person  
  shp[9] = "func=rect;x=130;y=40;width=15;height=8;bc=#FF5ACD;pc=#000000;pw=2;"             '  Muffler 
  XY0="X=600;Y=20"                                                                          '  X0 and Y0   .....  Initial Position 
  XYC="X=600;Y=320"                                                                         '   Xc and Yc   ....   Rotating Center  Position
  s=1                                                                                       '  scale  
  Shapes_Add()
  '--------------------------------------------------------------------------------------------------------------------------------------------------------------------------- 
  For i=  1 To 9
    If SHP[i]["func"] <>"tri" Then 
      X[i]=Shapes.GetLeft(SHP[i]["obj"])+shp[i]["width"]*s/2                                '  center of the shape ... X
      Y[i]=Shapes.GetTop(SHP[i]["obj"])+shp[i]["height"]*s/2                                '  center of the shape ... Y
    Else   '  func="tri"
      X[i]=Shapes.GetLeft(SHP[i]["obj"])+(shp[i]["x1"]*s+shp[i]["x2"]*s)/2                  '  center of the shape ... X   Note : X1<X3 <=  X2
      Y[i]=Shapes.GetTop(SHP[i]["obj"]) +(shp[i]["y1"]*s+shp[i]["y2"]*s)/2                  '  center of the shape ... Y    Note : Y1<Y3 <=  Y2
    EndIf
    DS0[i]=math.GetDegrees(Math.ArcTan((Y[i]-XYC["Y"])/(X[i]-XYC["X"])))                    '  degree // angle of the shape_Center  from   XYC position       
    DR[i]=Math.SquareRoot((X[i]-XYC["X"])*(X[i]-XYC["X"])+(Y[i]-XYC["Y"])*(Y[i]-XYC["Y"]))  '   radius  
  endfor
EndSub

Sub shapes_Add
  For i = 1 To 9
    GraphicsWindow.PenWidth = SHP[i]["pw"]*s
    GraphicsWindow.PenColor = SHP[i]["pc"]
    GraphicsWindow.BrushColor = SHP[i]["bc"]
    If SHP[i]["func"] = "rect" Then
      SHP[i]["obj"] = Shapes.AddRectangle(SHP[i]["width"]*s, SHP[i]["height"]*s)
    ElseIf SHP[i]["func"] = "ell" Then
      SHP[i]["obj"] = Shapes.AddEllipse(SHP[i]["width"]*s, SHP[i]["height"]*s)
    ElseIf SHP[i]["func"] = "tri" Then
      SHP[i]["obj"] = Shapes.AddTriangle(SHP[i]["x1"]*s, SHP[i]["y1"]*s, SHP[i]["x2"]*s, SHP[i]["y2"]*s, SHP[i]["x3"]*s, SHP[i]["y3"]*s)
    EndIf
    Shapes.Move(SHP[i]["obj"], XY0["X"] + SHP[i]["x"]*s, XY0["Y"] + SHP[i]["y"]*s)
  EndFor
endsub
    
