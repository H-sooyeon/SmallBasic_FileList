FirstInit()



While ("True")

  If (startGame) Then

    Init()

    Start()

    Finish()

  EndIf

  Program.Delay(100)

EndWhile



Sub FirstInit

  gw = GraphicsWindow.Width

  gh = GraphicsWindow.Height

  

  button = Controls.AddButton("Play",gw-50,10)

  Controls.ButtonClicked = NewGame

  

  GraphicsWindow.BrushColor = "SteelBlue"   ' N E W  new

  paddle = Shapes.AddRectangle(120, 12)

  ball = Shapes.AddEllipse(16, 16)

  GraphicsWindow.FontSize = 14

  GraphicsWindow.MouseMove = OnMouseMove

EndSub



Sub NewGame

  startGame = "True"

  Controls.HideControl(button)

EndSub



Sub Init

  difficulty = ""

  dead = ""

  x = 0

  y = 0

  deltaX = 1

  deltaY = 2

  startGame = "False"

  score = 0

  PrintScore()

EndSub



Sub Start

  While dead=""

    Play()

  EndWhile

EndSub



Sub Play

  CalcDelay()

  x = x + deltaX

  y = y + deltaY

  

  If (x >= gw - 16 Or x <= 0) Then

    deltaX = -deltaX

  EndIf

  If (y <= 0) Then

    deltaY = -deltaY

  EndIf

  

  padX = Shapes.GetLeft(paddle)

  If (y = gh - 28 And x >= padX And x <= padX + 120) Then

    Sound.PlayClick()

    score = score + 10

    PrintScore()

    deltaY = -deltaY

  EndIf

  

  'TextWindow.WriteLine(x+" : "+y)

  

  Shapes.Move(ball, x, y)

  Program.Delay(delay)

  

  If (y >= gh) then

    dead="true"

  EndIf

EndSub  



Sub Finish

  GraphicsWindow.ShowMessage(message, "")

  Controls.ShowControl(button)

EndSub



Sub OnMouseMove

  paddleX = GraphicsWindow.MouseX

  Shapes.Move(paddle, paddleX - 60, gh - 12)

EndSub



Sub PrintScore

  ' Clear the score first and then draw the real score text

  GraphicsWindow.BrushColor = "White"

  GraphicsWindow.FillRectangle(10, 10, 200, 20)

  GraphicsWindow.BrushColor = "Black"

  message = "Score: " + score + " Difficulty: " + Math.Abs(delay- 5)

  GraphicsWindow.DrawText(10, 10, message )

  GraphicsWindow.BrushColor = "SteelBlue"         ' N E W new

EndSub



Sub CalcDelay

  If difficulty = "" then 

    difficulty = 100

  EndIf

  difficulty = difficulty - .0025 ' how much it gets faster, higher = sooner

  delay = Math.Round(difficulty / 20)

  If oldDelay <> delay Then

    Sound.PlayChime() ' friendly warning

    oldDelay = delay

  EndIf

EndSub

    


