'################################################

'MAIN PROGRAM

'THE RUNNING TOTAL IS HELD IN VARIABLE 'balance'

'################################################



Introduction()

LoadBalance()



'Continue until we are finished

While ("True")

  ShowBalance()

  TextWindow.WriteLine("Are we going to subtract, add, or are we done (a/s/d)")

  answer = Text.ConvertToLowerCase(TextWindow.Read()) ' convert to lower case to accept A or a or S or s or D or d

  If (answer = "a") Then

    Add()

  ElseIf(answer = "s") Then

    Subtract()

  ElseIf(answer = "d") Then

    Confirmation()

  EndIf 

EndWhile



'################################################

'SUBROUTINES

'################################################



'Openning introduction

Sub Introduction

  TextWindow.WriteLine("Welcome to BalanceMeNow!")

  TextWindow.WriteLine("This program balances your checkbook for you!")

  Program.Delay(2000)

  TextWindow.Clear()

  TextWindow.WriteLine("Loading...")

  Program.Delay(2000)

EndSub



'Get the initial balance

Sub LoadBalance

  TextWindow.Clear()

  TextWindow.WriteLine("Please enter your current balance: ")

  balance = TextWindow.ReadNumber() 'Read a number

  Program.Delay(2000)

EndSub



'Make a deposit

Sub Add

  TextWindow.Clear()

  TextWindow.WriteLine("Welcome to the adding section of BalanceMeNow")

  TextWindow.WriteLine("This will add deposits to your bank account")

  Program.Delay(4000)

  TextWindow.Clear()

  TextWindow.WriteLine("Please input the deposit amount: ")

  deposit = TextWindow.ReadNumber() 'Read a number

  balance = balance + deposit

  ShowBalance()

EndSub



'Make a withdrawal

Sub Subtract

  TextWindow.Clear()

  TextWindow.WriteLine("Welcome to the subtracting part of BalanceMeNow!")

  TextWindow.WriteLine("This will subtract debits from your bank account")

  Program.Delay(4000)

  TextWindow.Clear()

  TextWindow.WriteLine("Please input the debit amount: ")

  debit = TextWindow.ReadNumber() 'Read a number

  balance = balance - debit

  ShowBalance()

EndSub



'Display the current balance

Sub ShowBalance

  RoundBalance()

  TextWindow.Clear()

  TextWindow.Write("Your subtotal is: ")

  If (balance < 0) Then

    TextWindow.ForegroundColor = "Red"

  Else

    TextWindow.ForegroundColor = "Green"

  EndIf

  TextWindow.WriteLine(balance)

  TextWindow.ForegroundColor = "White"

  Program.Delay(2000)

EndSub



'Round balance to nearest cent

Sub RoundBalance

  balance = 0.01*Math.Floor(100*balance + 0.5) 'Round to nearest cent

EndSub



'Confirmation to quit

Sub Confirmation

  ShowBalance()

  TextWindow.Clear()

  TextWindow.WriteLine("Are you ready to end the program (y/n): ")

  answer = Text.ConvertToLowerCase(TextWindow.Read())

  If (answer = "y")Then

    Finished()

  EndIf

EndSub



'Ending procedure on final quit

Sub Finished

  For i = 10 To 1 Step -1

    TextWindow.Clear()

    TextWindow.WriteLine("Closing program in "+i)

    Program.Delay(1000)

  EndFor

  TextWindow.Clear()

  TextWindow.WriteLine("Closing....")

  Program.Delay(3000)

  Program.End()

EndSub


