GraphicsWindow.Height = 600

GraphicsWindow.Width = 600

TextWindow.Left = GraphicsWindow.Left + 615

TextWindow.Top = GraphicsWindow.Top

Mouse.HideCursor()



Steam_Skin=ImageList.LoadImage("http://public.bay.livefilestore.com/y1pSDvsXajbdg0-y3xRxTA_W7gio52NarIE4aiSvo3pk3QhqsV72UwBk7HWeBd5i6PlZ2W5CtYZzegxsrkfsbLAxA/smoke1.png")

'Steam_Skin = ImageList.LoadImage(Program.Directory + "\smoke1.png")

Steam_Air_Resistance = -8





Circle = Shapes.AddEllipse(50,50)

Circle_X = 300

Circle_Y = 300

Circle_WX = 0

Shapes.Move(Circle,Circle_X-25,Circle_Y-25)



GraphicsWindow.BrushColor = "Black"

Emitter = Shapes.AddTriangle(0,0,70,0,35,70)





GraphicsWindow.PenColor= "Red"

While "True"

  TimeStart = Clock.ElapsedMilliseconds

  

  Mouse_X=GraphicsWindow.mouseX

  Mouse_Y=GraphicsWindow.mouseY

  Shapes.Move(Emitter,Mouse_X-35,Mouse_Y-35)

  

  If Mouse.IsRightButtonDown Then

    Emitter_Angle = Emitter_Angle+5

    Shapes.Rotate(Emitter,Emitter_Angle)

  EndIF

  If Mouse.IsLeftButtonDown Then

    Steam_EmitDelay =  Steam_EmitDelay + 1

    If Steam_EmitDelay = 2 Then

      SteamEmitter()

    elseIf Steam_EmitDelay = 4 Then

      SteamEmitter()

      Steam_EmitDelay = 0

    endif

  endif

  

  SteamCode()

  

  TextWindow.Clear()

  TextWindow.WriteLine("Steam Sprites: " + (Steam_New-Steam_Old))

  TextWindow.WriteLine("Steam_New: " + Steam_New)

  TextWindow.WriteLine("Steam_Old: " + Steam_Old)

  Circle_X = Circle_X + Circle_WX

  

  If Circle_X > 600 Then

    Circle_WX = -5

  ElseIf Circle_X < 0 Then

    Circle_WX = 5

  endif

  Shapes.Move(Circle,Circle_X-25,Circle_Y-25)

  

  TimeEnd = Clock.ElapsedMilliseconds

  PassedTime=TimeEnd-TimeStart

  PerfectDelay = 30-PassedTime

  If PerfectDelay > 0 Then

    Program.Delay(PerfectDelay)

  endif

endwhile



Sub SteamEmitter

  Steam_New = Steam_New + 1

  

  Steam[Steam_New] = Shapes.AddImage(Steam_Skin)

  Steam2[Steam_New] = Shapes.AddRectangle(2,2)

  Shapes.Move(Steam[Steam_New],-1000,-1000)

  

  Smoke_Velocity = 10+Math.GetRandomNumber(20)

  Smoke_EmitAngle = Math.Pi*(Emitter_Angle/180-0.5)

  

  Steam_WX[Steam_New] = Math.Cos(Smoke_EmitAngle) * Smoke_Velocity

  Steam_WY[Steam_New] = Math.Sin(Smoke_EmitAngle) * Smoke_Velocity

  Steam_X[Steam_New] = Mouse_X+35*Math.Cos(Smoke_EmitAngle)

  Steam_Y[Steam_New] = Mouse_Y+35*Math.Sin(Smoke_EmitAngle)

  

  

  Steam_Opacity[Steam_New] = 100

  Steam_Angle[Steam_New] = Math.GetRandomNumber(359)

  Steam_Zoom[Steam_New] = Math.GetRandomNumber(5) * 0.1 + 0.8

  Steam_Rotation[Steam_New] = 0.5*(Math.GetRandomNumber(9)-5)

endsub



Sub SteamCode

  For s = Steam_Old+1 To Steam_New

    Steam_WX[s] = Steam_WX[s] * 0.95

    Steam_WY[s] = Steam_WY[s] * 0.95

    

    Steam_X[s] = Steam_X[s] + Steam_WX[s]

    Steam_Y[s] = Steam_Y[s] + Steam_WY[s]

    

    'Steam_Angle[s] = Steam_Angle[s] + 1-((Steam_WY[s]*Steam_WX[s])*0.05)

    Steam_Angle[s] = Steam_Angle[s] + Steam_Rotation[s]+((Steam_Y[s]*Steam_WX[s])*0.0005)

    

    Steam_Distance[s] = math.SquareRoot((Circle_X-Steam_X[s])*(Circle_X-Steam_X[s])+(Circle_Y-Steam_Y[s])*(Circle_Y-Steam_Y[s]))

    If Steam_Distance[s] <= 25 Then

      Steam_WX[s] = Steam_WX[s]*0.65

      Steam_WY[s] = Steam_WY[s]*0.65

      Steam_Zoom[s] = Steam_Zoom[s] * 1.01

      Steam_Opacity[s] = Steam_Opacity[s] - 2

    endif

    

    Steam_Zoom[s] = Steam_Zoom[s] * 1.04

    Steam_Opacity[s] = Steam_Opacity[s] - 2

    

    Shapes.SetOpacity(Steam[s],Steam_Opacity[s])

    If Steam_Opacity[s] <= 0 Then

      Shapes.Remove(Steam[s])

      Shapes.Remove(Steam2[s])

      Steam[s] = ""

      Steam2[s] = ""

      Steam_X[s] = ""

      Steam_Y[s] = ""

      Steam_WX[s] = ""

      Steam_WY[s] = ""

      Steam_Opacity[s] = ""

      Steam_Angle[s] = ""

      Steam_Zoom[s] = ""

      Steam_Old = Steam_Old + 1

    endif

    

    Shapes.Zoom(Steam[s],Steam_Zoom[s],Steam_Zoom[s])

    Shapes.Rotate(Steam[s],Steam_Angle[s])

    Shapes.Move(Steam2[s],Steam_X[s],Steam_Y[s])

    Shapes.Move(Steam[s],Steam_X[s]-35,Steam_Y[s]-35)

  endfor

endsub








