'=============================================================
'INITIALISATION
'=============================================================

GraphicsWindow.BackgroundColor = "#200050" 'A dark sky background
'Load an image of a sprite and get its half width and half height
spriteURL = "http://icons.iconarchive.com/icons/lboi/tweetscotty/128/rocket-icon.png"
spriteImage = ImageList.LoadImage(spriteURL)
spriteW = ImageList.GetWidthOfImage(spriteImage)/2
spriteH = ImageList.GetHeightOfImage(spriteImage)/2
speed = 5 'The sprite speed
nSprite = 10 'The number of sprites

'Create the sprites
For i = 1 To nSprite
  'Create sprite using the image
  sprite[i] = Shapes.AddImage(spriteImage)
  'Set the sprite initial position - just off screen to the right and randomly down the screen
  resetPosition()
  'Move the sprite to be centred on its X and Y coordinates
  Shapes.Move(sprite[i],spriteX[i]-spriteW,spriteY[i]-spriteH)
  Shapes.Rotate(sprite[i],-90) 'Rotate to point the sprite in the direction it is travelling
EndFor

'=============================================================
'MAIN PROGRAM LOOP
'=============================================================

'Keep looping forever do the loop step 50 times a second (20ms delay each update)
While ("True")
  start = Clock.ElapsedMilliseconds 'Just used for the delay later
  'Move the sprites
  For i = 1 To nSprite
    'Update the sprite X position - move it to the left
    spriteX[i] = spriteX[i]-speed
    Shapes.Move(sprite[i],spriteX[i]-spriteW,spriteY[i]-spriteH)
    'If the sprite has moved off screen then just move it to the right of the screen
    If (spriteX[i] < -spriteW) Then
      resetPosition()
    EndIf
  EndFor
  'Do a delay up to 20 ms, depending on the time used to do the visuals
  delay = 20 - (Clock.ElapsedMilliseconds-start)
  If (delay > 0) Then
    Program.Delay(delay)
  EndIf
EndWhile

'=============================================================
'SUBROUTINES
'=============================================================

Sub resetPosition 'Do in a sub since it is done in different parts of the code and we may want to modify it often
  spriteX[i] = GraphicsWindow.Width+spriteW+Math.GetRandomNumber(GraphicsWindow.Width+2*spriteW)
  spriteY[i] = spriteH+Math.GetRandomNumber(GraphicsWindow.Height-2*spriteH)
EndSub
