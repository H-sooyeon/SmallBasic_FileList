GraphicsWindow.Width=500 'Set up the graphicswindow
GraphicsWindow.Height=600
GraphicsWindow.Top=50

Emitter=Shapes.AddTriangle(0,50,25,0,50,50) 'Add Emitter Triangle
EmitterX = 250
EmitterY = 300
Shapes.Move(Emitter,EmitterX-25,EmitterY-25)
GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(230,0,0) 'Set the color for the "blood"
GraphicsWindow.PenWidth=0
For i = 1 to 10 'Add the "Blood" sprites
  Blood[i]=shapes.AddEllipse(10,10)
endfor
dragging = 0 'dragging emitter

While "True" 'Repeat the whole time:
  MouseControl()
  BloodMovement()
  Program.Delay(1)
endwhile

Sub MouseControl
  If Mouse.IsLeftButtonDown Then 'Drag and drop the Emitter
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If dragging = 0 Then
      If MX >= EmitterX-25 And MX <= EmitterX+25 And MY >= EmitterY-25 And MY <= EmitterY+25 then
        dragging = 1
      endif
    EndIf
    If dragging = 1 Then
      EmitterX = MX
      EmitterY = MY
      Shapes.Move(Emitter,EmitterX-25,EmitterY-25)
    EndIf
  Else
    dragging = 0
  endif
  If Mouse.IsRightButtonDown Then 'Emitt the "blood" sprites
    EmitterX=Shapes.GetLeft(Emitter)
    EmitterY=Shapes.GetTop(Emitter)
    For i = 1 to 10
      BWX[i]=0.1*Math.GetRandomNumber(21)-1.1
      BWY[i]=0.1*(Math.GetRandomNumber(30)-31)
      BX[i]=EmitterX+25
      BY[i]=EmitterY
      Shapes.Move(Blood[i],EmitterX-5,EmitterY-5)
    endfor
  endif
endsub

Sub BloodMovement 'Move the "blood" sprites around by their corresponding WX and WY
  For i = 1 To 10
    If BY[i] > 550 Then
      BWX[i] = 0
      BWY[i] = 0
      moving=moving+1
    Else
      moving=moving-1
      If BWY[i]<3 Then
        BWY[i]=BWY[i]+0.05
      endif
      BWX[i] = BWX[i] * 0.998
    endif
    BX[i]=BX[i]+BWX[i]
    BY[i]=BY[i]+BWY[i]
    Shapes.Move(Blood[i],BX[i]-5,BY[i]-5)
  endfor
endsub

