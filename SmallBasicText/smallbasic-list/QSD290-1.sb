'  Challenge of the Month - October 2013  Write a simple Month Calendar in TW  by NaochanON 
'  Gregorian calendar     Start date   1582/10/15= Friday      QSD290-1
Months="1=Jan;2=Feb;3=Mar;4=Apr;5=May;6=Jun;7=Jul;8=Aug;9=Sep;10=Oct;11=nov;12=Dec"
Days="1=31;2=28;3=31;4=30;5=31;6=30;7=31;8=31;9=30;10=31;11=30;12=31"
Weekdays="1=Sun;2=Mon;3=Tue;4=Wes;5=Thu;6=Fri;7=Sat"
TextWindow.Write("  Input  year , month   Ex;   2013,9  " )  
YearMonth= TextWindow.Read()
TextWindow.WriteLine(" ")

GetWeekday()
Show_Calender()

Sub Show_Calender
  TextWindow.CursorLeft=5
  TextWindow.WriteLine("Calender of "+YearMonth+"  "+" // "+Months[B])
  For i=1 To 7
    TextWindow.CursorLeft=5+(i-1)*5
    TextWindow.Write(Weekdays[i])  
  EndFor
  TextWindow.WriteLine(" ")
  If B=2 Then                                     '  if   Feb 
    Y=A
    GetPLUS()                                     '  gets   PLUS =0 or 1
  endif
  For j=Lastday+1 To days[B]+Lastday+PLUS
    K= Math.Remainder(j-1,7)
    TextWindow.CursorLeft=5+5*K
    TextWindow.Write(text.GetSubTextToEnd(100+j-lastday,2)) 
    If K=6  Then  
      TextWindow.WriteLine(" ")
    EndIf
  EndFor
  TextWindow.WriteLine(" ")
endsub  

Sub GetWeekday
  midP=Text.GetIndexOf(YearMonth,",")
  A= Text.GetSubText(YearMonth,1,midP-1)*1       '   Year  >=1583
  B= Text.GetSubTextToEnd(YearMonth,midP+1)*1    '   Month
  Sumdays=365*(A-1583)
  For Y=1583 To A-1
    getplus()
    sumdays=sumdays+PLUS                         '   previous year
  Endfor
  For j=1 To B-1
    sumdays=sumdays+Days[j]
  EndFor
  Y=A
  getplus()
  If B>2 then 
    sumdays=sumdays+PLUS
  EndIf
  Lastday= Math.Remainder(sumdays+6,7)   '  last weekday   1583/01/01=Saturday =Weekdays[1+6]
endsub

Sub getplus
  PLUS=0
  If (Math.Remainder(Y,4)=0 And Math.Remainder(Y,100)<>0) Or Math.Remainder(Y,400)=0  Then 
    PLUS=1
  EndIf
EndSub


