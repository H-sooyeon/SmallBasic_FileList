' Dart game   by NaochanON     
'  shape making  codes are made by Nonki_Takahashi's_Shapes 1.31  
GraphicsWindow.MouseDown=Onmousedown 
GraphicsWindow.MouseMove=Onmousemove
GraphicsWindow.Mouseup=Onmouseup 
Controls.ButtonClicked = OnButtonClicked
Resister()


While "true"
  If move="true" Then 
    Shoot()                                            '   Dart is throwed
    Hitcheck()                                         '   if  dart hit at target 
    Program.Delay(20)
  EndIf
endwhile

Sub Hitcheck
   if  shx<newxy["X"] And newxy["X"]<(shx+shape[1]["width"])*3/4  And shy<newxy["Y"] And newxy["Y"]<327  Then  '
    LX=(Shapes.GetLeft(shape[8]["obj"])-Shapes.GetLeft(shape[4]["obj"])-9)*2.5
    LY=(Shapes.Gettop(shape[8]["obj"])-Shapes.Gettop(shape[4]["obj"])-20)
    L=math.SquareRoot(LX*LX+LY*LY)
    If L<50/2 Then 
      SCR=100
    ElseIf 50/2<=L and L<115/2 then 
      SCR=50
    ElseIf 115/2<=L and L<190/2 then 
      SCR=25
    ElseIf 190/2<=L and L<250/2 then 
      SCR=10
    EndIf
    MM=MM+SCR   '  count up  goals
    Shapes.SetText(smsg," Throw "+NN+" times Hit !!!!  Score= "+SCR+ " Total= "+MM)
    Shapes.Animate(smsg,600,50,1000)
    score=MM
    return()
  ElseIf   600<newxy["Y"] then 
    Shapes.Zoom(shape[12]["obj"],1,0.15)              '  Eye   close
    return()
  EndIf   
endsub

Sub onbuttonclicked
  User= Controls.GetTextBoxText(userbox)
  Shapes_Init()
  init_Man()
  man_animate()
  basedata()
EndSub

Sub onmousemove
  If catch="true" Then 
    movexy="X="+ (GraphicsWindow.MouseX)+";Y="+(GraphicsWindow.MouseY)   ' Mouse (X1 ,Y1) position
    Shapes.Move(shape[8]["obj"],movexy["X"]-32,movexy["y"]-8)   ' pin
    Shapes.Move(shape[9]["obj"],movexy["X"]-12,movexy["Y"])     'pin body
    angle= Math.ArcTan( (movexy["Y"]-sxy["Y"])/(movexy["X"]-sxy["X"]-30) )  'sita= math.ArcTan((y1-y0)/(x1-x0))
    If angle<0 Then 
      angle=3.14192+angle
    EndIf
    
    C2=(movexy["Y"]-sxy["Y"]-20)*(movexy["Y"]-sxy["Y"]-20)+(movexy["X"]-sxy["X"]-20)*(movexy["X"]-sxy["X"]-20) 
    C=math.SquareRoot(C2)             'Length (X1,Y1)-(X0,Y0)
    delta=(C2 + a*a -b*b)/2/C/a 　　  'angle (X2,Y2)-(X0,Y0)-(X1,Y1)
    If delta>=1.0 Then 
      delta=1.0
    endif
    angle2=math.ArcCos(delta)
    x2 = sxy["X"] + a*math.cos(angle+angle2)              '
    y2 = sxy["Y"] + a*math.sin(angle+angle2)              'Cross point 
    Shapes.Rotate(shape[7]["obj"],math.getdegrees(-3.4152/2+angle))
    Shapes.Move(shape[7]["obj"],sxy["X"]-b/2*math.cos(angle)+10,sxy["Y"]-b/2*math.sin(angle)-15)  '
    
    angle3　= Math.ArcTan((movexy["Y"]-y2)/(movexy["X"]-x2))   ' angle of arm top
    Shapes.Move(shape[6]["obj"],Shapes.GetLeft(shape[7]["obj"])-a*math.Cos(angle),Shapes.GetTop(shape[7]["obj"])-a*math.Sin(angle))
    Shapes.Rotate(shape[6]["obj"],math.getdegrees(angle3-3.14152/2))
    Shapes.Move(shape[5]["obj"],Shapes.GetLeft(shape[6]["obj"])-20*math.Cos(angle),Shapes.GetTop(shape[6]["obj"])-10*math.Sin(angle))  '
    if  movexy["X"]>Shapes.GetLeft(shape[11]["obj"]) Then 
      t1=clock.ElapsedMilliseconds 
    EndIf  
    If  movexy["X"]<(Shapes.GetLeft(shape[10]["obj"])-a-b) Then 
      onmouseup()
    EndIf  
  EndIf 
endsub

Sub Shoot  
  t3=clock.ElapsedMilliseconds
  newxy="X="+(newxy["X"]+vxy["X"])+";Y="+(newxy["Y"]+vxy["Y"] +(t3-t2)*(t3-t2)*Gravity/2/1000000)  '  flyineg new position 
  Shapes.Move(shape[8]["obj"],newxy["X"],newxy["y"])
  Shapes.Move(shape[9]["obj"],newxy["X"]+22,newxy["Y"]+7)
  GraphicsWindow.Title="Now Dart  X= "+math.Floor(newxy["X"])+ "   Y=  "+math.Floor(newxy["Y"])
EndSub

Sub return 
  If NN<10 Then 
    Move="False"
    Catch="False"
    Program.Delay(1500)
    For i = startNo To  endNo
      Shapes.Remove(shape[i]["obj"])
    EndFor
    init_Man()
    man_animate()
  Else 
    Move="False"
    Set_Highscore()
  EndIf
EndSub

Sub onmouseup
  If catch="true" Then 
    Upxy= "X="+(GraphicsWindow.MouseX)+";Y="+ (GraphicsWindow.MouseY)                   '  released    Mouse X Y position
    t2=clock.ElapsedMilliseconds                                                    '  Dart   release  time 
    catch="false"
    Vxy="X=-"+Math.abs(Upxy["X"]-Mdxy["X"])/(t2-t1)*10+";Y="+(Upxy["Y"]-Mdxy["Y"])/(t2-t1)*10 ' Dart  speed  X,Y
    Newxy="X="+shapes.GetLeft(shape[8]["obj"])+";Y="+shapes.Gettop(shape[8]["obj"])  ' Dart released position   NewXY  X,Y
    Move="true"
  EndIf
endsub

Sub onmousedown
  Mdxy="X=" +(GraphicsWindow.MouseX)+";Y="+ (GraphicsWindow.MouseY)                      '  caught    Mouse X Y position
  If math.Abs(Mdxy["X"]-xy0["X"])<50 And math.Abs(Mdxy["Y"]-xy0["Y"])<50 Then 
    NN=NN+1
    GraphicsWindow.Title=" Throwing......  "+NN+" times"   
    catch="true"
    Gravity=9.8
    Shapes.SetText(smsg," ")                                                         ' erase message 
  EndIf
endsub

Sub basedata
  GraphicsWindow.BrushColor="red" 
  GraphicsWindow.FontSize=25 
  smsg= Shapes.AddText(" ")
  a=shape[6]["height"]  '  arm top
  b=shape[7]["height"]  '  arm under
  xy0="X="+shapes.GetLeft(shape[8]["obj"])+";Y="+Shapes.GetTop(shape[8]["obj"])   '  8....  dart pin  position 
  sXY="X="+shapes.GetLeft(shape[10]["obj"])+";Y="+Shapes.GetTop(shape[10]["obj"])   '  10....  body  (shoulder) position 
  CXY="X="+(shapes.GetLeft(shape[7]["obj"])+b/2*math.Sin(math.GetRadians(60)) )+";Y="+(Shapes.GetTop(shape[7]["obj"])+ b/2*math.cos(math.GetRadians(60))) '  7....  arm (shoulder) position 
endsub  

Sub Shapes_Init  
  Controls.Remove(userbox)
  Controls.Remove(userTxt)
  Controls.Remove(userbutton)
  GraphicsWindow.BackgroundColor="khaki" 
  GraphicsWindow.Top=20
  GraphicsWindow.Left=50
  GraphicsWindow.Width=1200
  GraphicsWindow.Height=600
  message="Inner circle... 100point , 50point , 25point , 10point ....Outer circle"
  GraphicsWindow.DrawText(50,350,message)
  s = 1                   '       s=scale
  shX = 14                ' x offset
  shY = 96                ' y offset
  shape[1] = "func=ell;x=0;y=0;width=100;height=250;bc=#F9FDCD;pc=#FF0000;pw=2;"
  shape[2] = "func=ell;x=17;y=21;width=76;height=190;bc=#F9FDFF;pc=#6A5ACD;pw=2;"
  shape[3] = "func=ell;x=33;y=49;width=46;height=115;bc=#F9FDCD;pc=#FF0000;pw=2;"
  shape[4] = "func=ell;x=48;y=86;width=20;height=50;bc=#FFFD00;pc=#6A5ACD;pw=2;"
  startNo=1
  endNo=4
  shape_setting()
EndSub

Sub init_Man
  shape[5] = "func=ell;x=965;y=100;width=25;height=20;angle=15;bc=#F9FDCD;pc=#CB9065;pw=2;"     'hand
  shape[6] = "func=ell;x=985;y=115;width=20;height=62;angle=-30;bc=#8C9971;pc=#8C9971;pw=2;"    'arm top
  shape[7] = "func=ell;x=1030;y=155;width=20;height=78;angle=-60;bc=#8C9971;pc=#8C9971;pw=2;"   'arm under
  shape[8] = "func=line;x=940;y=90;x1=0;y1=0;x2=52;y2=13;pc=#FF0000;pw=2;"                      'dart pin
  shape[9] = "func=tri;x=965;y=100;x1=0;y1=0;x2=35;y2=8;x3=35;y3=-8;angle=15;bc=#8FB557;pc=#8FB557;pw=2;"      'dart body
  shape[10] = "func=ell;x=1047;y=192;width=53;height=112;angle=15;bc=#8C9971;pc=#8C9971;pw=2;"  'body
  shape[11] = "func=ell;x=1077;y=141;width=37;height=49;angle=10;bc=#F9FDCD;pc=#CB9065;pw=2;"   'face
  shape[12] = "func=ell;x=1088;y=157;width=10;height=9;angle=13;bc=#000000;pc=#000000;pw=2;"    'eye
  shape[13] = "func=line;x=1080;y=176;x1=0;y1=0;x2=10;y2=1;pc=#FF0000;pw=1;"                    'mouth
  shape[14] = "func=tri;x=1068;y=159;x1=5;y1=0;x2=0;y2=13;x3=10;y3=13;angle=279;bc=#F9FDCD;pc=#CB9065;pw=1;" 'nose
  shape[15] = "func=rect;x=1073;y=300;width=14;height=100;angle=350;bc=#8C9971;pc=#8C9971;pw=1;"  ' leg his left
  shape[16] = "func=rect;x=1039;y=295;width=14;height=100;angle=7;bc=#8C9971;pc=#8C9971;pw=1;"    'leg his right
  shape[17] = "func=ell;x=1064;y=396;width=34;height=17;angle=355;bc=#8C9971;pc=#8C9971;pw=2;"    'foot his left
  shape[18] = "func=ell;x=1012;y=387;width=34;height=17;angle=358;bc=#8C9971;pc=#8C9971;pw=2;"    'foot his right
  shape[19] = "func=rect;x=1083;y=187;width=13;height=12;angle=10;bc=#F9FDCD;pc=#8C9971;pw=1;"    '  neck
  shape[20] = "func=ell;x=1087;y=217;width=54;height=18;angle=29;bc=#8C9971;pc=#8C9971;pw=2;"     'arm
  shape[21] = "func=ell;x=1114;y=225;width=17;height=45;angle=246;bc=#8C9971;pc=#8C9971;pw=2;"    'arm
  shape[22] = "func=ell;x=1081;y=248;width=25;height=20;angle=15;bc=#F9FDCD;pc=#CB9065;pw=2;"     'hand
  startNo=5
  endNo=22
  shx=314
  shape_setting()
  Program.Delay(1500)
endsub

Sub man_animate
  shx=14
  For i=5 To 22
    Shapes.Animate(shape[i]["obj"], shX + shape[i]["x"] * s, shY + shape[i]["y"] * s,1500)
  EndFor
EndSub

sub shape_setting
  For i = startNo To  EndNo
    GraphicsWindow.PenWidth = shape[i]["pw"] * s
    GraphicsWindow.PenColor = shape[i]["pc"]
    GraphicsWindow.BrushColor = shape[i]["bc"]
    If shape[i]["func"] = "rect" Then
      shape[i]["obj"] = Shapes.AddRectangle(shape[i]["width"]* s, shape[i]["height"] * s)
    ElseIf shape[i]["func"] = "ell" Then
      shape[i]["obj"] = Shapes.AddEllipse(shape[i]["width"] * s, shape[i]["height"]* s)
    ElseIf shape[i]["func"] = "tri" Then
      shape[i]["obj"] = Shapes.AddTriangle(shape[i]["x1"] * s, shape[i]["y1"] * s, shape[i]["x2"] * s, shape[i]["y2"] * s, shape[i]["x3"] * s, shape[i]["y3"] * s)
    ElseIf shape[i]["func"] = "line" Then
      shape[i]["obj"] = Shapes.AddLine(shape[i]["x1"] * s, shape[i]["y1"]* s, shape[i]["x2"] * s, shape[i]["y2"] * s)
    EndIf
    Shapes.Move(shape[i]["obj"], shX + shape[i]["x"] * s, shY + shape[i]["y"] * s)
    Shapes.Rotate(shape[i]["obj"], shape[i]["angle"])
  EndFor
EndSub

Sub Resister      '  User name 
  GraphicsWindow.Title=" Easy  Darts Game  "
  GraphicsWindow.BackgroundColor="Lightcyan" 
  GraphicsWindow.BrushColor = "Red"
  GraphicsWindow.FontSize = 20
  user = ""
  userBox = Controls.AddTextBox(50,100)
  Controls.SetTextBoxText(userBox,"")
  Controls.SetSize(userBox,200,30)
  userTxt = Shapes.AddText("Enter your user name here")
  Shapes.Move(userTxt,50,50)
  userButton = Controls.AddButton("OK",50,150)
EndSub

Sub Set_Highscore   '  you need  Litdev Extension 
  highScore = LDNetwork.HighScore("Easy_Darts",user,score)    
  message=" Throw "+NN+" times  Hit !!!!! Last Score= "+SCR+Text.GetCharacter(10)+Text.GetCharacter(10) +"  Total Score= "+MM+Text.GetCharacter(10)+Text.GetCharacter(10)
  message=message+"The high score is "+highScore[2]+" by "+highScore[1]
  Shapes.SetText(smsg,message)
EndSub

