' Very simple analog clock

' Author : Chris Taylor



' Setup the Graphics Window

GraphicsWindow.Title = "Analog Clock"

GraphicsWindow.Height = 220

GraphicsWindow.Width = 220

GraphicsWindow.CanResize = "False"



' Initialize global variables

cx = GraphicsWindow.Width / 2

cy = GraphicsWindow.Height / 2





' Setup a timer to update the clock every second

Timer.Interval = 1000

Timer.Tick = OnTick



' Run the clock render routine once to get the initial clock rendered

OnTick()



' Tick routing called every second to update the clock display

Sub OnTick   

  ' Clear the current display

  GraphicsWindow.Clear()  

  

  ' Render new clock state

  DrawClockFace()

  DrawSecondHand()

  DrawMinuteHand()

  DrawHourHand()    

EndSub



Sub DrawSecondHand

  value = Clock.Second

  period = 60

  length = 100  

  width = 1

  DrawHand()

EndSub



Sub DrawMinuteHand

  value = Clock.Minute

  period = 60

  length = 80

  width = 2

  DrawHand()

EndSub



Sub DrawHourHand

  value = Clock.Hour

  period = 12

  length = 60

  width = 3

  DrawHand()

EndSub



' DrawHand - draws the clock hand based on the values of the following global variables

' value - The value used to calculate the angle of the line

' period - The number that represents a full 360 degree rotation. For the minute and second hand this is 60, for the hour had this is 12

' length - The length of the line to be drawn

' width - The width of the pen used to draw the line

' cx, cy - The starting point of the line, this is intialized to the center of the GraphicsWindow

Sub DrawHand  

  GraphicsWindow.PenWidth = width

  angle = (Math.Pi * value / (period / 2)) - (Math.Pi / 2)

  tx = cx + length * Math.Cos(angle)

  ty = cy + length * Math.Sin(angle)   

  GraphicsWindow.DrawLine(cx, cy, tx, ty)

EndSub



' DrawClockFace - draws the tick marks around the clock face.

Sub DrawClockFace

  GraphicsWindow.PenWidth = 1

  For t = 0 To 59

    angle = (Math.Pi * t / 30) - (Math.Pi / 2)

    cos = Math.Cos(angle)

    sin = Math.Sin(angle)

    tx = cx + 102 * cos

    ty = cy + 102 * sin

    If Math.Remainder(t, 5) = 0 then    

      tickLength = 8      

    Else

      tickLength = 4

    EndIf

    GraphicsWindow.DrawLine(tx, ty, tx + (cos * tickLength), ty + (sin * tickLength))    

  EndFor    

EndSub


