'Graphics Window
GraphicsWindow.Width = 600
GraphicsWindow.Height = 600
'Shapes size
dX = 400
dY = 40
'Centre of rotation
centreX = 300
centreY = 300
'Fraction of shape length from centre in X direction for rotation
'0 is bounding box centre, 1/3 is triangle centroid, 1 is shape edge
frac = 1/3
pi = Math.Pi

'Triangle and transparent bounding box
'GraphicsWindow.PenWidth = 0
triangle = Shapes.AddTriangle(0,0,0,dY,dX,dY/2)
bounding = Shapes.AddRectangle(dX,dY)
Shapes.SetOpacity(bounding,50)
'Centre of rotation point
GraphicsWindow.BrushColor = "Red"
centre = Shapes.AddEllipse(10,10)
Shapes.Move(centre,centreX-5,centreY-5)

'Note that the triangle is not actually bounded as expected! - Some sort of BUG possibly
'I think this due to the outline pen extending the triangle beyond the expected bounding box
'If the penwith is set to zero, then the fix below isn't necessary
'If we scale dX to near the actual bounding box (accounting for pen boundary) for the triangle it rotates about its centroid
dX = dX + dX/dY/2*GraphicsWindow.PenWidth
While ("True")
  For i = 1 To 360
    'Offest for shapes assuming centre of rotation is 1/3 along shape (centroid of triangle)
    xS = centreX-dX/2+frac*dX/2*Math.Cos(i*pi/180)
    yS = centreY-dY/2+frac*dX/2*Math.Sin(i*pi/180)
    'Move and rotate shapes about centre of rotation
    Shapes.Move(triangle,xS,yS)
    Shapes.Rotate(triangle,i)
    Shapes.Move(bounding,xS,yS)
    Shapes.Rotate(bounding,i)
    Program.Delay(25)
  EndFor
EndWhile
