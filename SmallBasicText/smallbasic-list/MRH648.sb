' SmallBasic Version 1.0

' Funktion: Single-/Doubleclick Demo

' Autor: Pappa Lapub

' Herkunft: 

' ImportURL: http://smallbasic.com/program/?

' Extension: LitDev

' Kommentar: L-SingleClick . / L-DoubleClick - / R-Click " "  (Space)

'  

' Variablen: 

'  

' --------------------------------------------------------------------------------

Delay = 350



GraphicsWindow.Width = 140

GraphicsWindow.Height = 150

GraphicsWindow.BrushColor = "Black"



Slider = LDControls.AddSlider(100,22,"H")

Controls.Move(Slider,20,20)

LDControls.SliderValue(Slider,Delay/10)

LDDialogs.ToolTip(Slider,Delay)

GraphicsWindow.DrawText(20,45,"0      [ms]      1000")



LDControls.SliderChanged = OnSliderChange

GraphicsWindow.MouseDown = OnMouseDown



' ----------     Identify Single-/Doubleclick  ----------

' http://www.autohotkey.com/board/topic/16321-map-a-double-left-mouse-click-to-a-single-key/

Sub OnMouseDown

  If Mouse.IsRightButtonDown Then

    ' Pause (  )

    TextWindow.Write(" ")

    

  Else

    If Clicks > 0 Then

      Clicks = Clicks + 1

    Else

      Clicks = 1

      Timer.Interval = Delay

      Timer.Tick = ClickWait

    EndIf

    

  EndIf

EndSub



Sub ClickWait

  Timer.Pause()

  If Clicks = 1 Then

    OnSingleClick()

  Else

    OnDoubleClick()

  EndIf

  Clicks = 0

EndSub

' ------------------------------------------------



Sub OnSingleClick

  ' . Dit / E

  TextWindow.Write(".")

EndSub



Sub OnDoubleClick

  ' - Dah (Doh) / T

  TextWindow.Write("-")

EndSub



Sub OnSliderChange

  Delay = Math.Floor(10*LDControls.LastSliderValue)

  LDDialogs.ToolTip(Slider, Delay)

EndSub

  


