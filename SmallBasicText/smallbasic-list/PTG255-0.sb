numWin = 2
'Create some windows with a ball and close button and hide initially
For i = 1 To numWin
  id[i] = LDWindows.Create()
  GraphicsWindow.Title = "Window "+i
  GraphicsWindow.Hide()
  ball[i] = Shapes.AddEllipse(20,20)
  button[i] = Controls.AddButton("Close",20,20)
  GraphicsWindow.MouseDown = OnMouseDown
  GraphicsWindow.MouseMove = OnMouseMove
EndFor

'Create the main window with buttons to show other windows
iWin = 0
LDWindows.CurrentID = iWin
GraphicsWindow.Show()
For i = 1 To numWin
  win[i] = Controls.AddButton("Show window "+i,20,20+40*(i-1))
EndFor
Controls.ButtonClicked = OnButtonClicked

'Change background colour on current window (not main window since no event registered for this window)
Sub OnMouseDown
  GraphicsWindow.BackgroundColor = GraphicsWindow.GetRandomColor()
EndSub

'Move the current window ball
Sub OnMouseMove
  Shapes.Move(ball[iWin],GraphicsWindow.MouseX-10,GraphicsWindow.MouseY-10)
EndSub

'Show and hide windows on button clicks
Sub OnButtonClicked
  For i = 1 To numWin
    If (Controls.LastClickedButton = button[i]) Then
      LDWindows.CurrentID = id[i]
      GraphicsWindow.Hide()
      iWin = 0
      LDWindows.CurrentID = iWin
    ElseIf (Controls.LastClickedButton = win[i]) Then
      iWin = i
      LDWindows.CurrentID = id[iWin]
      GraphicsWindow.Show()
      GraphicsWindow.BackgroundColor = GraphicsWindow.GetRandomColor()
    EndIf
  EndFor
EndSub
      
