w=650
h=600
GraphicsWindow.Title ="Fractals
ll=ldtext.Split ("1 2 3 27 81"," ")
GraphicsWindow.Width = w
GraphicsWindow.Height = h
GraphicsWindow.BackgroundColor ="teal
args=0
CreateGUI()
Menu()

Sub Menu
  Controls.ButtonClicked = bsel
Endsub

Sub CreateGUI 
  GraphicsWindow.BrushColor ="lime
  For r=1 To 7
    lbb[r] = ldControls.AddRadioButton (r,"a")
    
    Controls.Move(lbb[r] 20+r*60,550)
  EndFor 
  GraphicsWindow.BrushColor ="darkblue
  BReset = Controls.AddButton("RESET", 550,550)
  
  FKoch1 = Controls.AddButton("Koch 1", 550,20)
  Controls.SetSize(FKoch1, 80, 30)
  FKoch2 = Controls.AddButton("Koch 2", 550, 70)
  Controls.SetSize(FKoch2, 80, 30)
  FKoch3 = Controls.AddButton("Koch 3", 550, 120)
  Controls.SetSize(FKoch3, 80, 30)
  FSierpinsk1 = Controls.AddButton("Sierpinsk 1", 550, 170)
  Controls.SetSize(FSierpinsk1, 80, 30)
  FSierpinsk2 = Controls.AddButton("Sierpinsk 2", 550, 220)
  Controls.SetSize(FSierpinsk2, 80, 30)
  FTree = Controls.AddButton("Tree", 550, 270)
  Controls.SetSize(FTree, 80, 30)
  LDControls.RadioButtonClicked=rdd
  LDControls.RadioButtonSet(lbb[5])
EndSub

Sub rdd
  rr= LDControls.LastRadioButton 
  For r=1 To 7
    
    If rr=lbb[r] Then
      lv5=r
    EndIf
  EndFor  
EndSub 

Sub Reset
  GraphicsWindow.Clear()
  CreateGUI()
  Menu()
EndSub

Sub Clean
  GraphicsWindow.BrushColor ="teal
  GraphicsWindow.FillRectangle (0 0 800 800)
EndSub


Sub bsel
  lb= Controls.LastClickedButton 
  If inn Then
    GraphicsWindow.ShowMessage ("Wait to finish..." "WAIT")
  else
    If lb=FKoch1 then
      Clean()
      inn="true
      DrawKoch ()
    elseif lb=FKoch2 then
      Clean()
      inn="true 
      DrawKoch2()
    elseif lb=FKoch3   then
      Clean()
      inn="true
      DrawKoch3()
    elseif lb=FSierpinsk1  then
      Clean()
      inn="true
      Sierpinski ()
    elseif lb=FSierpinsk2  then
      Clean()
      inn="true
      Sierpinski2 ()
    elseif lb=FTree  then
      Clean()
      tree()
    elseif lb=BReset  then
      Reset()
    EndIf
    inn="false
  EndIf 
EndSub

Sub DrawKoch
  
  t_x= 20
  t_y=400
  ox=T_x
  oy=t_y 
  t_angle=90
  GraphicsWindow.PenColor = "white
  
  l = 500/ll[lv5]
  level = lv5
  angle = 60
  
  koch()
EndSub

Sub koch
  level = level - 1
  If (level = 0) Then
    ldcall.function("t_move", l)
  Else
    koch()
    t_angle=t_angle+(-angle)
    koch()
    t_angle=t_angle+(2*angle)
    koch()
    t_angle=t_angle+(-angle)
    koch()
  EndIf
  level = level+1
EndSub

Sub DrawKoch2
  
  t_x= 20
  t_y=400
  ox=T_x
  oy=t_y 
  t_angle=90
  GraphicsWindow.PenColor = "white
  
  l = 500/ll[lv5]
  level = lv5
  angle = 90
  
  koch2()
EndSub

Sub koch2
  level = level - 1
  If (level = 0) Then
    ldcall.function("t_move", l)
  Else
    koch2()
    t_angle=t_angle+(-angle)
    koch2()
    t_angle=t_angle+(angle)
    koch2()
    t_angle=t_angle+(angle)
    koch2()
    t_angle=t_angle+(-angle)
    koch2()
  EndIf
  level = level+1
EndSub

Sub DrawKoch3
  
  t_x= 20
  t_y= 270
  ox=T_x
  oy=t_y 
  t_angle=90
  GraphicsWindow.PenColor = "white
  If lv5=5 Then
    l=2
  else
    l = 500/(lv5*4)
  endif
  level = lv5
  angle = 90
  
  koch3()
EndSub

Sub koch3
  level = level - 1
  If (level = 0) Then
    ldcall.function("t_move", l)
  Else
    koch3()
    t_angle=t_angle+(-angle)
    koch3()
    t_angle=t_angle+(angle)
    koch3()
    t_angle=t_angle+(angle)
    koch3()
    koch3()
    t_angle=t_angle+(-angle)
    koch3()
    t_angle=t_angle+(-angle)
    koch3()
    t_angle=t_angle+(angle)
    koch3()
  EndIf
  level = level+1
EndSub

Sub  Tree_1
  t_x = 270
  t_y = 500
  t_angle = 0
  
  distance = 130
  delta = distance / 2
  
  angle = 20 + Math.GetRandomNumber(25)
  DrawTree()
EndSub

Sub Sierpinski
  x = 20
  y = 20
  GraphicsWindow.BrushColor = "skyblue"
  l = Math.Round(500/Math.Power (3,lv5))
  If lv5<6 then
    level = lv5+1
    z[1] = 1
    For i = 2 To level-1
      z[i] = 3*z[i-1]
    EndFor
    box()
  endif
EndSub

Sub box
 ' GraphicsWindow.Title =level
  
  level = level-1
  If (level = 0) Then
    
    GraphicsWindow.FillRectangle(x,y,l,l)
  Else
    box()
    x = x+z[level]*l
    box()
    x = x+z[level]*l
    box()
    y=y+z[level]*l
    box()
    y=y+z[level]*l
    box()
    x=x-z[level]*l
    box()
    x=x-z[level]*l
    box()
    y=y-z[level]*l
    box()   
    y=y-z[level]*l
  EndIf
  level = level+1
EndSub

Sub Sierpinski2 
  x = 20
  y = 450
  level = lv5+1
  
  l = 250/Math.Power (2 lv5-1)
  
  z[1] = 1
  For i = 2 to level -1
    z[i] = 2*z[i-1]
  Endfor
  hh=0
  Triangle()
  
Endsub

Sub Triangle
  a = Math.Tan(Math.GetRadians(60))
  level = level-1
  If (level = 0) Then
    GraphicsWindow.BrushColor = LDColours.HSLtoRGB (hh 1 .6)
    hh=hh+3
    GraphicsWindow.FillTriangle(x,y, x+l/2,y-a*l/2, x+l,y)
  Else
    Triangle()
    x = x + z[level]*l/2
    y = y - a*z[level]*l/2
    Triangle()
    x = x + z[level]*l/2
    y = y + a*z[level]*l/2
    Triangle()
    x = x - z[level]*l
  EndIf
  level = level+1
Endsub

Sub  Tree
  t_x = 270
  t_y = 500
  t_angle = 0
  ox=t_x
  oy=t_y
  distance = 130
  delta = distance / 6
  
  angle = 20 + Math.GetRandomNumber(25)
  DrawTree()
EndSub


Sub DrawTree
  GraphicsWindow.PenColor = "gold
  If (distance > 0) Then
    ldcall.function("t_move", distance)
    t_angle=t_angle+(angle)
    
    Stack.PushValue("distance", distance)
    distance = distance - delta
    DrawTree()
    t_angle=t_angle+(-angle * 2)
    DrawTree()
    t_angle=t_angle+(angle)
    distance = Stack.PopValue("distance")
    
    ldcall.function("t_move", -distance)
  EndIf
EndSub

sub t_Move
  ds=args[1]
  mm=LDMath.Convert2Cartesian  (t_x,t_y,ds, t_Angle-90 )
  
  t_x= (mm[1])
  t_y= (mm[2])
  GraphicsWindow.DrawLine (ox,oy T_x T_y)
  ii=ii+1
  ox=T_x
  oy=t_y 
EndSub
