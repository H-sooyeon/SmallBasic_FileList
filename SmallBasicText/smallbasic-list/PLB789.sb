'#############################################################
'MAIN RUN CODE
'#############################################################

Initialise() 'Set the array of shorthand turtle command strings for each letter
While ("True")
  Input() 'Set GraphicsWindow, get input name and initialise turtle
  Run() 'Draw all of the letters in name
EndWhile

'#############################################################
'SUBROUTINES
'#############################################################

'***********************************************************************************
'READ INPUT AND SET WINDOW AND INITIALISE TURTLE
'***********************************************************************************
Sub Input
  'Hide and Show TextWindow and GraphicsWindow as they are required
  GraphicsWindow.Hide()
  TextWindow.Show()
  TextWindow.Top = 50
  TextWindow.Left = 50
  TextWindow.Title = "Name Game"
  TextWindow.Write("Please type in your name: ")
  name = TextWindow.Read()
  TextWindow.Hide()
  GraphicsWindow.Show()
  
  If (Text.GetLength(name) = 0) Then 'Exit for zero length input name
    Program.End()
  EndIf
  
  'Note that 'scale' is used to scale the window and initial turtle position
  GraphicsWindow.Clear()
  GraphicsWindow.Top = 50
  GraphicsWindow.Left = 50
  GraphicsWindow.Height = 200*scale
  GraphicsWindow.Width = (100 + (Text.GetLength(name) * 100))*scale
  
  Turtle.Show()
  Turtle.X = 50*scale
  Turtle.Y = 150*scale
  Turtle.Speed = 9
  Turtle.Angle = 90 'Use absolute angle rather than rotation, since the current rotation may vary from last name drawn
EndSub

'***********************************************************************************
'RUN THE GAME FOR A GIVEN INPUT NAME
'***********************************************************************************
Sub Run
  For i = 1 To Text.GetLength(name)
    char = Text.GetSubText(name,i,1) 'Get next character
    letter = letters[char] 'Set the turtle command string for character - if the character isn't defined in letters, then letter will be empty ""
    If (letter = "") Then 'Space for unknown character - or we could ignore it?
      TextWindow.WriteLine(letter)
      letter = letters[" "]
    EndIf
    makeLetter() ' Draw the letter with turtle
  EndFor
EndSub

'***********************************************************************************
'DRAW INDIVIDUAL LETTERS
'Requires the variable 'letter' to be set to a command string
'The variable 'letter' will be empty on return so it should be a copy of the command string
'***********************************************************************************
Sub MakeLetter
  letter = letter+":" 'Add a final deliminator if not already present to catch the last turtle command correctly
  While (Text.GetLength(letter) > 0) 'Continue doing turtle command while there are more left to do 
    pos = Text.GetIndexOf(letter,":") 'The postion of the end of the next turtle command
    command = Text.GetSubText(letter,1,pos-1) 'Next command to do
    type = Text.GetSubText(command,1,1) 'First character of command tells us what we have to do
    If (type = "U") Then
      Turtle.PenUp()
    ElseIf (type = "D") Then
      Turtle.PenDown()
    ElseIf (type = "T") Then
      Turtle.Turn(Text.GetSubTextToEnd(command,2)) 'Remaing part of command (2nd character onwards) is angle
    ElseIf (type = "M") Then
      Turtle.Move(scale*Text.GetSubTextToEnd(command,2)) 'Remaing part of command (2nd character onwards) is distance - we scale it as an option
    EndIf
    letter = Text.GetSubTextToEnd(letter,pos+1) ' Set letter variable as remaining set of commands - will be empty when we are done and the letter is drawn
  EndWhile
EndSub

'***********************************************************************************
'INITIALISE LETTERS - COMMANDS (Up,Down,Move,Turn) DELIMINATED BY ":" FOLLOWED BY VALUE FOR MOVE AND TURN
'First attempt used "." as deliminator, but it requires numbers to be integers, so changed to colon
'***********************************************************************************
Sub Initialise
  scale = 1 ' This can be set to scale everything
  letters[" "] = "U:M100"
  letters["A"] = "U:M25:T-90:D:M50:T90:M50:T-90:M25:T-45:M35:T-90:M35:T-45:M25:T-90:U:M50:T90:D:M50:T-90:U:M25"
  letters["B"] = "U:M25:T-90:D:M100:T90:M25:T45:M25:T45:M25:T45:M12:T45:M30:T180:U:M30:T45:D:M12:T45:M25:T45:M25:T45:M25:T180:U:M75"
EndSub
