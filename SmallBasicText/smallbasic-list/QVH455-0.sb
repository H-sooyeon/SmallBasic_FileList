'  Challenge of the Month - November 2016   //  Guitar chords (By YLed) 

'  Finger action type   by NaochanON    QVH455-0

'  notes code is originally shown  by YLed  FQH300

Controls.ButtonClicked=Clicked

init()

data()



TF="False"

While "True"

  If TF="True" Then 

    noteA=A[chord]

    noteN=N[chord]

    Sound.PlayMusic (noteA+noteN)

    Sound.PlayMusic (noteA+noteN)

    TF="False"

  EndIf

  Program.Delay(200)

endwhile



Sub   Clicked

  chord =Controls.GetButtonCaption( Controls.LastClickedButton)

  GuitarPlay()

  TF="True" 

EndSub



Sub GuitarPlay

  Shapes.SetText(note,chord)

  dS=-15

  drad= Math.GetRadians(-ds)

  For i=1 To 4

    cp=text.GetIndexOf(Cpos[chord][i],",")

    CX=text.GetSubText(Cpos[chord][i],1,cp-1)*100

    CY=(text.GetSubTextToEnd(Cpos[chord][i],cp+1)-1)*20+205

    Shapes.Move(Cshp[i],CX,CY  )

    Shapes.Rotate(Cshp[i],dS)

    Fx=Shapes.GetLeft(Cshp[i])+Claw[i]["height"]*s*Math.Sin(drad)/2-Finger[i]["height"]*s*Math.Sin(drad)/2

    FY=Shapes.Gettop(Cshp[i])+Claw[i]["height"]*s*Math.cos(drad)/2-Finger[i]["height"]*s*Math.cos(drad)

    Shapes.Move(Fshp[i],FX-3,FY)

    Shapes.Rotate(Fshp[i],dS)

    Shapes.Move(ON[i],30,220+(El[chord][i]-1)*20-10)

    Shapes.Move(OFF[i],30,220+(XX[chord][i]-1)*20-10)

  EndFor

EndSub



Sub init

  GraphicsWindow.BrushColor="#F2F2B0"

  GraphicsWindow.FillRectangle(50,210,360,120)

  GraphicsWindow.BrushColor="#B48A76"

  GraphicsWindow.FillRectangle(10,180,50,180)

  For i= 1 To 6

    GraphicsWindow.DrawLine(50,220+(i-1)*20,350,220+(i-1)*20)

  EndFor

  For i= 1 To 4

    GraphicsWindow.DrawLine(50+(i-1)*100,220,50+(i-1)*100,320)

  EndFor  

  GraphicsWindow.BrushColor="Green"

  GraphicsWindow.DrawText(20,20,"Guitar play // Finger action")

  GraphicsWindow.FontSize=60

  note= Shapes.AddText("*")

  Shapes.Move(note,500,20)

  Finger[1]  = "func=ell;x=100;y=100;width=26;height=120;bc=#FDE8D0;pc=#F1BF99;pw=2;" 'Index

  Finger[2]  = "func=ell;x=120;y=100;width=26;height=120;bc=#FDE8D0;pc=#F1BF99;pw=2;" 'Middle

  Finger[3]  = "func=ell;x=140;y=100;width=26;height=120;bc=#FDE8D0;pc=#F1BF99;pw=2;" 'Ring

  Finger[4]  = "func=ell;x=160;y=100;width=26;height=120;bc=#FDE8D0;pc=#F1BF99;pw=2;"  'Little

  Finger[5]  = "func=ell;x=80;y=330;width=130;height=30;bc=#FDE8D0;pc=#F1BF99;pw=2;"  'Thumb

  Claw[1]    = "func=ell;x=103;y=190;width=20;height=30;bc=#3EB370;pc=#F1BF99;pw=2;"

  Claw[2]    = "func=ell;x=123;y=190;width=20;height=30;bc=#C97586;pc=#F1BF99;pw=2;"

  Claw[3]    = "func=ell;x=143;y=190;width=20;height=30;bc=#0094C8;pc=#F1BF99;pw=2;"

  Claw[4]    = "func=ell;x=163;y=190;width=20;height=30;bc=#FFEA00;pc=#F1BF99;pw=2;"

  Claw[5]    = "func=ell;x=80;y=340;width=30;height=15;bc=#E9DFE5;pc=#F1BF99;pw=2;"

  s=1

  For i = 1 To 5

    GraphicsWindow.penColor = Finger[i]["pc"]      

    GraphicsWindow.PenWidth = Finger[i]["pw"]     

    GraphicsWindow.BrushColor = Finger[i]["bc"]     

    Fshp[i] = Shapes.AddEllipse(Finger[i]["width"]*s, Finger[i]["height"]*s)

    Shapes.Animate(Fshp[i], Finger[i]["x"]*s, Finger[i]["y"]*s, 500)

    Shapes.Rotate(FShp[i], Finger[i]["angle"])  

  EndFor

  For i = 1 To 5

    GraphicsWindow.penColor = Claw[i]["pc"]      

    GraphicsWindow.PenWidth = Claw[i]["pw"]     

    GraphicsWindow.BrushColor = Claw[i]["bc"]     

    Cshp[i] = Shapes.AddEllipse(Claw[i]["width"]*s, claw[i]["height"]*s)

    Shapes.Animate(Cshp[i], Claw[i]["x"]*s, Claw[i]["y"]*s, 500)

    Shapes.Rotate(CShp[i], Claw[i]["angle"])  

  EndFor

  GraphicsWindow.fontsize=16

  For i=1 To 4

    GraphicsWindow.BrushColor="Red"

    ON[i]=Shapes.AddText("??")

    Shapes.Move(ON[i],20,-150)

    GraphicsWindow.BrushColor="Black

    OFF[i]=Shapes.AddText("X")

    Shapes.Move(OFF[i],30,-150)

  EndFor

EndSub



Sub data

  GraphicsWindow.BrushColor="Green"

  GraphicsWindow.FontSize=20

  name="1=C;2=D;3=E;4=E7;5=Em;"

  For i=1 To Array.GetItemCount(name)

    Controls.AddButton(name[i],500,100+40*i)

  EndFor

  '-----------------------------------------------------------------------------------------------

  Cpos["C"]="1=1,2;2=2,4;3=3,5;4=-20,-20;"

  El["C"]="1=1;2=3;3=-20;4=-20;"

  XX["C"]="1=6;2=-20;3=-20;4=-20;"

  A["C"]="o4l32 C"+"o4l32 E"+"o4l32 G"+ "o5l32 C" ' un accord DO

  N["C"]="o5l1 E" 

  '-----------------------------------------------------------------------------------------------

  Cpos["D"]="1=1.8,3;2=2.2,1;3=3,2;4=-20,-20;"

  El["D"]="1=4;2=-20;3=-20;4=-20;"

  XX["D"]="1=6;2=-20;3=-20;4=-20;"

  A["D"]="o4l32 D"+"o5l32 A"+"o5l32 D" ' un accord RE

  N["D"]="o5l1 F#" 

  '-----------------------------------------------------------------------------------------------

  Cpos["E"]="1=1,3;2=1.8,5;3=2.2,4;4=-20,-20;"

  El["E"]="1=1;2=2;3=6;4=-20;"

  XX["E"]="1=-20;2=-20;3=-20;4=-20;"

  A["E"]="o3l32 E"+"o4l32 B"+"o4l32 E"+ "o4l32 G#"+"o5l32 B" '  un accord MI

  N["E"]="o5l1 E" 

  '-----------------------------------------------------------------------------------------------

  Cpos["E7"]="1=1,3;2=2,5;3=-20,-20;4=-20,-20;"

  El["E7"]="1=1;2=2;3=4;4=6;"

  XX["E7"]="1=-20;2=-20;3=-20;4=-20;"

  A["E7"]="o3l32 E"+"o4l32 B"+"o4l32 E"+ "o4l32 G#" +"o5l32 D"  '  un accord MI7

  N["E7"]="o5l1 E" 

  '-----------------------------------------------------------------------------------------------

  Cpos["Em"]="1=-20,-20;2=1.8,5;3=2.2,4;4=-20,-20;"

  El["Em"]="1=1;2=2;3=3;4=6;"

  XX["Em"]="1=-20;2=-20;3=-20;4=-20;"

  A["Em"]="o3l32 E"+"o4l32 B"+"o4l32 B"+ "o4l32 G"+"o5l32 B" '  un accord MI

  N["Em"]="o5l1 E" 

EndSub


