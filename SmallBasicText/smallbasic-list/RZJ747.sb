'********************************************************************************
'                    NEWTON RAPHSON
'********************************************************************************

'Initialisation
GraphicsWindow.BackgroundColor = "White"
GraphicsWindow.Title="Newton Raphson"
GraphicsWindow.Width=720
GraphicsWindow.Height=720
GraphicsWindow.CanResize=0
GraphicsWindow.Show()
SetColours()
rand=math.GetRandomNumber(7)
Mouseclicks=0
GraphicsWindow.MouseDown=OnMouseDown
GraphicsWindow.BrushColor="Black"
GraphicsWindow.FontSize=36
GraphicsWindow.DrawText(40,300,"Click anywhere to draw the fractal")

While Mouseclicks=0
  Program.Delay(10)
Endwhile


'Use nested loops to colour each pixel one by one
For i=0 To 720
  For j=0 To 720
    'x runs from -2 to 2
    x=i/180-2
    'y runs from 2 to -2
    y=2-j/180
    root=0
    col=0
    'iterate until the output is the same as the input
    While root=0
      col=col+1
      p=x
      q=y
      'this part is NR for z^3-1
      ' x = Real(z) and y = Im(z)
      a=p*p+q*q
      x=2*p/3+(p*p-q*q)/(3*a*a)
      y=2*q/3-2*p*q/(3*a*a)
      If Math.Abs(x-p)<0.00001 And Math.Abs(y-q)<0.00001 Then
        root=1
      EndIf
    EndWhile
    'Now colour according to the root
    usecol = Math.Remainder(col+rand,7)+1
    If x>0.5 then
      GraphicsWindow.SetPixel(i,j,Colours[0][usecol])
    Elseif y>0.25 then
      GraphicsWindow.SetPixel(i,j,Colours[1][usecol])
    Else
      GraphicsWindow.SetPixel(i,j,Colours[2][usecol])
    EndIf
  EndFor
EndFor

Sub OnMouseDown
  Mouseclicks=Mouseclicks+1
EndSub

Sub SetColours
  Colours[0][1]="Red"
  Colours[0][2]="Crimson"
  Colours[0][3]="IndianRed"
  Colours[0][4]="LightCoral"
  Colours[0][5]="Salmon"
  Colours[0][6]="DarkSalmon"
  Colours[0][7]="LightSalmon"
  Colours[1][1]="Blue"
  Colours[1][2]="RoyalBlue"
  Colours[1][3]="DodgerBlue"
  Colours[1][4]="LightSkyBlue"
  Colours[1][5]="SkyBlue"
  Colours[1][6]="LightBlue"
  Colours[1][7]="PowderBlue"
  Colours[2][1]="Yellow"
  Colours[2][2]="Khaki"
  Colours[2][3]="PapayaWhip"
  Colours[2][4]="Moccasin"
  Colours[2][5]="LightGoldenrodYellow"
  Colours[2][6]="LemonChiffon"
  Colours[2][7]="LightYellow"
EndSub
