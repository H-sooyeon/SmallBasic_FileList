'Go to SubOnkeyDown/SubKeyUp when a key is pressed/released:

GraphicsWindow.KeyDown = OnKeyDown

GraphicsWindow.KeyUp = OnKeyUp

          'Reset Variables:

left = 0

right = 0

up = 0

down = 0

X=0

Y=0

          'Add the objects:

GraphicsWindow.PenColor="Black"

GraphicsWindow.BrushColor="Black"

GraphicsWindow.FillRectangle(0,350,700,1)

Box=Shapes.AddRectangle(50,50)

GraphicsWindow.BrushColor="Orange"

GraphicsWindow.PenColor="Orange"

LightSource=Shapes.AddEllipse(30,30)

          'Move the objects:

Shapes.Move(Box,300,300)

Shapes.Move(LightSource,400,50)

          'Prepare the Pen for the shadow:

GraphicsWindow.pencolor="Gray" '"#11000000"

GraphicsWindow.PenWidth=10

GraphicsWindow.PenColor="transparent"

GraphicsWindow.BrushColor="#88000000"

          'Set Turtle settings and Variables:

Turtle.speed=10

Turtle.Show()

Turtle.PenUp()

BoxX=Shapes.GetLeft(Box)

BoxY=Shapes.GetTop(Box)

  

          'Loop the whole time:

While "True"

  ResetTurtleXY()

          'Just create a new shadow if the lightsource moved:

  If Xold <> Shapes.GetLeft(LightSource) Or Yold <> Shapes.GetTop(LightSource) then

    CreateShadow()

  else

    Program.Delay(10)

  endif

  Xold=Shapes.GetLeft(LightSource)

  Yold=Shapes.GetTop(LightSource)

  If Right=1 Then 

    X=10

  ElseIf Left = 1 then

    X=-10

  elseif Left = 0 And Right = 0 then

    X=0

  endif

  If up=1 Then 

    Y=-10

  ElseIf down = 1 then

    Y=10

  elseif up = 0 And down = 0 then

    Y=0

  endif

  LightSourceX=Shapes.GetLeft(LightSource)+15

  LightSourceY=Shapes.GetTop(LightSource)+15

  Shapes.Move(LightSource,Shapes.GetLeft(LightSource)+X,Shapes.GetTop(LightSource)+Y)

endwhile

          'Move the Turtle back to the lightsource:

Sub ResetTurtleXY

  Turtle.Y=LightSourceY

  Turtle.X=LightSourceX

endsub

          'Create the Shadow:

Sub CreateShadow

  If LightSourceX<BoxX+25 then

    Turtle.MoveTo(BoxX+50,BoxY)

    Touchsensor()

    Shadow=Shapes.AddTriangle(BoxX+50,350,Turtle.X,350,boxX+50,BoXY)

  elseif LightSourceX>BoxX+25 then

    Turtle.MoveTo(BoxX,BoxY)

    Touchsensor()

    Shadow=Shapes.AddTriangle(BoxX,350,Turtle.X,350,boxX,BoXY)

  endif

endsub

          'Get if the Turtle reached the ground:

Sub Touchsensor

  While Turtle.Y < 350

    Turtle.Move(50)

  endwhile

  Shapes.Remove(shadow)

endsub

          'When a key is pressed:

Sub OnKeyDown

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    left = 1

  ElseIf (Key = "Right") Then

    right = 1

  endIf

    If (Key = "Up") Then

    up = 1

  ElseIf (Key = "Down") Then

    down = 1

  endIf

EndSub

          'When a key is Released:

Sub OnKeyUp

  Key = GraphicsWindow.LastKey

  If (Key = "Left") Then

    left = 0

  ElseIf (Key = "Right") Then

    right = 0

  endif

   If (Key = "Up") Then

    up = 0

  ElseIf (Key = "Down") Then

    down = 0

  endif

EndSub














