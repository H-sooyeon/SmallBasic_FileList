'  Flash anzan program   (Mental arithmetic)   made by  NaochanON     2012/06/02

initLevel()
showmessage()

GraphicsWindow.Mousemove = onMousemove
Controls.ButtonClicked=onbutton

Sub onbutton
  nm= Controls.GetButtonCaption(Controls.LastClickedButton)
  If nm="Execute" Then 
    anzan() 
    setanswer()
  Elseif nm>10 then 
    checkanswer()
  EndIf
EndSub

Sub checkanswer
  If nm=total Then 
    Shapes.Animate(smsg,100,1000,4000)    '  You are right 
    Sound.PlayBellRingAndWait() 
    For j=1 To 5
      Controls.HideControl(sans[j]) 
    EndFor
    Controls.SetButtonCaption(btn,"Execute")
  Else 
    Sound.PlayClickAndWait()
    Sound.PlayClickAndWait()
    Controls.SetButtonCaption(btn,"No good !   select again ! ")
  EndIf
EndSub

Sub anzan
  Shapes.Move(smsg,0,-300)                '   move to initial position
  Turtle.Speed=3+2*Math.Floor(level/3)     '  Program.delay  does not work well , so this is used 
  Total=0
  anslist=""
  For i=1 To NMB
    retry1:
    Number= Math.GetRandomNumber(Math.Power(10,math.Ceiling(level/2)))  ' random number
    If Text.IsSubText(anslist,Number) Then              '  to avoid same dumy number    
      Goto retry1
    EndIf
    anslist=anslist+number+":"            ' List up
    Shapes.SetText(snumber, Number)
    total=total+number                    '  total number  
    Turtle.Turn(60)                       '  Program.delay  does not work well , so this is used 
  EndFor
  Shapes.SetText(snumber," ")             '  erase last number 
EndSub

Sub setanswer
  L=text.GetLength(total)          
  NMbase= Text.GetSubText(total,1,1)*Math.Power(10,L-1)       '23456  =2*10000+3456 
  List=total+":"
  For j=1 To 5 
    Controls.ShowControl(sans[j])                   '  show   answer  control 
    retry2:
    dumy=NMBase+Math.GetRandomNumber(Math.Power(10,L-1))  '  dumy = wrong answer 
    If Text.IsSubText(list,dumy) Then              '  to avoid same dumy number    
      Goto retry2
    EndIf
    list=list+dumy+":"                             ' List up
    Controls.SetButtonCaption(sans[j],dumy)
  EndFor
  Controls.SetButtonCaption(btn,"Select   correct    answer   !")
  m= Math.GetRandomNumber(5)                        '  right  answer control number 
  Controls.SetButtonCaption(sans[m],total)          '  set right answer
EndSub

Sub onMousemove
  gX = GraphicsWindow.MouseX
  gY = GraphicsWindow.MouseY
  YMax = Base+DS*11
  YMin = Base+DS
  If gx<120 And  Ymin<GY And  Gy<Ymax And Mouse.IsLeftButtonDown Then  '   
    Level=11-math.Floor((gY-base)/DS)            '  Quiz level 
    Shapes.SetText(slevel,"Level= "+Level)
    NewY = base+(11-Level)*DS
    Shapes.Move(srect,100,NewY-10)               '  move  rectangle 
    message2()
  EndIf
EndSub

Sub initLevel
  GraphicsWindow.Show()
  GraphicsWindow.BackgroundColor="Darkgreen" 
  GraphicsWindow.Top=20
  GraphicsWindow.Width=1100
  GraphicsWindow.Height=600
  Turtle.Speed=10                      '  Program.delay  does not work well , so this is used 
  Turtle.PenUp()
  Turtle.MoveTo(250,150)
  base=100
  LEVEL = 4                            ' initial level=4 
  DS=30
  For i = 1 To 10
    Y = DS*i+base
    GraphicsWindow.DrawLine(100,Y,120,Y)   '  level line 
  EndFor
  srect = Shapes.AddRectangle(20,20)       '  rectangle shape
  Shapes.Move(srect,100,DS*(11-Level)+base-10)
  Shapes.SetOpacity(sRect,60)
  '  ----------------------     explanation  message   ----------------------
  message()
  GraphicsWindow.DrawText(50,Base-20,"High Level")
  GraphicsWindow.DrawText(50,Base+DS*10+20,"Low Level")
  slevel= Shapes.AddText("Level= "+Level)
  Shapes.Move(slevel,50,10)
EndSub 

Sub showmessage
  '------------------   answer control   ---------------------- 
  GraphicsWindow.BrushColor="Red"
  For j=1 To 5
    sans[j]=Controls.AddButton(" ",300+150*(j-1),400)  ' answer  control 
    Controls.SetSize(sans[j],140,50)
    Controls.HideControl(sans[j])                      '  hide 
  EndFor
  btn=Controls.AddButton("Execute",300,500)
  Controls.SetSize(btn,600,50)
  '------------------    Quiz Number  ---------------------- 
  GraphicsWindow.FontSize=200
  snumber=Shapes.AddText("  ")
  Shapes.Move(snumber,400,150)
   '------------------   message     ---------------------- 
  GraphicsWindow.FontSize=100
  smsg= Shapes.AddText("Congratulation right ! ")
  Shapes.Move(smsg,0,-300)  
EndSub

Sub message
  '------------------   message     ---------------------- 
  GraphicsWindow.BrushColor="Yellow"
  GraphicsWindow.FontItalic="True"
  GraphicsWindow.FontSize=30
  GraphicsWindow.DrawText(300,10,"Flash Anzan (High speed Mental arithmetic)")
  GraphicsWindow.DrawText(300,40,"Move slider to select Level and push Execute !")
  '------------------   message     ---------------------- 
  GraphicsWindow.FontSize=20
  sexp1= Shapes.AddText("Low Level ; 1 digit  ------   5 digit ;High Level ")
  Shapes.Move(sexp1,300,80)
  '------------------   message     ---------------------- 
  NMB=5*Math.Ceiling(level/4)                    '  number related to level
  tm= math.Ceiling( 10/Math.Ceiling(level/3))    '  display time related to level
  sexp2= Shapes.AddText("Different "+NMB+" numbers appear in "+tm+" seconds. Select right  total number. ")
  Shapes.Move(sexp2,300,100)
EndSub

Sub message2
  NMB=5*Math.Ceiling(level/4)
  tm= math.Ceiling( 10/Math.Ceiling(level/3))
  Shapes.SetText(sexp2,"Different "+NMB+" numbers appear in "+tm+" seconds. Select right  total number. ")
EndSub
