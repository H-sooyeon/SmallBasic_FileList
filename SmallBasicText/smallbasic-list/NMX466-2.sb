GraphicsWindow.Width=500 'Set up the graphicswindow
GraphicsWindow.Height=600
GraphicsWindow.Top=50

Emitter=Shapes.AddTriangle(0,50,25,0,50,50) 'Add Emitter Triangle
EmitterX = 225
EmitterY = 300
Shapes.Move(Emitter,EmitterX,EmitterY)
GraphicsWindow.BrushColor=GraphicsWindow.GetColorFromRGB(230,0,0) 'Set the color for the "blood"
GraphicsWindow.PenWidth=0
For i = 1 to 10 'Add the "Blood" sprites
  Blood[i]=shapes.AddEllipse(10,10)
endfor
BloodAmount=Array.GetItemCount(Blood)
dragging = 0 'dragging emitter
moving = 0

While "True" 'Repeat the whole time:
  MouseControl()
  If moving < BloodAmount then
    BloodMovement()
  endif
  Program.Delay(1)
endwhile

Sub MouseControl
  If Mouse.IsLeftButtonDown Then 'Drag and drop the Emitter
    MX=GraphicsWindow.MouseX
    MY=GraphicsWindow.MouseY
    If dragging = 0 Then
      If MX >= EmitterX And MX <= EmitterX+50 And MY >= EmitterY And MY <= EmitterY+50 then
        dragging = 1
      endif
    EndIf
    If dragging = 1 Then
      EmitterX = MX-25
      EmitterY = MY-25
      Shapes.Move(Emitter,EmitterX,EmitterY)
    EndIf
  Else
    dragging = 0
  endif
  If Mouse.IsRightButtonDown Then 'Emitt the "blood" sprites
    moving=0
    For i = 1 to BloodAmount
      BWX[i]=0.1*Math.GetRandomNumber(21)-1.1
      BWY[i]=0.1*(Math.GetRandomNumber(30)-31)
      BX[i]=EmitterX+25
      BY[i]=EmitterY-5
    endfor
  endif
endsub

Sub BloodMovement 'Move the "blood" sprites around by their corresponding WX and WY
  For i = 1 To BloodAmount
    If BY[i] > 550+i*2 Then
      BWX[i] = 0
      BWY[i] = 0
      GraphicsWindow.FillEllipse(BX[i]-5,BY[i]-5,10,10)
      moving=moving+1
    Else
      moving=moving-1
      BWY[i]=BWY[i]+0.02
      BWX[i] = BWX[i] * 0.998
    endif
    BX[i]=BX[i]+BWX[i]
    BY[i]=BY[i]+BWY[i]
    Shapes.Move(Blood[i],BX[i]-5,BY[i]-5)
  endfor
endsub



